/*	Management report for Default Prevention to identify forms 
	and correspondence sent to either the borrower, school,
	or servicer.
*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWDPG.LWDPGR2";
FILENAME REPORT3 "&RPTLIB/ULWDPG.LWDPGR3";

DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DPFRMS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.pf_act			AS PFACT,
		A.bf_lst_usr_ay01	AS LSTUSR
FROM  OLWHRM1.AY01_BR_ATY A
WHERE A.bd_aty_prf BETWEEN &BEGIN AND &END
AND A.pf_act in (
/*Deferments sent through LP80*/
'ADD01', 'ALHRD', 'ALPLW', 'ALPUB', 
'ALSCH', 'ALTDS', 'ALUNE', 'ALEDU', 
'ALPLU', 'ALPDS',
/*Forbearance sent through LP80*/
'ADD02', 'ALFRB',
/*Manual deferments or forbearance sent*/
'AFXFM', 'AMSFC',
/*Enrollment Verification to School*/
'ALEVR')
ORDER BY A.PF_ACT
);
DISCONNECT FROM DB2;
endrsubmit  ;

DATA DPFRMS; 
SET WORKLOCL.DPFRMS; 
RUN;

DATA TOTALS;
LP80DF = 0;
LP80FB = 0;
MANDFFB = 0;
ENRVFY = 0;
RUN;

PROC SQL;
UPDATE TOTALS
SET LP80DF=
	(SELECT COUNT(*)
	FROM DPFRMS
	WHERE PFACT IN
		('ADD01', 'ALHRD', 'ALPLW', 'ALPUB', 
		'ALSCH', 'ALTDS', 'ALUNE', 'ALEDU', 
		'ALPLU', 'ALPDS')
	);
UPDATE TOTALS
SET LP80FB=
	(SELECT COUNT(*)
	FROM DPFRMS
	WHERE PFACT IN
		('ADD02', 'ALFRB')
	);
UPDATE TOTALS
SET MANDFFB=
	(SELECT COUNT(*)
	FROM DPFRMS
	WHERE PFACT IN
		('AFXFM', 'AMFSC')
	);
UPDATE TOTALS
SET ENRVFY=
	(SELECT COUNT(*)
	FROM DPFRMS
	WHERE PFACT IN
		('ALEVR')
	);

;QUIT;

PROC SQL;
CREATE TABLE BYUSR1 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS DFFBTOT,
		0						AS ENRLTOT
FROM DPFRMS
WHERE /*LSTUSR <> 'LPXHH'
AND*/ PFACT in (
				'ADD01', 'ALHRD', 'ALPLW', 'ALPUB', 
				'ALSCH', 'ALTDS', 'ALUNE', 'ALEDU', 
				'ALPLU', 'ALPDS', 'ADD02', 'ALFRB',
				'AFXFM', 'AMFSC')
GROUP BY LSTUSR/*, ENRLTOT*/
ORDER BY LSTUSR;

CREATE TABLE BYUSR2 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS ENRLTOT
FROM DPFRMS
WHERE /*LSTUSR <> 'LPXHH'
AND*/ PFACT = 'ALEVR'
GROUP BY LSTUSR
ORDER BY LSTUSR;
QUIT;

DATA BYUSR;
MERGE BYUSR1 BYUSR2;
BY LSTUSR;
IF DFFBTOT=. THEN DFFBTOT=0;
IF ENRLTOT=. THEN ENRLTOT=0;
RUN;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

OPTIONS CENTER NODATE NONUMBER PS=40 LS=132;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=TOTALS WIDTH=UNIFORM WIDTH=MIN;
VAR LP80DF LP80FB MANDFFB ENRVFY;
LABEL LP80DF = 'Deferments Sent via LP80'
LP80FB = 'Forbearances Sent via LP80'
MANDFFB	= 'Manual Deferments and Forbearances'
ENRVFY = 'Enrollment Verifications Sent' ;
TITLE1 "Default Prevention Forms or Correspondence Sent - &EFFDATE";
TITLE2 'By Action Code Type';
FOOTNOTE  'JOB = UTLWDPG     REPORT = ULWDPG.LWDPGR2';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=BYUSR WIDTH=UNIFORM WIDTH=MIN;
VAR LSTUSR DFFBTOT ENRLTOT;
SUM DFFBTOT ENRLTOT;
LABEL	LSTUSR = 'Last User'
		DFFBTOT = 'Number of Forms Sent'
		ENRLTOT = 'Number of Enrollment Verifications Sent';
TITLE1 "Default Prevention Forms or Correspondence Sent - &EFFDATE";
TITLE2 'By User ID for user who last updated this AY01 data';
FOOTNOTE  'JOB = UTLWDPG     REPORT = ULWDPG.LWDPGR3';
RUN;

PROC PRINTTO;
RUN;
