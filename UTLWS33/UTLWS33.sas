*---------------------------------*
| SASR 2830 - UTLWS33 ACH reports |
*---------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS33.LWS33R2";
FILENAME REPORT3 "&RPTLIB/ULWS33.LWS33R3";
FILENAME REPORTZ "&RPTLIB/ULWS33.LWS33RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ACHDS1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,RTRIM(A.BF_SSN)||'0'||CHAR(A.LN_SEQ) AS LID
	,A.LA_CUR_PRI
	,B.WC_DW_LON_STA
	,C.LD_DLQ_OCC
	,E.BC_EFT_STA
	,1 AS I
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
	AND C.LC_STA_LON16 = '1'
	AND C.LC_DLQ_TYP = 'P'
LEFT OUTER JOIN OLWHRM1.LN83_EFT_TO_LON D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
	AND D.LC_STA_LN83 = 'A'
LEFT OUTER JOIN OLWHRM1.BR30_BR_EFT E
	ON D.BF_SSN = E.BF_SSN
	AND D.BN_EFT_SEQ = E.BN_EFT_SEQ
	AND E.BC_EFT_STA = 'A'
WHERE B.WC_DW_LON_STA IN ('03','14','08')
	AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWS33.LWS33RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA ACHDS1 ;
	SET WORKLOCL.ACHDS1;
RUN;
PROC SORT DATA=ACHDS1 NODUPKEY;
	BY LID;
RUN;
PROC SQL;
CREATE TABLE ACH1LN (DROP=I) AS 
SELECT DISTINCT A.I
	,TOT.TOT_BOR
	,TOT.TOT_BAL
	,ACH.ACH_BOR
	,ACH.ACH_BAL
	,ACH.ACH_BOR/TOT.TOT_BOR AS PRCNT_BOR
	,ACH.ACH_BAL/TOT.TOT_BAL AS PRCNT_BAL
FROM ACHDS1 A
LEFT OUTER JOIN (
	SELECT I
		,COUNT(DISTINCT BF_SSN) AS ACH_BOR
		,SUM(LA_CUR_PRI) AS ACH_BAL
	FROM ACHDS1
	WHERE WC_DW_LON_STA = '03' 
		AND BC_EFT_STA = 'A'
	GROUP BY I
	) ACH
	ON A.I = ACH.I 
LEFT OUTER JOIN (
	SELECT I
		,COUNT(DISTINCT BF_SSN) AS TOT_BOR
		,SUM(LA_CUR_PRI) AS TOT_BAL
	FROM ACHDS1
	WHERE WC_DW_LON_STA = '03' 
	GROUP BY I
	) TOT
	ON A.I = TOT.I
;
QUIT;

PROC SQL;
CREATE TABLE DEQPR (DROP=I) AS 
SELECT DISTINCT A.I
	,ACH.ACH_BOR
	,ACH.ACH_BAL
	,DLQ.DLQ_BOR
	,DLQ.DLQ_BAL
	,DLQ.DLQ_BOR/ACH.ACH_BOR AS PRCNT_BOR
	,DLQ.DLQ_BAL/ACH.ACH_BAL AS PRCNT_BAL
FROM ACHDS1 A
LEFT OUTER JOIN (
	SELECT I
		,COUNT(DISTINCT BF_SSN) AS ACH_BOR
		,SUM(LA_CUR_PRI) AS ACH_BAL
	FROM ACHDS1
	WHERE WC_DW_LON_STA = '03' 
		AND BC_EFT_STA = 'A'
	GROUP BY I
	) ACH
	ON A.I = ACH.I 
LEFT OUTER JOIN (
	SELECT I
		,COUNT(DISTINCT BF_SSN) AS DLQ_BOR
		,SUM(LA_CUR_PRI) AS DLQ_BAL
	FROM ACHDS1
	WHERE TODAY() - LD_DLQ_OCC > 1
		AND BC_EFT_STA = 'A'
	GROUP BY I
	) DLQ
	ON A.I = DLQ.I
;
QUIT;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'ACH ON AT LEAST ONE LOAN';
FOOTNOTE   'JOB = UTLWS33  	 REPORT = ULWS33.LWS33R2';
PROC PRINT NOOBS SPLIT='/' DATA=ACH1LN WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT TOT_BOR ACH_BOR COMMA6. TOT_BAL ACH_BAL DOLLAR18.2 PRCNT_BOR PRCNT_BAL PERCENT10.2;
VAR TOT_BOR
	TOT_BAL
	ACH_BOR
	ACH_BAL
	PRCNT_BOR
	PRCNT_BAL;
LABEL TOT_BOR = 'TOTAL #/OF BORROWERS'
	TOT_BAL = 'CURRENT/BALANCE'
	ACH_BOR = 'BORROWERS/ ON ACH'
	ACH_BAL = 'CURRENT BALANCE/ON ACH'
	PRCNT_BOR = '% OF BORROWERS/ON ACH'
	PRCNT_BAL = '% OF BALANCE/ON ACH'
;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'DELINQUENCY';
FOOTNOTE   'JOB = UTLWS33  	 REPORT = ULWS33.LWS33R3';
PROC PRINT NOOBS SPLIT='/' DATA=DEQPR WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT DLQ_BOR ACH_BOR COMMA6. DLQ_BAL ACH_BAL DOLLAR18.2 PRCNT_BOR PRCNT_BAL PERCENT10.2;
VAR ACH_BOR
	ACH_BAL
	DLQ_BOR
	DLQ_BAL
	PRCNT_BOR
	PRCNT_BAL;
LABEL ACH_BOR = 'ACH/BORROWERS'
	ACH_BAL = 'ACH/BALANCE'
	DLQ_BOR = 'DELQ ACH/BORROWERS'
	DLQ_BAL = 'DELQ ACH/BALANCE'
	PRCNT_BOR = '% DELQ ACH/BORROWERS'
	PRCNT_BAL = '% DELQ ACH/BALANCE'
;
RUN;
PROC PRINTTO;
RUN;
data _null_;
set  WORK.ACHDS1   end=EFIEOD;
file 'T:\SAS\UTLWS33.Detail.txt' delimiter=',' DSD DROPOVER lrecl=32767;
if _n_ = 1 then        
 do;
   put
      "BF_SSN"
   ','
      "LN_SEQ"
   ','
      "LID"
   ','
      "LA_CUR_PRI"
   ','
      "WC_DW_LON_STA"
   ','
      "LD_DLQ_OCC"
   ','
      "BC_EFT_STA"
   ','
      "I"
   ;
 end;
   format BF_SSN $9. ;
   format LN_SEQ 6. ;
   format LID $16. ;
   format LA_CUR_PRI 10.2 ;
   format WC_DW_LON_STA $2. ;
   format LD_DLQ_OCC date9. ;
   format BC_EFT_STA $1. ;
   format I 11. ;
do;
   put BF_SSN $ @;
   put LN_SEQ @;
   put LID $ @;
   put LA_CUR_PRI @;
   put WC_DW_LON_STA $ @;
   put LD_DLQ_OCC @;
   put BC_EFT_STA $ @;
   put I ;
   ;
end;
run;


