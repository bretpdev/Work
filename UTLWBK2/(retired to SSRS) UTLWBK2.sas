/*	Select Chapter 7 bankruptcies if the account is in an 01 pending bankruptcy
	status, if the account has a filing date that is greater than 120 days, and
	if no action code of DBKRW or BXMIS exists indicating a review in the last 30 days
	and there is no complaint received date for the account.  
	Queue will be used to review for discharge or dismissal.  MC  */

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWBK2.LWBK2R2";
FILENAME REPORTZ "&RPTLIB/ULWBK2.LWBK2RZ";

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT ;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEANB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,B.LD_BKR_FIL			
	,B.LC_BKR_STA			
	,CASE 
		WHEN B.LC_BKR_CHP = '01' THEN '07'
		WHEN B.LC_BKR_CHP = '04' THEN '13'
		WHEN B.LC_BKR_CHP = '02' THEN '11'
		WHEN B.LC_BKR_CHP = '03' THEN '12'
		ELSE B.LC_BKR_CHP
	 END AS LC_BKR_CHP
	,B.LF_BKR_DKT
	,C.MAXREVIEW
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.DC18_BKR B
	ON	 A.AF_APL_ID = B.AF_APL_ID			
	AND  A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
LEFT OUTER JOIN 
	(SELECT MAX(BD_ATY_PRF) AS MAXREVIEW
	,DF_PRS_ID
	FROM OLWHRM1.AY01_BR_ATY 	 
	WHERE PF_ACT IN ('DBKRW','BXMIS')
	GROUP BY DF_PRS_ID
	)C
	ON A.BF_SSN = C.DF_PRS_ID

WHERE A.BF_SSN NOT IN
	(SELECT	D.DF_PRS_ID
	FROM OLWHRM1.AY01_BR_ATY D
	WHERE D.PF_ACT IN ('DBKRW','BXMIS')
	AND DAYS(CURRENT DATE) - DAYS(D.BD_ATY_PRF) < 31
	)
AND B.LC_BKR_STA = '01'							/*PENDING (OPEN) BANKRUPTCY*/
AND B.LC_BKR_CHP = '01'							/*CHAPTER 7 BANKRUPTCY*/
AND DAYS(CURRENT DATE)-DAYS(B.LD_BKR_FIL) > 120	/*FILE DATE 120+ DAYS OLD*/
AND B.LD_CPT_RCV IS NULL						/*NO COMPLAINT RECEIVED*/
AND A.LC_STA_DC10 = '03'
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.CT30_CALL_QUE X
	WHERE X.DF_PRS_ID_BR = A.BF_SSN
	AND X.IF_WRK_GRP = 'BKCP7RVW'
	)
);	
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWBK2.LWBK2RZ);*/
/*QUIT;*/
PROC SORT DATA=DEANB NODUPREC;
BY BF_SSN;
RUN;
ENDRSUBMIT  ;
DATA DEANB;
SET WORKLOCL.DEANB;
RUN;
/*-----------------------------------------------------------------------------*
| THIS MACRO WILL CHANGE DATES INTO STIRNGS SO THEY CAN BE CONCATINATED IN THE |
| COMMENTS VARIABLE                                                            |
*-----------------------------------------------------------------------------*/
%MACRO DATE_2_CHAR(DS,CDATE,DAYPART);
DATA &DS;
SET &DS;
LENGTH &DAYPART $ 10. ;
IF &CDATE = . THEN DO;
	&DAYPART = '';
	END;
ELSE IF DAY(&CDATE) < 10 AND MONTH(&CDATE) < 10 THEN DO;
	&DAYPART = '0'||TRIM(LEFT(MONTH(&CDATE)))||'/'||'0'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE IF DAY(&CDATE) < 10 AND MONTH(&CDATE) >= 10 THEN DO;
	&DAYPART = TRIM(LEFT(MONTH(&CDATE)))||'/'||'0'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE IF DAY(&CDATE) >= 10 AND MONTH(&CDATE) < 10 THEN DO;
	&DAYPART ='0'||TRIM(LEFT(MONTH(&CDATE)))||'/'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE DO;
	&DAYPART = TRIM(LEFT(MONTH(&CDATE)))||'/'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
RUN;
%MEND DATE_2_CHAR;
%DATE_2_CHAR(DEANB,LD_BKR_FIL,CHAR_LD_BKR_FIL);
%DATE_2_CHAR(DEANB,MAXREVIEW,CHAR_MAXREVIEW);
/*------------------ VARIABLE INITIALIZATION ------------------*/
DATA DEANB (KEEP=TARGET_ID QUEUE_NAME INSTITUTION_ID INSTITUTION_TYPE DATE_DUE TIME_DUE COMMENTS);
SET DEANB ;
LENGTH COMMENTS $ 600.;
TARGET_ID = BF_SSN;
QUEUE_NAME = 'BKCP7RVW';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
COMMENTS = 'OneLINK BK Filed Date: '||TRIM(LEFT(CHAR_LD_BKR_FIL))||','||
	'BK Status: '||TRIM(LEFT(LC_BKR_STA))||','||
	'BK Chapter: '||TRIM(LEFT(LC_BKR_CHP))||','||
	'BK Doc: '||TRIM(LEFT(LF_BKR_DKT))||','||
	'Last RVW: '||TRIM(LEFT(CHAR_MAXREVIEW));
RUN;

DATA _NULL_;
SET  WORK.DEANB; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
