*--------------------------------------------------------------*
| CLAIM PAYMENT RECEIVED - POSSIBLE INVALID REFUND TRANSACTION |
*--------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR04.LWR04R2";
FILENAME REPORTZ "&RPTLIB/ULWR04.LWR04RZ";
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CPRPIRT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
    ,A.LN_SEQ
    ,D.DF_SPE_ACC_ID
    ,D.DM_PRS_LST
    ,A.LA_CUR_PRI
    ,B.WA_TOT_BRI_OTS
	,B.WC_DW_LON_STA
    ,COALESCE(A.LA_CUR_PRI,0) + 
		COALESCE(B.WA_TOT_BRI_OTS,0) AS PRI_INT
	,LN90A.LD_FAT_EFF AS DT_1030
    ,LN90B.LD_FAT_EFF AS DT_2030
	,COALESCE(LN90A.LA_FAT_NSI,0) +
		COALESCE(LN90A.LA_FAT_CUR_PRI,0) AS AMT_1030
    ,COALESCE(LN90B.LA_FAT_NSI,0) +
		COALESCE(LN90B.LA_FAT_CUR_PRI,0) AS AMT_2030
FROM OLWHRM1.PD10_PRS_NME D
INNER JOIN OLWHRM1.LN10_LON A
	ON D.DF_PRS_ID = A.BF_SSN
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
        ON A.BF_SSN = B.BF_SSN
        AND A.LN_SEQ = B.LN_SEQ
LEFT JOIN OLWHRM1.LN90_FIN_ATY LN90A
	ON A.BF_SSN = LN90A.BF_SSN
	AND A.LN_SEQ = LN90A.LN_SEQ
	AND LN90A.PC_FAT_TYP = '10' 
	AND	LN90A.PC_FAT_SUB_TYP = '30'
	AND LN90A.LC_FAT_REV_REA = ''
	AND LN90A.LC_STA_LON90 = 'A'
LEFT JOIN OLWHRM1.LN90_FIN_ATY LN90B
	ON A.BF_SSN = LN90B.BF_SSN
	AND A.LN_SEQ = LN90B.LN_SEQ
	AND LN90B.PC_FAT_TYP = '20' 
	AND	LN90B.PC_FAT_SUB_TYP = '30'
	AND LN90B.LC_FAT_REV_REA = ''
	AND LN90B.LC_STA_LON90 = 'A'
WHERE EXISTS (
    SELECT *
    FROM OLWHRM1.LN10_LON CSQ1
    INNER JOIN OLWHRM1.DW01_DW_CLC_CLU CSQ2
        ON CSQ1.BF_SSN = CSQ2.BF_SSN
        AND CSQ1.LN_SEQ = CSQ2.LN_SEQ
    INNER JOIN OLWHRM1.LN90_FIN_ATY CSQ3
        ON CSQ2.BF_SSN = CSQ3.BF_SSN
        AND CSQ2.LN_SEQ = CSQ3.LN_SEQ
    WHERE CSQ1.BF_SSN = A.BF_SSN
    AND COALESCE(CSQ1.LA_CUR_PRI,0) + COALESCE(CSQ2.WA_TOT_BRI_OTS,0) <> 0
    AND CSQ3.PC_FAT_TYP = '10'
    AND CSQ3.PC_FAT_SUB_TYP = '30'
    AND CSQ3.LC_FAT_REV_REA = ''
    AND CSQ3.LC_STA_LON90 = 'A'
	AND CSQ1.LC_STA_LON10 = 'R'
    )
	AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR04.LWR04RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA CPRPIRT;
SET WORKLOCL.CPRPIRT;
RUN;

DATA CPRPIRT;
SET CPRPIRT;
IF DT_1030 = . AND DT_2030 = . THEN DELETE;
RUN;

PROC SORT DATA=CPRPIRT;BY DF_SPE_ACC_ID LN_SEQ;
RUN;

PROC FORMAT;
VALUE $STAT '01'='IN GRACE' 
	'02'='IN SCHOOL'
	'03'='IN REPAY' 
	'04'='IN DEFER' 
	'05'='IN FORB' 
	'06'='CURE' 
	'07'='CLAIM PEND' 
	'08'='CLAIM SUB' 
	'09'='CLAIM CANC' 
	'10'='CLAIM REJ' 
	'11'='CLAIM RET' 
	'12'='CLAIM PAID' 
	'13'='PRE-CLAIM PEN' 
	'14'='PRE-CLAIM SUB'
	'15'='PRE-CLAIM CAN' 
	'16'='DEATH ALGD' 
	'17'='DEATH VERF' 
	'18'='DISAB ALGD' 
	'19'='DISAB VERF' 
	'20'='BK ALGD' 
	'21'='BK VRFD' 
	'22'='PIF' 
	'23'='NOT FULLY ORG' 
	'88'='ERROR'
	'98'='UNKNOWN';
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER DATE;
TITLE 'CLAIM PAYMENT RECEIVED - POSSIBLE INVALID REFUND TRANSACTION';
FOOTNOTE 'JOB = UTLWR04  	 REPORT = ULWR04.LWR04R2';
PROC CONTENTS DATA=CPRPIRT OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR04  	 REPORT = ULWR04.LWR04R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=CPRPIRT WIDTH=UNIFORM WIDTH=MIN;
FORMAT LA_CUR_PRI AMT_1030 AMT_2030 WA_TOT_BRI_OTS PRI_INT 14.2 DT_1030 DT_2030 MMDDYY10. 
	WC_DW_LON_STA $STAT.;
VAR DF_SPE_ACC_ID
	DM_PRS_LST
	LN_SEQ	
	LA_CUR_PRI
	WA_TOT_BRI_OTS
	PRI_INT
	AMT_1030
	DT_1030
	AMT_2030
	DT_2030
	WC_DW_LON_STA;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	DM_PRS_LST = 'LAST NAME'
	LN_SEQ	= 'LOAN SEQ'
	LA_CUR_PRI = 'PRINCIPAL'
	WA_TOT_BRI_OTS = 'OUTSTANDING INTEREST'
	PRI_INT = 'PRINCIPAL AND INTEREST TOTAL'
	AMT_1030 = '1030 CLAIM PMT AMT'
	DT_1030 = '1030 CLAIM PMT DATE'
	AMT_2030 = '2030 CLAIM REFUND AMT'
	DT_2030 = '2030 CLAIM REFUND DATE'
	WC_DW_LON_STA = 'LOAN STATUS'; 
RUN;

PROC PRINTTO;
RUN;
