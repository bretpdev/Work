FILENAME EA27_IN "Y:\Batch\NFP_EA27_OUT_20120202160627.txt";

%SYSLPUT DB = DNFPUTDL; *LIVE;
/*%SYSLPUT DB = DNFPRQUT; *TEST;*/

FILENAME REPORT2 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R2.csv";
FILENAME REPORT3 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R3.csv";
FILENAME REPORT4 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R4.csv";
FILENAME REPORT5 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R5.csv";
FILENAME REPORT6 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R6.csv";
FILENAME REPORT7 "T:\SAS\INCOMPLETE DEMOGRAPHICS.R7.csv";

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
RSUBMIT LEGEND;
LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;

PROC SQL;
CONNECT TO DB2 (DATABASE=&DB);
CREATE TABLE PD10 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT d.df_spe_acc_id
	,COALESCE(B.BF_SSN,C.BF_SSN,E.BF_SSN) AS BF_SSN
	,D.DF_PRS_ID
	,CASE 
		WHEN C.LF_EDS IS NOT NULL THEN 'E'
		WHEN E.LF_STU_SSN IS NOT NULL THEN 'S'
		WHEN B.BF_RFR IS NOT NULL THEN 'R'
		ELSE 'B'
	END AS DC_PRS_TYP
	,case 
		when d.dd_brt is null 
			and d.dm_prs_1 = ''
			and d.dm_prs_lst = '' then 'A'
		WHEN d.dm_prs_1 = ''
			and d.dm_prs_lst = '' THEN 'N'
		ELSE 'B'
	END AS MissCat
FROM PKUB.PD10_PRS_NME D
LEFT OUTER JOIN PKUB.RF10_RFR B
	ON D.DF_PRS_ID = B.BF_RFR
LEFT OUTER JOIN PKUB.LN20_EDS C
	ON D.DF_PRS_ID = C.LF_EDS
LEFT OUTER JOIN PKUB.LN10_LON E
	ON D.DF_PRS_ID = E.LF_STU_SSN
	AND E.LF_STU_SSN != E.BF_SSN
WHERE D.dd_brt is null
	or d.dm_prs_1 = ''
	or d.dm_prs_lst = ''
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

CREATE TABLE PD30 AS
SELECT DISTINCT d.DF_SPE_ACC_ID
	,COALESCE(B.BF_SSN,C.BF_SSN,E.BF_SSN) AS BF_SSN
	,D.DF_PRS_ID
	,d.dm_prs_1
	,d.dm_prs_mid
	,d.dm_prs_lst
	,CASE 
		WHEN C.LF_EDS IS NOT NULL THEN 'E'
		WHEN E.LF_STU_SSN IS NOT NULL THEN 'S'
		WHEN B.BF_RFR IS NOT NULL THEN 'R'
		ELSE 'B'
	END AS DC_PRS_TYP
FROM PKUB.PD10_PRS_NME D
LEFT OUTER JOIN PKUB.RF10_RFR B
	ON D.DF_PRS_ID = B.BF_RFR
LEFT OUTER JOIN PKUB.LN20_EDS C
	ON D.DF_PRS_ID = C.LF_EDS
LEFT OUTER JOIN PKUB.LN10_LON E
	ON D.DF_PRS_ID = E.LF_STU_SSN
	AND E.LF_STU_SSN ^= E.BF_SSN
LEFT OUTER JOIN PKUB.PD30_PRS_ADR A
	ON D.DF_PRS_ID = A.DF_PRS_ID
	AND DC_ADR = 'L'
WHERE (A.DX_STR_ADR_1 = ''
	AND A.DX_STR_ADR_2 = ''
	AND A.DM_CT = ''
	AND A.DC_DOM_ST = ''
	AND A.DF_ZIP_CDE = ''
	AND A.DM_FGN_CNY = '')
	OR A.DF_PRS_ID IS NULL;

CREATE TABLE PD40 AS
SELECT DISTINCT d.DF_SPE_ACC_ID
	,COALESCE(B.BF_SSN,C.BF_SSN,E.BF_SSN) AS BF_SSN
	,D.DF_PRS_ID
	,d.dm_prs_1
	,d.dm_prs_mid
	,d.dm_prs_lst
	,CASE 
		WHEN C.LF_EDS IS NOT NULL THEN 'E'
		WHEN E.LF_STU_SSN IS NOT NULL THEN 'S'
		WHEN B.BF_RFR IS NOT NULL THEN 'R'
		ELSE 'B'
	END AS DC_PRS_TYP
FROM PKUB.PD10_PRS_NME D
LEFT OUTER JOIN PKUB.RF10_RFR B
	ON D.DF_PRS_ID = B.BF_RFR
LEFT OUTER JOIN PKUB.LN20_EDS C
	ON D.DF_PRS_ID = C.LF_EDS
LEFT OUTER JOIN PKUB.LN10_LON E
	ON D.DF_PRS_ID = E.LF_STU_SSN
	AND E.LF_STU_SSN ^= E.BF_SSN
LEFT OUTER JOIN PKUB.PD40_PRS_PHN A
	ON D.DF_PRS_ID = A.DF_PRS_ID
	AND DC_PHN = 'H'
WHERE (A.DN_DOM_PHN_ARA = ''
	AND A.DN_DOM_PHN_XCH = ''
	AND A.DN_DOM_PHN_LCL = ''
	AND A.DN_PHN_XTN = ''
	AND A.DN_FGN_PHN_CNY = ''
	AND A.DN_FGN_PHN_CT = ''
	AND A.DN_FGN_PHN_LCL = '')
	OR A.DF_PRS_ID IS NULL;
QUIT;
ENDRSUBMIT;
DATA PD40; SET LEGEND.PD40; RUN;
DATA PD30; SET LEGEND.PD30; RUN;
DATA PD10; SET LEGEND.PD10; RUN;



DATA borr_dob(keep=BORR_ssn dob BOR_NAME)
	REF_ADR(keep=BORR_ssn REF_NAME ref_first_name ref_last_name ref_mid_name REF_STREET_1 REF_STREET_2
		REF_CITY REF_STATE REF_ZIP REF_COUNTRY REF_HOME_PHN REF_ALT_PHN REF_EML OTHER_SSN ref_dob)
	BOR_ADR(KEEP=BORR_ssn BOR_NAME street1_06 street2_06 ct_06 st_06 zip_06 EML_06 country_06
					ADR_VAL H_PHN H_PHN_VLD A_PHN A_PHN_VLD M_PHN M_PHN_VLD W_PHN W_PHN_VLD);
	INFILE EA27_IN DSD DLM = '10'x FIRSTOBS=1 MISSOVER END = EOF LRECL=32767;
	INPUT RECORD_TYP $ 1-2 @;

retain BORR_ssn BOR_NAME;

IF RECORD_TYP = '01' THEN do;
	INPUT BORR_ssn $ 3-11 dob $ 112-117 awardid $ 191-211 B_FIRST $ 167-178 B_MIDDLE $ 179-190 B_LAST $ 128-162  B_SUFFIX $ 163-166;
		BOR_NAME = TRIM(B_FIRST) || TRIM(' ' || B_MIDDLE) || ' ' || TRIM(B_LAST) || ' ' || B_SUFFIX;

	AWARD_ID = substr(awardid,1,18);
	AWARD_SEQUENCE_NUMBER = input(substr(awardid,19),8.0);
	output borr_dob;
END;

else IF RECORD_TYP = '05' THEN DO;
	INPUT ADR_VAL $ 47 H_PHN $ 13-22 H_PHN_VLD $ 33 A_PHN $ 23-32 A_PHN_VLD $ 34 
		M_PHN $ 220-229 M_PHN_VLD $ 230 W_PHN $ 231-240 W_PHN_VLD $ 241 ;
	RETAIN ADR_VAL H_PHN H_PHN_VLD A_PHN A_PHN_VLD M_PHN M_PHN_VLD W_PHN W_PHN_VLD;
END;

else IF RECORD_TYP = '06' THEN do;
	INPUT street1_06 $ 28-52 ALT_STR $ 3-27 ct_06 $ 53-68 st_06 $ 69-70 zip_06 $ 71-75
		EML_06 $ 81-130 country_06 $ 131-132 ;
	IF street1_06 = '' THEN street1_06 = ALT_STR;
	ELSE STREET2_06 = ALT_STR;
	OUTPUT BOR_ADR;
END;

else IF RECORD_TYP = '10' THEN do;
	INPUT OTHER_SSN $ 62-70 ref_dob $ 71-76 
		ref_last_name $ 77-111 ref_mid_name $ 128-139 ref_first_name $ 116-127 REF_SUFFIX $ 112-115;
		REF_NAME = TRIM(ref_first_name) || TRIM(' ' || ref_mid_name) || ' ' || TRIM(ref_last_name) || ' ' || REF_SUFFIX;
	RETAIN OTHER_SSN ref_dob ref_last_name ref_first_name ref_mid_name REF_NAME;
end;

else IF RECORD_TYP = '11' THEN do;
	INPUT REF_STREET_1 $ 06-33 REF_STREET_2 $ 82-109 REF_CITY $ 34-49 
		REF_STATE $ 50-51 REF_ZIP $ 52-56 REF_COUNTRY $ 110-111 REF_HOME_PHN $ 61-70 
		REF_ALT_PHN $ 71-80 REF_EML $ 112-171;
	output REF_ADR;
end;
RUN;

PROC SORT DATA=BOR_ADR NODUPREC; BY BORR_SSN; RUN;

PROC SQL;
CREATE TABLE R2 AS
SELECT DISTINCT DF_SPE_ACC_ID
	,BOR_NAME
	,DOB
FROM borr_dob A
INNER JOIN PD10 B
	ON A.BORR_SSN = B.DF_PRS_ID
WHERE MissCat IN ('A','B');
QUIT;

PROC SQL;
CREATE TABLE R3 AS
SELECT DISTINCT DF_SPE_ACC_ID
	,REF_NAME
	,DC_PRS_TYP
	,ref_dob
FROM REF_ADR A
INNER JOIN PD10 B
	ON A.other_ssn = B.DF_PRS_ID
WHERE DC_PRS_TYP IN ('E','S')
	AND MissCat IN ('A','B')
;
QUIT;

PROC SQL;
CREATE TABLE R4 AS
SELECT DISTINCT COALESCE(B.DF_SPE_ACC_ID,C.DF_SPE_ACC_ID) AS DF_SPE_ACC_ID
	,'B' AS DC_PRS_TYP
	,A.BOR_NAME
	,A.street1_06
	,A.street2_06
	,A.ct_06
	,A.st_06 
	,A.zip_06 
	,A.country_06
	,A.ADR_VAL 
	,A.H_PHN 
	,A.H_PHN_VLD 
	,A.A_PHN 
	,A.A_PHN_VLD 
	,A.M_PHN 
	,A.M_PHN_VLD 
	,A.W_PHN 
	,A.W_PHN_VLD 
	,A.EML_06
FROM BOR_ADR A
LEFT OUTER JOIN PD40 B
	ON B.DF_PRS_ID = A.BORR_ssn
LEFT OUTER JOIN PD30 C
	ON A.BORR_ssn = C.DF_PRS_ID
WHERE B.DF_SPE_ACC_ID IS NOT NULL OR C.DF_SPE_ACC_ID IS NOT NULL
UNION
SELECT DISTINCT COALESCE(B.DF_SPE_ACC_ID,C.DF_SPE_ACC_ID) AS DF_SPE_ACC_ID
	,COALESCE(B.DC_PRS_TYP,C.DC_PRS_TYP) AS DC_PRS_TYP
	,A.REF_NAME
	,A.REF_STREET_1
	,A.REF_STREET_2
	,A.REF_CITY
	,A.REF_STATE 
	,A.REF_ZIP 
	,A.REF_COUNTRY
	,''
	,A.REF_HOME_PHN 
	,''
	,A.REF_ALT_PHN 
	,''
	,''
	,''
	,''
	,''
	,A.REF_EML
FROM REF_ADR A
LEFT OUTER JOIN PD30 B
	ON B.DF_PRS_ID = A.other_ssn
LEFT OUTER JOIN PD40 C
	ON A.OTHER_SSN = C.DF_PRS_ID
WHERE (B.DF_SPE_ACC_ID IS NOT NULL and b.DC_PRS_TYP IN ('E','S'))
	OR (C.DF_SPE_ACC_ID IS NOT NULL and c.DC_PRS_TYP IN ('E','S'))
;
QUIT;

PROC SQL;
CREATE TABLE R5 AS
SELECT DISTINCT COALESCE(B.DF_PRS_ID,C.DF_PRS_ID) AS DF_SPE_ACC_ID
	,A.*
FROM REF_ADR A
LEFT OUTER JOIN PD30 B
	ON A.BORR_SSN = B.BF_SSN
	AND A.ref_first_name = B.DM_PRS_1
	AND A.ref_last_name = B.DM_PRS_LST
	and b.DC_PRS_TYP = 'R'
LEFT OUTER JOIN pd40 c
	ON A.BORR_SSN = C.BF_SSN
	AND A.ref_first_name = C.DM_PRS_1
	AND A.ref_last_name = C.DM_PRS_LST
	AND A.ref_mid_name = C.DM_PRS_MID
	and c.DC_PRS_TYP = 'R'
WHERE (B.BF_SSN IS NOT NULL OR C.BF_SSN IS NOT NULL)
;
QUIT;

PROC SQL;
CREATE TABLE R6 AS
SELECT DISTINCT DF_SPE_ACC_ID
	,DC_PRS_TYP
	,BOR_NAME
FROM borr_dob A
INNER JOIN PD10 B
	ON A.BORR_ssn = B.DF_PRS_ID
WHERE MissCat IN ('A','N')
union 
SELECT DISTINCT DF_SPE_ACC_ID
	,DC_PRS_TYP
	,REF_NAME
FROM REF_ADR A
INNER JOIN PD10 B
	ON A.other_ssn = B.DF_PRS_ID
WHERE DC_PRS_TYP IN ('E','S')
	AND MissCat IN ('A','N')
;
QUIT;

PROC SQL;
CREATE TABLE R7 AS
SELECT DISTINCT DF_PRS_ID
	,REF_NAME
FROM REF_ADR A
INNER JOIN PD10 B
	ON A.other_ssn = B.DF_PRS_ID
WHERE DC_PRS_TYP IN ('R')
	AND MissCat IN ('A','N');
QUIT;

proc format;
	value $per_typ
		'E' = 'Endorser'
		'B' = 'Borrower'
		'S' = 'Student';
	value $VALID
		'G' = 'Y'
		'B' = 'N';
quit;

DATA _NULL_;
SET R2 ;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Account Number,Name,Date of Birth";
PUT DF_SPE_ACC_ID BOR_NAME DOB;
RUN;
DATA _NULL_;
SET R3 ;
format dc_prs_typ $per_typ.;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Account Number,Name,Person Type,Date of Birth";
PUT DF_SPE_ACC_ID REF_NAME DC_PRS_TYP ref_dob;
RUN;

DATA _NULL_;
SET R4 ;
format dc_prs_typ $per_typ.;
format ADR_VAL H_PHN_VLD A_PHN_VLD M_PHN_VLD W_PHN_VLD $valid.;
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Account Number,Person Type,Name,Street 1,Street 2,City,State,Zip,Country,Address Validity"
	",Home Phone Number,Home Phone Validity,Alt Phone Number,Alt Phone Validity,Cell Phone,Cell Phone Validity"
	",Work Phone,Work Phone Validity,Email Address";
PUT DF_SPE_ACC_ID DC_PRS_TYP BOR_NAME street1_06 street2_06 ct_06 st_06 zip_06 country_06 ADR_VAL H_PHN H_PHN_VLD 
	A_PHN A_PHN_VLD M_PHN M_PHN_VLD W_PHN W_PHN_VLD EML_06;
RUN;

DATA _NULL_;
SET R5 ;
FILE REPORT5 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Account Number,Name,Street 1,Street 2,City,State,Zip,Country,Home Phone Number,Alt Phone Number,Email Address";
PUT DF_SPE_ACC_ID REF_NAME REF_STREET_1 REF_STREET_2 REF_CITY REF_STATE 
	REF_ZIP REF_COUNTRY REF_HOME_PHN REF_ALT_PHN REF_EML;
RUN;

DATA _NULL_;
SET R6 ;
format dc_prs_typ $per_typ.;
FILE REPORT6 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Account Number,Person Type,Name";
PUT DF_SPE_ACC_ID DC_PRS_TYP BOR_NAME ;
RUN;

DATA _NULL_;
SET R7 ;
FILE REPORT7 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Reference ID,Name";
PUT DF_PRS_ID REF_NAME ;
RUN;
