/*	Used by Accounting to reconcile refunds processed in each month
	and transactionS which are reversed for bad check, injured 
	spouse (state), and posting error.*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWA03.LWA03R2";
FILENAME REPORT2 "&RPTLIB/ULWA03.LWA03R3";*/

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*DATA FOR REVERSALS*/
CREATE TABLE REVERSE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.lc_trx_typ						AS TRXTYP,
		A.lc_rev_ind_typ					AS REVTYP,
		integer(A.bf_ssn)					as SSN,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
											AS NAME,
		A.ld_trx_eff						AS TRXEFF,
		A.ld_trx_adj						AS ADJDT,
		A.bd_trx_pst_hst 					AS POSTDT,
		SUM(A.la_apl_pri + A.la_apl_int + A.la_apl_leg_cst + 
		A.la_apl_oth_chr + A.la_apl_col_cst + A.la_ov_pay)
											AS POSTAMT,
		A.lf_usr_pst_txn					AS USRPST
FROM  OLWHRM1.DC11_LON_FAT A inner join OLWHRM1.PD01_PDM_INF D
	on A.bf_ssn = D.DF_PRS_ID
WHERE A.ld_trx_adj BETWEEN &BEGIN AND &END
	and A.lc_rev_ind_typ in ('BC','PE')
GROUP BY A.lc_trx_typ, A.lc_rev_ind_typ, A.ld_trx_eff, 
	A.bd_trx_pst_hst, A.ld_trx_adj, A.bf_ssn, A.lf_usr_pst_txn,
	RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
ORDER BY A.lc_trx_typ, A.lc_rev_ind_typ, A.bf_ssn
);

/*DATA FOR 001 AND 007 REFUNDS TIED TO THEIR REVERSALS*/
CREATE TABLE REF17 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.lc_dsb_typ						AS DSBTYP,
		A.lc_dsb_sta						AS DSBSTA,
		A.ld_rtn_psf						AS RTNDT,
		date(A.lf_crt_dts_fd01)				AS DTSFD01,
		integer(A.lf_prs_id)				AS SSN,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
											AS NAME,
		A.LA_DSB_AMT_PSF 					AS REFAMT,
		A.ld_pay_sus						AS SUSPDT,
		A.LC_RMT_TRX_TYP					AS RMTTYP,
CASE
		WHEN A.lc_dsb_typ <> '007' THEN
		B.LC_TRX_TYP
END																	AS TRXTYP,
CASE
		WHEN A.lc_dsb_typ <> '007' THEN
		B.BD_TRX_PST_HST					
END											AS REVPOSTDT,
CASE
		WHEN A.lc_dsb_typ <> '007' THEN
		(B.la_apl_pri + B.la_apl_int + B.la_apl_leg_cst + 
		B.la_apl_oth_chr + B.la_apl_col_cst + B.la_ov_pay) 
END											AS REVAMT
FROM  OLWHRM1.FD01_QUE_TAB A inner join OLWHRM1.PD01_PDM_INF D
	on A.lf_prs_id = D.DF_PRS_ID
	and A.ld_rtn_psf BETWEEN &BEGIN AND &END
	and A.lc_dsb_typ IN ('001','007')
left outer join OLWHRM1.DC11_LON_FAT B
	on a.lf_prs_id = b.bf_ssn
	and A.LD_RTN_PSF = B.LD_OV_PAY_RFD
ORDER BY A.lc_dsb_typ, A.ld_rtn_psf, A.lf_prs_id
);

/*DATA FOR THE REST OF THE REFUNDS (EXCEPT 011s)*/
CREATE TABLE REFGEN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.lc_dsb_typ						AS DSBTYP,
		A.lc_dsb_sta						AS DSBSTA,
		A.ld_rtn_psf						AS RTNDT,
		date(A.lf_crt_dts_fd01)				AS DTSFD01,
		integer(A.lf_prs_id)				AS SSN,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
											AS NAME,
		A.LA_DSB_AMT_PSF 					AS REFAMT,

		A.LD_RTN_PSF						AS REFDATE
FROM  OLWHRM1.FD01_QUE_TAB A inner join OLWHRM1.PD01_PDM_INF D
	on A.lf_prs_id = D.DF_PRS_ID
	and A.ld_rtn_psf BETWEEN &BEGIN AND &END
	and A.lc_dsb_typ not in ('001','007','011')
ORDER BY A.lc_dsb_typ, A.ld_rtn_psf, A.lf_prs_id
);
DISCONNECT FROM DB2;

DATA REFUND; SET REF17 REFGEN; RUN;
PROC SORT DATA = REFUND; BY DSBTYP TRXTYP RTNDT SSN; RUN;
endrsubmit  ;

DATA REFUND; SET WORKLOCL.REFUND; RUN;
DATA REVERSE; SET WORKLOCL.REVERSE; RUN;

*UNPRINTED SORT VARIABLE;
DATA REFUND;
SET REFUND;
TOTAL = DSBTYP||'  '||TRXTYP;
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS NOCENTER NODATE NUMBER PAGENO=1 LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=REFUND WIDTH=UNIFORM WIDTH=MIN N='COUNT:  ';
VAR DSBTYP DSBSTA RTNDT DTSFD01 SSN NAME REFAMT TRXTYP REVPOSTDT SUSPDT RMTTYP;
BY TOTAL;
SUM REFAMT;
LABEL DSBTYP="DISB TYPE" DSBSTA="DISB STATUS"
RTNDT="RETURN DATE" DTSFD01="CREATE DATE"
REFAMT="REFUND AMOUNT" TRXTYP="REVERSAL TYPE"
REVPOSTDT="REVERSAL POST DATE" SUSPDT="SUSPENSE DATE"
RMTTYP="REMITTANCE TYPE" TOTAL="TYPE";
FORMAT SSN SSN11. RTNDT MMDDYY8. DTSFD01 MMDDYY8.	
REFAMT COMMA12.2 REVPOSTDT MMDDYY8. SUSPDT MMDDYY8.;
TITLE 'Refund and Reversal Monthly Report';
TITLE2 "Refunds - &EFFDATE";
FOOTNOTE  'JOB = UTLWA03     REPORT = ULWA03.LWA03R2 (73)';
RUN;

*UNPRINTED SORT VARIABLE;
DATA REVERSE;
SET REVERSE;
TOTAL = TRXTYP||" & "||REVTYP;
RUN;

/*PROC PRINTTO PRINT=REPORT3;
RUN;*/
OPTIONS NOCENTER NODATE NUMBER PAGENO=1 LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=REVERSE WIDTH=UNIFORM WIDTH=MIN N='COUNT:  ';
VAR TRXTYP REVTYP SSN NAME TRXEFF ADJDT POSTDT POSTAMT USRPST;
SUM POSTAMT;
BY TOTAL;
LABEL TRXTYP="TRANS TYPE" REVTYP="REVERSAL TYPE"
TRXEFF="EFFECTIVE DATE" ADJDT="DATE TRANS ADJUSTED"
POSTDT="DATE POSTED" POSTAMT="AMOUNT POSTED" USRPST="POSTING AUTHORITY"
TOTAL = "TRANSACTION & REVERSAL TYPES";
FORMAT SSN SSN11. TRXEFF MMDDYY8. ADJDT MMDDYY8. POSTDT MMDDYY8.	
POSTAMT COMMA12.2;
TITLE 'Refund and Reversal Monthly Report';
TITLE2 "Reversals - &EFFDATE";
FOOTNOTE  'JOB = UTLWA03     REPORT = ULWA03.LWA03R3 (73)';
RUN;
