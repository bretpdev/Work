*-------------------------------------------------------------*
| UTLWS30 CSV - SPECIAL CAMPAIGN REPORTING INITIAL POPULATION |
*-------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*LIBNAME SAS_TAB V8 '/sas/whse/progrevw'; */
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS30.LWS30R2";
FILENAME REPORT3 "&RPTLIB/ULWS30.LWS30R3";
FILENAME REPORTZ "&RPTLIB/ULWS30.LWS30RZ";
DATA _NULL_;
	CUR_DT = TODAY();
	IF MONTH(CUR_DT) < 9 THEN DO;
		CALL SYMPUT('CRIT_DT',COMPRESS("'"||CATX('/','01','31',YEAR(CUR_DT)))||"'");
	END;
	ELSE DO;
		CALL SYMPUT('CRIT_DT',COMPRESS("'"||CATX('/','08','31',YEAR(CUR_DT)))||"'");
	END;
RUN;
%SYSLPUT CRIT_DT = &CRIT_DT;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
LIBNAME SAS_TAB V8 'Q:\Process Automation\TabSAS';
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CSVSCR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN  
	,A.LN_SEQ 
	,COALESCE(A.LA_CUR_PRI,0) AS LA_CUR_PRI
	,COALESCE(A.LA_NSI_OTS,0) AS LA_NSI_OTS
	,COALESCE(A.LA_CUR_PRI,0) +
	 COALESCE(A.LA_NSI_OTS,0) AS PIBAL
	,C.DF_SPE_ACC_ID
	,D.DC_ADR
	,D.DI_VLD_ADR
	,E.DC_PHN
	,E.DI_PHN_VLD
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR D
	ON C.DF_PRS_ID = D.DF_PRS_ID
INNER JOIN OLWHRM1.PD42_PRS_PHN E
	ON D.DF_PRS_ID = E.DF_PRS_ID
WHERE A.LC_STA_LON10 = 'R'
	AND DAYS(&CRIT_DT) - DAYS(B.LD_DLQ_OCC) BETWEEN 0 AND 30
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWS30.LWS30RZ);*/
/*QUIT;*/
PROC SORT DATA=CSVSCR OUT=ADR (KEEP=BF_SSN DC_ADR DI_VLD_ADR WHERE=(DC_ADR='L')) NODUPKEY;
	BY BF_SSN;
RUN; 
PROC SORT DATA=CSVSCR OUT=PHN (KEEP=BF_SSN DC_PHN DI_PHN_VLD ) NODUPKEY;
	BY BF_SSN DC_PHN;
RUN; 
PROC SORT DATA=CSVSCR (KEEP=BF_SSN LN_SEQ LA_CUR_PRI LA_NSI_OTS PIBAL DF_SPE_ACC_ID) NODUPKEY;
	BY BF_SSN LN_SEQ;
RUN; 
PROC SQL;
CREATE TABLE BLREP AS
	SELECT DISTINCT A.DF_SPE_ACC_ID
		,SUM(PIBAL) AS PIBAL
		,SUM(LA_CUR_PRI) AS LA_CUR_PRI
		,SUM(LA_NSI_OTS) AS LA_NSI_OTS
		,CASE 
			WHEN B.BF_SSN IS NULL THEN ' '
			ELSE B.DI_VLD_ADR
		 END AS DI_VLD_ADR
		,CASE 
			WHEN HMP.BF_SSN IS NULL THEN ' '
			ELSE HMP.DI_PHN_VLD
		 END AS PHOME
		,CASE 
			WHEN WKP.BF_SSN IS NULL THEN ' '
			ELSE WKP.DI_PHN_VLD
		 END AS PWORK
		,CASE 
			WHEN ALP.BF_SSN IS NULL THEN ' '
			ELSE ALP.DI_PHN_VLD
		 END AS PALT
	FROM CSVSCR A
	LEFT OUTER JOIN ADR B
		ON A.BF_SSN = B.BF_SSN
	LEFT OUTER JOIN PHN HMP
		ON A.BF_SSN = HMP.BF_SSN
		AND HMP.DC_PHN = 'H'
	LEFT OUTER JOIN PHN WKP
		ON A.BF_SSN = WKP.BF_SSN
		AND WKP.DC_PHN = 'W'
	LEFT OUTER JOIN PHN ALP
		ON A.BF_SSN = ALP.BF_SSN
		AND ALP.DC_PHN = 'A'
	GROUP BY A.DF_SPE_ACC_ID
	;
CREATE TABLE SCSUM AS
	SELECT COUNT(DISTINCT BF_SSN) AS TNB
		,SUM(LA_CUR_PRI) AS TOB
		,SUM(LA_NSI_OTS) AS TIB
	FROM CSVSCR A
;
QUIT;
ENDRSUBMIT;
DATA BLREP;
	SET WORKLOCL.BLREP;
RUN;
DATA SCSUM;
	SET WORKLOCL.SCSUM;
RUN;
DATA CSVSCR;
	SET WORKLOCL.CSVSCR;
RUN;
DATA SAS_TAB.UTLWS30_SDS;
	SET CSVSCR;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER PAGENO=1 DATE;
TITLE 'DELINQUENCY MGMT BORROWER-LEVEL STATIC INFORMATION';
FOOTNOTE   'JOB = UTLWS30  	 REPORT = ULWS30.LWS30R2';
PROC CONTENTS DATA=BLREP OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWS30  	 REPORT = ULWS30.LWS30R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=BLREP WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT PIBAL LA_CUR_PRI LA_NSI_OTS 14.2;
VAR DF_SPE_ACC_ID PIBAL LA_CUR_PRI LA_NSI_OTS DI_VLD_ADR PHOME PWORK PALT ;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	PIBAL = 'TOTAL/OUTSTANDING/BALANCE'
	LA_CUR_PRI = 'OUTSTANDING/PRINCIPAL'
	LA_NSI_OTS = 'OUTSTANDING/INTEREST'
	DI_VLD_ADR = 'ADDRESS/VALIDITY'
	PHOME = 'HOME PHONE/VALIDITY'
	PWORK = 'WORK PHONE/VALIDITY'
	PALT = 'ALTERNATE PHONE/VALIDITY'
	;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER PAGENO=1 DATE;
TITLE 'SPECIAL CAMPAIGN SUMMARY - STATIC INFORMATION';
FOOTNOTE   'JOB = UTLWS30  	 REPORT = ULWS30.LWS30R3';
PROC CONTENTS DATA=SCSUM OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWS30  	 REPORT = ULWS30.LWS30R3";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=SCSUM WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT TNB COMMA8. TOB TIB DOLLAR20.2;
VAR TNB TOB TIB ;
LABEL TNB = 'TOTAL # OF/BORROWERS'
	TOB = 'TOTAL OUTSTANDING/BALANCE'
	TIB = 'TOTAL INTEREST/BALANCE';
RUN;
PROC PRINTTO;
RUN;
