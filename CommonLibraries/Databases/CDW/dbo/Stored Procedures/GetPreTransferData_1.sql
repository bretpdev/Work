

CREATE PROCEDURE [dbo].[GetPreTransferData]
	@LetterId varchar(10)
AS

SELECT
	LT20.LT20_LETTER_REQUEST_ID [LetterRequestId],
	LT20.RM_DSC_LTR_PRC [LetterId],
	LT20.DF_SPE_ACC_ID [AccountNumber],
	LT20.RF_SBJ_PRC [Ssn],
	CASE WHEN PH05.[DI_CNC_ELT_OPI] = ' ' THEN 'N'
		 ELSE ISNULL(PH05.[DI_CNC_ELT_OPI], 'N')
	END [OnEcorr],
	ISNULL(PH05.DI_VLD_CNC_EML_ADR, 0) [ValidEcorrEmail],
	ISNULL(PH05.DX_CNC_EML_ADR, '') [Email],
	PD10.DM_PRS_1 [FirstName],
	PD10.DM_PRS_LST [LastName],
	PD30.DX_STR_ADR_1 [Address1],
	PD30.DX_STR_ADR_2 [Address2],
	PD30.DM_CT [City],
	PD30.DC_DOM_ST [State],
	PD30.DF_ZIP_CDE [Zip],
	PD30.DM_FGN_CNY[Country],
	PD30.DI_VLD_ADR [ValidAddress],
	PS.LoanSaleStatus,
	PS.RegionDeconversion,
	PS.SellingOwnerId,
	PS.TransferDate,
	PS.DelayCancelCode,
	LN83.LC_STA_LN83 [AchStatus],
	LN83.LC_EFT_SUS_REA [AchSuspensionReason],
	LN90.LD_FAT_EFF [LastPaymentDate],
	LN94.LC_RMT_BCH_SRC_IPT [LastPaymentSource],
	RM30.LC_RMT_PAY_SRC [LastPaymentSubSource],
	LN65.DUE_DAY [DueDay]
FROM
	LT20_LetterRequests LT20
	LEFT JOIN PD10_PRS_NME PD10 ON PD10.DF_PRS_ID = LT20.RF_SBJ_PRC
	LEFT JOIN PD30_PRS_ADR PD30 ON PD30.DF_PRS_ID = PD10.DF_PRS_ID
	LEFT JOIN PH05_CNC_EML PH05 ON PH05.DF_SPE_ID = PD10.DF_SPE_ACC_ID
	LEFT JOIN 
	(--Presale info
		SELECT DISTINCT
			COALESCE(WK1J.BF_SSN, LN99.BF_SSN, 'Borrower Ssn Not Found') [BF_SSN],
			COALESCE(WK1J.IF_LON_SLE, LN99.IF_LON_SLE, 'Loan Sale Not Found') [IF_LON_SLE],
			OW30.IC_LON_SLE_STA  AS [LoanSaleStatus],
			OW30.IC_RGN_RCV_DCV_LON AS [RegionDeconversion],
			OW30.IF_SLL_OWN AS [SellingOwnerId],
			OW30.ID_LON_SLE AS [TransferDate],
			OW30.IC_DLA_CAN_LTR AS [DelayCancelCode]
		FROM
			WK1J_LON_SLE_PLR WK1J
			LEFT JOIN LN99_LON_SLE_FAT LN99 ON WK1J.BF_SSN = LN99.BF_SSN AND LN99.LN_SEQ = WK1J.LN_SEQ
			LEFT JOIN OW30_LON_SLE_CTL OW30 ON OW30.IF_LON_SLE = COALESCE(WK1J.IF_LON_SLE, LN99.IF_LON_SLE, 'Loan Sale Not Found')
		WHERE
			ISNULL(LN99.LF_LST_DTS_LN99, GETDATE()) > DATEADD(DAY, -14, GETDATE())
	) PS ON PS.BF_SSN = PD10.DF_PRS_ID
	LEFT JOIN 
	(
		SELECT
			LC_STA_LN83,
			LC_EFT_SUS_REA,
			BF_SSN,
			LN_SEQ,
			LD_EFT_EFF_BEG,
			ROW_NUMBER() OVER(PARTITION BY BF_SSN ORDER BY BF_SSN, LD_EFT_EFF_BEG DESC) [MaxFlag]
		FROM
			LN83_EFT_TO_LON
		WHERE
			LD_EFT_EFF_BEG <= GETDATE()
			AND 
			ISNULL(LD_EFT_EFF_END, DATEADD(DAY, 1, GETDATE())) > GETDATE() --If we don't know what the date is, then the date is not set so use tomorrow's date
		) LN83 ON LT20.RF_SBJ_PRC = LN83.BF_SSN	AND LN83.MaxFlag = 1
	LEFT JOIN
	(
		SELECT
			LN90.BF_SSN,
			LD_FAT_EFF,
			LN_SEQ,
			LN_FAT_SEQ,
			ROW_NUMBER() OVER (PARTITION BY LN90.BF_SSN ORDER BY LD_FAT_EFF DESC) [MaxFlag]
		FROM
			LN90_FIN_ATY LN90
		WHERE
			LC_STA_LON90 = 'A'
			AND (LC_FAT_REV_REA = '' OR LC_FAT_REV_REA IS NULL)
			AND PC_FAT_TYP = '10'
			AND PC_FAT_SUB_TYP = '10'
	) LN90 ON LN90.BF_SSN = PD10.DF_PRS_ID AND LN90.MaxFlag = 1
	LEFT JOIN LN94_LON_PAY_FAT LN94 ON LT20.RF_SBJ_PRC = LN94.BF_SSN AND LN90.LN_SEQ = LN94.LN_SEQ AND LN90.LN_FAT_SEQ = LN94.LN_FAT_SEQ
	LEFT JOIN RM30_BR_RMT RM30 ON LN94.LD_RMT_BCH_INI = RM30.LD_RMT_BCH_INI
		AND LN94.LC_RMT_BCH_SRC_IPT = RM30.LC_RMT_BCH_SRC_IPT
		AND LN94.LN_RMT_BCH_SEQ = RM30.LN_RMT_BCH_SEQ
		AND LN94.LN_RMT_SEQ = RM30.LN_RMT_SEQ
		AND LN94.LN_RMT_ITM = RM30.LN_RMT_ITM
		AND LN94.LN_RMT_ITM_SEQ = RM30.LN_RMT_ITM_SEQ
		AND LT20.RF_SBJ_PRC = RM30.BF_SSN
	LEFT JOIN LN65_RepaymentSched LN65 ON LN65.DF_SPE_ACC_ID = LT20.DF_SPE_ACC_ID
		AND LN65.LN_SEQ = LN90.LN_SEQ
WHERE
    LT20.RM_DSC_LTR_PRC = @LetterId
    AND LT20.PrintedAt IS NULL
    AND LT20.InactivatedAt IS NULL 
RETURN 0