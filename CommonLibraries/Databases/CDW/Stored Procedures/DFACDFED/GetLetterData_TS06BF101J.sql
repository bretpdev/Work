
CREATE PROCEDURE [dbo].[GetLetterData_TS06BF101J]
	@AccountNumber char(10),
	@Today DATETIME = NULL
AS

DECLARE	@LetterId varchar(10) = 'TS06BF101J'
DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

SELECT
	CASE 
		WHEN forb.LC_FOR_STA = 'A' and forb.LD_CRT_REQ_FOR = forb.LF_LST_DTS_FB10 THEN 'Approved'
		WHEN forb.LC_FOR_STA = 'A' and forb.LD_CRT_REQ_FOR != forb.LF_LST_DTS_FB10 THEN 'Adjusted'
		ELSE 'Denied'
	END as [Action],
	COALESCE(FTD.Label,forb.LC_FOR_TYP) as [Type],
	CONVERT(VARCHAR(10),forb.LD_FOR_BEG,101) as [Begin Date],
	CONVERT(VARCHAR(10),forb.LD_FOR_END,101) as [End Date],
	LN10.LN_SEQ AS [Loan Sequence],
	COALESCE(FTL.Label,LN10.IC_LON_PGM) as [Loan Program]
FROM 
	CDW..PD10_PRS_NME PD10
	INNER JOIN CDW.dbo.LN10_lON LN10 ON LN10.BF_SSN = PD10.DF_PRS_ID
	INNER JOIN
	(
		SELECT
			LN85.BF_SSN,
			LN85.LN_SEQ
		FROM
			CDW..LN85_LON_ATY LN85
			INNER JOIN --GETS THE MOST RECENT ARC LEFT ON THE BORROWERS ACCOUNT TO GET THE LOAN SEQ'S THE LETTER APPLIES TO
			(
				SELECT
					AY10.BF_SSN,
					MAX(AY10.LN_ATY_SEQ) AS LN_ATY_SEQ,
					MAX(AY10.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
				FROM 
					CDW..AY10_BR_LON_ATY AY10
				WHERE
					AY10.PF_REQ_ACT = @PF_REQ_ACT
				GROUP BY
					AY10.BF_SSN
			)AY10
				ON AY10.BF_SSN = LN85.BF_SSN
				AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
	)LN85
		ON LN85.BF_SSN = LN10.BF_SSN
		AND LN85.LN_SEQ = LN10.LN_SEQ
	INNER JOIN
	(
		SELECT 
			FB10.LC_FOR_TYP,
			LN60.LD_FOR_BEG,
			LN60.LD_FOR_END,
			FB10.LD_CRT_REQ_FOR,
			FB10.LF_LST_DTS_FB10,
			FB10.LC_FOR_STA,
			LN60.BF_SSN,
			LN60.LN_SEQ
		FROM
			CDW..FB10_BR_FOR_REQ FB10
			INNER JOIN CDW..LN60_BR_FOR_APV LN60
				ON LN60.BF_SSN = FB10.BF_SSN
				AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
			INNER JOIN 
			(
				SELECT
					MAX(LN60.LD_STA_LON60) AS MaxLN60,
					BF_SSN,
					LN_SEQ
				FROM
					CDW..LN60_BR_FOR_APV LN60
				WHERE
					LN60.LC_STA_LON60 = 'A'
				GROUP BY
					LN60.BF_SSN,
					LN60.LN_SEQ
			) LN60Max
				ON LN60.BF_SSN = LN60Max.BF_SSN
				AND LN60.LN_SEQ = LN60Max.LN_SEQ
				AND LN60Max.MaxLN60 = LN60.LD_STA_LON60
		WHERE
			LN60.LC_STA_LON60 = 'A'
			AND FB10.LC_STA_FOR10 = 'A'
			--AND FB10.LC_FOR_STA = 'A' removing this so denied records will pick up
	) Forb
		ON Forb.BF_SSN = LN10.BF_SSN
		AND Forb.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN CDW..FormatTranslation FTD
		ON FTD.Start = Forb.LC_FOR_TYP
		AND FTD.FmtName = '$FORSTA'
	LEFT JOIN CDW..FormatTranslation FTL
		ON FTL.Start = LN10.IC_LON_PGM
		AND FTL.FmtName = '$LNPROG'
WHERE
	PD10.DF_SPE_ACC_ID = @AccountNumber
