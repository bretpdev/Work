CREATE PROCEDURE [dbo].[LoanDetail_Forb_Delete]
	@LetterId varchar(10),
	@BF_SSN  CHAR(9)
AS

DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

SELECT
	'Delete' AS LetterAction,
	Forb.LC_FOR_TYP AS LetterTypeCode,
	CONVERT(VARCHAR(10),Forb.LD_FOR_BEG,101) AS BeginDate,
	CONVERT(VARCHAR(10),Forb.LD_FOR_END,101) AS EndDate,
	LN10.LN_SEQ,
	ISNULL(FT.Label, LN10.IC_LON_PGM) AS LoanProgram
FROM
	UDW..LN10_LON LN10
	LEFT JOIN UDW..FormatTranslation FT
		ON FT.Start = LN10.IC_LON_PGM
	INNER JOIN
	(
		SELECT
			LN85.BF_SSN,
			LN85.LN_SEQ
		FROM
			UDW..LN85_LON_ATY LN85
			INNER JOIN --GETS THE MOST RECENT ARC LEFT ON THE BORROWERS ACCOUNT TO GET THE LOAN SEQ'S THE LETTER APPLIES TO
			(
				SELECT
					AY10.BF_SSN,
					AY10.LN_ATY_SEQ,
					MAX(AY10.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
				FROM 
					UDW..AY10_BR_LON_ATY AY10
				WHERE
					AY10.PF_REQ_ACT = @PF_REQ_ACT
				GROUP BY
					AY10.BF_SSN,
					AY10.LN_ATY_SEQ
			)AY10
				ON AY10.BF_SSN = LN85.BF_SSN
				AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
	)LN85
		ON LN85.BF_SSN = LN10.BF_SSN
		AND LN85.LN_SEQ = LN10.LN_SEQ
	INNER JOIN
	(
		SELECT 
			FB10.LC_FOR_TYP,
			LN60.LD_FOR_BEG,
			LN60.LD_FOR_END,
			FB10.LD_CRT_REQ_FOR,
			FB10.LF_LST_DTS_FB10,
			LN60.BF_SSN,
			LN60.LN_SEQ
		FROM
			UDW..FB10_BR_FOR_REQ FB10
			INNER JOIN UDW..LN60_BR_FOR_APV LN60
				ON LN60.BF_SSN = FB10.BF_SSN
				AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
			INNER JOIN 
			(
				SELECT
					MAX(LN60.LD_STA_LON60) AS MaxLN60,
					BF_SSN,
					LN_SEQ
				FROM
					UDW..LN60_BR_FOR_APV LN60
				GROUP BY
					LN60.BF_SSN,
					LN60.LN_SEQ
			) LN60Max
				ON LN60.BF_SSN = LN60Max.BF_SSN
				AND LN60.LN_SEQ = LN60Max.LN_SEQ
				AND LN60Max.MaxLN60 = LN60.LD_STA_LON60
		WHERE
			LN60.LC_STA_LON60 = 'A'
			AND FB10.LC_STA_FOR10 = 'I'
			AND FB10.LC_FOR_STA = 'A'
	) Forb
		ON Forb.BF_SSN = LN10.BF_SSN
		AND Forb.LN_SEQ = LN10.LN_SEQ

WHERE
	LN10.BF_SSN = @BF_SSN
ORDER BY 
	LN10.LN_SEQ