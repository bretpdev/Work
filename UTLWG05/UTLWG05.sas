/* UTLWG05 - LOANS GUARANTEED
Lists the number and net amount of loans guaranteed during the previous fiscal year by loan type.
One report is grouped by school and the other is grouped by lender.*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWG05.LWG05R2";
FILENAME REPORT3 "&RPTLIB/ULWG05.LWG05R3";
FILENAME REPORT4 "&RPTLIB/ULWG05.LWG05R4";*/

DATA _NULL_;
BEGDATE = PUT(INTNX('YEAR.7',TODAY(),-1), YYMMDD10.);	/*RESOLVES TO 1ST OF PRIOR MONTH*/
ENDDATE = PUT(INTNX('YEAR.7',TODAY(),0) - 1 , YYMMDD10.);	/*RESOLVES TO END OF PRIOR MONTH*/
DATE = PUT(INTNX('YEAR.7',TODAY(),0) - 1, MMDDYY10.);
CALL SYMPUT('BEGIN',"'"||BEGDATE||"'");	            /*CREATES MACRO VARIABLE WITH IN FORMAT  'YYYY/MM/DD'*/
CALL SYMPUT('END',"'"||ENDDATE||"'");              	/* WILL BE USED AS REPLACEMENTS IN CODE*/
CALL SYMPUT('EFFDATE',DATE);
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
OPTIONS MPRINT SYMBOLGEN;

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE YRGUARS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.AF_APL_ID||A.AF_APL_ID_SFX							AS CLID
	,B.AF_CUR_APL_OPS_LDR									AS LENDER
	,D.IM_IST_FUL											AS LNAME
	,B.AF_APL_OPS_SCL										AS SCHOOL
	,C.IM_IST_FUL											AS SNAME
	,C.IC_GEN_ST
	,A.AC_LON_TYP											AS TYPE
	,A.AA_GTE_LON_AMT
	-A.AA_TOT_RFD
	-(COALESCE(A.AA_TOT_CAN,0))								AS NETAMT
FROM OLWHRM1.GA10_LON_APP A 
INNER JOIN OLWHRM1.GA01_APP B 
	ON A.AF_APL_ID = B.AF_APL_ID 
	AND	A.AC_PRC_STA = 'A' 
INNER JOIN 
	(SELECT DISTINCT
	IF_IST
	,IM_IST_FUL
	,IC_GEN_ST
	FROM OLWHRM1.SC01_LGS_SCL_INF) C 
	ON B.AF_APL_OPS_SCL = C.IF_IST 
INNER JOIN OLWHRM1.LR01_LGS_LDR_INF D 
	ON B.AF_CUR_APL_OPS_LDR = D.IF_IST
	/*ON A.AF_CUR_LON_OPS_LDR = D.IF_IST <1>*/

WHERE A.AC_LON_TYP IN ('SF','SU','PL')
AND A.AD_PRC BETWEEN &BEGIN AND &END 
AND ((A.AA_TOT_CAN IS NULL 
	AND A.AA_GTE_LON_AMT - A.AA_TOT_RFD > 0)
	OR
	(A.AA_TOT_CAN IS NOT NULL 
	AND A.AA_GTE_LON_AMT - A.AA_TOT_RFD - A.AA_TOT_CAN > 0))
);
DISCONNECT FROM DB2;

ENDRSUBMIT;
DATA YRGUARS;
SET WORKLOCL.YRGUARS;
RUN;

/*SCHOOL REPORTING*/
PROC SQL;
CREATE TABLE SUMS AS
SELECT	SNAME,
		SCHOOL,
		IC_GEN_ST,
		TYPE,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM YRGUARS
GROUP BY SCHOOL, SNAME, IC_GEN_ST, TYPE
ORDER BY SNAME, SCHOOL, IC_GEN_ST, TYPE;
QUIT;

PROC TRANSPOSE DATA=SUMS OUT=SUMS1 (DROP=_NAME_ RENAME=(PL=PLNO SF=SFNO SU=SUNO));
VAR NUMBER;
BY SNAME SCHOOL IC_GEN_ST;
ID TYPE;
RUN;
PROC TRANSPOSE DATA=SUMS OUT=SUMS2 (DROP=_NAME_ RENAME=(PL=PLAMT SF=SFAMT SU=SUAMT));
VAR AMOUNT;
BY SNAME SCHOOL IC_GEN_ST;
ID TYPE;
RUN;

DATA SUMS;
MERGE SUMS1 SUMS2;
BY SNAME SCHOOL;
TTLNO = SUM(PLNO,SFNO,SUNO);
TTLAMT = SUM(PLAMT,SFAMT,SUAMT);
IF SFNO = . THEN SFNO = 0;
IF SUNO = . THEN SUNO = 0;
IF PLNO = . THEN PLNO = 0;
IF SFAMT = . THEN SFAMT = 0;
IF SUAMT = . THEN SUAMT = 0;
IF PLAMT = . THEN PLAMT = 0;
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 NUMBER NODATE CENTER PAGENO=1;
PROC REPORT DATA = SUMS NOWD HEADLINE HEADSKIP SPACING = 1 SPLIT = '/';
WHERE IC_GEN_ST = 'UT';

TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE FISCAL YEAR ENDING &EFFDATE";
TITLE3	'BY UTAH SCHOOL';
FOOTNOTE	'JOB = UTLWG05     REPORT = ULWG05.LWG05R2';
COLUMN	SCHOOL	SNAME	SFNO	SFAMT	SUNO	SUAMT	PLNO
PLAMT	TTLNO	TTLAMT;
DEFINE 	SCHOOL / DISPLAY 'SCHOOL ID' WIDTH = 9;
DEFINE	SNAME / DISPLAY 'SCHOOL NAME' WIDTH = 35;
DEFINE	SFAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	SFNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	SUAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SUNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	PLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	PLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	TTLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	TTLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;

COMPUTE BEFORE _PAGE_ / CENTER;
LINE @57  'STAFFORD' @78 'STAFFORD';
LINE @56 'SUBSIDIZED' @76 'UNSUBSIDIZED' @99 'PLUS'  @119 'TOTAL';
ENDCOMP;

RBREAK AFTER / OL SUMMARIZE PAGE;
RUN;

/*PROC PRINTTO PRINT=REPORT3;
RUN;*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 NUMBER NODATE CENTER PAGENO=1;
PROC REPORT DATA = SUMS NOWD HEADLINE HEADSKIP SPACING = 1 SPLIT = '/';
WHERE IC_GEN_ST NE 'UT';

TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE FISCAL YEAR ENDING &EFFDATE";
TITLE3	'BY OUT OF STATE SCHOOL';
FOOTNOTE	'JOB = UTLWG05     REPORT = ULWG05.LWG05R3';
COLUMN	SCHOOL	SNAME	SFNO	SFAMT	SUNO	SUAMT	PLNO
PLAMT	TTLNO	TTLAMT;
DEFINE 	SCHOOL / DISPLAY 'SCHOOL ID' WIDTH = 9;
DEFINE	SNAME / DISPLAY 'SCHOOL NAME' WIDTH = 35;
DEFINE	SFAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	SFNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	SUAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SUNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	PLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	PLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	TTLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	TTLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;

COMPUTE BEFORE _PAGE_ / CENTER;
LINE @57  'STAFFORD' @78 'STAFFORD';
LINE @56 'SUBSIDIZED' @76 'UNSUBSIDIZED' @99 'PLUS'  @119 'TOTAL';
ENDCOMP;

RBREAK AFTER / OL SUMMARIZE PAGE;
RUN;

/*LENDER REPORTING*/
PROC SQL;
CREATE TABLE SUML AS
SELECT	LNAME,
		LENDER,
		TYPE,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	YRGUARS
GROUP BY LENDER, LNAME, TYPE
ORDER BY LNAME, LENDER, TYPE;
QUIT;

PROC TRANSPOSE DATA=SUML OUT=SUML1 (DROP=_NAME_ RENAME=(PL=PLNO SF=SFNO SU=SUNO));
VAR NUMBER;
BY LNAME LENDER;
ID TYPE;
RUN;
PROC TRANSPOSE DATA=SUML OUT=SUML2 (DROP=_NAME_ RENAME=(PL=PLAMT SF=SFAMT SU=SUAMT));
VAR AMOUNT;
BY LNAME LENDER;
ID TYPE;
RUN;

DATA SUML;
MERGE SUML1 SUML2;
BY LNAME LENDER;
TTLNO = SUM(PLNO,SFNO,SUNO);
TTLAMT = SUM(PLAMT,SFAMT,SUAMT);
IF SFNO = . THEN SFNO = 0;
IF SUNO = . THEN SUNO = 0;
IF PLNO = . THEN PLNO = 0;
IF SFAMT = . THEN SFAMT = 0;
IF SUAMT = . THEN SUAMT = 0;
IF PLAMT = . THEN PLAMT = 0;
RUN;

/*PROC PRINTTO PRINT=REPORT4;
RUN;*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 NUMBER NODATE CENTER PAGENO=1;
PROC REPORT DATA = SUML NOWD HEADLINE HEADSKIP SPACING = 1 SPLIT = '/';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE FISCAL YEAR ENDING &EFFDATE";
TITLE3	'BY LENDER';
FOOTNOTE	'JOB = UTLWG05     REPORT = ULWG05.LWG05R4';
COLUMN	LENDER	LNAME	SFNO	SFAMT	SUNO	SUAMT	PLNO
PLAMT	TTLNO	TTLAMT;
DEFINE 	LENDER / DISPLAY 'LENDER ID' WIDTH = 9;
DEFINE	LNAME / DISPLAY 'LENDER NAME' WIDTH = 35;
DEFINE	SFAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	SFNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	SUAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SUNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	PLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	PLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;
DEFINE	TTLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	TTLNO / FORMAT = COMMA7. 'NO' WIDTH = 7;

COMPUTE BEFORE _PAGE_ / CENTER;
LINE @57  'STAFFORD' @78 'STAFFORD';
LINE @56 'SUBSIDIZED' @76 'UNSUBSIDIZED' @99 'PLUS'  @119 'TOTAL';
ENDCOMP;

RBREAK AFTER / OL SUMMARIZE PAGE;
RUN;

*<1> sr202, jd, 12/19/02;

/*TESTFILE;
DATA YR1;
SET YRGUARS (FIRSTOBS=1 OBS=50000);
RUN;
DATA YR2;
SET YRGUARS (FIRSTOBS=50001);
RUN;
PROC EXPORT DATA= YR1
            OUTFILE= "T:\SAS\YRGUARS1.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;
PROC EXPORT DATA= YR2
            OUTFILE= "T:\SAS\YRGUARS2.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;
*/
