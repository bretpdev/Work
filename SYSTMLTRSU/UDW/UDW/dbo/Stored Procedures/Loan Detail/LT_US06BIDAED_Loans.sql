CREATE PROCEDURE [dbo].[LT_US06BIDAED_Loans]
	@LetterId VARCHAR(10),
	@BF_SSN VARCHAR(9),
	@IsCoborrower BIT = 0,
	@RN_ATY_SEQ_PRC INT
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

IF @IsCoborrower = 0
	BEGIN
		SELECT
			COALESCE(FT.label, LN10.IC_LON_PGM, '') AS [Loan Program],
			CONVERT(VARCHAR, LN10.LD_LON_1_DSB, 101) AS [Date Disbursed],
			LN72.LR_ITR AS [Interest],
			LN10.LA_LON_AMT_GTR AS [Original Principal],
			LN10.LA_CUR_PRI AS [Current Principal]
		FROM 
			LN10_LON LN10
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN AY10_BR_LON_ATY AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
				WHERE   
					AY10.PF_REQ_ACT = @PF_REQ_ACT
					AND AY10.BF_SSN = @BF_SSN
					AND AY10.LN_ATY_SEQ = @RN_ATY_SEQ_PRC
					--Active flag ignored, as LT20 provides the exact record that is tied to this request
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ 
			INNER JOIN 
			(
				SELECT
					LN72.BF_SSN,
					LN72.LN_SEQ,
					LN72.LR_ITR,
					ROW_NUMBER() OVER (PARTITION BY LN72.BF_SSN, LN72.LN_SEQ, LN72.LD_ITR_EFF_BEG, LN72.LD_ITR_EFF_END ORDER BY LD_STA_LON72 DESC) AS SEQ
				FROM
					LN72_INT_RTE_HST LN72
				WHERE
					LN72.LC_STA_LON72 = 'A'
					AND	CAST(GETDATE() AS DATE) BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
					AND	LN72.BF_SSN = @BF_SSN
			) LN72
				ON LN10.LN_SEQ = LN72.LN_SEQ
				AND LN10.BF_SSN = LN72.BF_SSN
				AND LN72.SEQ = 1
			LEFT JOIN FormatTranslation FT
				ON LN10.IC_LON_PGM = FT.Start
				AND FT.FmtName = '$LNPROG'
		WHERE 
			LN10.BF_SSN = @BF_SSN
	END
ELSE
	BEGIN
		SELECT
			COALESCE(FT.label, LN10.IC_LON_PGM, '') AS [Loan Program],
			CONVERT(VARCHAR, LN10.LD_LON_1_DSB, 101) AS [Date Disbursed],
			LN72.LR_ITR AS [Interest],
			LN10.LA_LON_AMT_GTR AS [Original Principal],
			LN10.LA_CUR_PRI AS [Current Principal]
		FROM 
			LN10_LON LN10
			INNER JOIN LN20_EDS LN20
				ON LN20.BF_SSN = LN10.BF_SSN
				AND LN20.LN_SEQ = LN10.LN_SEQ
				AND LN20.LC_STA_LON20 = 'A'
				AND LN20.LC_EDS_TYP = 'M'
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN AY10_BR_LON_ATY AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
					INNER JOIN LN20_EDS LN20
						ON LN20.BF_SSN = LN85.BF_SSN
						AND LN20.LN_SEQ = LN85.LN_SEQ
						AND LN20.LC_STA_LON20 = 'A'
						AND LN20.LC_EDS_TYP = 'M'
				WHERE   
					AY10.PF_REQ_ACT = @PF_REQ_ACT
					AND LN20.LF_EDS = @BF_SSN
					AND AY10.LN_ATY_SEQ = @RN_ATY_SEQ_PRC
					--Active flag ignored, as LT20 provides the exact record that is tied to this request
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ 
			INNER JOIN 
			(
				SELECT
					LN72.BF_SSN,
					LN72.LN_SEQ,
					LN72.LR_ITR,
					ROW_NUMBER() OVER (PARTITION BY LN72.BF_SSN, LN72.LN_SEQ, LN72.LD_ITR_EFF_BEG, LN72.LD_ITR_EFF_END ORDER BY LD_STA_LON72 DESC) AS SEQ
				FROM
					LN72_INT_RTE_HST LN72
					INNER JOIN LN20_EDS LN20
						ON LN20.BF_SSN = LN72.BF_SSN
						AND LN20.LN_SEQ = LN72.LN_SEQ
						AND LN20.LC_STA_LON20 = 'A'
						AND LN20.LC_EDS_TYP = 'M'
				WHERE
					LN72.LC_STA_LON72 = 'A'
					AND	CAST(GETDATE() AS DATE) BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
					AND	LN20.LF_EDS = @BF_SSN
			) LN72
				ON LN10.LN_SEQ = LN72.LN_SEQ
				AND LN10.BF_SSN = LN72.BF_SSN
				AND LN72.SEQ = 1
			LEFT JOIN FormatTranslation FT
				ON LN10.IC_LON_PGM = FT.Start
				AND FT.FmtName = '$LNPROG'
		WHERE 
			LN20.LF_EDS = @BF_SSN
	END
END