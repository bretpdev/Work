CREATE PROCEDURE [dbo].[LT_US06BIDRFS_Loans]
	@BF_SSN CHAR(9),
	@IsCoborrower BIT = 0,
	@RN_ATY_SEQ_PRC INT
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE	@LetterId VARCHAR(10) = 'US06BIDRFS'
DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

IF @IsCoborrower = 0
	BEGIN
		SELECT
			ISNULL(FMT.Label, LN10.IC_LON_PGM) AS [Loan Program],
			CONVERT(VARCHAR(10), LN10.LD_LON_1_DSB, 101) AS [Disbursement Date],
			LN10.LA_CUR_PRI AS [Current Principal Balance],
			(LNPC.LN_PAY_PSC_QLF_PAY + LNPC.LN_PAY_PRE_QLF_PAY) AS [QualPay]
		FROM
			LN10_LON LN10
			INNER JOIN LNPC_LFG_PAY_DTL LNPC
				ON LNPC.BF_SSN = LN10.BF_SSN
				AND LNPC.LN_SEQ = LN10.LN_SEQ
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN AY10_BR_LON_ATY AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
				WHERE   
					AY10.PF_REQ_ACT = @PF_REQ_ACT
					AND AY10.BF_SSN = @BF_SSN
					AND AY10.LN_ATY_SEQ = @RN_ATY_SEQ_PRC
					--Active flag ignored, as LT20 provides the exact record that is tied to this request
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ 
			LEFT JOIN FormatTranslation FMT
				ON FMT.FmtName = '$LNPROG'
				AND FMT.Start = LN10.IC_LON_PGM
			WHERE
				LN10.BF_SSN = @BF_SSN

	END
ELSE
	BEGIN
		SELECT
			ISNULL(FMT.Label, LN10.IC_LON_PGM) AS [Loan Program],
			CONVERT(VARCHAR(10), LN10.LD_LON_1_DSB, 101) AS [Disbursement Date],
			LN10.LA_CUR_PRI AS [Current Principal Balance],
			(LNPC.LN_PAY_PSC_QLF_PAY + LNPC.LN_PAY_PRE_QLF_PAY) AS [QuaPay]
		FROM
			LN20_EDS LN20
			INNER JOIN LN10_LON LN10
				ON LN10.BF_SSN = LN20.BF_SSN
				AND LN10.LN_SEQ = LN20.LN_SEQ
			LEFT JOIN LNPC_LFG_PAY_DTL LNPC
				ON LNPC.BF_SSN = LN10.BF_SSN
				AND LNPC.LN_SEQ = LN10.LN_SEQ
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN AY10_BR_LON_ATY AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
					INNER JOIN LN20_EDS LN20
						ON LN20.BF_SSN = LN85.BF_SSN
						AND LN20.LN_SEQ = LN85.LN_SEQ
						AND LN20.LC_STA_LON20 = 'A'
						AND LN20.LC_EDS_TYP = 'M'
				WHERE   
					AY10.PF_REQ_ACT = @PF_REQ_ACT
					AND LN20.LF_EDS = @BF_SSN
					AND AY10.LN_ATY_SEQ = @RN_ATY_SEQ_PRC
					--Active flag ignored, as LT20 provides the exact record that is tied to this request
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ 
			LEFT JOIN FormatTranslation FMT
				ON FMT.FmtName = '$LNPROG'
				AND FMT.Start = LN10.IC_LON_PGM
			WHERE
			LN20.LF_EDS = @BF_SSN 
			AND LN20.LC_EDS_TYP = 'M'
			AND LN20.LC_STA_LON20 = 'A'
	END
END