*------------------------------------------*
| UTLWU29 QC - Reduced Payment Forbearance |
*------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU29.LWU29R2";
FILENAME REPORT3 "&RPTLIB/ULWU29.LWU29R3";
FILENAME REPORT4 "&RPTLIB/ULWU29.LWU29R4";
FILENAME REPORTZ "&RPTLIB/ULWU29.LWU29RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE QCRPF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,B.LF_FOR_CTL_NUM
	,B.LN_FOR_OCC_SEQ
	,A.LA_CUR_PRI
	,E.LR_ITR
	,D.DF_SPE_ACC_ID
	,B.LD_FOR_BEG
	,B.LD_FOR_END
	,B.LD_STA_LON60
	,COALESCE(B.LA_ACL_RDC_PAY,0) AS LA_ACL_RDC_PAY
	,F.LD_DLQ_OCC
	,DAYS(CURRENT DATE) - DAYS(F.LD_DLQ_OCC) AS DAYS_DELQ
	,G.BN_EFT_SEQ
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN60_BR_FOR_APV B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.FB10_BR_FOR_REQ C
	ON B.BF_SSN = C.BF_SSN
	AND B.LF_FOR_CTL_NUM = C.LF_FOR_CTL_NUM
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.LN72_INT_RTE_HST E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
	AND E.LC_STA_LON72 = 'A'
	AND B.LD_STA_LON60 >= E.LD_ITR_EFF_BEG
	AND B.LD_STA_LON60 <= E.LD_ITR_EFF_END
LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST F
	ON A.BF_SSN = F.BF_SSN
	AND A.LN_SEQ = F.LN_SEQ
	AND F.LC_DLQ_TYP = 'I'
	AND F.LC_STA_LON16 = '1'
LEFT OUTER JOIN OLWHRM1.LN83_EFT_TO_LON G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ
	AND G.LC_STA_LN83 = 'A'
	AND G.LC_EFT_SUS_REA = ''
	AND G.LD_EFT_EFF_END IS NULL
	AND DAYS(CURRENT DATE) >= DAYS(G.LD_EFT_EFF_BEG)
WHERE C.LC_FOR_TYP = '31'
	AND C.LC_STA_FOR10 = 'A'
	AND C.LC_FOR_STA = 'A'
	AND B.LC_STA_LON60 = 'A'
	AND B.LC_FOR_RSP != '003'
	AND DAYS(CURRENT DATE) BETWEEN DAYS(B.LD_FOR_BEG) AND DAYS(B.LD_FOR_END)
	AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU29.LWU29RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA QCRPF ;
	SET WORKLOCL.QCRPF;
RUN;
PROC SORT DATA=QCRPF;
	BY BF_SSN LN_SEQ LF_FOR_CTL_NUM LN_FOR_OCC_SEQ;
RUN;
/*R2 DATA SET*/
DATA PLTI;
	SET QCRPF;
	BY BF_SSN LF_FOR_CTL_NUM LN_FOR_OCC_SEQ;
	RETAIN RTOT DD;
	IF FIRST.LN_FOR_OCC_SEQ THEN DO;
		RTOT = LA_CUR_PRI;
	END;
	ELSE DO;
		RTOT = RTOT + LA_CUR_PRI;
	END;
	IF LAST.LN_FOR_OCC_SEQ THEN DO;
		XTOT = ROUND((((RTOT/365.25)*(LR_ITR*.01))*31)+5,.01);
		IF XTOT > LA_ACL_RDC_PAY THEN DO;
			OUTPUT;
		END;
	END;
RUN;
/*R3 DATA SET*/
PROC SORT DATA=QCRPF OUT=MT25DD 
	(
		KEEP=DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 LD_DLQ_OCC DAYS_DELQ
	 	WHERE=(DAYS_DELQ ^= .)
	) 
	NODUPKEY;
	BY DAYS_DELQ DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 LD_DLQ_OCC;
RUN;
/*R4 DATA SET*/
PROC SORT DATA=QCRPF OUT=BRPFWAA 
	(
		KEEP=DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 BN_EFT_SEQ
	 	WHERE=(BN_EFT_SEQ ^= .)
	) 
	NODUPKEY;
	BY LD_FOR_BEG DF_SPE_ACC_ID LD_FOR_END LD_STA_LON60 BN_EFT_SEQ;
RUN;
/*CREATE REPORTS*/
%MACRO NAPRT(DS,RNO);
PROC CONTENTS DATA= &DS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWU29  	 REPORT = ULWU29.LWU29R&RNO";
	END;
RETURN;
RUN;
%MEND NAPRT;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
TITLE 'REDUCED PAYMENT FORBEARANCE';
TITLE2	"PAYMENT LESS THAN INTEREST + $5";
FOOTNOTE   'JOB = UTLWU29  	 REPORT = ULWU29.LWU29R2';
%NAPRT(PLTI,2);
PROC PRINT NOOBS SPLIT='/' DATA=PLTI WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_FOR_BEG LD_FOR_END LD_STA_LON60 MMDDYY10. LA_ACL_RDC_PAY XTOT DOLLAR12.2;
VAR DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 LA_ACL_RDC_PAY XTOT;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LD_FOR_BEG = 'FORB/BEGIN DATE' 
	LD_FOR_END = 'FORB/END DATE' 
	LD_STA_LON60 = 'FORB/APPROVED DATE' 
	LA_ACL_RDC_PAY = 'PAYMENT/AMOUNT' 
	XTOT = 'CALCULATED/PAYMENT AMOUNT'
	;
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
TITLE 'REDUCED PAYMENT FORBEARANCE';
TITLE2	"MORE THAN 25 DAYS DELINQUENT";
FOOTNOTE   'JOB = UTLWU29  	 REPORT = ULWU29.LWU29R3';
%NAPRT(MT25DD,3);
PROC PRINT NOOBS SPLIT='/' DATA=MT25DD WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_FOR_BEG LD_FOR_END LD_STA_LON60 LD_DLQ_OCC MMDDYY10.;
VAR DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 LD_DLQ_OCC DAYS_DELQ;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LD_FOR_BEG = 'FORB/BEGIN DATE' 
	LD_FOR_END = 'FORB/END DATE' 
	LD_STA_LON60 = 'FORB/APPROVED DATE' 
	LD_DLQ_OCC = 'DELINQUENCY/DATE' 
	DAYS_DELQ = '# DAYS/DELINQUENT'
	;
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
TITLE "BORROWERS ON REDUCED PAYMENT FORBEARANCE WITH AN ACTIVE AUTOPAY";
FOOTNOTE   'JOB = UTLWU29  	 REPORT = ULWU29.LWU29R4';
%NAPRT(BRPFWAA,4);
PROC PRINT NOOBS SPLIT='/' DATA=BRPFWAA WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_FOR_BEG LD_FOR_END LD_STA_LON60 MMDDYY10.;
VAR DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END LD_STA_LON60 BN_EFT_SEQ;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LD_FOR_BEG = 'FORB/BEGIN DATE' 
	LD_FOR_END = 'FORB/END DATE' 
	LD_STA_LON60 = 'FORB/APPROVED DATE' 
	BN_EFT_SEQ = 'EFT/SEQUENCE'
	;
RUN;
PROC PRINTTO;
RUN;
