/*OPEN A TUNNEL TO LEGEND - PROD BEFORE RUNNING THIS CODE*/

FILENAME REPORT2 "T:\SAS\AAC Errors.csv";

/*The error translations are gathered from a table:*/
%LET MD = %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\CDW.dsn; update_lock_typ=nolock; bl_keepnulls=no");
LIBNAME CDW ODBC &MD ;
PROC FORMAT LIBRARY=WORK.FORMATS CNTLIN=CDW.FORMATTRANSLATION; RUN;

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
RSUBMIT LEGEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL);
CREATE TABLE TESTQUERY AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,D.DM_PRS_LST
	,D.DD_BRT
	,A.LN_SEQ
	,B.LN_BR_DSB_SEQ
	,B.PF_ERR_MSG
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,A.LD_LON_GTR
	,A.LA_LON_AMT_GTR
	,E.WC_DW_LON_STA
	,A.LD_LON_APL_RCV
	,F.LD_SCL_SPR
	,A.LD_END_GRC_PRD
	,G.LC_TYP_SCH_DIS
	,H.LR_ITR
	,A.LD_LTS_STS_BIL
	,I.LN_BBS_STS_PCV_PAY
	,CASE
		WHEN G.LC_TYP_SCH_DIS IN ('IL','IB') THEN G.LD_CRT_LON65
		ELSE null
	END AS ENTERED_IBR
	,CASE
		WHEN G.LC_TYP_SCH_DIS IN ('IL','IB') THEN G.LC_TYP_SCH_DIS
		ELSE ''
	END AS IBR_PMT_TYP
	,J.LN_IBR_FGV_MTH_CTR
	,J.LN_ICR_FGV_MTH_CTR
	,A.PF_MAJ_BCH
	,A.PF_MNR_BCH
FROM PKUB.PD10_PRS_NME D
INNER JOIN PKUB.LN10_LON A
	ON D.DF_PRS_ID = A.BF_SSN
INNER JOIN (SELECT BF_SSN
				,LN_SEQ
				,PF_ERR_MSG
				,LN_BR_DSB_SEQ
			FROM PKUB.LN31_DSB_ERR B
			WHERE B.LC_STA_LON31 = 'A'
				AND B.PF_ERR_MSG NOT IN ('00001','00002','06562')
		UNION
			SELECT BF_SSN
				,LN_SEQ
				,PF_ERR_MSG
				,0 AS LN_BR_DSB_SEQ
			FROM PKUB.LN30_LON_ERR B
			WHERE B.LC_STA_LON30 = 'A'
				AND B.PF_ERR_MSG NOT IN ('00001','00002','06562')) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN PKUB.DW01_DW_CLC_CLU E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
LEFT OUTER JOIN PKUB.LN65_LON_RPS G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ
	AND G.LC_STA_LON65 = 'A'
LEFT OUTER JOIN PKUB.LN72_INT_RTE_HST H
	ON A.BF_SSN = H.BF_SSN
	AND A.LN_SEQ = H.LN_SEQ
	AND H.LC_STA_LON72 = 'A'
	AND H.LD_ITR_EFF_BEG <= CURRENT DATE
	AND H.LD_ITR_EFF_END >= CURRENT DATE
LEFT OUTER JOIN PKUB.LN54_LON_BBS I
	ON A.BF_SSN = I.BF_SSN 
	AND A.LN_SEQ = I.LN_SEQ
	AND I.LC_STA_LN54 = 'A'
LEFT OUTER JOIN PKUB.LN09_RPD_PIO_CVN J
	ON A.BF_SSN = J.BF_SSN 
	AND A.LN_SEQ = J.LN_SEQ
LEFT OUTER JOIN PKUB.SD10_STU_SPR F
	ON A.LF_STU_SSN = F.LF_STU_SSN
	AND F.LC_STA_STU10 = 'A'

WHERE	LC_STA_LON10 = 'P'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
QUIT;
PROC SORT DATA=TESTQUERY ; BY PF_ERR_MSG BF_SSN LN_SEQ; RUN;

ENDRSUBMIT;
DATA TESTQUERY;
	SET LEGEND.TESTQUERY;
RUN;
DATA _null_;
SET TESTQUERY ;
TRANSLATION = PF_ERR_MSG;
FORMAT TRANSLATION $AACERR.;
FORMAT DD_BRT LD_LON_1_DSB LD_LON_GTR LD_LON_APL_RCV LD_SCL_SPR LD_END_GRC_PRD LD_LTS_STS_BIL ENTERED_IBR MMDDYY10.;
FORMAT LA_LON_AMT_GTR 12.2;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN PUT "Borrower SSN,Borrower Last Name,Borrower DOB,Loan Seq,Disbursement Seq,Error Code,Error Message,Loan Program,"
	"1st Disb Date,Guaranty Date,Guaranty Amount,Loan Status,Loan App Rec'd Date,Separation Date,Grace Period End,"
	"Repayment Schedule Type,Interest Rate,Date of Last Satisfied Bill,# BB Payments,Date Entered IBR,"
	"IBR Payment Type,IBR Payment Counter,ICR Payment Counter,Major Batch,Minor Batch";
PUT BF_SSN DM_PRS_LST DD_BRT LN_SEQ LN_BR_DSB_SEQ PF_ERR_MSG TRANSLATION IC_LON_PGM LD_LON_1_DSB LD_LON_GTR LA_LON_AMT_GTR
	WC_DW_LON_STA LD_LON_APL_RCV LD_SCL_SPR LD_END_GRC_PRD LC_TYP_SCH_DIS LR_ITR LD_LTS_STS_BIL LN_BBS_STS_PCV_PAY ENTERED_IBR
	IBR_PMT_TYP LN_IBR_FGV_MTH_CTR LN_ICR_FGV_MTH_CTR PF_MAJ_BCH PF_MNR_BCH;
RUN;
