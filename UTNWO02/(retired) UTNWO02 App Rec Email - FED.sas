/******************************
UTNWO02 App Rec Email - FED
*******************************/
%LET RPTLIB = T:\SAS;
%LET DSLIB = Y:\Development\SAS Test Files;
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET DSLIB = /sas/whse;*/
FILENAME REPORTZ "&RPTLIB/UNWO02.NWO02RZ";
FILENAME REPORT2 "&RPTLIB/UNWO02.NWO02R2";
FILENAME REPORT3 "&RPTLIB/UNWO02.NWO02R3";
FILENAME REPORT4 "&RPTLIB/UNWO02.NWO02R4";
FILENAME REPORT5 "&RPTLIB/UNWO02.NWO02R5";
LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=WORK  ;
LIBNAME SAS_TAB V8 "&DSLIB/progrevw";
%LET JOBNM = UTNWO02;
/*READ THE LAST RUN DATE OF THE UTNWO02 JOB*/
DATA _NULL_; 
	SET SAS_TAB.LASTRUN_JOBS;
	IF JOB = "&JOBNM" THEN DO;
		CALL SYMPUT('LAST_RUN',"'"||PUT(LAST_RUN,MMDDYY10.)||"'");
	END;
RUN; 
%SYSLPUT JOBNM = &JOBNM ;
%SYSLPUT LAST_RUN = &LAST_RUN ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL);
CREATE TABLE IBR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT distinct B.DF_SPE_ACC_ID
/*	,RTRIM(B.DM_PRS_1)||','||B.DM_PRS_LST AS NAME*/
	,B.DM_PRS_1 AS FNAME
	,B.DM_PRS_LST AS LNAME
	,C.DX_ADR_EML AS EMAIL
	,A.PF_REQ_ACT
	,CASE 
		WHEN C.DC_ADR_EML = 'H' THEN 1
		WHEN C.DC_ADR_EML = 'A' THEN 2
		WHEN C.DC_ADR_EML = 'W' THEN 3
		ELSE 4
	END AS EMAIL_TYPE
FROM PKUB.AY10_BR_LON_ATY A
INNER JOIN PKUB.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN PKUB.PD32_PRS_ADR_EML C
	ON B.DF_PRS_ID = C.DF_PRS_ID 
WHERE C.DI_VLD_ADR_EML = 'Y'
	AND C.DC_STA_PD32 = 'A'
	AND A.PF_REQ_ACT IN 
	(
		'CODCA','CODPA','DIDFR','DIFRB','DITLF','DICSK','DIFCR','DIUPR','DIS11'
	)
	AND LD_ATY_REQ_RCV >= &LAST_RUN
	AND A.LC_STA_ACTY10 = 'A'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
QUIT;
ENDRSUBMIT;
DATA IBR;
	SET LEGEND.IBR;
RUN;
/*WRITE THE RUN DATE TO THE SAVED DATA SET*/
DATA SAS_TAB.LASTRUN_JOBS;
	SET SAS_TAB.LASTRUN_JOBS;
	IF JOB = 'UTNWO02' THEN LAST_RUN = TODAY();
RUN;
DATA IBR;
	SET IBR;
	IF PF_REQ_ACT IN ('CODCA','CODPA') THEN RNUM = 'R2';
	ELSE IF PF_REQ_ACT IN ('DIDFR') THEN RNUM = 'R3';
	ELSE IF PF_REQ_ACT IN ('DITLF','DICSK','DIFCR','DIUPR','DIS11') THEN RNUM = 'R4';
	ELSE RNUM = 'R5';
RUN;
PROC SORT DATA=IBR; BY RNUM DF_SPE_ACC_ID; RUN; 
%MACRO CRT_FILE(NUM);
	PROC SORT DATA=IBR OUT=RDS NODUPKEY;
		WHERE RNUM = "R&NUM";
		BY DF_SPE_ACC_ID EMAIL_TYPE;
	RUN;
	DATA RDS; 
		SET RDS;
		BY DF_SPE_ACC_ID;
		IF FIRST.DF_SPE_ACC_ID;
	RUN;
	DATA _NULL_;
		SET RDS;
		FILE REPORT&NUM DELIMITER=',' DSD DROPOVER LRECL=32767;
		IF _N_ = 1 THEN DO;
			PUT "DF_SPE_ACC_ID,DM_PRS_1,DM_PRS_LST,DX_ADR_EML";
		END;
		DO;
		   PUT DF_SPE_ACC_ID @;
		   PUT FNAME @;
		   PUT LNAME @;
		   PUT EMAIL ;
		END;
	RUN;
%MEND CRT_FILE;
%CRT_FILE(2);
%CRT_FILE(3);
%CRT_FILE(4);
%CRT_FILE(5);
