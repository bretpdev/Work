/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWM41.LWM41RZ";
FILENAME REPORT2 "&RPTLIB/ULWM41.LWM41R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT D.DF_SPE_ACC_ID
	,D.DF_PRS_ID
	,D.DC_ADR
	,TRIM(D.DM_PRS_1) || ' ' || TRIM(D.DM_PRS_LST) AS NAME
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP
	,(COALESCE(A.LA_CLM_PRI,0) + COALESCE(A.LA_CLM_INT,0) - COALESCE(A.LA_PRI_COL,0) + COALESCE(A.LA_INT_ACR,0) + COALESCE(C.LA_CLM_INT_ACR,0) 
		- COALESCE(A.LA_INT_COL,0) + COALESCE(A.LA_LEG_CST_ACR,0) - COALESCE(A.LA_LEG_CST_COL,0) + COALESCE(A.LA_OTH_CHR_ACR,0)
		- COALESCE(A.LA_OTH_CHR_COL,0) + COALESCE(A.LA_COL_CST_ACR,0) - COALESCE(A.LA_COL_CST_COL,0) 
		+ COALESCE(C.LA_CLM_PRJ_COL_CST,0)) AS OUTSTANDING
	,(COALESCE(A.LA_COL_CST_ACR,0) - COALESCE(A.LA_COL_CST_COL,0)) / 5 AS WAIVED
FROM OLWHRM1.PD01_PDM_INF D
INNER JOIN OLWHRM1.DC01_LON_CLM_INF A
	ON D.DF_PRS_ID = A.BF_SSN
INNER JOIN OLWHRM1.DC02_BAL_INT C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
INNER JOIN OLWHRM1.BR01_BR_CRF B
	ON D.DF_PRS_ID = B.BF_SSN
WHERE 
A.LC_STA_DC10 = '03'
	AND A.LC_AUX_STA = ''
	AND 
A.LC_PCL_REA IN ('DF','DQ','DB')
	AND A.LD_CLM_ASN_DOE IS NULL
	AND (COALESCE(A.LA_CLM_PRI,0) + COALESCE(A.LA_CLM_INT,0) - COALESCE(A.LA_PRI_COL,0) + COALESCE(A.LA_INT_ACR,0) 
		+ COALESCE(C.LA_CLM_INT_ACR,0) - COALESCE(A.LA_INT_COL,0) + COALESCE(A.LA_LEG_CST_ACR,0) 
		- COALESCE(A.LA_LEG_CST_COL,0) + COALESCE(A.LA_OTH_CHR_ACR,0) - COALESCE(A.LA_OTH_CHR_COL,0) 
		+ COALESCE(A.LA_COL_CST_ACR,0) - COALESCE(A.LA_COL_CST_COL,0) + COALESCE(LA_CLM_PRJ_COL_CST,0)) > 25
	AND C.LA_CLM_PRJ_COL_CST > 25
	AND A.LI_COL_CST_ASS = 'Y'
	AND B.BC_RHB_NOT_ELG = ''
	AND B.BN_RHB_PAY_CTR > 11
	AND A.LC_RHB_CON IN ('1','')
	AND DAYS(A.LD_LST_PAY) >= DAYS(CURRENT DATE) - 30
	AND D.DI_VLD_ADR = 'Y'
	AND D.DC_ADR <> 'T'
	AND A.BF_SSN NOT IN (SELECT DF_PRS_ID_BR
				FROM OLWHRM1.LA11_LEG_ACT_ATY
				WHERE BC_LEG_ACT_ATY_TYP IN ('GG','JD')
					AND BC_ATY_WDR_REA = '')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/

ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;
PROC SORT DATA=DEMO NODUPKEY;
	BY DF_SPE_ACC_ID;
RUN;

*CALCULATE KEYLINE;
DATA DEMO (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET DEMO;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

DATA _NULL_;
SET DEMO ;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
DEADLINE = TODAY() + 45;
CCC = 'MA2329';
REHAB = OUTSTANDING - WAIVED;
FORMAT DEADLINE MMDDYY10. ;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNTNUMBER,NAME,STREET1,STREET2,CITY,STATE,ZIP,OUTSTANDING,WAIVED,REHAB,DEADLINE,STATECODE,CCC,ACSKEY";
END;
DO;
   PUT DF_SPE_ACC_ID @;
   PUT NAME @;
   PUT DX_STR_ADR_1	@;
   PUT DX_STR_ADR_2 @;
   PUT DM_CT @;
   PUT DC_DOM_ST @;
   PUT DF_ZIP @;
   PUT OUTSTANDING @;
   PUT WAIVED @;
   PUT REHAB @;
   PUT DEADLINE @;
   PUT DC_DOM_ST @;
   PUT CCC @;
   PUT ACSKEY $;
END;
RUN;
