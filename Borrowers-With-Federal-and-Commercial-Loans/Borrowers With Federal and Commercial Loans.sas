%LET RPTLIB= T:\SAS;
%LET MD = %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\cdw.dsn;");
LIBNAME cdw ODBC USER="sqlserver;" PASSWORD="procauto;" &MD ;
%LET MD2 = %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\udw.dsn;");
LIBNAME udw ODBC USER="sqlserver;" PASSWORD="procauto;" &MD2 ;

PROC SQL;
CREATE TABLE BOR_LIST AS
SELECT DISTINCT A.DF_SPE_ACC_ID AS FED_ID
		,C.DF_SPE_ACC_ID AS UHEAA_ID
		,B.DM_PRS_1 
		,B.DM_PRS_LST
		,FED.FED_BAL
		,FDEL.FED_DEL
		,COM.UHEAA_BAL
		,UDEL.UHEAA_DEL
	FROM CDW.LN10_LOAN A
		INNER JOIN CDW.PD10_BORROWER B
		ON A.DF_SPE_ACC_ID = B.DF_SPE_ACC_ID
		INNER JOIN UDW.PD10_BORROWER C
		ON B.BF_SSN = C.BF_SSN
		INNER JOIN(
				SELECT DISTINCT
					D.DF_SPE_ACC_ID
					,SUM(D.LA_CUR_PRI) AS UHEAA_BAL
				FROM UDW.LN10_LOAN D
				GROUP BY D.DF_SPE_ACC_ID
						) COM
			ON C.DF_SPE_ACC_ID = COM.DF_SPE_ACC_ID
		LEFT OUTER JOIN (
					SELECT DISTINCT
						E.DF_SPE_ACC_ID
						,MAX(E.LN_DLQ_MAX) AS FED_DEL
					FROM CDW.LN16_DELINQUENCY E
					GROUP BY E.DF_SPE_ACC_ID
						) FDEL
			ON A.DF_SPE_ACC_ID = FDEL.DF_SPE_ACC_ID
		LEFT OUTER JOIN (
					SELECT DISTINCT 
						F.DF_SPE_ACC_ID
						,MAX(F.LN_DLQ_MAX) AS UHEAA_DEL
					FROM UDW.LN16_DELINQUENCY F
					GROUP BY F.DF_SPE_ACC_ID
						) UDEL
			ON C.DF_SPE_ACC_ID = UDEL.DF_SPE_ACC_ID
		INNER JOIN (
				SELECT DISTINCT
					G.DF_SPE_ACC_ID
					,SUM(G.LA_CUR_PRI) AS FED_BAL
				FROM CDW.LN10_LOAN G
				GROUP BY G.DF_SPE_ACC_ID
					) FED
			ON A.DF_SPE_ACC_ID = FED.DF_SPE_ACC_ID
	WHERE A.LA_CUR_PRI > 0
		AND COM.UHEAA_BAL> 0
;
QUIT;

OPTIONS NODATE nonumber;
ODS _ALL_ CLOSE;
TITLE "Borrowers With Federal and Commercial Loans";
TITLE2 "%SYSFUNC(TODAY(),WORDDATE21.)";
FOOTNOTE "At least one loan with a balance in each region" ;
ODS PDF FILE="&RPTLIB/Borrowers With Federal and Commercial Loans-FED.R2.PDF" STYLE=SASWEB;
PROC PRINT DATA=BOR_LIST LABEL;
FORMAT FED_BAL UHEAA_BAL DOLLAR18.2;
VAR FED_ID UHEAA_ID DM_PRS_1 DM_PRS_LST FED_BAL FED_DEL UHEAA_BAL UHEAA_DEL;
LABEL UHEAA_ID = 'Unique Borrowers Commercial'
	FED_ID = 'Unique Borrowers Federal'
	DM_PRS_1 = 'Borrower First Name'
	DM_PRS_LST = 'Last Name'
	FED_BAL = 'Federal Principle Balance'
	FED_DEL = 'Current Federal Delinquency'
	UHEAA_BAL = 'Commercial Principal Balance'
	UHEAA_DEL = 'Current Commercial Delinquency';
RUN;
ODS PDF CLOSE;
ODS LISTING;
