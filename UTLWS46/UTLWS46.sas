/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWS46.LWS46RZ";
FILENAME REPORT2 "&RPTLIB/ULWS46.LWS46R2";

/*%LET TBLLIB = /sas/whse/progrevw;*/
%LET TBLLIB = Q:\Process Automation\TabSAS;

/*INPUT LOAN TYPES FOR PRIVATE AND FFEL LOANS*/
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "&TBLLIB/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
/*CREATE MACRO VARIALBE LISTS OF LOAN PROGRAMS(FFEL AND PRIVATE LOANS)*/
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY "," /*FFEL LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';

	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :PRIVATE_LIST SEPARATED BY "," /*PRIVATE LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM ^= 'FFEL';
QUIT;
%SYSLPUT FFELP_LIST = &FFELP_LIST;
%SYSLPUT PRIVATE_LIST = &PRIVATE_LIST;
%PUT &PRIVATE_LIST;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,DELINQ
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN (SELECT BF_SSN
				,LN_SEQ
				,'1' AS DELINQ
			FROM OLWHRM1.LN16_LON_DLQ_HST
			WHERE LC_STA_LON16 = '1'
				AND DAYS(LD_DLQ_OCC) < DAYS(CURRENT DATE) - 30) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LA_CUR_PRI > 0 
	AND A.LC_STA_LON10 = 'R' 
	AND C.WC_DW_LON_STA NOT IN ('12','17','21','22')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/

DATA FFEL PRIVATE ;
SET DEMO;
IF IC_LON_PGM IN (&FFELP_LIST) THEN OUTPUT FFEL;
IF IC_LON_PGM IN (&PRIVATE_LIST) THEN OUTPUT PRIVATE;
RUN;

PROC SQL;
CREATE TABLE DEMO AS
SELECT 'FFEL' AS LOAN_TYPE
	,COUNT(*) AS TOTAL
	,COUNT(DELINQ) AS DELINQ
	,COUNT(DELINQ) / COUNT(*) AS RATIO
FROM FFEL 
UNION SELECT 'PRIVATE' AS LOAN_TYPE
	,COUNT(*) AS TOTAL
	,COUNT(DELINQ) AS DELINQ
	,COUNT(DELINQ) / COUNT(*) AS RATIO
FROM PRIVATE;
QUIT;
ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'DELINQUENCY RATES FOR FFEL AND PRIVATE LOANS';
TITLE2	"RUNDATE &SYSDATE9";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT UHEAA HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWS46  	 REPORT = ULWS46.LWS46R2';

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT TOTAL DELINQ COMMA10.0 RATIO PERCENT8.2;
VAR LOAN_TYPE TOTAL DELINQ RATIO;
LABEL LOAN_TYPE = 'PORTFOLIO'
	TOTAL = 'NUMBER OF LOANS'
	DELINQ = 'DELINQUENT LOANS'
	RATIO = 'DELINQUENCY RATE';
RUN;

PROC PRINTTO;
RUN;
