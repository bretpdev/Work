*-----------------------------------------------*
| UTLWO43 - IN SCHOOL LONAS SOLD				|
*-----------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO43.LWO43R2";
FILENAME REPORTZ "&RPTLIB/ULWO43.LWO43RZ";

DATA _NULL_;
     CALL SYMPUT('PRVDY',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
RUN;

%LET ARC_DT = '03/01/2006';

%SYSLPUT PRVDY = &PRVDY;
%SYSLPUT ARC_DT = &ARC_DT;

LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE INSLSDA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,B.LN_FAT_SEQ
	,D.LD_ATY_REQ_RCV
	,AY01.BD_ATY_PRF
	,LCO.LCO_LN_SEQ
	,SD02.IF_OPS_SCL_RPT
/*--------------------COMPASS--------------------*/
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN90_FIN_ATY B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY D
	ON A.BF_SSN = D.BF_SSN
	AND D.PF_REQ_ACT = 'WVGRC'
	AND D.LD_ATY_REQ_RCV >= &ARC_DT
/*----------------------LCO----------------------*/
INNER JOIN (
	SELECT LC10.DF_LCO_PRS_SSN_BR AS BF_SSN
		,SUBSTR(LC10.LF_UND_LN_ACC_NUM, 8,2) AS LN_SEQ
		,LC10.LN_LCO_UND_LN_SEQ AS LCO_LN_SEQ
	FROM OLWHRM1.AP1A_LCO_APL AP1A
	INNER JOIN OLWHRM1.LC10_UND_LN_INF LC10
		ON AP1A.DF_LCO_PRS_SSN_BR = LC10.DF_LCO_PRS_SSN_BR
		AND AP1A.AN_LCO_APL_SEQ = LC10.AN_LCO_APL_SEQ 
	WHERE AP1A.AD_LCO_APL_RCV IS NOT NULL
	AND AP1A.IC_LCO_DSB_APV_STA = ''
	AND LC10.LI_UND_LN_CON = 'Y'
	AND LC10.LI_UND_LN_SER_ORG = 'Y' 
	AND SUBSTR(LC10.LF_UND_LN_ACC_NUM,1,4) = 'L0UT' 
	 ) LCO
	ON A.BF_SSN = LCO.BF_SSN
	AND A.LN_SEQ = INT(LCO.LN_SEQ)
/*--------------------ONELINK--------------------*/
INNER JOIN OLWHRM1.GA16_ENR_RPT_CRF GA16
	ON A.LF_LON_ALT = GA16.AF_APL_ID
	AND A.LN_LON_ALT_SEQ = INT(GA16.AF_APL_ID_SFX)
INNER JOIN OLWHRM1.GA01_APP GA01
	ON GA16.AF_APL_ID = GA01.AF_APL_ID
INNER JOIN OLWHRM1.SD02_STU_ENR SD02
	ON GA01.DF_PRS_ID_STU = SD02.DF_PRS_ID_STU
	AND GA16.LF_CRT_DTS_SD02 = SD02.LF_CRT_DTS_SD02
	AND GA16.LF_STU_ENR_RPT_SEQ = SD02.LF_STU_ENR_RPT_SEQ
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY AY01
	ON A.BF_SSN = AY01.DF_PRS_ID
	AND AY01.PF_ACT = 'WVGRC'
	AND AY01.BD_ATY_PRF >= &ARC_DT
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON A.BF_SSN = DW01.BF_SSN
	AND A.LN_SEQ = DW01.LN_SEQ
/*-------------------LENDER MERGE----------------*/
LEFT OUTER JOIN (
	SELECT LN99.BF_SSN
		,LN99.LN_SEQ
		,LN99.LN_FAT_SEQ
		,'X' AS LMRG_IND
	FROM OLWHRM1.LN99_LON_SLE_FAT LN99
	INNER JOIN OLWHRM1.LR10_LDR_DMO LR10
		ON LN99.IF_SLL_OWN_SLD = LR10.IF_DOE_LDR
		AND LN99.IF_BUY_OWN_SLD = LR10.IF_LDR_MRG_TO
	WHERE LR10.IC_LDR_MRG_TYP IN ('F','1')
	) LM
	ON B.BF_SSN = LM.BF_SSN
	AND B.LN_SEQ = LM.LN_SEQ
	AND B.LN_FAT_SEQ = LM.LN_FAT_SEQ

WHERE A.LA_CUR_PRI > 0 
AND A.LC_STA_LON10 = 'R'
AND A.LF_LON_CUR_OWN = '828476'
AND A.IC_LON_PGM NOT IN (
	'SUBCNS','UNCNS','SUBSPC','UNSPC'
	)
AND B.LD_FAT_EFF = &PRVDY
AND B.PC_FAT_TYP IN (
	'03','04'
	)
AND B.PC_FAT_SUB_TYP = '95'
AND B.LC_STA_LON90 = 'A'
AND B.LC_FAT_REV_REA = ''
AND DW01.WC_DW_LON_STA = '02'
AND GA16.AF_CRT_DTS_GA16 = (
	SELECT MAX(X.AF_CRT_DTS_GA16)
	FROM OLWHRM1.GA16_ENR_RPT_CRF X
	WHERE X.AF_APL_ID = GA16.AF_APL_ID
	AND X.AF_APL_ID_SFX = GA16.AF_APL_ID_SFX
	)
AND LM.LMRG_IND IS NULL

FOR READ ONLY WITH UR
);

CREATE TABLE INSCL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,LCO.LCO_LN_SEQ
	,SD02.IF_OPS_SCL_RPT
FROM OLWHRM1.LN10_LON A
LEFT OUTER JOIN (
	SELECT LC10.DF_LCO_PRS_SSN_BR AS BF_SSN
		,SUBSTR(LC10.LF_UND_LN_ACC_NUM, 8,2) AS LN_SEQ
		,LC10.LN_LCO_UND_LN_SEQ AS LCO_LN_SEQ
	FROM OLWHRM1.LC10_UND_LN_INF LC10
	WHERE SUBSTR(LC10.LF_UND_LN_ACC_NUM,1,4) = 'L0UT' 
	 ) LCO
	ON A.BF_SSN = LCO.BF_SSN
	AND A.LN_SEQ = INT(LCO.LN_SEQ)
INNER JOIN OLWHRM1.GA16_ENR_RPT_CRF GA16
	ON A.LF_LON_ALT = GA16.AF_APL_ID
	AND A.LN_LON_ALT_SEQ = INT(GA16.AF_APL_ID_SFX)
INNER JOIN OLWHRM1.GA01_APP GA01
	ON GA16.AF_APL_ID = GA01.AF_APL_ID
INNER JOIN OLWHRM1.SD02_STU_ENR SD02
	ON GA01.DF_PRS_ID_STU = SD02.DF_PRS_ID_STU
	AND GA16.LF_CRT_DTS_SD02 = SD02.LF_CRT_DTS_SD02
	AND GA16.LF_STU_ENR_RPT_SEQ = SD02.LF_STU_ENR_RPT_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON A.BF_SSN = DW01.BF_SSN
	AND A.LN_SEQ = DW01.LN_SEQ
WHERE DW01.WC_DW_LON_STA = '02'
AND A.LC_STA_LON10 = 'R'
AND A.LF_LON_CUR_OWN = '828476'
AND A.LA_CUR_PRI > 0 
AND A.IC_LON_PGM NOT IN (
	'SUBCNS','UNCNS','SUBSPC','UNSPC'
	)
FOR READ ONLY WITH UR
);

/*CREATE EXCLUSION DATA SET*/
CREATE TABLE EXCLUDE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT	LN99.BF_SSN
	,LN99.LN_SEQ
	,LN99.LN_FAT_SEQ	
FROM OLWHRM1.LN99_LON_SLE_FAT LN99
WHERE (LN99.IF_SLL_OWN_SLD = '813760' AND LN99.IF_BUY_OWN_SLD = '813760UT')
OR (LN99.IF_SLL_OWN_SLD = '828476' AND LN99.IF_BUY_OWN_SLD = '828476')
OR (LN99.IF_SLL_OWN_SLD = '813915' AND LN99.IF_BUY_OWN_SLD = '813894')
OR (LN99.IF_SLL_OWN_SLD = '820043' AND LN99.IF_BUY_OWN_SLD = '829505')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO43.LWO43RZ);*/
/*QUIT;*/
PROC SQL;
CREATE TABLE INSLSDB AS 
SELECT DISTINCT A.*
FROM INSLSDA A
WHERE NOT EXISTS (
	SELECT *
	FROM EXCLUDE X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.LN_SEQ = A.LN_SEQ
	AND X.LN_FAT_SEQ = A.LN_FAT_SEQ
	)
;
QUIT;

DATA INSLSDB (DROP=LD_ATY_REQ_RCV BD_ATY_PRF);
SET INSLSDB;
IF LD_ATY_REQ_RCV = BD_ATY_PRF 
	THEN ARC_DT = LD_ATY_REQ_RCV;
ELSE IF LD_ATY_REQ_RCV ^= . AND BD_ATY_PRF = . 
	THEN ARC_DT = LD_ATY_REQ_RCV;
ELSE IF LD_ATY_REQ_RCV = . AND BD_ATY_PRF ^= . 
	THEN ARC_DT = BD_ATY_PRF;
ELSE IF LD_ATY_REQ_RCV ^= . AND BD_ATY_PRF ^= . 
	THEN ARC_DT = LD_ATY_REQ_RCV;
IF ARC_DT = . THEN DELETE;
RUN;

PROC SQL;
CREATE TABLE INSCL2 AS
SELECT DISTINCT A.*
	,B.ARC_DT
FROM INSCL A
INNER JOIN (
	SELECT BF_SSN
		,MAX(ARC_DT) AS ARC_DT
	FROM INSLSDB 
	GROUP BY BF_SSN
	) B
	ON A.BF_SSN = B.BF_SSN
;
RUN;

DATA INSLSDB;
SET INSCL2;
RUN;

ENDRSUBMIT;
DATA INSLSDB;SET WORKLOCL.INSLSDB;RUN;

PROC SORT DATA=INSLSDB;BY BF_SSN LN_SEQ;RUN;

DATA _NULL_;
SET  WORK.INSLSDB;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT LN_SEQ 6. ;
   FORMAT IC_LON_PGM $6. ;
   FORMAT LD_LON_1_DSB MMDDYY10. ;
   FORMAT LCO_LN_SEQ $2. ;
   FORMAT IF_OPS_SCL_RPT $8. ;
   FORMAT ARC_DT MMDDYY10. ;
IF _N_ = 1 THEN 
DO;
   PUT
   'BF_SSN'
   ','
   'LN_SEQ'
   ','
   'LD_LON_1_DSB'
   ','
   'ARC_DT'
   ','
   'LCO_LN_SEQ'
   ','
   'IF_OPS_SCL_RPT'
   ','
   'IC_LON_PGM'
   ;
END;
DO;
   PUT BF_SSN $ @;
   PUT LN_SEQ @;
   PUT LD_LON_1_DSB @;
   PUT ARC_DT @;
   PUT LCO_LN_SEQ $ @;
   PUT IF_OPS_SCL_RPT $ @;
   PUT IC_LON_PGM $ ;
   ;
END;
RUN;
