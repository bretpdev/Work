*--------------------------------------------*
| UTLWN18 - Zero O-fee Loans - PIF Report    |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWN18.LWN18RZ";
FILENAME REPORT3 "&RPTLIB/ULWN18.LWN18R3";
FILENAME REPORT4 "&RPTLIB/ULWN18.LWN18R4";
FILENAME REPORT5 "&RPTLIB/ULWN18.LWN18R5";
FILENAME REPORT6 "&RPTLIB/ULWN18.LWN18R6";
FILENAME REPORT7 "&RPTLIB/ULWN18.LWN18R7";
FILENAME REPORT8 "&RPTLIB/ULWN18.LWN18R8";
FILENAME REPORT9 "&RPTLIB/ULWN18.LWN18R9";
FILENAME REPORT10 "&RPTLIB/ULWN18.LWN18R10";
FILENAME REPORT11 "&RPTLIB/ULWN18.LWN18R11";
FILENAME REPORT12 "&RPTLIB/ULWN18.LWN18R12";
FILENAME REPORT13 "&RPTLIB/ULWN18.LWN18R13";
FILENAME REPORT14 "&RPTLIB/ULWN18.LWN18R14";
FILENAME REPORT15 "&RPTLIB/ULWN18.LWN18R15";
FILENAME REPORT16 "&RPTLIB/ULWN18.LWN18R16";
FILENAME REPORT17 "&RPTLIB/ULWN18.LWN18R17";
FILENAME REPORT18 "&RPTLIB/ULWN18.LWN18R18";
FILENAME REPORT19 "&RPTLIB/ULWN18.LWN18R19";
FILENAME REPORT20 "&RPTLIB/ULWN18.LWN18R20";
FILENAME REPORT21 "&RPTLIB/ULWN18.LWN18R21";
FILENAME REPORT22 "&RPTLIB/ULWN18.LWN18R22";
FILENAME REPORT23 "&RPTLIB/ULWN18.LWN18R23";
DATA _NULL_;
	CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'END'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
/*GET LIST OF UHEAA LENDER IDs*/
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ZOFLN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,CASE 
		WHEN A.LF_LON_CUR_OWN = '813760' THEN '813760UT'
		ELSE A.LF_LON_CUR_OWN
	 END AS LF_LON_CUR_OWN
	,A.LD_PIF_RPT
	,INT(A.LF_RGL_CAT_LP20) AS REG_CAT
	,B.DISB
FROM OLWHRM1.LN10_LON A
INNER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,SUM(LA_DSB - COALESCE(LA_DSB_CAN,0)) AS DISB
	FROM OLWHRM1.LN15_DSB
	WHERE LC_DSB_TYP = '2'
		AND LC_STA_LON15 IN ('1','3')
	GROUP BY BF_SSN
		,LN_SEQ
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.IF_TIR_PCE <> ''
	AND B.DISB > 0
	AND A.LD_PIF_RPT BETWEEN &BEGIN AND &END
	AND A.LF_LON_CUR_OWN NOT IN 
	(
		&UHEAA_LIST
	)
FOR READ ONLY WITH UR
);

CREATE TABLE LENDERS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT IF_DOE_LDR
	,IM_LDR_FUL
FROM OLWHRM1.LR10_LDR_DMO 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWN18.LWN18RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA ZOFLN ;SET WORKLOCL.ZOFLN;RUN;
DATA LENDERS ;SET WORKLOCL.LENDERS;RUN;
PROC SORT DATA=ZOFLN NODUPKEY;BY BF_SSN LN_SEQ;RUN;
DATA ZOFLN;
SET ZOFLN;
	SELECT;
		WHEN (REG_CAT LE 1999030) PRCNT = .03;
		WHEN (REG_CAT EQ 2007020) PRCNT = .015;
		WHEN (REG_CAT EQ 2006020) DO;
			IF IC_LON_PGM IN ('PLUS','PLUSGB') & '01JUL2006'D <= LD_LON_1_DSB <= '07AUG2006'D 
				THEN PRCNT = .02;
			ELSE 
				PRCNT = .03;		
		END;
		OTHERWISE PRCNT=.;
	END;
	/*CALCULATE THE LENDER ORIGINATION FEE AMOUNT*/
	DO;
		LDROF = ROUND(DISB*PRCNT,.01);
	END;
RUN;

%MACRO PRNTRP(RNO,LDR_ID);

/*CREATE REPORTING DATA SET*/
DATA REPDS;
SET ZOFLN; 
	WHERE LF_LON_CUR_OWN = "&LDR_ID";
RUN;
PROC SORT DATA=REPDS; BY LF_LON_CUR_OWN BF_SSN LN_SEQ;RUN;
/*GET LENDER NAME EVEN IF THERE ARE NO RESULTS*/
DATA _NULL_;
SET LENDERS;
IF IF_DOE_LDR = "&LDR_ID" THEN DO;
	CALL SYMPUT('LDR_NAME',TRIM(IM_LDR_FUL));
END;
RUN;
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE CENTER PAGENO=1 NODATE;
OPTIONS PS=39 LS=127;
TITLE 'ZERO ORIGINATION FEE LOANS PAID IN FULL';
TITLE2 "&LDR_NAME";
TITLE3	"&RUNDATE";
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   "JOB = UTLWN18  	 REPORT = ULWN18.LWN18R&RNO";
PROC CONTENTS DATA=REPDS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWN18  	 REPORT = ULWN18.LWN18R&RNO";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=REPDS WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT DISB LDROF 14.2 LD_PIF_RPT MMDDYY10.;
VAR LF_LON_CUR_OWN BF_SSN LN_SEQ DISB LDROF LD_PIF_RPT;
LABEL LF_LON_CUR_OWN = 'LENDER'
	BF_SSN = 'BORROWER SSN'
	LN_SEQ = 'LOAN SEQ #'
	DISB = 'ORIGINAL DISBURSEMENT AMOUNT '
	LDROF = 'LENDER ORIGINATION FEE AMOUNT'
	LD_PIF_RPT = 'PAID IN FULL DATE';
RUN;
PROC PRINTTO;
RUN;
%MEND PRNTRP;
%PRNTRP(3,817575);
%PRNTRP(4,822373);
%PRNTRP(5,833577);
%PRNTRP(6,819628);
%PRNTRP(7,833828);
%PRNTRP(8,817440);
%PRNTRP(9,817545);
%PRNTRP(10,834265);
%PRNTRP(11,830791);
%PRNTRP(12,813760UT);
%PRNTRP(13,817546);
%PRNTRP(14,834122);
%PRNTRP(15,832241);
%PRNTRP(16,820200);
%PRNTRP(17,830132);
%PRNTRP(18,829123);
%PRNTRP(19,811698);
%PRNTRP(20,830146);
%PRNTRP(21,829158);
%PRNTRP(22,813894);
%PRNTRP(23,817455);
