/*UTLWD06 AUX SERVICES ACTION CODE-ARC USAGE SUMMARY*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWD06.LWD06RZ";
FILENAME REPORT2 "&RPTLIB/ULWD06.LWD06R2";
FILENAME REPORT3 "&RPTLIB/ULWD06.LWD06R3";
FILENAME REPORT4 "&RPTLIB/ULWD06.LWD06R4";
FILENAME REPORT5 "&RPTLIB/ULWD06.LWD06R5";
DATA _NULL_;
     CALL SYMPUT('PRVDY',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT PRVDY = &PRVDY;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.BF_LST_USR_AY01	AS USER_ID,
	A.PF_ACT			AS ACTION_CODE,
	RTRIM(B.PX_ACT_DSC)	AS ACTION_CODE_DESCRIPTION,  
	A.BC_ATY_TYP		AS ACTIVITY_TYPE,
	A.BC_ATY_CNC_TYP	AS CONTACT_TYPE, 
	A.BD_ATY_PRF		AS DATE,
	A.BX_CMT			AS COMMENTS,
	C.DF_SPE_ACC_ID
FROM OLWHRM1.AY01_BR_ATY A 
 	INNER JOIN OLWHRM1.AC01_ACT B
ON A.PF_ACT = B.PF_ACT
INNER JOIN OLWHRM1.US10_USR_PFL F
	ON A.BF_LST_USR_AY01 = F.PF_USR
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID = C.DF_PRS_ID
WHERE A.BD_ATY_PRF = &PRVDY
AND F.PC_SER_DPT = 'AUX'
AND PD_USR_IN_DPT_END IS NULL
FOR READ ONLY WITH UR
);

CREATE TABLE BSDTR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.LF_PRF_BY				AS USER_ID,
	A.PF_REQ_ACT			AS ACTION_CODE,
	RTRIM(B.PX_ACT_DSC_REQ)	AS ACD,  
	A.LC_ATY_RCP			AS ACTIVITY_TYPE,
	A.PF_RSP_ACT			AS RESPONSE_CODE, 
	A.LD_ATY_RSP			AS DATE,
	D.LX_ATY				AS COMMENTS,
	D.LF_LST_DTS_AY20		AS CDTSMP,
	D.LN_ATY_CMT_SEQ,
	C.LN_ATY_SEQ,
	G.DF_SPE_ACC_ID
FROM OLWHRM1.AY10_BR_LON_ATY A 
INNER JOIN OLWHRM1.AC10_ACT_REQ B
	ON A.PF_REQ_ACT = B.PF_REQ_ACT
INNER JOIN OLWHRM1.AY15_ATY_CMT C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_ATY_SEQ = C.LN_ATY_SEQ
INNER JOIN OLWHRM1.AY20_ATY_TXT D
 	ON C.BF_SSN = D.BF_SSN
	AND C.LN_ATY_SEQ = D.LN_ATY_SEQ
	AND C.LN_ATY_CMT_SEQ = D.LN_ATY_CMT_SEQ
INNER JOIN OLWHRM1.US10_USR_PFL F
	ON A.LF_PRF_BY = F.PF_USR
INNER JOIN OLWHRM1.PD10_PRS_NME G
	ON A.BF_SSN = G.DF_PRS_ID
WHERE A.LD_ATY_RSP = &PRVDY
AND F.PC_SER_DPT = 'AUX'
AND PD_USR_IN_DPT_END IS NULL
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=UTLWD06.LWD06RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA DEMO;
SET WORKLOCL.DEMO;
RUN;

DATA BSDTR;
SET WORKLOCL.BSDTR;
RUN;

PROC SORT DATA = BSDTR;
BY DF_SPE_ACC_ID 
   LN_ATY_SEQ 
   USER_ID
   ACTION_CODE
   ACD
   ACTIVITY_TYPE
   RESPONSE_CODE
   DATE;
RUN;

PROC TRANSPOSE DATA=BSDTR OUT=BSDTR (DROP=_NAME_ _LABEL_) PREFIX=CMT;
VAR COMMENTS;
BY DF_SPE_ACC_ID 
   LN_ATY_SEQ 
   USER_ID
   ACTION_CODE
   ACD
   ACTIVITY_TYPE
   RESPONSE_CODE
   DATE;
RUN;

PROC SQL;
CREATE TABLE BSSUM AS
SELECT 
	USER_ID
	,ACTION_CODE
	,ACD AS ACTION_CODE_DESCRIPTION
	,COUNT(ACTION_CODE) AS TTL_BY_CD
FROM BSDTR
GROUP BY USER_ID, ACTION_CODE, ACTION_CODE_DESCRIPTION;

DATA BSDTR (DROP=CMT1-CMT15);
LENGTH CMT1-CMT15 $ 360.;
SET BSDTR;
COMMENTS = TRIM(CMT1)||' '||TRIM(CMT2)||' '||TRIM(CMT3)||' '||TRIM(CMT4)||' '||TRIM(CMT5)||' '||
		   TRIM(CMT6)||' '||TRIM(CMT7)||' '||TRIM(CMT8)||' '||TRIM(CMT9)||' '||TRIM(CMT10)||' '||
		   TRIM(CMT11)||' '||TRIM(CMT12)||' '||TRIM(CMT13)||' '||TRIM(CMT14)||' '||TRIM(CMT15);		   
COMMENTS = TRIM(COMMENTS);
RUN;

PROC SORT DATA = BSDTR;
BY USER_ID DF_SPE_ACC_ID LN_ATY_SEQ;
RUN;

PROC SQL;
CREATE TABLE DEMOSUM AS
SELECT 
	USER_ID
	,ACTION_CODE
	,ACTION_CODE_DESCRIPTION
	,COUNT(ACTION_CODE) AS TTL_BY_CD
FROM DEMO
GROUP BY USER_ID, ACTION_CODE, ACTION_CODE_DESCRIPTION;

DATA DEMOSUM;
SET DEMOSUM BSSUM;
RUN;

PROC SORT DATA=DEMOSUM;
BY USER_ID ACTION_CODE;
RUN;

PROC SORT DATA = DEMO;
BY USER_ID DESCENDING ACTION_CODE;
RUN;

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS = 39 LS = 128;
OPTIONS PAGENO=1;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

PROC PRINT SPLIT='/' DATA=DEMO N="TOTAL:  " /*WIDTH=MIN*/;
BY USER_ID;
LABEL USER_ID = 'User ID'
ACTION_CODE = 'Action Code'
ACTION_CODE_DESCRIPTION = 'Action Code Description'
ACTIVITY_TYPE = 'Activity Type'
CONTACT_TYPE = 'Contact Type'
DF_SPE_ACC_ID = 'Acct #'
DATE = 'Date'
COMMENTS = 'Comments';
VAR USER_ID ACTION_CODE ACTION_CODE_DESCRIPTION ACTIVITY_TYPE CONTACT_TYPE DF_SPE_ACC_ID DATE;
FORMAT DATE MMDDYY.;
TITLE	"Auxiliary Services' Activity History Entries - OneLINK";
FOOTNOTE  'JOB = UTLWD06     REPORT = ULWD06.LWD06R2';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS PAGENO=1;
PROC PRINT SPLIT='/' DATA=DEMO N="TOTAL:  " /*WIDTH=MIN*/;
BY USER_ID;
LABEL COMMENTS = 'Comments';
VAR COMMENTS;
FORMAT COMMENTS $122.;
TITLE	"Auxiliary Services' Comments";
FOOTNOTE  'JOB = UTLWD06     REPORT = ULWD06.LWD06R3';
RUN;

PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS = 52 LS = 96;
OPTIONS PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=DEMOSUM  /*WIDTH=MIN*/;
BY 		USER_ID;
SUM 	TTL_BY_CD;
LABEL 	USER_ID = 'User ID'
		ACTION_CODE = 'Action Code'
		ACTION_CODE_DESCRIPTION = 'Action Code Description'
		TTL_BY_CD = 'Total Used';
VAR 	ACTION_CODE ACTION_CODE_DESCRIPTION TTL_BY_CD;
TITLE	"Auxiliary Services' Action Code/ARC Usage Summary";
FOOTNOTE  'JOB = UTLWD06     REPORT = ULWD06.LWD06R4';
RUN;

DATA _NULL_;
      CALL SYMPUT("RUNDT",LEFT(PUT("&SYSDATE"D,MMDDYY10.)));
RUN;
%MACRO FDATE(FMT);
   %GLOBAL FDATE;
   DATA _NULL_;
      CALL SYMPUT("FDATE",LEFT(PUT("&SYSDATE"D,&FMT)));
   RUN;
%MEND FDATE;
%FDATE(WORDDATE.);

PROC PRINTTO PRINT=REPORT5 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS = 39 LS = 128;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
PROC REPORT DATA= BSDTR HEADSKIP SPLIT='*' NOWD;
TITLE	"Auxiliary Services' Activity History Entries - COMPASS";
TITLE2 "RUN DATE: &FDATE";
FOOTNOTE 'JOB = UTLWD06     REPORT = ULWD06.LWD06R5';
COLUMN USER_ID ACTION_CODE ACD ACTIVITY_TYPE RESPONSE_CODE DF_SPE_ACC_ID DATE 
COMMENTS;
DEFINE USER_ID / DISPLAY "USER ID" WIDTH=9;
DEFINE ACTION_CODE /  "ACTION*CODE" WIDTH=6;
DEFINE ACD /  "ACTION CODE*DESCRIPTION" WIDTH=15;
DEFINE ACTIVITY_TYPE /  "ACTIVITY*TYPE" WIDTH=8;
DEFINE RESPONSE_CODE /  "RESPONSE*CODE" WIDTH=8;
DEFINE DF_SPE_ACC_ID /  'ACCT #';
DEFINE DATE /  'DATE' FORMAT=MMDDYY10.;
DEFINE COMMENTS / DISPLAY WIDTH=50 FLOW;
RUN;
