/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD10.LWD10R2";

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PNI25 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS BF_SSN
	,A.AF_APL_ID||A.AF_APL_ID_SFX AS CLUID
	,A.LF_CLM_ID

	,A.LA_CLM_PRI 
	+ A.LA_CLM_INT
	- A.LA_PRI_COL
	+A.LA_INT_ACR
	+C.LA_CLM_INT_ACR
	-A.LA_INT_COL			AS PNI

	,A.LA_LEG_CST_ACR
	-A.LA_LEG_CST_COL
	+A.LA_OTH_CHR_ACR
	-A.LA_OTH_CHR_COL
	+A.LA_COL_CST_ACR
	-A.LA_COL_CST_COL
	+C.LA_CLM_PRJ_COL_CST	AS OOC

	,A.LD_LST_PAY
	,A.LA_LST_PAY
	,B.LC_TRX_TYP			AS TRXTYP
FROM OLWHRM1.DC01_LON_CLM_INF A	
INNER JOIN OLWHRM1.DC02_BAL_INT C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = C.LF_CRT_DTS_DC10
LEFT OUTER JOIN OLWHRM1.DC11_LON_FAT B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND B.LF_CRT_DTS_DC10 = A.LF_CRT_DTS_DC10
	AND B.LD_TRX_EFF = A.LD_LST_PAY
	AND B.LC_RCI_TYP = 'CR'
WHERE A.LC_STA_DC10 = '03'
AND A.LD_CLM_ASN_DOE IS NULL
ORDER BY A.AF_APL_ID||A.AF_APL_ID_SFX 
);
DISCONNECT FROM DB2;
QUIT;

PROC TRANSPOSE DATA=PNI25 OUT=PNI25X (DROP=_NAME_ _LABEL_) PREFIX=TRX;
VAR TRXTYP;
BY CLUID;
RUN;

PROC SORT DATA=PNI25 (DROP=TRXTYP) NODUPKEY;
BY CLUID;
RUN;

DATA PNI25;
LENGTH TRX1 TRX2 TRX3 $ 2;
MERGE PNI25 PNI25X;
BY CLUID;
RUN;

PROC SQL;
CREATE TABLE PNI25B AS
SELECT A.*
FROM PNI25 A
INNER JOIN
	(SELECT BF_SSN
	FROM PNI25 B
	GROUP BY BF_SSN
	HAVING SUM(PNI) < 25
	AND SUM(OOC) > 100) B
	ON A.BF_SSN = B.BF_SSN
ORDER BY A.BF_SSN
;
QUIT;

PROC SQL;
CREATE TABLE PNI25X AS
SELECT BF_SSN
,SUM(PNI) AS PNI_SUM FORMAT=14.2
,SUM(OOC) AS OOC_SUM FORMAT=14.2
,SUM(PNI) + SUM(OOC) AS TOT_SUM FORMAT=14.2
FROM PNI25B
GROUP BY BF_SSN
ORDER BY BF_SSN;
QUIT;

DATA PNI25B;
MERGE PNI25B PNI25X;
BY BF_SSN;
TOT_SUM_PR = TOT_SUM;
IF FIRST.BF_SSN THEN ACCT = 1;
ELSE ACCT = 0;
RUN;

endrsubmit  ;
DATA PNI25B; 
SET WORKLOCL.PNI25B;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=126;
PROC REPORT DATA=PNI25B NOWD SPACING=2 HEADSKIP SPLIT='/';
TITLE 'ACCOUNTS WITH P&I < $25.00';
TITLE2 "AND REMAINING COSTS > $100.00";
TITLE3 "RUN DATE:  &RUNDATE";
FOOTNOTE  'JOB = UTLWD10     REPORT = ULWD10.LWD10R2';
COLUMN TOT_SUM BF_SSN PNI_SUM OOC_SUM TOT_SUM_PR LF_CLM_ID 
LD_LST_PAY LA_LST_PAY TRX1 TRX2 TRX3 ACCT;
DEFINE TOT_SUM / NOPRINT GROUP;
DEFINE BF_SSN / GROUP "SSN" FORMAT=SSN11. LEFT;
DEFINE PNI_SUM / GROUP 'COMBINED/P&I BALANCE' FORMAT=COMMA12.2;
DEFINE OOC_SUM / GROUP "COMBINED/OTHER COSTS/BALANCE" FORMAT=COMMA12.2;
DEFINE TOT_SUM_PR / GROUP "TOTAL/BALANCE" FORMAT=COMMA12.2;
DEFINE LF_CLM_ID / DISPLAY "CLAIM/ID" WIDTH=5 CENTER;
DEFINE LD_LST_PAY / DISPLAY "LAST PAY/DATE" FORMAT=MMDDYY10. CENTER;
DEFINE LA_LST_PAY / DISPLAY "LAST PAY/AMOUNT" FORMAT=COMMA12.2;
DEFINE TRX1 / DISPLAY "TRANS/TYPE 1" WIDTH=10 CENTER;
DEFINE TRX2 / DISPLAY "TRANS/TYPE 2" WIDTH=10 CENTER;
DEFINE TRX3 / DISPLAY "TRANS/TYPE 3" WIDTH=10 CENTER;
DEFINE ACCT / ANALYSIS SUM NOPRINT;

COMPUTE AFTER;
LINE " ";
LINE "TOTAL NUMBER OF ACCOUNTS:  " ACCT.SUM;
ENDCOMP;
RUN;

PROC PRINTTO;
RUN;
