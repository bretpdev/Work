/*UTLWK11 SKIP ACCOUNT - 255 PLUS DAYS DELINQUENT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWK11.LWK11R2";
FILENAME REPORT5 "&RPTLIB/ULWK11.LWK11R5";
FILENAME REPORTZ "&RPTLIB/ULWK11.LWK11RZ";
OPTIONS SYMBOLGEN;

LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,PD10.DF_SPE_ACC_ID
	,DAYS(CURRENT DATE) - DAYS(W.LD_DLQ_OCC) AS DAYS_DEL
	,D.DC_SKP_TYP
	,G.IF_WRK_GRP AS ONEQ
	,F.WF_QUE AS COMPQ
	,J.START_DATE
FROM OLWHRM1.LN65_LON_RPS A
INNER JOIN OLWHRM1.LN10_LON B
ON A.BF_SSN = B.BF_SSN
AND A.LN_SEQ = B.LN_SEQ
AND B.LC_STA_LON10 = 'R'
INNER JOIN (SELECT DISTINCT BF_SSN, LN_SEQ,MAX(LD_DLQ_OCC) AS LD_DLQ_OCC
				FROM OLWHRM1.LN16_LON_DLQ_HST W
				WHERE DAYS(CURRENT DATE) - DAYS(W.LD_DLQ_OCC) >= 255
				AND W.LC_STA_LON16 = '1'
				GROUP BY BF_SSN, LN_SEQ) W
ON A.BF_SSN = W.BF_SSN
AND A.LN_SEQ = W.LN_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C
ON A.BF_SSN = C.BF_SSN
AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.PD27_PRS_SKP_PRC D
ON A.BF_SSN = D.BF_SSN
INNER JOIN OLWHRM1.AY10_BR_LON_ATY E
ON A.BF_SSN = E.BF_SSN
LEFT OUTER JOIN OLWHRM1.WQ20_TSK_QUE F
ON A.BF_SSN = F.BF_SSN
AND F.WC_STA_WQUE20 = 'A'
LEFT OUTER JOIN OLWHRM1.CT30_CALL_QUE G
ON A.BF_SSN = G.DF_PRS_ID_BR
INNER JOIN OLWHRM1.PD30_PRS_ADR H
	ON A.BF_SSN = H.DF_PRS_ID
	AND H.DC_ADR = 'L'
INNER JOIN OLWHRM1.PD42_PRS_PHN I
	ON A.BF_SSN = I.DF_PRS_ID
	AND I.DC_PHN = 'H'
INNER JOIN (
	SELECT DF_PRS_ID
		,MAX(X.VLD_DT) AS START_DATE
	FROM (
			SELECT DF_PRS_ID
				,DD_VER_ADR AS VLD_DT
			FROM OLWHRM1.PD30_PRS_ADR
			WHERE DI_VLD_ADR = 'N'
			AND DC_ADR = 'L'
		UNION
			SELECT DF_PRS_ID
				,DD_PHN_VER AS VLD_DT
			FROM OLWHRM1.PD42_PRS_PHN
			WHERE DI_PHN_VLD = 'N'
			AND DC_PHN = 'H'
			) X
	GROUP BY DF_PRS_ID
	) J
	ON A.BF_SSN = J.DF_PRS_ID
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON PD10.DF_PRS_ID = A.BF_SSN
WHERE A.LC_STA_LON65 = 'A'
AND B.LA_CUR_PRI + C.WA_TOT_BRI_OTS > 0
AND (H.DI_VLD_ADR = 'N' OR I.DI_PHN_VLD = 'N')
AND DAYS(J.START_DATE) <  DAYS(W.LD_DLQ_OCC) + 240
AND DAYS(J.START_DATE) <= (
	SELECT DAYS(MAX(X.LD_ATY_RSP))
	FROM OLWHRM1.AY10_BR_LON_ATY X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.PF_REQ_ACT = 'DLFDL'
	)
FOR READ ONLY WITH UR
);

CREATE TABLE INVAD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT BF_SSN
	,X.LD_ATY_RSP AS FDLNS
FROM OLWHRM1.AY10_BR_LON_ATY X
WHERE X.PF_REQ_ACT = 'DLFDL'
AND X.PF_RSP_ACT = 'INVAD'
FOR READ ONLY WITH UR
);

CREATE TABLE KDSKPA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DF_PRS_ID AS SSN
	,MAX(X.BD_ATY_PRF) AS TIP
FROM OLWHRM1.AY01_BR_ATY X
WHERE X.PF_ACT IN ('KDSKP','KSEFR','KUBFR','KSBFR')
GROUP BY DF_PRS_ID
FOR READ ONLY WITH UR
);

DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
ENDRSUBMIT;

DATA DEMO; SET WORKLOCL.DEMO; RUN;
DATA INVAD; SET WORKLOCL.INVAD; RUN;
DATA KDSKPA; SET WORKLOCL.KDSKPA; RUN;
PROC SORT DATA=INVAD;
	BY BF_SSN FDLNS;
RUN;
DATA INVAD;
SET INVAD;
BY BF_SSN FDLNS;
IF LAST.BF_SSN THEN OUTPUT;
RUN;
PROC SQL;
CREATE TABLE KDSKP AS
SELECT DISTINCT SSN
	,TIP
	,TRIM(LEFT(PUT(MONTH(TIP),8.)))||'/'||
	TRIM(LEFT(PUT(DAY(TIP),8.)))||'/'||
	TRIM(LEFT(PUT(YEAR(TIP),8.))) AS TIPS
FROM KDSKPA 
;
QUIT;

PROC SQL;
CREATE TABLE DEMO2 AS
SELECT DISTINCT 
	A.SSN
	,A.DF_SPE_ACC_ID
	,START_DATE
	,DC_SKP_TYP
	,DAYS_DEL
	,CASE
		WHEN FDLNS = . THEN ' ' 
		ELSE PUT(FDLNS,MMDDYY10.)
	 END AS FDLNS
	,CASE WHEN C.TIP IS NULL 
		THEN 'Not Started'
		ELSE C.TIPS
	END AS TIPS
	,ONEQ
	,COMPQ
	,TIP
FROM DEMO A
LEFT OUTER JOIN INVAD B
	ON A.SSN = B.BF_SSN
LEFT OUTER JOIN KDSKP C
	ON A.SSN = C.SSN
;
QUIT;

DATA EX (KEEP=SSN ONEQ TIP START_DATE);
SET DEMO2;
WHERE ONEQ = 'SKPACCEL' OR
TIP > START_DATE;
RUN;

PROC SQL;
CREATE TABLE REP2 AS
SELECT DISTINCT SSN AS TARGET_ID
	,DF_SPE_ACC_ID
	,'KFINALRV' AS QUEUE_NAME
	,'' AS INSTITUTION_ID
	,'' AS INSTITUTION_TYPE
	,'' AS DATE_DUE 
	,'' AS TIME_DUE 
	,'' AS COMMENTS
FROM DEMO2
WHERE SSN NOT IN (
	SELECT DISTINCT SSN 
	FROM EX
	)
;
QUIT;

PROC SQL;
CREATE TABLE REP5 AS 
SELECT DISTINCT SSN
	,DF_SPE_ACC_ID
	,START_DATE
	,DC_SKP_TYP
	,DAYS_DEL
	,FDLNS
	,TIPS
FROM DEMO2 
WHERE SSN NOT IN (
	SELECT DISTINCT SSN 
	FROM EX
	)
AND DAYS_DEL >= 265
;
QUIT;
DATA _NULL_;
SET  WORK.REP2; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
DATA _NULL_;
SET REP5 ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = START_DATE;
DESCRIPTION = 
		TRIM(DF_SPE_ACC_ID) ||
		',' || TRIM(PUT(START_DATE, MMDDYY10.)) ||
		',' || TRIM(DC_SKP_TYP) ||
		',' || TRIM(LEFT(DAYS_DEL)) ||
		',' || TRIM(FDLNS) ||
		',' || TRIM(TIPS);
FILE REPORT5 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
