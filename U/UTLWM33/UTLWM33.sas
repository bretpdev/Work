*---------------------------------------------------------*
| UTLWM33 INCREASE COLLECTION REVENUE - BILLING STATEMENT |
*---------------------------------------------------------*;
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM33.LWM33R2";
FILENAME REPORT3 "&RPTLIB/ULWM33.LWM33R3";
FILENAME REPORTZ "&RPTLIB/ULWM33.LWM33RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
PROC SQL;
CREATE TABLE ICRBSA AS
SELECT DISTINCT GA10.AF_APL_ID||GA10.AF_APL_ID_SFX AS CLUID
	,BDEM.DF_PRS_ID AS BF_SSN
	,BDEM.DF_SPE_ACC_ID 
	,BDEM.DM_PRS_1
	,BDEM.DM_PRS_LST
	,BDEM.DX_STR_ADR_1
	,BDEM.DX_STR_ADR_2
	,BDEM.DM_CT
	,BDEM.DC_DOM_ST
	,BDEM.DF_ZIP
	,BDEM.DM_FGN_CNY
	,BDEM.DC_ADR
	,EDEM.DF_PRS_ID 	AS EDR_SSN
	,EDEM.DM_PRS_1 		AS EDR_1_NM
	,EDEM.DM_PRS_LST	AS EDR_LST_NM
	,EDEM.DX_STR_ADR_1 	AS EDR_ADD_1
	,EDEM.DX_STR_ADR_2 	AS EDR_ADD_2
	,EDEM.DM_CT			AS EDR_DM_CT
	,EDEM.DC_DOM_ST		AS EDR_DC_DOM_ST
	,EDEM.DF_ZIP		AS EDR_DF_ZIP
	,EDEM.DM_FGN_CNY	AS EDR_DM_FGN_CNY
	,DC01.LD_LST_PAY
	,DC01.LA_LST_PAY
	,DC01.LA_MIN_PAY
	,DC01.LA_PAY_XPC
	,DC01.LD_NXT_PAY_DUE
	,DC01.LA_CLM_PRI +
		DC01.LA_CLM_INT -
		DC01.LA_PRI_COL +
		DC01.LA_INT_ACR +
		COALESCE(DC02.LA_CLM_INT_ACR,0) -
		DC01.LA_INT_COL + 
		DC01.LA_LEG_CST_ACR -
		DC01.LA_LEG_CST_COL + 
		DC01.LA_OTH_CHR_ACR -
		DC01.LA_OTH_CHR_COL +
		DC01.LA_COL_CST_ACR -
		DC01.LA_COL_CST_COL +
		COALESCE(DC02.LA_CLM_PRJ_COL_CST,0) AS OB	
FROM DLGSUTWH.GA01_APP GA01
INNER JOIN DLGSUTWH.GA10_LON_APP GA10
	ON GA01.AF_APL_ID = GA10.AF_APL_ID
INNER JOIN DLGSUTWH.DC01_LON_CLM_INF DC01
	ON DC01.AF_APL_ID = GA10.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = GA10.AF_APL_ID_SFX
INNER JOIN (SELECT AF_APL_ID
				,AF_APL_ID_SFX
				,MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
		FROM DLGSUTWH.DC01_LON_CLM_INF DC01
		GROUP BY AF_APL_ID,AF_APL_ID_SFX) Z
	ON DC01.AF_APL_ID = Z.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = Z.AF_APL_ID_SFX
	AND DC01.LF_CRT_DTS_DC10 = Z.LF_CRT_DTS_DC10
INNER JOIN DLGSUTWH.PD01_PDM_INF BDEM
	ON DC01.BF_SSN = BDEM.DF_PRS_ID
LEFT OUTER JOIN DLGSUTWH.DC02_BAL_INT DC02
	ON DC01.AF_APL_ID = DC02.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
	AND DC01.LF_CRT_DTS_DC10 = DC02.LF_CRT_DTS_DC10
LEFT OUTER JOIN DLGSUTWH.PD01_PDM_INF EDEM
	ON GA01.DF_PRS_ID_EDS = EDEM.DF_PRS_ID
	AND EDEM.DI_VLD_ADR = 'Y'
WHERE BDEM.DI_VLD_ADR = 'Y'
	AND DC01.LC_STA_DC10 = '03'
	AND DC01.LD_LST_BIL_SNT >= today() - 5
	AND DC01.LD_CLM_ASN_DOE = .
	AND DC01.LC_AUX_STA NOT IN ('05','07','10')
;
QUIT;
ENDRSUBMIT;
DATA ICRBSA ;
SET WORKLOCL.ICRBSA;
RUN;

PROC SORT DATA=ICRBSA; BY BF_SSN CLUID EDR_SSN;RUN;

*CALCULATE KEYLINE;
DATA ICRBSA (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET ICRBSA;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
/*DATA SET MACRO*/
%MACRO ENTDS(NEWDS,SEL_STR,CRIT,GRP_STR);
PROC SQL;
	CREATE TABLE &NEWDS AS 
		SELECT &SEL_STR
			,SUM(OB) AS OB
			,SUM(LA_LST_PAY) AS LA_LST_PAY
			,MAX(LD_LST_PAY) AS LD_LST_PAY
			,MAX(COALESCE(LD_NXT_PAY_DUE,mdy(month(today()),22,year(today())))) AS LD_NXT_PAY_DUE 
			,CASE
				WHEN SUM(LA_PAY_XPC) > 0
					THEN SUM(LA_PAY_XPC)
				ELSE SUM(OB)
			 END AS AMT_DUE
		FROM ICRBSA 
		WHERE &CRIT
		GROUP BY &GRP_STR 
	;
QUIT;
%MEND ENTDS;
/*CREATE BORROWER DATA SET*/
%ENTDS(BORD
	,%STR(BF_SSN,DF_SPE_ACC_ID,DM_PRS_1,DM_PRS_LST,DX_STR_ADR_1,
		DX_STR_ADR_2,DM_CT,DC_DOM_ST,DF_ZIP,DM_FGN_CNY,ACSKEY,TODAY() AS CUR_DT)
	,BF_SSN NE ''
	,%STR(BF_SSN,DF_SPE_ACC_ID,DM_PRS_1,DM_PRS_LST,DX_STR_ADR_1,
		DX_STR_ADR_2,DM_CT,DC_DOM_ST,DF_ZIP,DM_FGN_CNY,ACSKEY,CUR_DT)
	);
/*CREATE ENDORSER DATA SET*/
%ENTDS(EDSD	
	,%STR(BF_SSN,DF_SPE_ACC_ID,EDR_1_NM AS DM_PRS_1,EDR_LST_NM AS DM_PRS_LST,
		EDR_ADD_1 AS DX_STR_ADR_1,EDR_ADD_2 AS DX_STR_ADR_2,EDR_DM_CT AS DM_CT,
		EDR_DC_DOM_ST AS DC_DOM_ST,EDR_DF_ZIP AS DF_ZIP,EDR_DM_FGN_CNY AS DM_FGN_CNY,ACSKEY,
		TODAY() AS CUR_DT)
	,EDR_SSN NE ''
	,%STR(BF_SSN,DF_SPE_ACC_ID,EDR_1_NM,EDR_LST_NM,EDR_ADD_1,EDR_ADD_2,EDR_DM_CT,EDR_DC_DOM_ST,
		EDR_DF_ZIP,EDR_DM_FGN_CNY,ACSKEY,CUR_DT)
	);


/*REPORT MACRO*/
%MACRO CRT_FILE(DS,RNO);
DATA &DS;
	SET &DS;
	IF DC_DOM_ST = 'FC' THEN SVAR = 1;
	ELSE SVAR = 2;
RUN;
PROC SORT DATA=&DS; 
	BY SVAR DC_DOM_ST;
RUN;
DATA _NULL_;
	SET &DS ;
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT CUR_DT WORDDATE12. LD_LST_PAY LD_NXT_PAY_DUE MMDDYY10. AMT_DUE LA_LST_PAY OB BEST12.;
	IF _N_ = 1 THEN DO;
	   PUT
		'BF_SSN,TODAY,DF_SPE_ACC_ID,DM_PRS_1,DM_PRS_LST,DX_STR_ADR_1,DX_STR_ADR_2,DM_CT,DC_DOM_ST,'
		'DF_ZIP,DM_FGN_CNY,ACSKEY,LD_LST_PAY,LA_LST_PAY,OUTSTANDING BALANCE,AMT_DUE,LD_NXT_PAY_DUE'
		',STATE_INDICATOR,COST_CENTER_CODE';
	END;
	DO;
		PUT BF_SSN $ @;
		PUT CUR_DT @;
	    PUT DF_SPE_ACC_ID $ @;
	    PUT DM_PRS_1 $ @;
	    PUT DM_PRS_LST $ @;
	    PUT DX_STR_ADR_1 $ @;
	    PUT DX_STR_ADR_2 $ @;
	    PUT DM_CT $ @;
	    PUT DC_DOM_ST $ @;
	    PUT DF_ZIP $ @;
	    PUT DM_FGN_CNY $ @;
	    PUT ACSKEY $ @;
	    PUT LD_LST_PAY @;
	    PUT LA_LST_PAY @;
		PUT OB @;
	    PUT AMT_DUE @;
	    PUT LD_NXT_PAY_DUE @;
		PUT DC_DOM_ST $ @;
	    PUT "MA2329" ;
	END;
RUN;
%MEND CRT_FILE;

/*CREATE REPORTS*/
%CRT_FILE(BORD,2);
%CRT_FILE(EDSD,3);
