/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=WORK ;
FILENAME REPORTZ "&RPTLIB/UNWS82.NWS82RZ";
FILENAME REPORT2 "&RPTLIB/UNWS82.NWS82R2";

RSUBMIT ;
LIBNAME PKUB DB2 DATABASE=DNFPUTDL OWNER=PKUB;

DATA _NULL_;
	RUN_DAY = TODAY();
	CALL SYMPUT('FIRST_DAY_CURR_MON',"'"||PUT(INTNX('MONTH',RUN_DAY,0,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('LAST_DAY_CURR_MON',"'"||PUT(INTNX('MONTH',RUN_DAY,0,'E'), MMDDYYD10.)||"'");
RUN;

PROC SQL;
	CONNECT TO DB2 (DATABASE=DNFPUTDL);
	CREATE TABLE LN_DET0 AS
		SELECT
			*
		FROM 
			CONNECTION TO DB2 
				(
					SELECT DISTINCT
						LN10.BF_SSN,
						LN10.LN_SEQ,
						COALESCE(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS,0) AS BAL_OUTSTANDING,
						CASE
							WHEN LN10.LA_CUR_PRI <= 0 THEN 0
							WHEN LN10.LC_STA_LON10 = 'D' THEN DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC)
							WHEN DW01.WC_DW_LON_STA IN ('04','05') THEN COALESCE(LN16.LN_DLQ_MAX,0)
							ELSE COALESCE(LN16.LN_DLQ_MAX + 1,0)
                        END AS LN_DLQ_MAX,
						CASE
							WHEN FORB.BF_SSN IS NOT NULL THEN 1
							ELSE 0
						END AS SPEC_FORB_IND,
						DW01.WC_DW_LON_STA,
						CASE
							WHEN LN80.BF_SSN IS NULL THEN 0
							ELSE 1
						END AS BILL_SATISFIED
					FROM 
						PKUB.LN10_LON LN10
						LEFT JOIN PKUB.DW01_DW_CLC_CLU DW01
							ON LN10.BF_SSN = DW01.BF_SSN
							AND LN10.LN_SEQ = DW01.LN_SEQ
						LEFT JOIN PKUB.LN16_LON_DLQ_HST LN16
							ON LN10.BF_SSN = LN16.BF_SSN
							AND LN10.LN_SEQ = LN16.LN_SEQ
							AND LN16.LC_STA_LON16 = '1'
							AND LN16.LC_DLQ_TYP = 'P'
						/*ASSIGN INDICATOR FOR FORBEARANCES*/
						LEFT JOIN
							(
								SELECT 
									FB10.BF_SSN
								FROM 
									PKUB.FB10_BR_FOR_REQ FB10
									INNER JOIN PKUB.LN60_BR_FOR_APV LN60
										ON FB10.BF_SSN = LN60.BF_SSN
										AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM
								WHERE
									FB10.LC_FOR_STA = 'A'
									AND LN60.LC_STA_LON60 = 'A'
									AND FB10.LC_STA_FOR10 = 'A'      
									AND FB10.LC_FOR_TYP IN ('13','14','33','44','10','13','14')
									AND LN60.LD_FOR_END >= CURRENT_DATE
									AND LN60.LD_FOR_APL <= CURRENT_DATE
									AND LN60.LD_FOR_BEG <= CURRENT_DATE
							) FORB
							ON FORB.BF_SSN = LN10.BF_SSN	
						LEFT JOIN PKUB.LN80_LON_BIL_CRF LN80
							ON LN10.BF_SSN = LN80.BF_SSN
							AND LN10.LN_SEQ = LN80.LN_SEQ
							AND LN80.LD_BIL_DU_LON BETWEEN &FIRST_DAY_CURR_MON AND &LAST_DAY_CURR_MON
							AND LN80.LA_TOT_BIL_STS >= COALESCE(LN80.LA_BIL_CUR_DU,0)
							AND LN80.LC_STA_LON80 = 'A'
							AND LN80.LC_BIL_TYP_LON = 'P'
					WHERE 
						LN10.LC_STA_LON10 IN ('R','L','D')
						AND LN10.LA_CUR_PRI > 0
						AND NOT (LN10.LC_STA_LON10 = 'D' AND LN10.LA_CUR_PRI = 0)
						AND LN10.LD_PIF_RPT IS NULL
					ORDER BY
						LN10.BF_SSN

				FOR READ ONLY WITH UR
			)
	;
	DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CONNECT TO DB2 (DATABASE=DNFPUTDL);
	CREATE TABLE MILT_STA AS
		SELECT
			*
		FROM
			CONNECTION TO DB2
				(
					SELECT DISTINCT
						AY10.BF_SSN,
						CASE 
							WHEN AY10.PF_REQ_ACT = 'ASCRA' THEN 'Y'
							ELSE 'N' 
						END AS IS_ACTIVE_MILT
					FROM
						PKUB.AY10_BR_LON_ATY AY10
						JOIN 
							(
								SELECT
									BF_SSN,
									MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
								FROM
									PKUB.AY10_BR_LON_ATY
								WHERE
									PF_REQ_ACT IN ('ASCRA','ISCRA')
								GROUP BY
									BF_SSN
							) MSCRA
							ON AY10.BF_SSN = MSCRA.BF_SSN
							AND AY10.LD_ATY_REQ_RCV = MSCRA.LD_ATY_REQ_RCV
							AND AY10.PF_REQ_ACT IN ('ASCRA','ISCRA')
				)
	;

	CREATE TABLE LN_DET1 AS
		SELECT DISTINCT
			LN.*,
			CATS(LN.BF_SSN,PUT(LN.LN_SEQ,Z4.)) AS LNID,
			COALESCE(MSTAB.IS_ACTIVE_MILT,MSTAE.IS_ACTIVE_MILT,'N') AS IS_ACTIVE_MILT
		FROM
			LN_DET0 LN
			LEFT JOIN MILT_STA MSTAB
				ON LN.BF_SSN = MSTAB.BF_SSN
			LEFT JOIN PKUB.LN20_EDS LN20
				ON LN.BF_SSN = LN20.BF_SSN
			LEFT JOIN MILT_STA MSTAE
				ON LN20.LF_EDS = MSTAE.BF_SSN
	;

DISCONNECT FROM DB2;
QUIT;

ENDRSUBMIT;
DATA LN_DET1; SET LEGEND.LN_DET1 (OBS=1000); RUN;

DATA LN_DET1 STATUS_ERR;
	SET LN_DET1;
	IF WC_DW_LON_STA IN ('06','88','98') THEN OUTPUT STATUS_ERR;
	ELSE OUTPUT LN_DET1;
RUN;

/*assign categories*/
%MACRO ASSIGN_CATS(INFILE,OUTFILE,ERRFILE);

	DATA &OUTFILE &ERRFILE;
		SET &INFILE;

		IF IS_ACTIVE_MILT = 'Y' THEN PF = '04';
		ELSE IF WC_DW_LON_STA IN ('02','23') THEN PF = '01';
		ELSE IF WC_DW_LON_STA = '01' THEN PF = '02';
		ELSE IF WC_DW_LON_STA IN ('03','20','09','10','11','15','16','18','20','22', '17','19','21') THEN 
			DO; /*22 WILL BE IN THIS GROUP, BUT NOT ALL LOANS PIF*/
				IF SPEC_FORB_IND = 1 AND LN_DLQ_MAX = 0 THEN PF = '06';
				ELSE IF 0 <= LN_DLQ_MAX <= 5 THEN PF = '03';
				ELSE IF LN_DLQ_MAX <= 30 THEN PF = '07';
				ELSE IF LN_DLQ_MAX <= 90 THEN PF = '08';
				ELSE IF LN_DLQ_MAX <= 150 THEN PF = '09';
				ELSE IF LN_DLQ_MAX <= 270 THEN PF = '10';
				ELSE IF LN_DLQ_MAX <= 360 THEN PF = '11';
				ELSE IF LN_DLQ_MAX > 360 THEN PF = '12';
				ELSE PF = '99';
			END;
		ELSE IF WC_DW_LON_STA = '04' THEN 
			DO;
				IF LN_DLQ_MAX = 0 AND BILL_SATISFIED = 1 THEN PF = '03';
				ELSE IF LN_DLQ_MAX = 0 THEN PF = '05';
				ELSE IF 1 <= LN_DLQ_MAX <= 5 THEN PF = '03';
				ELSE IF LN_DLQ_MAX <= 30 THEN PF = '07';
				ELSE IF LN_DLQ_MAX <= 90 THEN PF = '08';
				ELSE IF LN_DLQ_MAX <= 150 THEN PF = '09';
				ELSE IF LN_DLQ_MAX <= 270 THEN PF = '10';
				ELSE IF LN_DLQ_MAX <= 360 THEN PF = '11';
				ELSE IF LN_DLQ_MAX > 360 THEN PF = '12';
				ELSE PF = '99';
			END;
		ELSE IF WC_DW_LON_STA = '05' THEN
			DO;
				IF LN_DLQ_MAX = 0 AND BILL_SATISFIED = 1 THEN PF = '03';
				ELSE IF LN_DLQ_MAX = 0 THEN PF = '06';
				ELSE IF 1 <= LN_DLQ_MAX <= 5 THEN PF = '03';
				ELSE IF LN_DLQ_MAX <= 30 THEN PF = '07';
				ELSE IF LN_DLQ_MAX <= 90 THEN PF = '08';
				ELSE IF LN_DLQ_MAX <= 150 THEN PF = '09';
				ELSE IF LN_DLQ_MAX <= 270 THEN PF = '10';
				ELSE IF LN_DLQ_MAX <= 360 THEN PF = '11';
				ELSE IF LN_DLQ_MAX > 360 THEN PF = '12';
				ELSE PF = '99';
			END;
		ELSE IF LN_DLQ_MAX <= 5 THEN PF = '03';
		ELSE IF LN_DLQ_MAX <= 30 THEN PF = '07';
		ELSE IF LN_DLQ_MAX <= 90 THEN PF = '08';
		ELSE IF LN_DLQ_MAX <= 150 THEN PF = '09';
		ELSE IF LN_DLQ_MAX <= 270 THEN PF = '10';
		ELSE IF LN_DLQ_MAX <= 360 THEN PF = '11';
		ELSE IF LN_DLQ_MAX > 360 THEN PF = '12';
		ELSE PF = '99';
	RUN;

	DATA &OUTFILE;
		SET &OUTFILE;
		WHERE PF IN ('03','07','08','09','10','11','12');

		IF PF IN ('03') THEN CAT = '1RPAY'; *repayment;
		ELSE IF PF IN ('09','10') THEN CAT = '2DELQ'; *delinquent;
		ELSE IF PF IN ('11') THEN CAT = '3DFLT'; *default;
		ELSE CAT = '4OTH'; *other;
	RUN;
%MEND;

%ASSIGN_CATS(LN_DET1,LN_DET,OTH_LNS);

PROC FREQ DATA=LN_DET NOPRINT;
	TABLE BF_SSN*WC_DW_LON_STA / OUT=STAT_TAB(DROP=PERCENT) ;
RUN;

PROC SORT DATA=STAT_TAB;
	BY BF_SSN COUNT;
RUN;

DATA STAT_TAB(KEEP=BF_SSN WC_DW_LON_STA);
	SET STAT_TAB;
	BY BF_SSN;
	IF LAST.BF_SSN;
RUN;

/*CREATE BORROWER LEVEL DATA SET */
PROC SQL;
	CREATE TABLE BRW_DET0 AS 
		SELECT DISTINCT 
			LN.BF_SSN,
			STAT.WC_DW_LON_STA,
			LN.SPEC_FORB_IND,
			LN.BILL_SATISFIED,
			MAX(LN.LN_DLQ_MAX) AS LN_DLQ_MAX,
			SUM(LN.BAL_OUTSTANDING) AS BAL_OUTSTANDING,
			LN.IS_ACTIVE_MILT
		FROM
			LN_DET LN
			JOIN STAT_TAB STAT
				ON LN.BF_SSN = STAT.BF_SSN
		GROUP BY 
			LN.BF_SSN
		ORDER BY 
			LN.BF_SSN
	;
QUIT;

PROC SORT DATA=BRW_DET0 DUPOUT=DUPDS NODUPKEY; 
	BY BF_SSN; 
RUN;

%ASSIGN_CATS(BRW_DET0,BRW_DET,OTH_BRWS);

PROC SQL;
	CREATE TABLE CNTS_TTL AS
		SELECT
			COUNT(DISTINCT BF_SSN) AS BRWS,
			COUNT(LNID) AS LNS,
			SUM(BAL_OUTSTANDING) AS TTL_BAL
		FROM
			LN_DET
	;

	CREATE TABLE CNTS_BRWS AS
		SELECT DISTINCT
			BRW.CAT,
			COUNT(BRW.BF_SSN) AS BRWS,
			COUNT(BRW.BF_SSN)/TTL.BRWS AS PCNT_BRWS,
			SUM(BRW.BAL_OUTSTANDING) AS BAL_OUTSTANDING,
			SUM(BRW.BAL_OUTSTANDING)/TTL.TTL_BAL AS PCNT_BAL
		FROM
			BRW_DET BRW
			JOIN CNTS_TTL TTL
				ON 1 = 1
		GROUP BY
			BRW.CAT
	;

	CREATE TABLE CNTS_LNS AS
		SELECT DISTINCT
			LN.CAT,
			COUNT(LN.LNID) AS LNS,
			COUNT(LN.LNID)/TTL.LNS AS PCNT_LNS
		FROM
			LN_DET LN
			JOIN CNTS_TTL TTL
				ON 1 = 1
		GROUP BY
			LN.CAT
	;

	CREATE TABLE CNTS_OUT AS
		SELECT
			BRW.CAT,
			BRW.BRWS,
			BRW.PCNT_BRWS,
			BRW.BAL_OUTSTANDING,
			BRW.PCNT_BAL,
			LN.LNS,
			LN.PCNT_LNS
		FROM
			CNTS_BRWS BRW
			JOIN CNTS_LNS LN
				ON BRW.CAT = LN.CAT
		ORDER BY
			BRW.CAT
	;
QUIT;

PROC FORMAT;
	VALUE $CAT
		'1RPAY' = 'Current Repay (0-5 Days Delq)'	
		'2DELQ' = 'Delinquent Borrowers (90-270 Days Delq)'
		'3DFLT' = 'Defaulted Borrowers (271-360 days Past Due)'
		'4OTH' = 'Other'		
	;
QUIT;


PROC PRINTTO PRINT=REPORT2 NEW; RUN;

OPTIONS ORIENTATION=LANDSCAPE PS=39 LS=127;
TITLE 		'FSA Daily Delinquency Metrics - FED';
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT UHEAA HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWS82  	 REPORT = UNWS82.NWS82R2';

PROC PRINT 
		NOOBS SPLIT = '\' 
		DATA = CNTS_OUT 
		WIDTH = UNIFORM 
		WIDTH = MIN 
		LABEL;

	WHERE CAT IN ('1RPAY','2DELQ','3DFLT');

	FORMAT
		CAT $CAT.
		PCNT_BRWS PCNT_BAL PCNT_LNS PERCENT10.3
		BAL_OUTSTANDING DOLLAR18.2
	;

	VAR 
		CAT
		BRWS
		PCNT_BRWS 
		BAL_OUTSTANDING
		PCNT_BAL 
		LNS
		PCNT_LNS
	;

	LABEL
		CAT = ''
		BRWS = '# Borrowers'
		PCNT_BRWS = '% of\ Borrowers/\Portfolio\ Total'
		BAL_OUTSTANDING = 'Balance\ Outstanding'
		PCNT_BAL = '% of\ Balance\ Outstanding/\Portfolio\ Total' 
		LNS = '# Loans'
		PCNT_LNS = '% of\ Loans/\Portfolio\ Total'
	;
RUN;

PROC PRINTTO; RUN;		

/*PROC EXPORT*/
/*		DATA=LN_DET1*/
/*		OUTFILE='T:\SAS\LN_DET1.XLSX'*/
/*		REPLACE;*/
/*RUN;*/
