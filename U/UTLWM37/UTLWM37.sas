/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM37.LWM37R2";
FILENAME REPORT3 "&RPTLIB/ULWM37.LWM37R3";
FILENAME REPORTZ "&RPTLIB/ULWM37.LWM37RZ";


OPTIONS SYMBOLGEN NOCENTER NODATE NONUMBER LS=132;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;


PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (

SELECT PD01.DF_SPE_ACC_ID
	,SUM(DC01.LA_CLM_PRI 
		+DC01.LA_CLM_INT
		-DC01.LA_PRI_COL
		+DC01.LA_INT_ACR
		+DC02.LA_CLM_INT_ACR
		-DC01.LA_INT_COL)					
		 + SUM (DC01.LA_LEG_CST_ACR
		-DC01.LA_LEG_CST_COL
		+DC01.LA_OTH_CHR_ACR
		-DC01.LA_OTH_CHR_COL
		+DC01.LA_COL_CST_ACR
		-DC01.LA_COL_CST_COL)
		+SUM(DC02.LA_CLM_PRJ_COL_CST) AS TOT_PAYOFF
	,AY01.PF_ACT	
	,AY01.BD_ATY_PRF
	,DC11.LC_TRX_TYP
	,DC11.LD_TRX_EFF
	,PD01.DI_PHN_VLD
	,PD01.DI_ALT_PHN_VLD
	,AY01B.PF_ACT AS DRBRZ

FROM	OLWHRM1.AY01_BR_ATY AY01
INNER JOIN OLWHRM1.DC01_LON_CLM_INF DC01
	ON AY01.DF_PRS_ID = DC01.BF_SSN
INNER JOIN OLWHRM1.DC02_BAL_INT DC02
	ON DC01.AF_APL_ID = DC02.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
	AND DC01.LF_CRT_DTS_DC10 = DC02.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD01_PDM_INF PD01
	ON AY01.DF_PRS_ID = PD01.DF_PRS_ID
INNER JOIN OLWHRM1.DC11_LON_FAT DC11
	ON DC01.AF_APL_ID = DC11.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = DC11.AF_APL_ID_SFX
	AND DC01.LF_CRT_DTS_DC10 = DC11.LF_CRT_DTS_DC10 
INNER JOIN (SELECT AF_APL_ID,AF_APL_ID_SFX,LF_CRT_DTS_DC10,MAX(LD_TRX_EFF) as LD_TRX_EFF
			FROM OLWHRM1.DC11_LON_FAT DC11
			group by AF_APL_ID,AF_APL_ID_SFX,LF_CRT_DTS_DC10
			) B
	ON DC11.AF_APL_ID = B.AF_APL_ID
	AND DC11.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND DC11.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
	AND DC11.LD_TRX_EFF = B.LD_TRX_EFF
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY AY01B
	ON AY01.DF_PRS_ID = AY01B.DF_PRS_ID
	AND AY01B.PF_ACT = 'DRBRZ'
	AND DAYS(AY01B.BD_ATY_PRF) > DAYS(AY01.BD_ATY_PRF)

WHERE AY01.PF_ACT IN ('DLIV1','DLIV2','DLIV3','DLIV4','DLIV5','DLIV6')
AND AY01.BD_ATY_PRF > CURRENT DATE - 6 MONTHS
AND ((DC11.LC_TRX_TYP IN ('BR','EP','CP') 
		AND DAYS(DC11.LD_TRX_EFF) > DAYS(AY01.BD_ATY_PRF)
		)
	OR PD01.DI_PHN_VLD = 'Y'
	OR PD01.DI_ALT_PHN_VLD = 'Y'
	OR AY01B.PF_ACT IS NOT NULL)

GROUP BY PD01.DF_SPE_ACC_ID
	,AY01.PF_ACT	
	,AY01.BD_ATY_PRF
	,DC11.LC_TRX_TYP
	,DC11.LD_TRX_EFF
	,PD01.DI_PHN_VLD
	,PD01.DI_ALT_PHN_VLD
	,AY01B.PF_ACT 
	

FOR READ ONLY WITH UR

);
DISCONNECT FROM DB2;

/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

ENDRSUBMIT;

DATA DEMO; SET WORKLOCL.DEMO; RUN;

DATA DEMO;
SET DEMO;
IF TOT_PAYOFF = 0 THEN ZERO_BAL = 1;
ELSE ZERO_BAL = 0;	

IF DI_PHN_VLD = 'Y' THEN PHONE = 1;
ELSE PHONE = 0;	

IF DI_ALT_PHN_VLD = 'Y' THEN ALT = 1;
ELSE ALT = 0;	

IF LC_TRX_TYP = 'BR' OR LC_TRX_TYP = 'EP' OR LC_TRX_TYP = 'CP' THEN CP = 1;
ELSE CP = 0;

IF DRBRZ EQ 'DRBRZ' THEN DRBRZ_IND = 1;
ELSE DRBRZ_IND = 0;

RUN;

PROC SORT DATA=DEMO;
BY TOT_PAYOFF;
RUN;

PROC SQL;
CREATE TABLE TOTALS AS
SELECT SUM(ZERO_BAL) AS ZERO_BAL
	,SUM(PHONE) + SUM(ALT) AS PHONE
	,SUM(CP) AS CP
	,SUM(DRBRZ_IND) AS DRBRZ
FROM (SELECT DISTINCT DF_SPE_ACC_ID
	,TOT_PAYOFF
	,PF_ACT	
	,LC_TRX_TYP
	,LD_TRX_EFF
	,DI_PHN_VLD
	,DI_ALT_PHN_VLD
	,DRBRZ
	,DRBRZ_IND
	,ZERO_BAL
	,PHONE
	,ALT
	,CP
	FROM DEMO)
;
QUIT;


PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

/*For portrait reports;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=52 LS=96;
PROC CONTENTS DATA=DEMO OUT=EMPTYSET NOPRINT;
/*PORTRAIT*/
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 96*'-';
	PUT      ////////
		@35 '**** NO RECORDS FOUND ****';
	PUT ////////
		@38 '-- END OF REPORT --';
	PUT ////////////////
		@27 "JOB = UTLWM37     REPORT = ULWM37.LWM37R2";
	END;
RETURN;
TITLE "Increase Collection Revenue - No Phone Results";
RUN;

PROC PRINT NOOBS SPLIT='/' DATA=DEMO;
FORMAT TOT_PAYOFF DOLLAR17.2 LD_TRX_EFF MMDDYY10. BD_ATY_PRF MMDDYY10.;
VAR DF_SPE_ACC_ID TOT_PAYOFF PF_ACT BD_ATY_PRF LC_TRX_TYP LD_TRX_EFF DI_PHN_VLD DI_ALT_PHN_VLD;
LABEL	DF_SPE_ACC_ID = 'ACCT #'
		TOT_PAYOFF = 'BALANCE'
		PF_ACT = 'ACTION CODE'
		BD_ATY_PRF = 'ACTION DATE'
		LC_TRX_TYP = 'PAY TYPE'
		LD_TRX_EFF = 'EFFECTIVE DATE'
		DI_PHN_VLD = 'VALID PHONE'
		DI_ALT_PHN_VLD = 'VALID ALT PHONE'
		;

TITLE 'Increase Collection Revenue - No Phone Results';
FOOTNOTE  'JOB = UTLWM37     REPORT = UTLWM37.LWM37R2';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;

/*For portrait reports;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=52 LS=96;

PROC PRINT NOOBS SPLIT='/' DATA=TOTALS;
VAR ZERO_BAL PHONE CP DRBRZ;
LABEL	ZERO_BAL = 'ACCOUNTS PAID OFF'
		PHONE = 'PHONES UPDATED'
		CP = 'BORROWERS MADE PAYMENTS'
		DRBRZ = 'BORROWERS COMPROMISED'
		;

TITLE 'Increase Collection Revenue - No Phone Result Totals';
FOOTNOTE  'JOB = UTLWM37     REPORT = UTLWM37.LWM37R3';
RUN;

PROC PRINTTO;
RUN;
