/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWK20.LWK20R2";
FILENAME REPORTZ "&RPTLIB/ULWK20.LWK20RZ";

options symbolgen;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;


PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SKPVOL AS
SELECT *
FROM CONNECTION TO DB2 (

SELECT DISTINCT
	A.DF_PRS_ID AS SSN
	,CASE 
	 WHEN (A.DI_VLD_ADR = 'N' AND A.DI_PHN_VLD = 'N')
	 THEN 'BOTH'
	 WHEN A.DI_VLD_ADR = 'N'
	 THEN 'ADDRESS'
	 WHEN A.DI_PHN_VLD = 'N'
	 THEN 'PHONE'
	 END AS SKP_TYPE
FROM	OLWHRM1.PD01_PDM_INF A
INNER JOIN OLWHRM1.GA01_APP B
	ON A.DF_PRS_ID = B.DF_PRS_ID_BR
INNER JOIN OLWHRM1.GA10_LON_APP C
	ON B.AF_APL_ID = C.AF_APL_ID
	AND C.AC_PRC_STA = 'A'
	AND C.AA_CUR_PRI > 0
WHERE C.AF_APL_ID || C.AF_APL_ID_SFX NOT IN (SELECT DISTINCT
								B.AF_APL_ID || C.AF_APL_ID_SFX
							FROM	OLWHRM1.PD01_PDM_INF A
							INNER JOIN OLWHRM1.GA01_APP B
								ON A.DF_PRS_ID = B.DF_PRS_ID_BR
							INNER JOIN OLWHRM1.GA10_LON_APP C
								ON B.AF_APL_ID = C.AF_APL_ID
								AND C.AC_PRC_STA = 'A'
								AND C.AA_CUR_PRI > 0
							INNER JOIN OLWHRM1.GA14_LON_STA DD
								ON B.AF_APL_ID = DD.AF_APL_ID
								AND C.AF_APL_ID_SFX = DD.AF_APL_ID_SFX
								AND DD.AC_STA_GA14 = 'A'
							INNER JOIN OLWHRM1.DC01_LON_CLM_INF EE1
								ON B.AF_APL_ID = EE1.AF_APL_ID
								AND C.AF_APL_ID_SFX = EE1.AF_APL_ID_SFX
							INNER JOIN (SELECT ZZ.AF_APL_ID, ZZ.AF_APL_ID_SFX, MAX(ZZ.LD_STA_UPD_DC10) AS LD_STA_UPD_DC10
										FROM OLWHRM1.DC01_LON_CLM_INF ZZ
										GROUP BY ZZ.AF_APL_ID, ZZ.AF_APL_ID_SFX
										) EE
								ON B.AF_APL_ID = EE.AF_APL_ID
								AND C.AF_APL_ID_SFX = EE.AF_APL_ID_SFX
								AND EE.LD_STA_UPD_DC10 = EE1.LD_STA_UPD_DC10
							WHERE (DD.AC_LON_STA_TYP IN ('CA','PF','PM','PN','RF')
							OR (DD.AC_LON_STA_TYP = 'CP' AND EE1.LC_STA_DC10 = '03' 
								AND LD_CLM_ASN_DOE IS NOT NULL)
							OR (DD.AC_LON_STA_TYP = 'CP' AND EE1.LC_STA_DC10 = '04'))
							)
FOR READ ONLY WITH UR
);





/*SKIP REASON = DEFAULT*/
CREATE TABLE DEFAULT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.BF_SSN AS SSN
FROM	OLWHRM1.DC01_LON_CLM_INF A
WHERE A.LC_STA_DC10 = '03'
AND A.LD_CLM_ASN_DOE IS NULL
AND A.LC_PCL_REA NOT IN ('BC','BH','BO','DI','DE','CS','FC') 
FOR READ ONLY WITH UR
);

/*SKIP REASON = In-House Servicing*/
CREATE TABLE IN_HOUSE AS
SELECT *
FROM CONNECTION TO DB2 (

SELECT DISTINCT
	A.BF_SSN AS SSN
FROM	OLWHRM1.LN10_LON A
WHERE A.LC_STA_LON10 = 'R'
AND LA_CUR_PRI > 0
FOR READ ONLY WITH UR
);

DISCONNECT FROM DB2;

PROC SQL;
CREATE TABLE DEFAULT_SKPVOL AS
SELECT A.*
	,'DEFAULT' AS SKP_RSN
FROM SKPVOL A
INNER JOIN DEFAULT B
ON A.SSN = B.SSN

;
QUIT;
PROC SQL;
CREATE TABLE IN_HOUSE_SKPVOL AS
SELECT A.*
	,'INHOUSE' AS SKP_RSN
FROM SKPVOL A
INNER JOIN IN_HOUSE B
ON A.SSN = B.SSN
WHERE A.SSN NOT IN(SELECT Z.SSN FROM DEFAULT_SKPVOL Z)
;
QUIT;

PROC SQL;
CREATE TABLE NON_SERVICING_SKPVOL AS
SELECT A.*
	,'NONSERV' AS SKP_RSN
FROM SKPVOL A
WHERE A.SSN NOT IN(SELECT Z.SSN FROM DEFAULT Z)
AND A.SSN NOT IN(SELECT ZZ.SSN FROM IN_HOUSE ZZ)
;
QUIT;

DATA SKPVOL2;
SET DEFAULT_SKPVOL IN_HOUSE_SKPVOL NON_SERVICING_SKPVOL;

RUN;

/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

ENDRSUBMIT;

DATA SKPVOL2; SET WORKLOCL.SKPVOL2; RUN;
DATA SKPVOL; SET WORKLOCL.SKPVOL; RUN;
/*TOTAL NUMBER OF BORROWERS*/
PROC CONTENTS DATA=SKPVOL OUT=SKPVOLB NOPRINT;RUN;
DATA _NULL_;
SET SKPVOLB;
CALL SYMPUT('TOTAL_BORROWERS',NOBS);
RUN;
%SYSLPUT TOTAL_BORROWERS = &TOTAL_BORROWERS;

/*TOTAL ADDRESS SKIP BORROWERS*/
PROC SQL;
CREATE TABLE TEMP1 AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2
WHERE SKP_TYPE = 'ADDRESS'
;
QUIT;
DATA _NULL_;
SET TEMP1;
CALL SYMPUT('ADDRESS_SKIP_BORROWERS',CNT);
RUN;
%SYSLPUT ADDRESS_SKIP_BORROWERS = &ADDRESS_SKIP_BORROWERS;

/*TOTAL PHONE SKIP BORROWERS*/
PROC SQL;
CREATE TABLE TEMP2 AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2
WHERE SKP_TYPE = 'PHONE'
;
QUIT;
DATA _NULL_;
SET TEMP2;
CALL SYMPUT('PHONE_SKIP_BORROWERS',CNT);
RUN;
%SYSLPUT PHONE_SKIP_BORROWERS = &PHONE_SKIP_BORROWERS;

/*TOTAL BOTH SKIP BORROWERS*/
PROC SQL;
CREATE TABLE TEMP3 AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2
WHERE SKP_TYPE = 'BOTH'
;
QUIT;
DATA _NULL_;
SET TEMP3;
CALL SYMPUT('BOTH_SKIP_BORROWERS',CNT);
RUN;
%SYSLPUT BOTH_SKIP_BORROWERS = &BOTH_SKIP_BORROWERS;

/*TOTAL ALL SKIP BORROWERS*/
DATA _NULL_;
CALL SYMPUT('SKIP_BORROWERS',&ADDRESS_SKIP_BORROWERS + &PHONE_SKIP_BORROWERS + &BOTH_SKIP_BORROWERS);
RUN;
%SYSLPUT SKIP_BORROWERS = &SKIP_BORROWERS;

/*TOTAL DEFAULT BORROWERS*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_RSN = 'DEFAULT'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('DEFAULT_TOTAL_BORROWERS',CNT);
RUN;
%SYSLPUT DEFAULT_TOTAL_BORROWERS = &DEFAULT_TOTAL_BORROWERS;
/*TOTAL DEFAULT BORROWERS IN SKIP*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_TYPE IN ('ADDRESS','PHONE','BOTH')
AND SKP_RSN = 'DEFAULT'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('SKP_DEFAULT_TOTAL_BORROWERS',CNT);
RUN;
%SYSLPUT SKP_DEFAULT_TOTAL_BORROWERS = &SKP_DEFAULT_TOTAL_BORROWERS;

/*TOTAL IN HOUSE BORROWERS*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_RSN = 'INHOUSE'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('TOTAL_IN_HOUSE_BORROWERS',CNT);
RUN;
%SYSLPUT TOTAL_IN_HOUSE_BORROWERS = &TOTAL_IN_HOUSE_BORROWERS;

/*TOTAL IN HOUSE BORROWERS IN SKIP*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_TYPE IN ('ADDRESS','PHONE','BOTH')
AND SKP_RSN = 'INHOUSE'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('SKP_TOTAL_IN_HOUSE_BORROWERS',CNT);
RUN;
%SYSLPUT SKP_TOTAL_IN_HOUSE_BORROWERS = &SKP_TOTAL_IN_HOUSE_BORROWERS;

/*TOTAL NON-SERVICING BORROWERS*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_RSN = 'NONSERV'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('TOTAL_NON_SERVICING',CNT);
RUN;
%SYSLPUT TOTAL_NON_SERVICING = &TOTAL_NON_SERVICING;

/*TOTAL NON-SERVICING BORROWERS IN SKIP*/
PROC SQL;
CREATE TABLE TEMP AS
SELECT COUNT(*) AS CNT
FROM SKPVOL2 
WHERE SKP_TYPE IN ('ADDRESS','PHONE','BOTH')
AND SKP_RSN = 'NONSERV'
;
QUIT;
DATA _NULL_;
SET TEMP;
CALL SYMPUT('SKP_TOTAL_NON_SERVICING',CNT);
RUN;
%SYSLPUT SKP_TOTAL_NON_SERVICING = &SKP_TOTAL_NON_SERVICING;


OPTIONS ORIENTATION = PORTRAIT;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=52 LS=96;
PROC PRINTTO PRINT=REPORT2;
RUN;
DATA _NULL_;
X = &SKIP_BORROWERS/&TOTAL_BORROWERS;
PERCENT_SKP_BORROWERS = PUT(X*100,6.1)||"%";

X = &SKP_DEFAULT_TOTAL_BORROWERS/&DEFAULT_TOTAL_BORROWERS;
PERCENT_SKP_DEFAULT = PUT(X*100,6.1)||"%";

X = &SKP_TOTAL_IN_HOUSE_BORROWERS/&TOTAL_IN_HOUSE_BORROWERS;
PERCENT_SKP_TOTAL_IN_HOUSE = PUT(X*100,6.1)||"%";

X = &SKP_TOTAL_NON_SERVICING/&TOTAL_NON_SERVICING;
PERCENT_SKP_TOTAL_NON_SERVICING = PUT(X*100,6.1)||"%";
TITLE 'Skip Volume Report';
FOOTNOTE  'JOB = UTLWK20     REPORT = UTLWK20.LWK20R2';
FILE PRINT;

DO;
	PUT      ////
		@25 'TOTAL BORROWERS:' @65 "&TOTAL_BORROWERS";
	PUT /
		@25 "TOTAL SKIP BORROWERS:" @65 "&SKIP_BORROWERS";
	PUT /
		@25 "% SKIP BORROWERS:" @72 PERCENT_SKP_BORROWERS;
	PUT /
		@25 "# ADDRESS ONLY SKIP:" @65 "&ADDRESS_SKIP_BORROWERS";
	PUT /
		@25 "# PHONE ONLY SKIP:" @65 "&PHONE_SKIP_BORROWERS";
	PUT /
		@25 "# BOTH SKIP:" @65 "&BOTH_SKIP_BORROWERS";
	PUT ///
		@25 "TOTAL DEFAULTED BORRWERS:" @65 "&DEFAULT_TOTAL_BORROWERS";
	PUT /
		@25 "TOTAL SKIP BORROWERS IN DEFAULT:" @65 "&SKP_DEFAULT_TOTAL_BORROWERS";
	PUT /
		@25 "PERCENT SKIP DEFAULT BORROWERS:" @72 PERCENT_SKP_DEFAULT;
	PUT ///
		@25 "TOTAL COMPASS BORROWERS:" @65 "&TOTAL_IN_HOUSE_BORROWERS";
	PUT /
		@25 "TOTAL SKIP BORROWERS IN-HOUSE SERVICING:" @65 "&SKP_TOTAL_IN_HOUSE_BORROWERS";
	PUT /
		@25 "PERCENT SKIP IN-HOUSE BORROWERS:" @72 PERCENT_SKP_TOTAL_IN_HOUSE;
	PUT ///
		@25 "TOTAL BORROWERS NON-SERVICING:" @65 "&TOTAL_NON_SERVICING";
	PUT /
		@25 "TOTAL SKIP BORROWERS NON-SERVICING:" @65 "&SKP_TOTAL_NON_SERVICING";
	PUT /
		@25 "PERCENT SKIP BORROWERS NON-SERVICING:" @72 PERCENT_SKP_TOTAL_NON_SERVICING;
	PUT //////
		@30 "JOB = UTLWK20     REPORT = UTLWK20.LWK20R2";
	END;

TITLE 'Skip Volume Report';
/*FOOTNOTE  'JOB = UTLWK20     REPORT = UTLWK20.LWK20R2';*/
RUN;

