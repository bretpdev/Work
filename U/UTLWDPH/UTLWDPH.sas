/*	Management report for Default Prevention to identify all 
	phone contacts and attempts made each month by the area.  
*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWDPH.LWDPHR2";
FILENAME REPORT3 "&RPTLIB/ULWDPH.LWDPHR3";*/

DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DPCTC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.pf_act						AS PFACT,
		CASE
			WHEN A.bf_lst_usr_ay01 = ' '
				THEN 'UNKNOWN - NO CONTACT'
			ELSE A.bf_lst_usr_ay01 END 	AS LSTUSR
FROM  OLWHRM1.AY01_BR_ATY A
WHERE A.bd_aty_prf BETWEEN &BEGIN AND &END
AND A.pf_act in (
/*Autodialer Contacts*/
'ADMXC','ADD14','ADD33','ADD36',
/*Autodialer Attempts*/
'AAMXA','ABADP','ADD03','ADD08','ADD51','ADD52','ADD56','ADD59','ADD89',
/*Manual Contacts*/
'AD114',
/*Manual Attempts*/
'AD102','AD103','AD108','AD151','AD152','AD156','AD159','AD189',
/*Calls Received on Non-Default Prevention Account*/
'ABNDP',
/*Follow-Up with Servicer or School*/
'AFUWS'
)
ORDER BY A.PF_ACT
);
DISCONNECT FROM DB2;
endrsubmit  ;

DATA DPCTC; 
SET WORKLOCL.DPCTC; 
RUN;

DATA TOTALS;
ADCT = 0;
ADAT = 0;
MANCT = 0;
MANAT = 0;
NDCAL = 0;
FOLUP = 0;
RUN;

PROC SQL;
UPDATE TOTALS
SET ADCT=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('ADMXC','ADD14','ADD33','ADD36')
	);
UPDATE TOTALS
SET ADAT=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('AAMXA','ABADP','ADD03','ADD08','ADD51','ADD52','ADD56','ADD59','ADD89')
	);
UPDATE TOTALS
SET MANCT=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('AD114')
	);
UPDATE TOTALS
SET MANAT=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('AD102','AD103','AD108','AD151','AD152','AD156','AD159','AD189')
	);
UPDATE TOTALS
SET NDCAL=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('ABNDP')
	);
UPDATE TOTALS
SET FOLUP=
	(SELECT COUNT(*)
	FROM DPCTC
	WHERE PFACT IN
		('AFUWS')
	);
;QUIT;

PROC SQL;
CREATE TABLE BYUSR1 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS TOTCT
FROM DPCTC
WHERE PFACT in ('ADMXC','ADD14','ADD33','ADD36','AD114')
GROUP BY LSTUSR
ORDER BY LSTUSR;

CREATE TABLE BYUSR2 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS TOTAT
FROM DPCTC
WHERE PFACT IN ('AAMXA','ABADP','ADD03','ADD08','ADD51','ADD52','ADD56','ADD59','ADD89',
			    'AD102','AD103','AD108','AD151','AD152','AD156','AD159','AD189')
GROUP BY LSTUSR
ORDER BY LSTUSR;

CREATE TABLE BYUSR3 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS TOTND
FROM DPCTC
WHERE PFACT = 'ABNDP'
GROUP BY LSTUSR
ORDER BY LSTUSR;

CREATE TABLE BYUSR4 AS
SELECT 	LSTUSR,
		COUNT(LSTUSR)			AS TOTFU
FROM DPCTC
WHERE PFACT = 'AFUWS'
GROUP BY LSTUSR
ORDER BY LSTUSR;

QUIT;

DATA BYUSR;
MERGE BYUSR1 BYUSR2 BYUSR3 BYUSR4;
BY LSTUSR;
IF TOTCT=. THEN TOTCT=0;
IF TOTAT=. THEN TOTAT=0;
IF TOTND=. THEN TOTND=0;
IF TOTFU=. THEN TOTFU=0;
RUN;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/

OPTIONS NOCENTER NODATE NONUMBER PS=40 LS=120;
PROC PRINT NOOBS SPLIT='`' DATA=TOTALS WIDTH=UNIFORM WIDTH=MIN;
VAR ADCT ADAT MANCT MANAT NDCAL FOLUP;
LABEL ADCT = 'Autodialer Contacts'
ADAT = 'Autodialer Attempts'
MANCT = 'Manual Contacts'
MANAT = 'Manual Attempts' 
NDCAL = 'Non-DP Account Calls Received '
FOLUP = 'Follow-up Calls with Servicer or School';
TITLE1 "Phone Contacts and Attempts in Default Prevention - &EFFDATE";
TITLE2 'By Action Code Type';
FOOTNOTE  'JOB = UTLWDPH     REPORT = ULWDPH.LWDPHR2';
RUN;

/*PROC PRINTTO PRINT=REPORT3;
RUN;*/

OPTIONS NOCENTER NODATE NONUMBER PS=40 LS=120;
PROC PRINT NOOBS SPLIT='`' DATA=BYUSR WIDTH=UNIFORM WIDTH=MIN;
VAR LSTUSR TOTCT TOTAT TOTND TOTFU;
SUM TOTCT TOTAT TOTND TOTFU;
LABEL	LSTUSR = 'Last User'
TOTCT = 'Contacts'
TOTAT = 'Attempts'
TOTND = 'Non-DP Accounts Worked'
TOTFU = 'Follow-up with Servicer or School';
TITLE1 "Phone Contacts and Attempts in Default Prevention - &EFFDATE";
TITLE2 'By User ID for user who last updated this AY01 data';
FOOTNOTE  'JOB = UTLWDPH     REPORT = ULWDPH.LWDPHR3';
RUN;
