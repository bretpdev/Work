*----------------------------------------*
| UTLWM23 NSLDS Cohort Default Rate Data |
*----------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM23.LWM23R2";
FILENAME REPORT3 "&RPTLIB/ULWM23.LWM23R3";
FILENAME REPORT4 "&RPTLIB/ULWM23.LWM23R4";
FILENAME REPORT5 "&RPTLIB/ULWM23.LWM23R5";
FILENAME REPORT6 "&RPTLIB/ULWM23.LWM23R6";
FILENAME REPORT7 "&RPTLIB/ULWM23.LWM23R7";
FILENAME REPORT8 "&RPTLIB/ULWM23.LWM23R8";
FILENAME REPORT9 "&RPTLIB/ULWM23.LWM23R9";
FILENAME REPORT10 "&RPTLIB/ULWM23.LWM23R10";
FILENAME REPORTZ "&RPTLIB/ULWM23.LWM23RZ";
/***************************************************************************
* CALCULATE CURRENT AND FUTURE COHORT PERIODS CONTINGENT UPON WHAT TODAY IS
****************************************************************************/
DATA _NULL_;
DTPARM = YEAR(INTNX('MONTH',today(),3,'B'));
	CALL SYMPUT('PrvChrtYrBeg',DTPARM-3);
	CALL SYMPUT('PrvChrtYrEnd',DTPARM-2);
	CALL SYMPUT('CurChrtYrBeg',DTPARM-2);
	CALL SYMPUT('CurChrtYrEnd',DTPARM-1);
	CALL SYMPUT('NxtChrtYrBeg',DTPARM-1);
	CALL SYMPUT('NxtChrtYrEnd',DTPARM);
	CALL SYMPUT('TreChrtYrBeg',DTPARM);
	CALL SYMPUT('TreChrtYrEnd',DTPARM+1);
	CALL SYMPUT('PrvChrtDecYr',DTPARM);
	CALL SYMPUT('CurChrtDecYr',DTPARM);
	CALL SYMPUT('NxtChrtDecYr',DTPARM+1);
	CALL SYMPUT('TreChrtDecYr',DTPARM+2);
RUN;
DATA _NULL_;
/*CHARACTER DATES FOR PASS THROUGH SQL*/
	CALL SYMPUT('cPrvChrtBeg',"'"||PUT("01OCT&PrvChrtYrBeg"d,MMDDYYS10.)||"'");
	CALL SYMPUT('cTreChrtEnd',"'"||PUT("30SEP&TreChrtYrEnd"d,MMDDYYS10.)||"'");
/*INTEGER DATES FOR SAS PROCESSING*/
	CALL SYMPUT('iPrvChrtBeg',"01OCT&PrvChrtYrBeg"d);
	CALL SYMPUT('iPrvChrtEnd',"30SEP&PrvChrtYrEnd"d);
	CALL SYMPUT('iCurChrtBeg',"01OCT&CurChrtYrBeg"d);
	CALL SYMPUT('iCurChrtEnd',"30SEP&CurChrtYrEnd"d);
	CALL SYMPUT('iNxtChrtBeg',"01OCT&NxtChrtYrBeg"d);
	CALL SYMPUT('iNxtChrtEnd',"30SEP&NxtChrtYrEnd"d);
	CALL SYMPUT('iTreChrtBeg',"01OCT&TreChrtYrBeg"d);
	CALL SYMPUT('iTreChrtEnd',"30SEP&TreChrtYrEnd"d);
	CALL SYMPUT('iPrvDecDt',"04DEC&PrvChrtDecYr"d);
	CALL SYMPUT('iCurDecDt',"04DEC&CurChrtDecYr"d);
	CALL SYMPUT('iNxtDecDt',"04DEC&NxtChrtDecYr"d);
	CALL SYMPUT('iTreDecDt',"04DEC&TreChrtDecYr"d);
RUN;
/*CREATE STATIC TABLE OF CATEGORIES*/
DATA STAT_CAT;
INFILE CARDS DLM=',' DSD MISSOVER;
INFORMAT CAT $15.;
INPUT CAT $  SUBCAT  CSRT;
CARDS; 
TOTAL DEFAULT,1,1
ACTIVE PRECLAIM,2,1
DEFERMENT,3,1
FORBEARANCE,3,2
PIF,3,3
NOT PRECLAIM,2,2
TIMED OUT,3,4
TOTAL ERRORS,4,1
RUN;
PROC SORT DATA=STAT_CAT; BY SUBCAT CSRT; RUN;
%SYSLPUT cPrvChrtBeg = &cPrvChrtBeg;
%SYSLPUT cTreChrtEnd = &cTreChrtEnd;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*********************************************************************************************
* IDENTIFY LOANS THAT COULD APPEAR IN EITHER CURRENT OR FUTURE COHORT 
**********************************************************************************************/
CREATE TABLE NCDRD AS
SELECT DISTINCT BF_SSN
	,CLUID
	,DT_ENT_REPMT
FROM CONNECTION TO DB2 (
	SELECT GA01.DF_PRS_ID_BR AS BF_SSN
		,GA10.AF_APL_ID||GA10.AF_APL_ID_SFX AS CLUID
		,GA15.AD_NDS_CLC_ENT_RPD AS DT_ENT_REPMT
	FROM OLWHRM1.GA01_APP GA01
	INNER JOIN OLWHRM1.GA10_LON_APP GA10
		ON GA01.AF_APL_ID = GA10.AF_APL_ID
	INNER JOIN OLWHRM1.GA15_NDS_ID GA15
		ON GA10.AF_APL_ID = GA15.AF_APL_ID
		AND GA10.AF_APL_ID_SFX = GA15.AF_APL_ID_SFX
	WHERE GA10.AC_LON_TYP IN ('SF','SU','SL')
		AND GA10.AC_GTE_TRF != 'O'
		AND GA15.AD_NDS_CLC_ENT_RPD BETWEEN &cPrvChrtBeg AND &cTreChrtEnd
		AND GA15.AC_STA_GA15 = 'A'
UNION
		SELECT CNSL.BF_SSN
			,CNSL.AF_APL_ID||CNSL.AF_APL_ID_SFX AS CLUID
			,USTAF.AD_NDS_CLC_ENT_RPD AS DT_ENT_REPMT
		FROM (
			SELECT A.DF_PRS_ID_BR AS BF_SSN
				,B.AF_APL_ID
				,B.AF_APL_ID_SFX
			FROM OLWHRM1.GA01_APP A
			INNER JOIN OLWHRM1.GA10_LON_APP B
				ON A.AF_APL_ID = B.AF_APL_ID
			WHERE B.AC_LON_TYP = 'CL'
			) CNSL
		INNER JOIN (
				SELECT C.DF_PRS_ID_BR
					,F.AD_NDS_CLC_ENT_RPD
				FROM OLWHRM1.GA01_APP C
				INNER JOIN OLWHRM1.GA10_LON_APP D
					ON C.AF_APL_ID = D.AF_APL_ID
				INNER JOIN (
						SELECT AF_APL_ID
							,AF_APL_ID_SFX
						FROM OLWHRM1.GA14_LON_STA 
						WHERE AC_STA_GA14 = 'A'
						AND AC_LON_STA_TYP = 'PN'
						AND AD_LON_STA BETWEEN &cPrvChrtBeg AND &cTreChrtEnd
					) E
					ON D.AF_APL_ID = E.AF_APL_ID
					AND D.AF_APL_ID_SFX = E.AF_APL_ID_SFX
				INNER JOIN (
						SELECT AF_APL_ID
							,AF_APL_ID_SFX
							,AD_NDS_CLC_ENT_RPD
						FROM OLWHRM1.GA15_NDS_ID
						WHERE AC_STA_GA15 = 'A'
						AND AD_NDS_CLC_ENT_RPD BETWEEN &cPrvChrtBeg AND &cTreChrtEnd
					) F
					ON D.AF_APL_ID = F.AF_APL_ID
					AND D.AF_APL_ID_SFX = F.AF_APL_ID_SFX
				WHERE D.AC_LON_TYP IN ('SF','SU')
			) USTAF
			ON CNSL.BF_SSN = USTAF.DF_PRS_ID_BR

FOR READ ONLY WITH UR
);
/********************************************************************************
* GET DATA FOR THE LOANS SELECTED ABOVE
*********************************************************************************/
CREATE TABLE LNDET AS
SELECT DISTINCT A.CLUID AS LNID
	,A.DT_ENT_REPMT
	,B.* 
FROM NCDRD A
LEFT OUTER JOIN CONNECTION TO DB2 (
	SELECT GA01.DF_PRS_ID_BR
		,GA10.AF_APL_ID||GA10.AF_APL_ID_SFX AS CLUID
		,GA10.AC_LON_TYP
		,PD03.DI_PHN_VLD
		,PD03.DI_VLD_ADR
		,PD03.DI_FGN_PHN
		,DC01.LC_STA_DC10
		,DC01.LC_PCL_REA
		,DC01.LD_DCO
		,DC01.LD_PCL_SUP_LST_ATT
		,DC01.LD_PCL_SUP_LST_CNC
		,DC01.LD_LDR_POF
		,DC01.LC_AUX_STA
		,DC01.LD_AUX_STA_UPD
		,GA14.AC_LON_STA_TYP
		,GA14.AC_LON_STA_REA
		,PD01.DF_SPE_ACC_ID
		,PD01.DM_PRS_1
		,PD01.DM_PRS_LST
	FROM OLWHRM1.GA01_APP GA01
	INNER JOIN OLWHRM1.GA10_LON_APP GA10
		ON GA01.AF_APL_ID = GA10.AF_APL_ID
	LEFT OUTER JOIN OLWHRM1.PD01_PDM_INF PD01
		ON GA01.DF_PRS_ID_BR = PD01.DF_PRS_ID
	LEFT OUTER JOIN OLWHRM1.PD03_PRS_ADR_PHN PD03
		ON GA01.DF_PRS_ID_BR = PD03.DF_PRS_ID
		AND PD03.DC_ADR = 'L'
	LEFT OUTER JOIN (
		SELECT DISTINCT A.AF_APL_ID
			,A.AF_APL_ID_SFX
			,A.LC_STA_DC10
			,A.LC_PCL_REA
			,A.LD_DCO
			,A.LD_PCL_SUP_LST_ATT
			,A.LD_PCL_SUP_LST_CNC
			,A.LD_LDR_POF
			,A.LC_AUX_STA
			,A.LD_AUX_STA_UPD
		FROM OLWHRM1.DC01_LON_CLM_INF A
		INNER JOIN (
			SELECT AF_APL_ID
				,AF_APL_ID_SFX
				,MAX(LD_STA_UPD_DC10) AS LD_STA_UPD_DC10
			FROM OLWHRM1.DC01_LON_CLM_INF 
			GROUP BY AF_APL_ID
				,AF_APL_ID_SFX
			) B
			ON A.AF_APL_ID = B.AF_APL_ID
			AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
			AND A.LD_STA_UPD_DC10 = B.LD_STA_UPD_DC10
		) DC01
		ON GA10.AF_APL_ID = DC01.AF_APL_ID
		AND GA10.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	LEFT OUTER JOIN OLWHRM1.GA14_LON_STA GA14
		ON GA10.AF_APL_ID = GA14.AF_APL_ID
		AND GA10.AF_APL_ID_SFX = GA14.AF_APL_ID_SFX
		AND GA14.AC_STA_GA14 = 'A'
	LEFT OUTER JOIN OLWHRM1.GA15_NDS_ID GA15
		ON GA10.AF_APL_ID = GA15.AF_APL_ID
		AND GA10.AF_APL_ID_SFX = GA15.AF_APL_ID_SFX
		AND GA15.AC_STA_GA15 = 'A'
	FOR READ ONLY WITH UR
	) B
	ON A.CLUID = B.CLUID
;
/********************************************************************************
* GET ACTION CODE DATA
*********************************************************************************/
CREATE TABLE DACT AS
SELECT DISTINCT B.*
FROM NCDRD A
INNER JOIN CONNECTION TO DB2 (
	SELECT DF_PRS_ID AS BF_SSN
		,'X' AS AD_IND
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT IN (
		'ADVNW','ADEVW'
		)
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN
;
/**************************************************************************
* CREATE TABLE WITH COMPASS DEFERMENT DATA
***************************************************************************/
CREATE TABLE CO_DEFR AS
SELECT DISTINCT A.*
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LF_LON_ALT||'0'||RTRIM(CHAR(A.LN_LON_ALT_SEQ)) AS CLUID
    ,B.LC_DFR_TYP
    ,C.LD_DFR_BEG
    ,C.LD_DFR_END
 FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN50_BR_DFR_APV C
    ON A.BF_SSN = C.BF_SSN
    AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.DF10_BR_DFR_REQ B
    ON B.BF_SSN = C.BF_SSN
    AND B.LF_DFR_CTL_NUM = C.LF_DFR_CTL_NUM
WHERE B.LC_STA_DFR10 = 'A'
AND C.LC_STA_LON50 = 'A'
AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
) A
INNER JOIN NCDRD B
	ON A.CLUID = B.CLUID
;
/**************************************************************************
* CREATE TABLE WITH COMPASS FORBEARANCE DATA
***************************************************************************/
CREATE TABLE CO_FORB AS
SELECT DISTINCT A.*
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LF_LON_ALT||'0'||RTRIM(CHAR(A.LN_LON_ALT_SEQ)) AS CLUID
    ,B.LC_FOR_TYP
    ,C.LD_FOR_BEG
    ,C.LD_FOR_END
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN60_BR_FOR_APV C
    ON A.BF_SSN = C.BF_SSN
    AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.FB10_BR_FOR_REQ B
    ON B.BF_SSN = C.BF_SSN
    AND B.LF_FOR_CTL_NUM = C.LF_FOR_CTL_NUM
WHERE B.LC_STA_FOR10 = 'A'
AND C.LC_STA_LON60 = 'A'
AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
) A
INNER JOIN NCDRD B
	ON A.CLUID = B.CLUID
;
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA NCDRD; SET WORKLOCL.NCDRD; RUN; 
DATA LNDET; SET WORKLOCL.LNDET; RUN;
DATA DACT; SET WORKLOCL.DACT; RUN; 
DATA CO_DEFR; SET WORKLOCL.CO_DEFR; RUN; 
DATA CO_FORB; SET WORKLOCL.CO_FORB; RUN; 
/*************************************************
* ASSIGN COHORT YEAR INDICATOR
**************************************************/
DATA LNDET;
	SET LNDET;
	IF (&iPrvChrtBeg <= DT_ENT_REPMT <= &iPrvChrtEnd) THEN 
		CHRT_YR = 'P';
	ELSE IF (&iCurChrtBeg <= DT_ENT_REPMT <= &iCurChrtEnd) THEN 
		CHRT_YR = 'C';
	ELSE IF (&iNxtChrtBeg <= DT_ENT_REPMT <= &iNxtChrtEnd) THEN 
		CHRT_YR = 'F';
	ELSE IF (&iTreChrtBeg <= DT_ENT_REPMT <= &iTreChrtEnd) THEN 
		CHRT_YR = 'T';
	ELSE
		CHRT_YR = '';
RUN;
/********************************************************
* BEGIN COHORT PROCESSING AT A CHORT YEAR LEVEL
* ASSIGN EACH BORROWER WITHIN A COHORT PERIOD A CATEGORY
*********************************************************/
%MACRO ASCAT(DS,CYR,YR_END,DEC_DT);
PROC SQL;
CREATE TABLE &DS AS 
SELECT DISTINCT A.DF_PRS_ID_BR
	,A.CHRT_YR
	,SCN1.C1 
	,SCN2.C2 
	,SCN3.C3 
	,SCN4.C4 
	,SCN5.C5 
	,SCN6.C6 
	,SCN7.C7 
	,CASE 
		WHEN SCN1.C1 = '' 
			AND SCN2.C2 = '' 
			AND SCN3.C3 = ''
			AND SCN4.C4 = '' 
			AND SCN5.C5 = '' 
			AND SCN6.C6 = '' 
			AND SCN7.C7 = ''    
			THEN 'X'
		ELSE ''
	 END AS C8
FROM LNDET A
/*SCENARIO 1*/
LEFT OUTER JOIN (
	SELECT DISTINCT DF_PRS_ID_BR
		,'X' AS C1
	FROM LNDET
	WHERE AC_LON_STA_TYP = 'CP'
	AND AC_LON_STA_REA IN ('DB','DF','DQ')
	AND CHRT_YR = "&CYR"
	) SCN1
	ON A.DF_PRS_ID_BR = SCN1.DF_PRS_ID_BR
/*SCENARIO 2*/
LEFT OUTER JOIN (
	SELECT DISTINCT DF_PRS_ID_BR
		,'X' AS C2
	FROM LNDET 
	WHERE LC_STA_DC10 = '01'
	AND LC_PCL_REA IN ('DF','RS','DB','DQ')
	AND CHRT_YR = "&CYR"
	AND ABS(LD_DCO - INTNX('YEAR',&YR_END,+1,'S')) >= 300
	) SCN2
	ON A.DF_PRS_ID_BR = SCN2.DF_PRS_ID_BR
/*SCENARIO 3*/
LEFT OUTER JOIN (
	SELECT DISTINCT D1.DF_PRS_ID_BR
		,'X' AS C3
	FROM LNDET D1
	INNER JOIN CO_DEFR D2
		ON D1.DF_PRS_ID_BR = D2.BF_SSN
	WHERE D1.CHRT_YR = "&CYR"
	AND (
			D2.LD_DFR_END > INTNX('YEAR',&YR_END,+1,'S') OR 
			ABS(D2.LD_DFR_END - INTNX('YEAR',&YR_END,+1,'S')) <= 300
		)
	) SCN3
	ON A.DF_PRS_ID_BR = SCN3.DF_PRS_ID_BR
/*SCENARIO 4*/
LEFT OUTER JOIN (
	SELECT DISTINCT F1.DF_PRS_ID_BR
		,'X' AS C4
	FROM LNDET F1
	INNER JOIN CO_FORB F2
		ON F1.DF_PRS_ID_BR = F2.BF_SSN
	WHERE F1.CHRT_YR = "&CYR"
	AND (
			F2.LD_FOR_END > INTNX('YEAR',&YR_END,+1,'S') OR 
			ABS(F2.LD_FOR_END - INTNX('YEAR',&YR_END,+1,'S')) <= 300
		)
	) SCN4
	ON A.DF_PRS_ID_BR = SCN4.DF_PRS_ID_BR
/*SCENARIO 5*/
LEFT OUTER JOIN (
	SELECT DISTINCT DF_PRS_ID_BR
		,'X' AS C5
	FROM LNDET
	WHERE AC_LON_STA_TYP = 'PF'
	AND CHRT_YR = "&CYR"
	) SCN5
	ON A.DF_PRS_ID_BR = SCN5.DF_PRS_ID_BR
/*SCENARIO 6*/
LEFT OUTER JOIN (
	SELECT DISTINCT DF_PRS_ID_BR
		,'X' AS C6
	FROM LNDET 
	WHERE CHRT_YR = "&CYR"
	AND TODAY() <= &DEC_DT 
	) SCN6
	ON A.DF_PRS_ID_BR = SCN6.DF_PRS_ID_BR
/*SCENARIO 7*/
LEFT OUTER JOIN (
		SELECT DISTINCT DF_PRS_ID_BR
			,'X' AS C7
		FROM LNDET
		WHERE LC_STA_DC10 ^= '01'
		AND TODAY() > &DEC_DT 
		AND CHRT_YR = "&CYR"
	UNION 
		SELECT DISTINCT DF_PRS_ID_BR
			,'X' AS CAT7
		FROM LNDET 
		WHERE LC_STA_DC10 = '01'
		AND LC_PCL_REA IN ('DF','RS','DB','DQ')
		AND CHRT_YR = "&CYR"
		AND ABS(LD_DCO - INTNX('YEAR',&YR_END,+1,'S')) < 300
	) SCN7
	ON A.DF_PRS_ID_BR = SCN7.DF_PRS_ID_BR
WHERE A.CHRT_YR = "&CYR"
;
QUIT;
%MEND ASCAT;
%ASCAT(PRV,P,&iPrvChrtEnd,&iPrvDecDt);
%ASCAT(CUR,C,&iCurChrtEnd,&iCurDecDt);
%ASCAT(NXT,F,&iNxtChrtEnd,&iNxtDecDt);
%ASCAT(TRE,T,&iTreChrtEnd,&iTreDecDt);
/*****************************************************************************
* CONTINUE COHORT PROCESSING AT A CHORT YEAR LEVEL AND CREATE SUMMARY REPORTS
******************************************************************************/
%MACRO PROC2(DS1,DS2,DEF_YR,IREP_YR,NewInd);
/*ASSIGN A CATEGORY TO EACH BORROWER*/
DATA &DS1 (KEEP=DF_PRS_ID_BR CHRT_YR CAT SUBCAT CSRT AN_VAR);
SET &DS1;
LENGTH CAT $15.;
CAT = '';
IF C1 = 'X' 
	THEN DO;
		SUBCAT = 1;
		CSRT = 1;
	END;
ELSE IF C1 = '' AND 
	C2 = 'X' 
		THEN DO;
			SUBCAT = 2;
			CSRT = 1;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = 'X' 
		THEN DO;
			SUBCAT = 3;
			CSRT = 1;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = '' AND 
	C4 = 'X' 
		THEN DO;
			SUBCAT = 3;
			CSRT = 2;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = '' AND 
	C4 = '' AND 
	C5 = 'X' 
		THEN DO;
			SUBCAT = 3;
			CSRT = 3;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = '' AND 
	C4 = '' AND 
	C5 = '' AND 
	C6 = 'X' 
		THEN DO;
			SUBCAT = 2;
			CSRT = 2;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = '' AND 
	C4 = '' AND 
	C5 = '' AND 
	C6 = '' AND 
	C7 = 'X' 
		THEN DO;
			SUBCAT = 3;
			CSRT = 4;
		END;
ELSE IF C1 = '' AND 
	C2 = '' AND 
	C3 = '' AND 
	C4 = '' AND 
	C5 = '' AND 
	C6 = '' AND 
	C7 = '' AND 
	C8 = 'X'
		THEN DO;
			SUBCAT = 4;
			CSRT = 1;
		END;
AN_VAR = 1;
RUN;
/*SUMMARIZE AT A CATEGORY LEVEL*/
PROC SQL;
CREATE TABLE &DS2 AS 
SELECT COALESCE(B.CHRT_YR,"&DEF_YR") AS CHRT_YR
	,A.CSRT 
	,A.CAT
	,A.SUBCAT
	,COALESCE(SUM(B.AN_VAR),0) AS CTOT
FROM STAT_CAT A
LEFT OUTER JOIN &DS1 B
	ON A.SUBCAT = B.SUBCAT
	AND A.CSRT = B.CSRT
GROUP BY B.CHRT_YR 
	,A.CSRT 
	,A.CAT
	,A.SUBCAT
;
QUIT;
PROC SORT DATA=&DS2 ; BY SUBCAT CSRT ; RUN;
/*GET GRAND TOTAL (NUMERATOR) FOR COHORT CALCULATIONS AND TO CHARACTER VALUE TO DISPLAY ON REPORT*/
PROC SQL NOPRINT;
	SELECT SUM(CTOT) FORMAT=COMMA6.
	INTO : cCHRT_GT
	FROM &DS2;

	SELECT SUM(CTOT)
	INTO : iCHRT_GT
	FROM &DS2;
QUIT;
%PUT cCHRT_GT = &cCHRT_GT;
%PUT iCHRT_GT = &iCHRT_GT;
/*CALCULATE LINE ITEM PERCENTAGES*/
DATA &DS2;
SET &DS2;
PRCNT = ROUND(CTOT / &iCHRT_GT,.0001);
RUN;
/*----------------------------------- CREATE REPORTS -----------------------------------*/
PROC SQL NOPRINT;
	SELECT &IREP_YR FORMAT=YEAR2.
	INTO : REPYR
	FROM &DS1;
QUIT;
%PUT &REPYR;

DATA PRNT_DS;
SET &DS2;
RUN;
/*DECIDE WEATHER TO CREATE NEW REPORT OR APPEND INFO TO EXISTING REPORT*/
%IF &NewInd = 1 %THEN %DO;
	PROC PRINTTO PRINT=REPORT5 NEW;
	RUN;
	OPTIONS ORIENTATION = LANDSCAPE;
	OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
%END;
%ELSE %DO;
	PROC PRINTTO PRINT=REPORT5;
	RUN;
	OPTIONS ORIENTATION = LANDSCAPE;
	OPTIONS PS=39 LS=127 CENTER DATE;
%END;

TITLE "COHORT DEFAULT RATE ANALYSIS FFY&REPYR";
FOOTNOTE   'JOB = UTLWM23  	 REPORT = ULWM23.LWM23R5';
PROC REPORT DATA=PRNT_DS NOWD SPLIT='/' HEADSKIP ;
COLUMN SUBCAT CSRT CAT CTOT PRCNT;
DEFINE SUBCAT / GROUP noprint;
DEFINE CSRT / GROUP NOPRINT;
DEFINE CAT / DISPLAY "CATEGORY" WIDTH=15 ;
DEFINE CTOT / ANALYSIS SUM "NUMBER OF/BORROWERS" FORMAT=COMMA6. WIDTH=10;
DEFINE PRCNT / ANALYSIS "PERCENTAGE" FORMAT=PERCENT10.2;

BREAK AFTER SUBCAT / SUMMARIZE SUPPRESS SKIP OL;

COMPUTE BEFORE SUBCAT;
	IF SUBCAT EQ 1 THEN
		TEXT = 'CURRENT CHORT DEFAULT RATE';
	ELSE IF SUBCAT EQ 2 THEN 
		TEXT = 'BORROWERS AT RISK';
	ELSE IF SUBCAT EQ 3 THEN 
		TEXT = 'BORROWERS SAVED';
	ELSE IF SUBCAT EQ 4 THEN 
		TEXT = 'DATA ERRORS';
	LINE @27 TEXT $35.;
ENDCOMP;

COMPUTE AFTER;
LINE ' ';
LINE @1 'TOTAL # OF BORROWERS IN COHORT: ' @35 "&cCHRT_GT" ;
ENDCOMP;
RUN;

PROC PRINTTO;
RUN;

%MEND PROC2;
%PROC2(PRV,P,P,&iPrvChrtEnd,1);
%PROC2(CUR,C,C,&iCurChrtEnd,0);
%PROC2(NXT,D,F,&iNxtChrtEnd,0);
%PROC2(TRE,E,T,&iTreChrtEnd,0);
/*************************************************
* CREATE FILES FOR QUEUE BUILDER SCRIPT
**************************************************/
DATA CN;
	SET PRV CUR NXT TRE;
RUN;

PROC SQL;
CREATE TABLE QFIL AS 
SELECT DISTINCT A.*
	,B.SUBCAT
	,B.CSRT
FROM LNDET A
LEFT OUTER JOIN CN B
	ON A.DF_PRS_ID_BR = B.DF_PRS_ID_BR
	AND A.CHRT_YR = B.CHRT_YR
;
QUIT;

%MACRO STFL(RNUM,DQ,OPR,CHRT_YR_IND,PHN_IND,CAT_CRIT,CSRT_CRIT);
PROC SQL;
CREATE TABLE DSX AS
SELECT DISTINCT A.DF_PRS_ID_BR AS BF_SSN
	,"&DQ" AS QUEUE
	,'' AS INST_ID
	,'' AS INST_TYPE
	,'' AS DUE_DATE
	,'' AS DUE_TIME
	,'' AS TEXT
FROM QFIL A
WHERE A.DI_PHN_VLD = "&PHN_IND"
	AND A.DI_FGN_PHN ^= 'Y'
	AND 
	(
		(
			TODAY() - LD_PCL_SUP_LST_ATT > 3 AND  
			TODAY() - LD_PCL_SUP_LST_CNC > 5
		)
	OR
		(
			TODAY() - LD_PCL_SUP_LST_ATT > 3 AND  
			LD_PCL_SUP_LST_CNC = .
		)
	OR
		(
			LD_PCL_SUP_LST_ATT = . AND
			LD_PCL_SUP_LST_CNC = .
		)
	)
	AND A.SUBCAT IN (&CAT_CRIT)
	AND A.CSRT = &CSRT_CRIT
	AND A.CHRT_YR = "&CHRT_YR_IND"
;
QUIT;

DATA _NULL_;
SET  WORK.DSX;
FILE REPORT&RNUM DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT QUEUE $8. ;
   FORMAT INST_ID $1. ;
   FORMAT INST_TYPE $1. ;
   FORMAT DUE_DATE $1. ;
   FORMAT DUE_TIME $1. ;
   FORMAT TEXT $1. ;
DO;
   PUT BF_SSN @;
   PUT QUEUE @;
   PUT INST_ID @;
   PUT INST_TYPE @;
   PUT DUE_DATE @;
   PUT DUE_TIME @;
   PUT TEXT $ ;
END;
RUN;
%MEND STFL;
/*this is where questions are*/
%STFL(2,CCOHORTS,EQ,P,Y,%STR(2),1);
%STFL(3,CCOHORTS,EQ,C,Y,%STR(2),1);
%STFL(7,FCOHORTS,EQ,F,Y,%STR(2),1);
%STFL(9,P3YCHRT,EQ,T,Y,%STR(2),1);


/*WHAT IT WAS:*/
/*%STFL(2,CCOHORTS,EQ,C,Y,%STR(2),1);*/
/*%STFL(3,FCOHORTS,EQ,F,Y,%STR(2),1);*/
/*%STFL(9,P3YCHRT,EQ,T,Y,%STR(2),1);*/
/*CREATE SKIP FILE*/
PROC SQL;
CREATE TABLE DSX AS
SELECT DISTINCT A.DF_PRS_ID_BR AS BF_SSN
	,"CHRTSKIP" AS QUEUE
	,'' AS INST_ID
	,'' AS INST_TYPE
	,'' AS DUE_DATE
	,'' AS DUE_TIME
	,'' AS TEXT
FROM QFIL A
WHERE A.DI_PHN_VLD = "N"
	AND 
		(
			(
				TODAY() - A.LD_PCL_SUP_LST_ATT > 3 AND  
				TODAY() - A.LD_PCL_SUP_LST_CNC > 5
			)
		OR
			(
				TODAY() - A.LD_PCL_SUP_LST_ATT > 3 AND  
				A.LD_PCL_SUP_LST_CNC = .
			)
		OR
			(
				A.LD_PCL_SUP_LST_ATT = . AND
				A.LD_PCL_SUP_LST_CNC = .
			)
		)
	AND A.SUBCAT = 2
	AND A.CSRT = 1
;
QUIT;
DATA _NULL_;
SET  WORK.DSX;
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT QUEUE $8. ;
   FORMAT INST_ID $1. ;
   FORMAT INST_TYPE $1. ;
   FORMAT DUE_DATE $1. ;
   FORMAT DUE_TIME $1. ;
   FORMAT TEXT $1. ;
DO;
   PUT BF_SSN $ @;
   PUT QUEUE $ @;
   PUT INST_ID $ @;
   PUT INST_TYPE $ @;
   PUT DUE_DATE $ @;
   PUT DUE_TIME $ @;
   PUT TEXT $ ;
END;
RUN;


/***********************************************************************
* CREATE ERROR REPORT
************************************************************************/
PROC SQL;
CREATE TABLE ERR_DAT AS 
SELECT DISTINCT A.DF_SPE_ACC_ID
	,A.AC_LON_STA_TYP
	,A.DM_PRS_1
	,A.DM_PRS_LST
FROM LNDET A
RIGHT OUTER JOIN (
		SELECT DF_PRS_ID_BR
		FROM CUR 
		WHERE SUBCAT = 4
	UNION
		SELECT DF_PRS_ID_BR
		FROM NXT
		WHERE SUBCAT = 4
	UNION
		SELECT DF_PRS_ID_BR
		FROM TRE
		WHERE SUBCAT = 4
	) B
	ON A.DF_PRS_ID_BR = B.DF_PRS_ID_BR
ORDER BY A.DF_SPE_ACC_ID
;
QUIT;

PROC PRINTTO PRINT=REPORT6 NEW;
RUN;

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'NSLDS COHORT DEFAULT RATE DATA ERROR REPORT';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWM23  	 REPORT = ULWM23.LWM23R6';

PROC CONTENTS DATA=ERR_DAT OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWM23  	 REPORT = ULWM23.LWM23R6";
	END;
RETURN;
RUN;

PROC PRINT NOOBS SPLIT='/' DATA=ERR_DAT WIDTH=UNIFORM WIDTH=MIN;
VAR DF_SPE_ACC_ID
	DM_PRS_1
	DM_PRS_LST
	AC_LON_STA_TYP;
LABEL DF_SPE_ACC_ID = 'BORROWER/ACCOUNT/NUMBER'
	DM_PRS_1 = 'BORROWER/FIRST/NAME'
	DM_PRS_LST = 'BORROWER/LAST/NAME'
	AC_LON_STA_TYP = 'LOAN/STATUS/TYPE';
RUN;
PROC PRINTTO;
RUN;
/*FILE FOR REPURCHASED BORROWERS*/
PROC SQL;
CREATE TABLE RPBR AS
SELECT DISTINCT A.DF_PRS_ID_BR AS BF_SSN
	,"PCHTRPCH" AS QUEUE
	,'' AS INST_ID
	,'' AS INST_TYPE
	,'' AS DUE_DATE
	,'' AS DUE_TIME
	,CASE
		WHEN A.CHRT_YR = 'P' THEN 'PREVIOUS'
		WHEN A.CHRT_YR = 'C' THEN 'CURRENT'
		WHEN A.CHRT_YR = 'F' THEN 'FUTURE' 
		WHEN A.CHRT_YR = 'T' THEN 'FUTURE3' 
	 END AS TEXT
	,A.LD_AUX_STA_UPD
	,A.LD_LDR_POF
FROM QFIL A
WHERE &iCurChrtBeg <= A.LD_LDR_POF <= &iCurChrtEnd
	AND &iCurChrtBeg <= A.DT_ENT_REPMT <= &iCurChrtEnd
	AND LC_AUX_STA = '01'
	AND COALESCE(A.LD_AUX_STA_UPD,A.LD_LDR_POF + 59) < A.LD_LDR_POF + 60
	AND NOT EXISTS 
	(
		SELECT 1
		FROM DACT X
		WHERE X.BF_SSN = A.DF_PRS_ID_BR
	)
;
QUIT;
DATA _NULL_;
SET  RPBR;
FILE REPORT8 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT QUEUE $8. ;
   FORMAT INST_ID $1. ;
   FORMAT INST_TYPE $1. ;
   FORMAT DUE_DATE $1. ;
   FORMAT DUE_TIME $1. ;
   FORMAT TEXT $7. ;
DO;
   PUT BF_SSN $ @;
   PUT QUEUE $ @;
   PUT INST_ID $ @;
   PUT INST_TYPE $ @;
   PUT DUE_DATE $ @;
   PUT DUE_TIME $ @;
   PUT TEXT $ ;
END;
RUN;
/*DEFAULT SUMMARY REPORT*/
PROC SQL;
	CREATE TABLE DEF_SUM AS
		SELECT DISTINCT A.DF_SPE_ACC_ID
			,A.DM_PRS_LST
			,CASE
				WHEN A.DI_PHN_VLD = 'N' AND A.DI_VLD_ADR = 'N' THEN 'B'
				WHEN A.DI_PHN_VLD = 'Y' AND A.DI_VLD_ADR = 'N' THEN 'A'
				WHEN A.DI_PHN_VLD = 'N' AND A.DI_VLD_ADR = 'Y' THEN 'P'
				ELSE ''
			 END AS SKP_STA
		FROM LNDET A
		INNER JOIN CN B
			ON A.DF_PRS_ID_BR = B.DF_PRS_ID_BR
		WHERE B.SUBCAT = 1
			AND B.CSRT = 1 
		ORDER BY A.DM_PRS_LST
;
QUIT;
PROC PRINTTO PRINT=REPORT10 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'DEFAULT SUMMARY NSLDS';
FOOTNOTE   'JOB = UTLWM23  	 REPORT = ULWM23.LWM23R10';
PROC CONTENTS DATA=DEF_SUM OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWM23  	 REPORT = ULWM23.LWM23R10";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=DEF_SUM WIDTH=UNIFORM WIDTH=MIN;
VAR DF_SPE_ACC_ID
	DM_PRS_LST
	SKP_STA;
LABEL DF_SPE_ACC_ID = 'BORROWER/ACCOUNT/NUMBER'
	DM_PRS_LST = 'BORROWER/LAST/NAME'
	SKP_STA = 'SKIP STATUS';
RUN;
PROC PRINTTO;
RUN;

/********************************************************************************
* DETAIL FILE - TESTING ONLY COMMENT FOR PRODUCTION
*********************************************************************************/
/*DATA _NULL_;*/
/*SET  WORK.LNDET ;*/
/*FILE 'T:\SAS\UTLWM23.DETAIL.TXT' DELIMITER=',' DSD DROPOVER LRECL=32767;*/
/*   FORMAT LNID $19. ;*/
/*   FORMAT DT_ENT_REPMT DATE9. ;*/
/*   FORMAT DF_PRS_ID_BR $9. ;*/
/*   FORMAT CLUID $19. ;*/
/*   FORMAT AC_LON_TYP $2. ;*/
/*   FORMAT DI_PHN_VLD $1. ;*/
/*   FORMAT LC_STA_DC10 $2. ;*/
/*   FORMAT LC_PCL_REA $2. ;*/
/*   FORMAT LD_DCO DATE9. ;*/
/*   FORMAT AC_LON_STA_TYP $2. ;*/
/*   FORMAT AC_LON_STA_REA $2. ;*/
/*   FORMAT DF_SPE_ACC_ID $10. ;*/
/*   FORMAT DM_PRS_1 $12. ;*/
/*   FORMAT DM_PRS_LST $35. ;*/
/*   FORMAT CHRT_YR $1. ;*/
/*IF _N_ = 1 THEN        */
/*DO;*/
/* PUT*/
/*   'LNID'*/
/*   ','*/
/*   'DT_ENT_REPMT'*/
/*   ','*/
/*   'DF_PRS_ID_BR'*/
/*   ','*/
/*   'CLUID'*/
/*   ','*/
/*   'AC_LON_TYP'*/
/*   ','*/
/*   'DI_PHN_VLD'*/
/*   ','*/
/*   'LC_STA_DC10'*/
/*   ','*/
/*   'LC_PCL_REA'*/
/*   ','*/
/*   'LD_DCO'*/
/*   ','*/
/*   'AC_LON_STA_TYP'*/
/*   ','*/
/*   'AC_LON_STA_REA'*/
/*   ','*/
/*   'DF_SPE_ACC_ID'*/
/*   ','*/
/*   'DM_PRS_1'*/
/*   ','*/
/*   'DM_PRS_LST'*/
/*   ','*/
/*   'CHRT_YR'*/
/*   ;*/
/*END;*/
/*DO;*/
/*   PUT LNID $ @;*/
/*   PUT DT_ENT_REPMT @;*/
/*   PUT DF_PRS_ID_BR $ @;*/
/*   PUT CLUID $ @;*/
/*   PUT AC_LON_TYP $ @;*/
/*   PUT DI_PHN_VLD $ @;*/
/*   PUT LC_STA_DC10 $ @;*/
/*   PUT LC_PCL_REA $ @;*/
/*   PUT LD_DCO @;*/
/*   PUT AC_LON_STA_TYP $ @;*/
/*   PUT AC_LON_STA_REA $ @;*/
/*   PUT DF_SPE_ACC_ID $ @;*/
/*   PUT DM_PRS_1 $ @;*/
/*   PUT DM_PRS_LST $ @;*/
/*   PUT CHRT_YR $ ;*/
/*   ;*/
/*END;*/
/*RUN;*/
