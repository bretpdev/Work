/*TILP Teaching Credit Applied*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS26.LWS26R2";
FILENAME REPORTZ "&RPTLIB/ULWS26.LWS26RZ";

OPTIONS SYMBOLGEN NOCENTER NODATE NONUMBER LS=132;

LIBNAME DUSTER REMOTE SERVER=DUSTER SLIBREF=WORK;
RSUBMIT;

PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE TILPPOP AS
	SELECT 
		*
	FROM
		CONNECTION TO DB2 
		(
			SELECT
				 LN10.BF_SSN
				,PD30.DC_ADR
				,PD10.DF_SPE_ACC_ID
				,PD10.DM_PRS_1
				,PD10.DM_PRS_LST
				,PD30.DX_STR_ADR_1
				,PD30.DX_STR_ADR_2
				,PD30.DM_CT
				,CASE 
					WHEN PD30.DC_DOM_ST = '' OR PD30.DC_DOM_ST = 'FC'
					THEN PD30.DM_FGN_ST 
					ELSE PD30.DC_DOM_ST
				END AS DC_DOM_ST
				,PD30.DF_ZIP_CDE
				,PD30.DM_FGN_CNY
				,SUM(LN10.LA_CUR_PRI) - SUM(LN90.LA_FAT_CUR_PRI) AS BEGIN_BAL
				,MAX(LN90.LD_FAT_EFF) - 1 DAYS AS BEGIN_DATE
				,SUM(LN90.LA_FAT_CUR_PRI) AS LA_FAT_CUR_PRI
				,MAX(LN90.LD_FAT_EFF) AS LD_FAT_EFF
				,SUM(LN10.LA_CUR_PRI) AS LA_CUR_PRI
				,CURRENT DATE AS EFF_DATE
				,YEAR(CURRENT DATE) AS CUR_YR
				,MONTH(CURRENT DATE) AS CUR_MO
				,CASE 
					WHEN PD30.DC_DOM_ST = '' OR PD30.DC_DOM_ST = 'FC'
					THEN ''
					ELSE PD30.DC_DOM_ST
				END AS STATE_IND
				,'MA2324' AS COST_CENTER_CODE
			FROM 
				OLWHRM1.LN10_LON LN10
				INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
					ON LN90.BF_SSN = LN10.BF_SSN
					AND LN90.LN_SEQ = LN10.LN_SEQ
					AND LN90.PC_FAT_TYP = '10' /*teaching credit*/
					AND LN90.PC_FAT_SUB_TYP = '54' /*teaching credit*/
					AND LN90.LD_FAT_PST <= (CURRENT DATE) - 31 DAYS /*within past month*/
					AND LN90.LC_STA_LON90 = 'A' /*active*/
				INNER JOIN OLWHRM1.PD10_PRS_NME PD10
					ON PD10.DF_PRS_ID = LN10.BF_SSN
				INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
					ON PD30.DF_PRS_ID = LN10.BF_SSN
					AND PD30.DC_ADR = 'L'
				LEFT JOIN 
				(/*statement already sent to borrower this year*/
					SELECT DISTINCT 
						BF_SSN
						,MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
					FROM 
						OLWHRM1.AY10_BR_LON_ATY
					WHERE 
						PF_REQ_ACT = 'TLTCP'
					GROUP BY 
						BF_SSN
				) EXCLUDE
					ON EXCLUDE.BF_SSN = LN10.BF_SSN
					AND EXCLUDE.LD_ATY_REQ_RCV > LN90.LD_FAT_PST
			WHERE 
				LN10.IC_LON_PGM = 'TILP'
				AND EXCLUDE.BF_SSN IS NULL
			GROUP BY 
				PD10.DF_SPE_ACC_ID
				,PD10.DM_PRS_1
				,PD10.DM_PRS_LST
				,PD30.DX_STR_ADR_1
				,PD30.DX_STR_ADR_2
				,PD30.DM_CT
				,PD30.DC_DOM_ST 
				,PD30.DF_ZIP_CDE
				,PD30.DM_FGN_ST 
				,PD30.DM_FGN_CNY
				,LN10.BF_SSN
				,PD30.DC_ADR

			FOR READ ONLY WITH UR
		)
	WHERE
		LA_CUR_PRI > 0.00
;
DISCONNECT FROM DB2;
QUIT;
ENDRSUBMIT;

DATA TILPPOP; 
	SET DUSTER.TILPPOP; 
RUN;
PROC SORT DATA=TILPPOP;
	BY STATE_IND;
RUN;

DATA TILPPOP2 (DROP=CDT PDT CUR_YR CUR_MO);
	SET TILPPOP;
	FORMAT 	CDT $4.
			PDT $4.
			DTSTR $23.
	;
	IF CUR_MO < 4
	THEN 
		DO;
			CDT = CUR_YR-1;
			PDT = CUR_YR-2;
			DTSTR = '07/01/' || (PDT) || ' - ' || '06/30/' || CDT ;
		END;
	ELSE 
		DO;
			CDT = CUR_YR;
			PDT = CUR_YR-1;
			DTSTR = '07/01/' || (PDT) || ' - ' || '06/30/' || CDT ;
		END;
RUN;

/**************************************************************
* CALCULATE KEYLINE FOR BORROWER AND COBORROWER
***************************************************************/
%MACRO KEYLINE(DS,SSN);
	DATA &DS (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
		CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
		SET &DS;
		KEYSSN = TRANSLATE(&SSN,'MYLAUGHTER','0987654321');
		MODAY = PUT(DATE(),MMDDYYN4.);
		KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
		CHKDIG = 0;
		LENGTH DIG $2.;
		DO I = 1 TO LENGTH(KEYLINE);
			IF I/2 NE ROUND(I/2,1) 
				THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
			ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
			IF SUBSTR(DIG,1,1) = " " 
				THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
				ELSE DO;
					CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
					CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
					IF CHK1 + CHK2 >= 10
						THEN DO;
							CHK3 = PUT(CHK1 + CHK2,2.);
							CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
							CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
						END;
					CHKDIG = CHKDIG + CHK1 + CHK2;
				END;
		END;
		CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
		IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
		CHECK = PUT(CHKDIGIT,1.);
		ACSKEY = "#"||KEYLINE||CHECK||"#";
	RUN;
%MEND KEYLINE;
%KEYLINE(TILPPOP2,BF_SSN);

DATA _NULL_;
	SET WORK.TILPPOP2 ;
	FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT  BF_SSN $9.
			DF_SPE_ACC_ID $10.
			DM_PRS_1 $13.
			DM_PRS_LST $23.
			DX_STR_ADR_1 $30.
			DX_STR_ADR_2 $30.
			DM_CT $20.
			DC_DOM_ST $15.
			DF_ZIP_CDE $17.
			DM_FGN_CNY $25.
			BEGIN_BAL DOLLAR18.
			BEGIN_DATE MMDDYY10.
			LA_FAT_CUR_PRI DOLLAR18.
			LD_FAT_EFF MMDDYY10.
			LA_CUR_PRI DOLLAR18.
			EFF_DATE MMDDYY10.
			STATE_IND $2.
			COST_CENTER_CODE $6.
			DTSTR $23.
			ACSKEY $18.
	;
	IF _N_ = 1 
	THEN
		DO;
			PUT
				'BF_SSN'
				','
				'DF_SPE_ACC_ID'
				','
				'DM_PRS_1'
				','
				'DM_PRS_LST'
				','
				'DX_STR_ADR_1'
				','
				'DX_STR_ADR_2'
				','
				'DM_CT'
				','
				'DC_DOM_ST'
				','
				'DF_ZIP_CDE'
				','
				'DM_FGN_CNY'
				','
				'BEGIN_BAL'
				','
				'BEGIN_DATE'
				','
				'LA_FAT_CUR_PRI'
				','
				'LD_FAT_EFF'
				','
				'LA_CUR_PRI'
				','
				'EFF_DATE'
				','
				'DTSTR'
				','
				'ACSKEY'
				','
				'STATE_IND'
				','
				'COST_CENTER_CODE'
			;
		END;
	DO;
		EFIOUT + 1;
		PUT BF_SSN $ @ ;
		PUT DF_SPE_ACC_ID $ @;
		PUT DM_PRS_1 $ @;
		PUT DM_PRS_LST $ @;
		PUT DX_STR_ADR_1 $ @;
		PUT DX_STR_ADR_2 $ @;
		PUT DM_CT $ @;
		PUT DC_DOM_ST $ @;
		PUT DF_ZIP_CDE $ @;
		PUT DM_FGN_CNY $ @;
		PUT BEGIN_BAL $ @;
		PUT BEGIN_DATE $ @;
		PUT LA_FAT_CUR_PRI @;
		PUT LD_FAT_EFF @;
		PUT LA_CUR_PRI @;
		PUT EFF_DATE @;
		PUT DTSTR $ @;
		PUT ACSKEY $ @;
		PUT STATE_IND $ @;
		PUT COST_CENTER_CODE $ ;
	END;
RUN;
