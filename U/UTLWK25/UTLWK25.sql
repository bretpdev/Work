USE UDW
GO

DECLARE 
	@Now DATETIME = GETDATE(),
	@Today DATE = GETDATE(),
	@Queue VARCHAR(10) = 'KREFRLTR',
	@SasId VARCHAR(20) = 'UTLWK25',
	@D1 DATE = DATEADD(DAY, -40, GETDATE()),
	@D2 DATE = DATEADD(DAY, -90, GETDATE()), 
	@D3 DATE = DATEADD(DAY, -10000, GETDATE());
	  
WITH BR03_Q AS 
(
	SELECT DISTINCT 
		BR03.DF_PRS_ID_BR,
		BR03.DF_PRS_ID_RFR,
		BR03.BD_RFR_LST_CNC_HME,
		BR03.BD_RFR_LST_ATT_HME
	FROM 
		ODW..BR03_BR_REF BR03
		INNER JOIN 
		(
			SELECT DISTINCT 
				GA01.DF_PRS_ID_BR
			FROM 
				ODW..GA01_APP GA01
				INNER JOIN ODW..GA10_LON_APP GA10
					ON GA01.AF_APL_ID = GA10.AF_APL_ID
				INNER JOIN ODW..GA14_LON_STA GA14
					ON GA10.AF_APL_ID = GA14.AF_APL_ID
					AND GA10.AF_APL_ID_SFX = GA14.AF_APL_ID_SFX
			WHERE 
				GA14.AC_LON_STA_TYP IN 
				(
					'CR','DA','FB','IA','ID','IG','IM',
					'RP','CP','UA','UB','UC','UD','UI'		
				)
				AND GA14.AC_STA_GA14 = 'A'
		) GA14
			ON GA14.DF_PRS_ID_BR = BR03.DF_PRS_ID_BR
WHERE 
	BR03.BC_STA_BR03 = 'A'
	AND BR03.BC_HME_CNC_RSL != '04'
	AND BR03.BI_VLD_ADR = 'Y'
	AND BX_RFR_STR_ADR_1 IS NOT NULL
), FL1_Q AS 
(
	SELECT DISTINCT 
		LN10.BF_SSN,
		CASE 
			WHEN PD42.DF_PRS_ID IS NULL AND PD30.DF_PRS_ID IS NULL THEN 'N'
			ELSE 'Y'
		END AS COMP_SKP
	FROM 
		LN10_LON LN10
		LEFT JOIN PD30_PRS_ADR PD30
			ON PD30.DF_PRS_ID = LN10.BF_SSN
			AND DI_VLD_ADR = 'N'
			AND DC_ADR = 'L'
		LEFT JOIN PD42_PRS_PHN PD42
			ON PD42.DF_PRS_ID = LN10.BF_SSN
			AND DI_PHN_VLD = 'N'
			AND DC_PHN = 'H'
		LEFT JOIN
		(
			SELECT 
				LN10.BF_SSN
			FROM 
				LN10_LON LN10
			WHERE 
				LN10.LI_CON_PAY_STP_PUR = 'Y'
				AND LN10.LC_STA_LON10 = 'R'
			GROUP BY
				LN10.BF_SSN
		) EXC
			ON EXC.BF_SSN = LN10.BF_SSN
	WHERE 
		LN10.LA_CUR_PRI > 0.00
		AND LN10.LC_STA_LON10 = 'R'
		AND EXC.BF_SSN IS NULL
), EXC_Q AS 
(
	SELECT
		DF_PRS_ID_TGT,
		BD_ATY_PRF,
		EX_TYP
	FROM
	(
		SELECT DISTINCT 
			DF_PRS_ID_TGT, 
			LD_LST_CNC AS BD_ATY_PRF, /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
			'CQ' AS EX_TYP
		FROM 
			ODW..CT30_CALL_QUE
		WHERE 
			IF_WRK_GRP = @Queue

		UNION
	
		SELECT DISTINCT 
			DF_PRS_ID AS DF_PRS_ID_TGT,
			BD_ATY_PRF,
			'AC' AS EX_TYP
		FROM 
			ODW..AY01_BR_ATY
		WHERE 
			PF_ACT IN ('KLREF','KRREF','KABL2')

		UNION
	
		SELECT DISTINCT 
			DF_PRS_ID AS DF_PRS_ID_TGT,
			DD_SKP_TRC_EFF AS BD_ATY_PRF, /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
			'ST' AS EX_TYP
		FROM 
			ODW..PD01_PDM_INF
		WHERE
			DC_SKP_TRC_STA = 'S'
			AND CAST(DD_SKP_TRC_EFF AS DATE) > DATEADD(DAY, - 29, @Today)

		UNION

		SELECT DISTINCT 
			BF_SSN AS DF_PRS_ID_TGT,
			LD_ATY_REQ_RCV AS BD_ATY_PRF, /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
			'CA' AS EX_TYP
		FROM 
			UDW..AY10_BR_LON_ATY
		WHERE 
			PF_REQ_ACT = 'SKPME'
			AND LC_STA_ACTY10 = 'A'
			AND CAST(LD_ATY_REQ_RCV AS DATE) > DATEADD(DAY, -7, @Today)

		UNION

		SELECT 
			X.DF_PRS_ID AS DF_PRS_ID_TGT,
			X.DD_VER_ADR AS BD_ATY_PRF, /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
			'PD' AS EX_TYP
		FROM 
			UDW..PD30_PRS_ADR X
			INNER JOIN UDW..PD42_PRS_PHN Y
				ON X.DF_PRS_ID = Y.DF_PRS_ID
			LEFT JOIN 
			(
				SELECT 
					LN16.BF_SSN
				FROM 
					UDW..LN16_LON_DLQ_HST LN16
				WHERE 
					LN16.LC_STA_LON16 = '1'
					AND @Today > CAST(LN16.LD_DLQ_OCC AS DATE)
			) LN16
				ON LN16.BF_SSN = X.DF_PRS_ID
		WHERE 
			X.DI_VLD_ADR = 'Y'
			AND X.DC_ADR = 'L'
			AND Y.DI_PHN_VLD = 'N'
			AND Y.DC_PHN = 'H'

		UNION
	
		SELECT 
			BF_SSN AS DF_PRS_ID_TGT,
			@Today AS BD_ATY_PRF, /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
			'CS' AS EX_TYP
		FROM
			UDW..DW01_DW_CLC_CLU
		WHERE 
			WC_DW_LON_STA IN ('17','21')
	) EXC

) 

INSERT INTO OLS.olqtskbldr.Queues
(
	TargetId,
	QueueName,
	InstitutionId,
	InstitutionType,
	DateDue,
	TimeDue,
	Comment,
	SourceFilename,
	AddedAt,
	AddedBy
)
SELECT DISTINCT
	POP.DF_PRS_ID_RFR AS TargetId,
	@Queue AS QueueName,
	''	 AS InstitutionId,
	''	 AS InstitutionType,
	NULL AS DateDue,
	NULL AS TimeDue,
	POP.DF_PRS_ID_RFR AS Comment, --Original just adds the reference SSN as the Comment
	''	 AS SourceFilename,
	@Now AS AddedAt,
	@SasId AS AddedBy		
FROM
(
	--CREATE DATA FOR REPORT 2
	SELECT DISTINCT 
		BR03.DF_PRS_ID_BR,
		BR03.DF_PRS_ID_RFR
	FROM 
		BR03_Q BR03
		INNER JOIN FL1_Q FLA1
			ON BR03.DF_PRS_ID_BR = FLA1.BF_SSN
		LEFT JOIN
		(
			SELECT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP = 'CQ'

			UNION

			SELECT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP = 'AC'
				AND BD_ATY_PRF >= @D3
		) EXC1
			ON EXC1.DF_PRS_ID_TGT = BR03.DF_PRS_ID_RFR
		LEFT JOIN 
		(
			SELECT DISTINCT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP IN ('ST','CA','PD','CS')
		) EXC2
			ON EXC2.DF_PRS_ID_TGT = BR03.DF_PRS_ID_BR
	WHERE 
		FLA1.COMP_SKP = 'Y'
		AND BR03.BD_RFR_LST_CNC_HME < @D1
		AND BR03.BD_RFR_LST_ATT_HME < @D2
		AND EXC1.DF_PRS_ID_TGT IS NULL
		AND EXC2.DF_PRS_ID_TGT IS NULL

	UNION

	--CREATE DATA FOR REPORT 3 & 4
	SELECT DISTINCT 
		BR03.DF_PRS_ID_BR,
		BR03.DF_PRS_ID_RFR
	FROM 
		BR03_Q BR03
		INNER JOIN
		(
			SELECT 
				GA01.DF_PRS_ID_BR,
				DC01_A.STA_03,
				DC01_B.STA_03_ONLY,
				GA10.OL_BAL
			FROM 
				ODW..GA01_APP GA01 
				INNER JOIN ODW..PD03_PRS_ADR_PHN PD03
					ON GA01.DF_PRS_ID_BR = PD03.DF_PRS_ID
				LEFT JOIN 
				(
					SELECT 
						BF_SSN,
						'Y' AS STA_03
					FROM 
						ODW..DC01_LON_CLM_INF
					WHERE 
						LC_STA_DC10 = '03'
						AND LD_CLM_ASN_DOE IS NULL
						AND LC_AUX_STA = ''
				) DC01_A
					ON GA01.DF_PRS_ID_BR = DC01_A.BF_SSN
				LEFT JOIN 
				(
					SELECT DISTINCT 
						GX.DF_PRS_ID_BR,
						'Y' AS OL_BAL 
					FROM 
						ODW..GA01_APP GX 
						INNER JOIN ODW..GA10_LON_APP GY
							ON GX.AF_APL_ID = GY.AF_APL_ID
					WHERE 
						GY.AA_CUR_PRI > 0
				) GA10
					ON GA01.DF_PRS_ID_BR = GA10.DF_PRS_ID_BR
				LEFT JOIN 
				(
					SELECT
						BF_SSN,
						'Y' AS STA_03_ONLY
					FROM 
						ODW..DC01_LON_CLM_INF
					WHERE 
						LC_STA_DC10 = '03'
				) DC01_B
					ON GA01.DF_PRS_ID_BR = DC01_B.BF_SSN
			WHERE
				PD03.DC_ADR = 'L'
				AND 
				(
					PD03.DI_VLD_ADR = 'N' 
					OR PD03.DI_PHN_VLD = 'N'
				)
		) FL2_A
			ON BR03.DF_PRS_ID_BR = FL2_A.DF_PRS_ID_BR
		LEFT JOIN
		(
			SELECT 
				BF_SSN AS DF_PRS_ID_TGT 
			FROM 
				FL1_Q

			UNION

			SELECT DISTINCT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP IN ('ST','CS')
		) EXC1
			ON FL2_A.DF_PRS_ID_BR = EXC1.DF_PRS_ID_TGT
		LEFT JOIN	
		(
			SELECT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP = 'CQ'

			UNION

			SELECT 
				DF_PRS_ID_TGT 
			FROM 
				EXC_Q
			WHERE 
				EX_TYP = 'AC'
				AND BD_ATY_PRF >= @D3
		) EXC2
			ON BR03.DF_PRS_ID_RFR = EXC2.DF_PRS_ID_TGT
	WHERE 
		(
			FL2_A.STA_03 = 'Y'
			OR
			(
				FL2_A.STA_03_ONLY = ''
				AND FL2_A.OL_BAL = 'Y'
			)
		)
		AND BR03.BD_RFR_LST_CNC_HME < @D1
		AND BR03.BD_RFR_LST_ATT_HME < @D2
		AND EXC1.DF_PRS_ID_TGT IS NULL
		AND EXC2.DF_PRS_ID_TGT IS NULL
) POP
	LEFT JOIN OLS.olqtskbldr.Queues EXISTING
		ON EXISTING.TargetId = POP.DF_PRS_ID_RFR
		AND EXISTING.QueueName = @Queue
		AND CAST(EXISTING.AddedAt AS DATE) = @Today
		AND EXISTING.AddedBy = @SasId
WHERE
	EXISTING.TargetId IS NULL;


			