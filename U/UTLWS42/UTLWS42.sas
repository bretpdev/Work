/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWS42.LWS42RZ";
FILENAME REPORT2 "&RPTLIB/ULWS42.LWS42R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
PROC SQL;
CREATE TABLE DEMO AS
SELECT DISTINCT A.DF_PRS_ID
	,A.DF_SPE_ACC_ID
	,TRIM(A.DM_PRS_1) || ' ' ||TRIM(A.DM_PRS_LST) AS BOR_NAME
	,B.DX_ADR_EML
	,CASE
		WHEN B.DC_ADR_EML = 'H' THEN '1'
		WHEN B.DC_ADR_EML = 'A' THEN '2'
		WHEN B.DC_ADR_EML = 'W' THEN '3'
	END AS DC_ADR_EML
FROM DLGSUTWH.PD10_PRS_NME A
INNER JOIN DLGSUTWH.PD32_PRS_ADR_EML B
	ON A.DF_PRS_ID = B.DF_PRS_ID
INNER JOIN DLGSUTWH.LN10_LON	C
	ON A.DF_PRS_ID = C.BF_SSN
	LEFT OUTER JOIN DLGSUTWH.AY10_BR_LON_ATY D
		ON C.BF_SSN = D.BF_SSN
		AND D.PF_REQ_ACT = 'ERPMT'
		AND D.LD_ATY_REQ_RCV >= TODAY() - 10
	LEFT OUTER JOIN DLGSUTWH.DF10_BR_DFR_REQ E
		ON C.BF_SSN = E.BF_SSN
		AND LD_DFR_REQ_BEG = C.LD_END_GRC_PRD + 1
		AND LC_STA_DFR10 = 'A'
	LEFT OUTER JOIN DLGSUTWH.LN50_BR_DFR_APV F
		ON C.BF_SSN = F.BF_SSN 
		AND LD_DFR_BEG = C.LD_END_GRC_PRD + 1
		AND LC_STA_LON50 = 'A'
	LEFT OUTER JOIN DLGSUTWH.FB10_BR_FOR_REQ G
		ON C.BF_SSN = G.BF_SSN 
		AND LD_FOR_REQ_BEG = C.LD_END_GRC_PRD + 1
		AND LC_FOR_TYP IN ('10','22')
WHERE LC_STA_LON10 = 'R'
	AND LA_CUR_PRI > 0
	AND C.LD_END_GRC_PRD BETWEEN TODAY() AND TODAY() + 10
	AND B.DI_VLD_ADR_EML = 'Y'
	AND B.DC_STA_PD32 = 'A'
	AND B.DC_ADR_EML IN ('H','A','W')
	AND D.BF_SSN IS NULL
	AND E.BF_SSN IS NULL
	AND F.BF_SSN IS NULL
	AND G.BF_SSN IS NULL
;
QUIT;
ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN; 
/*EXCLUDE BORROWERS ENTERING A DEFERMENT, OR A BANKRUPTCY OR MISSIONARY 
FORBEARANCE IMMEDIATELY AFTER THE GRACE PERIOD*/
/*The report only calls for one email per borrower*/
/*if multiple emails were picked up, it will print */
/*only one, prioritizing the home email*/
PROC SORT DATA=DEMO NODUPKEY;
	BY DF_SPE_ACC_ID DC_ADR_EML;
RUN;

DATA _NULL_;
SET DEMO ;
BY DF_SPE_ACC_ID DC_ADR_EML;
/*TO SELECT ONLY THE FIRST EMAIL PER BORROWER*/
IF FIRST.DF_SPE_ACC_ID;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNT NUMBER,NAME,RECIPIENT";
END;
DO;
   PUT DF_SPE_ACC_ID @;
   PUT BOR_NAME @;
   PUT DX_ADR_EML $;
END;
RUN;
