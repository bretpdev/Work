/*	THIS REPORT PRINTS THE TOTAL NUMBER OF PCAS AND TOTAL DOLLAR AMOUNT
	FOR THE PREVIOUS FEDERAL FISCAL YEAR PERIOD.
*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(REPORTDIR);
FILENAME REPORT2 "&RPTLIB/ULWM06.LWM06R2";*/

/*FISCAL YEAR*/
DATA _NULL_;
CALL SYMPUT('BEGIN',"'"||PUT(INTNX('YEAR.7',TODAY()+3,-1,'BEGINNING'), MMDDYYD10.)||"'");
CALL SYMPUT('END',"'"||PUT(INTNX('YEAR.7',TODAY()+3,-1,'END'), MMDDYYD10.)||"'");
CALL SYMPUT('FY',PUT(YEAR(INTNX('YEAR.7',TODAY()+3,-1,'END')),4.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;
RSUBMIT;
OPTIONS SYMBOLGEN;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FYDAAR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	
		A.BF_SSN						AS SSN,
		A.AF_APL_ID||A.AF_APL_ID_SFX	AS CLUID,
		A.LD_PCL_RCV					AS PCLRCV,
		A.LA_CLM_PRI					AS PRIDFL,
		A.LA_CLM_INT					AS INTDFL,
		A.LC_CLM_LON_TYP				AS LONTYP,
		CASE
		WHEN A.LD_ORG_DCO IS NOT NULL THEN A.LD_ORG_DCO
		ELSE A.LD_DCO
		END								AS DCODT,
		A.LF_CUR_LON_SER_AGY			AS CURSER
FROM  OLWHRM1.DC01_LON_CLM_INF A
WHERE A.LD_PCL_RCV BETWEEN &BEGIN AND &END
AND A.LC_PCL_REA IN ('DF','DQ')
ORDER BY A.BF_SSN, A.AF_APL_ID||A.AF_APL_ID_SFX
);
DISCONNECT FROM DB2;
ENDRSUBMIT  ;

DATA FYDAAR; 
SET WORKLOCL.FYDAAR; 
RUN;

PROC SQL;
CREATE TABLE FYDAARSTAT AS
SELECT	SUM(PRIDFL)	AS SUMPRI,
		SUM(INTDFL)	AS SUMINT,
		COUNT(*)	AS LONCNT,
		.			AS PCACNT
FROM FYDAAR
;
QUIT;

DATA FYDAAR; 
SET FYDAAR; 
IF LONTYP IN ('SF','SU') THEN
	LONTYP = 'S';
RUN;

PROC SORT DATA=FYDAAR NODUPKEY;
BY SSN LONTYP PCLRCV DCODT CURSER;
RUN;

PROC SQL;
UPDATE FYDAARSTAT
SET PCACNT=(SELECT COUNT(*) FROM FYDAAR);
QUIT;

DATA FYDAARSTAT;
SET FYDAARSTAT;
TOTDOL = SUM(SUMINT, SUMPRI);
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS CENTER NODATE NONUMBER LS=132;
PROC PRINT NOOBS SPLIT='/' DATA=FYDAARSTAT WIDTH=UNIFORM WIDTH=MIN;
VAR LONCNT PCACNT TOTDOL;
LABEL LONCNT='NUMBER OF LOANS' PCACNT='NUMBER OF PCAS'
	  TOTDOL = 'TOTAL DOLLAR AMOUNT';
FORMAT LONCNT PCACNT COMMA10. TOTDOL DOLLAR15.2;
TITLE "FEDERAL FISCAL YEAR &FY DAAR REQUEST TOTALS";
TITLE2 "RUN DATE:  &RUNDATE";
FOOTNOTE  'JOB = UTLWM06     REPORT = ULWM06.LWM06R2';
RUN;

/*TESTFILE*/
/*
DATA FYDAAR; 
SET WORKLOCL.FYDAAR; 
RUN;

DATA FYD1;
SET FYDAAR (FIRSTOBS=1 OBS=50000);
RUN;
DATA FYD2;
SET FYDAAR (FIRSTOBS=50001 OBS=100000);
RUN;

PROC EXPORT DATA= FYD1 
            OUTFILE= "T:\SAS\FYD1.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;
PROC EXPORT DATA= FYD2 
            OUTFILE= "T:\SAS\FYD2.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;
*/
