/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS22.NWS22RZ";
FILENAME REPORT2 "&RPTLIB/UNWS22.NWS22R2";

data _null_  ;
   call symput  ( 'begin', "'"||put(today()-7,yymmdd10.)||"'" )   ;
   call symput  ( 'end', "'"||put(today()-1,yymmdd10.)||"'" )   ;
run;
%put &begin;
%put &end;

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=work  ;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
RSUBMIT LEGEND;

/*%let DB = DNFPRQUT;  *This is test;*/
%let DB = DNFPUTDL;  *This is live;

LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;

%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;


PROC SQL;
CONNECT TO DB2 (DATABASE=&DB);

/*GET FORBEARANCE INFO*/
CREATE TABLE FORB_INFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT 	DISTINCT
		FB10.BF_SSN
		,PD10.DF_SPE_ACC_ID
		,FB10.LF_USR_CRT_REQ_FOR
		,LN60.LD_FOR_APL
FROM 	PKUB.FB10_BR_FOR_REQ FB10
		INNER JOIN PKUB.LN60_BR_FOR_APV LN60
			ON FB10.BF_SSN = LN60.BF_SSN
			AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM
			AND FB10.LC_FOR_TYP = '05'
			AND LN60.LC_STA_LON60 = 'A'
			AND FB10.LC_FOR_STA = 'A'
			AND FB10.LC_STA_FOR10 = 'A'
			AND LN60.LD_FOR_APL BETWEEN &BEGIN AND &END
		INNER JOIN PKUB.PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = FB10.BF_SSN
WHERE	NOT EXISTS (SELECT	AY10.BF_SSN
					FROM	PKUB.AY10_BR_LON_ATY AY10
					WHERE	AY10.PF_REQ_ACT = 'FBEMA'
							AND AY10.LD_ATY_REQ_RCV BETWEEN &BEGIN AND &END
							AND AY10.BF_SSN = FB10.BF_SSN)

FOR READ ONLY WITH UR
);

/*GET GENERAL FORBEARNCE INFORMATION FOR ALL RECORDS*/
CREATE TABLE MASTER_FORBS_ALL AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT 	DISTINCT
			LN60.BF_SSN
			,LN60.LN_SEQ
			,LN60.LD_FOR_BEG
			,LN60.LD_FOR_END
			,(DAYS(LN60.LD_FOR_END) - DAYS(LN60.LD_FOR_BEG))/365.00*12.00 AS MONTHS_USED 
 	FROM	PKUB.FB10_BR_FOR_REQ FB10
 			INNER JOIN PKUB.LN60_BR_FOR_APV LN60
				ON FB10.BF_SSN = LN60.BF_SSN
				AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM
				AND FB10.LC_FOR_TYP = '05'
				AND LN60.LC_STA_LON60 = 'A'
				AND FB10.LC_FOR_STA = 'A'
				AND FB10.LC_STA_FOR10 = 'A'
FOR READ ONLY WITH UR
);

DISCONNECT FROM DB2;

CREATE TABLE MASTER_FORBS AS
SELECT	A.*
FROM	MASTER_FORBS_ALL A INNER JOIN
		FORB_INFO B ON
			A.BF_SSN = B.BF_SSN
;

/*GET DURATION OF ALL FORBEARANCES*/
CREATE TABLE ALL_FORBS AS
SELECT 	DISTINCT
		BF_SSN
		,LN_SEQ
		,SUM(MONTHS_USED) AS MONTHS_USED
FROM 	MASTER_FORBS
GROUP BY BF_SSN, LN_SEQ
ORDER BY BF_SSN, LN_SEQ
;


CREATE TABLE MAX_FORBS AS
SELECT		BF_SSN
			,LN_SEQ
			,MAX(LD_FOR_END) AS LD_FOR_END
FROM		MASTER_FORBS
GROUP BY 	BF_SSN, LN_SEQ
;

/*GET DURATION OF MOST RECENT FORBEARANCES*/
CREATE TABLE CURRENT_FORBS AS
SELECT 	DISTINCT
		A.BF_SSN
		,A.LN_SEQ
		,B.LD_FOR_END
		,MONTHS_USED
FROM 	MASTER_FORBS A INNER JOIN
		MAX_FORBS B ON
			A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
			AND A.LD_FOR_END = B.LD_FOR_END
;

/*DETAIL TABLES*/
CREATE TABLE ALL_FORBS_DETAIL AS
SELECT *
FROM 	MASTER_FORBS
;

CREATE TABLE CURRENT_FORBS_DETAIL AS
SELECT	A.*
FROM 	MASTER_FORBS A
		INNER JOIN CURRENT_FORBS B ON
			A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
			AND A.LD_FOR_END = B.LD_FOR_END
;
/*END DETAIL TABLES*/

/*SUBTRACT DURATION OF CURRENT FORBEARANCES FROM DURATION OF ALL FORBEARANCE*/
CREATE TABLE MONTHS_USED AS
SELECT	A.BF_SSN
		,A.LN_SEQ
		,A.MONTHS_USED - COALESCE(B.MONTHS_USED,0) AS MONTHS_USED
FROM	ALL_FORBS A
		LEFT OUTER JOIN CURRENT_FORBS B
			ON A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
ORDER BY BF_SSN
;

/*GET FORBEARANCE INFO FOR BORROWERS WHERE DURATION OF FORBEARANCES > 36 MONTHS*/
CREATE TABLE FORB_36 AS
SELECT	DISTINCT
		A.DF_SPE_ACC_ID
		,A.LF_USR_CRT_REQ_FOR
		,A.LD_FOR_APL
FROM	FORB_INFO A
		INNER JOIN MONTHS_USED B
			ON A.BF_SSN = B.BF_SSN
WHERE	B.MONTHS_USED > 36
;



/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
QUIT;


/*TEST FILES*/
PROC SQL;
CREATE TABLE SSNS AS
SELECT	B.DF_SPE_ACC_ID
		,B.BF_SSN
FROM	FORB_36 A
		INNER JOIN FORB_INFO B
		ON A.DF_SPE_ACC_ID = B.DF_SPE_ACC_ID;
QUIT;

PROC SQL;
CREATE TABLE ALL_FORBS_DETAIL_R AS
SELECT	B.*
FROM	SSNS A
		INNER JOIN ALL_FORBS_DETAIL B
		ON A.BF_SSN  = B.BF_SSN;
QUIT;

PROC SQL;
CREATE TABLE CURRENT_FORBS_DETAIL_R AS
SELECT	B.*
FROM	SSNS A
		INNER JOIN CURRENT_FORBS_DETAIL B
		ON A.BF_SSN  = B.BF_SSN;
QUIT;

PROC SQL;
CREATE TABLE MONTHS_USED_R AS
SELECT	B.*
FROM	SSNS A
		INNER JOIN MONTHS_USED B
		ON A.BF_SSN  = B.BF_SSN;
QUIT;

ENDRSUBMIT;

DATA FORB_36; SET LEGEND.FORB_36; RUN;

/*write to comma delimited file*/
DATA _NULL_;
	SET  WORK.FORB_36;
	FILE REPORT2 DSD DROPOVER lrecl=32767;

	FORMAT LF_USR_CRT_REQ_FOR $7.;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT LD_FOR_APL MMDDYY10. ;

	DO;
		PUT @1 DF_SPE_ACC_ID @11 ',G303M,,,,,,,ALL,forbearance processed by ' LF_USR_CRT_REQ_FOR @60 ' on ' LD_FOR_APL;
		;
	END;
RUN;


/*PROCESS TEST FILES*/
/*DATA ALL_FORBS_DETAIL_R; SET LEGEND.ALL_FORBS_DETAIL_R; RUN;*/
/*DATA CURRENT_FORBS_DETAIL_R; SET LEGEND.CURRENT_FORBS_DETAIL_R; RUN;*/
/*DATA MONTHS_USED_R; SET LEGEND.MONTHS_USED_R; RUN;*/
/**/
/*PROC EXPORT DATA= WORK.ALL_FORBS_DETAIL_R */
/*            OUTFILE= "T:\SAS\ALL_FORBS_DETAIL.xls" */
/*            DBMS=EXCEL REPLACE;*/
/*     SHEET="A"; */
/*RUN;*/
/**/
/*PROC EXPORT DATA= WORK.CURRENT_FORBS_DETAIL_R */
/*            OUTFILE= "T:\SAS\CURRENT_FORBS_DETAIL.xls" */
/*            DBMS=EXCEL REPLACE;*/
/*     SHEET="A"; */
/*RUN;*/
/**/
/*PROC EXPORT DATA= WORK.MONTHS_USED_R */
/*            OUTFILE= "T:\SAS\MONTHS_USED.xls" */
/*            DBMS=EXCEL REPLACE;*/
/*     SHEET="A"; */
/*RUN;*/

/*ADDITIONAL TEST FILES*/
/*DATA FORB_INFO; 	SET LEGEND.FORB_INFO; RUN;*/
/*DATA ALL_FORBS; SET LEGEND.ALL_FORBS; RUN;*/
/*DATA CURRENT_FORBS; SET LEGEND.CURRENT_FORBS; RUN;*/
/*DATA ALL_FORBS_DETAIL; SET LEGEND.ALL_FORBS_DETAIL; RUN;*/
/*DATA CURRENT_FORBS_DETAIL; SET LEGEND.CURRENT_FORBS_DETAIL; RUN;*/
/*DATA MONTHS_USED; SET LEGEND.MONTHS_USED; RUN;*/
