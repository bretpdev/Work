/*****************************************************************************
UTNWR03
******************************************************************************/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/UNWR03.NWR03R2";
FILENAME REPORT3 "&RPTLIB/UNWR03.NWR03R3";
FILENAME REPORTZ "&RPTLIB/UNWR03.NWR03RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
DATA _NULL_;
	CALL SYMPUTX('DT_RNG_BEG',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'),MMDDYY10.)||"'");
	CALL SYMPUTX('DT_RNG_END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'),MMDDYY10.)||"'");
	CALL SYMPUTX('RUN_DATE',PUT(INTNX('MONTH',TODAY(),0,'end'),MMDDYY10.));
RUN;
%SYSLPUT DT_RNG_BEG = &DT_RNG_BEG;
%SYSLPUT DT_RNG_END = &DT_RNG_END;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL) ;
CREATE TABLE MRRCRRF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT ZC12.WF_FMS_BCH
	,ZC12.WF_RFD_ID
	,ZC12.WC_RFD_TRX_TYP
	,ZC12.WF_CAN_SCH_NUM	
	,ZC12.WD_CAN
	,CASE
		WHEN ZC12.TYP_IND = '1' THEN 'Overpaid'
		WHEN ZC12.TYP_IND = '2' THEN 'Unapplied'
	 END AS REF_SOURCE
	,CASE 
		WHEN LN10.LC_FED_PGM_YR IS NULL THEN 'Unapplied'
		ELSE LN10.LC_FED_PGM_YR 
	 END AS LN_PGM
	,ZC12.RC_DATE AS REF_DT
	,ZC12.BF_SSN
	,ZC12.WA_RFD_CHK_AMT
	,ZC12.RC_DATE AS SENT_DT
	,COALESCE(LN76.LF_RFD_TRY_CHK_NUM,COALESCE(RM25.LF_SPS_RFD_TRY_CHK,NULL)) AS CHK_NUM
	,COALESCE(LN76.LD_SCH_CHK_PRT,COALESCE(RM25.LD_SPS_SCH_CHK_PRT,NULL)) AS CHK_DT
	,COALESCE(LN76.LF_RFD_SCH_NUM,COALESCE(RM25.LF_SPS_RFD_SCH_NUM,NULL)) AS SHD_NUM
	,COALESCE(PD10.DF_SPE_ACC_ID,'9999999999') AS DF_SPE_ACC_ID
	,1 AS RC
FROM 
	(
	SELECT SUBSTR(DF_SPE_ACC_ID,2) AS BF_SSN
		,SUBSTR(WF_RFD_ID,11,1) AS TYP_IND
		,WF_FMS_BCH
		,WF_RFD_ID
		,WC_RFD_TRX_TYP
		,WM_RFD_PYE
		,WC_RFD_PYE_TYP
		,DF_SPE_ACC_ID
		,WA_RFD_CHK_AMT
		,WF_CAN_SCH_NUM
		,WD_CAN
		,WX_CAN_REA
		,WF_BNK_ABA_NUM
		,WF_BNK_ACC_NUM
		,DATE(RTRIM(SUBSTR(WF_RFD_ID,7,2))||'/'||
			  RTRIM(SUBSTR(WF_RFD_ID,9,2))||'/'||
			  SUBSTR(WF_RFD_ID,3,4)) AS RC_DATE
	FROM FRDWODS.ZC12_FMS_RFD 
	) ZC12
LEFT OUTER JOIN PKUB.LN76_LON_RFD LN76
	ON ZC12.WF_RFD_ID = LN76.LF_LON_RFD_TRY
LEFT OUTER JOIN PKUB.LN10_LON LN10
	ON LN76.BF_SSN = LN10.BF_SSN
	AND LN76.LN_SEQ = LN10.LN_SEQ
LEFT OUTER JOIN PKUB.RM25_RMT_SPS_RFD RM25
	ON ZC12.WF_RFD_ID = RM25.LF_RMT_SPS_RFD_TRY
LEFT OUTER JOIN PKUB.PD10_PRS_NME PD10
	ON ZC12.BF_SSN = PD10.DF_PRS_ID
WHERE ZC12.RC_DATE BETWEEN &DT_RNG_BEG AND &DT_RNG_END
	AND ZC12.WC_RFD_TRX_TYP IN ('REFUND','CANCEL')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA MRRCRRF;
	SET WORKLOCL.MRRCRRF;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=128;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
TITLE 'Monthly Refund Reconcilliation Report';
TITLE2 "Date: &Run_Date";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR03  	 REPORT = UNWR03.NWR03R2';
PROC REPORT DATA=MRRCRRF NOWD SPLIT='~' HEADSKIP;
	WHERE WC_RFD_TRX_TYP = 'REFUND';
	COLUMN REF_SOURCE LN_PGM REF_DT BF_SSN WA_RFD_CHK_AMT SENT_DT CHK_NUM CHK_DT SHD_NUM
		DF_SPE_ACC_ID RC;
	DEFINE REF_SOURCE / GROUP 'Refund~Source';
	DEFINE LN_PGM / GROUP 'Loan~Program' ;
	DEFINE REF_DT / GROUP 'Refund~Date' FORMAT=MMDDYY10.;
	DEFINE BF_SSN / GROUP 'Borrower~SSN';
	DEFINE WA_RFD_CHK_AMT / ANALYSIS 'Refund~Amount' ;
	DEFINE SENT_DT / GROUP 'Date Sent~to FMS' FORMAT=MMDDYY10.; 
	DEFINE CHK_NUM / DISPLAY 'Check~Number' ;
	DEFINE CHK_DT / DISPLAY  'Check~Date' FORMAT=MMDDYY10.;
	DEFINE SHD_NUM / DISPLAY 'Schedule~Number' ;
	DEFINE DF_SPE_ACC_ID / GROUP 'Account~Number';
	DEFINE RC / ANALYSIS NOPRINT;
	COMPUTE AFTER LN_PGM;
		LINE @48 14*'ƒ' @117 10*'ƒ';
		LINE @50 WA_RFD_CHK_AMT.SUM DOLLAR12.2 @118 RC.SUM COMMA9.;
		LINE @1 '';
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
TITLE 'Monthly Refund Cancellation Reconcilliation Report';
TITLE2 "Date: &RUN_DATE";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR03  	 REPORT = UNWR03.NWR03R3';
PROC REPORT DATA=MRRCRRF NOWD SPLIT='~' HEADSKIP;
	WHERE WC_RFD_TRX_TYP = 'CANCEL';
	COLUMN LN_PGM WD_CAN BF_SSN WA_RFD_CHK_AMT SENT_DT SHD_NUM DF_SPE_ACC_ID RC;
	DEFINE LN_PGM / GROUP 'Loan~Program' ;
	DEFINE WD_CAN / GROUP 'Cancellation~Date' FORMAT=MMDDYY10. WIDTH=12;
	DEFINE BF_SSN / GROUP 'Borrower~SSN';
	DEFINE WA_RFD_CHK_AMT / ANALYSIS 'Cancellation~Amount' WIDTH=12;
	DEFINE SENT_DT / GROUP 'Date Sent~to FMS' FORMAT=MMDDYY10.; 
	DEFINE SHD_NUM / DISPLAY 'Schedule~Number' ;
	DEFINE DF_SPE_ACC_ID / GROUP 'Account~Number';
	DEFINE RC / ANALYSIS NOPRINT;
	COMPUTE AFTER LN_PGM;
		LINE @59 12*'ƒ' @97 10*'ƒ';
		LINE @59 WA_RFD_CHK_AMT.SUM DOLLAR12.2 @98 RC.SUM COMMA9.;
		LINE @1 '';
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
