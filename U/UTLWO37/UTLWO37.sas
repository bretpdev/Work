/*============================================================================*/
/*UTLWO37 - NO CLAIM FILED - ACCT GREATER THAN 330 DAYS DELINQUENT*/
/*============================================================================*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO37.LWO37R2";
FILENAME REPORTZ "&RPTLIB/ULWO37.LWO37RZ";

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PCWDGT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT D.DF_SPE_ACC_ID
	,A.BF_SSN
	,D.DM_PRS_LST
	,A.LN_SEQ
	,RTRIM(A.LF_LON_ALT)||'0'||CHAR(A.LN_LON_ALT_SEQ) AS CLUID
	,C.LD_DLQ_OCC
	,F.LD_FOR_BEG
	,F.LD_FOR_END
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT LN.BF_SSN
		,LN.LN_SEQ
		,LN.LD_FOR_BEG
		,LN.LD_FOR_END
	FROM OLWHRM1.FB10_BR_FOR_REQ FB
	INNER JOIN OLWHRM1.LN60_BR_FOR_APV LN
		ON FB.BF_SSN = LN.BF_SSN
		AND FB.LF_FOR_CTL_NUM = LN.LF_FOR_CTL_NUM
	WHERE FB.LC_FOR_STA = 'A'
		AND FB.LC_STA_FOR10 = 'A'
		AND LN.LC_STA_LON60 = 'A'
	) F
	ON C.BF_SSN = F.BF_SSN
	AND C.LN_SEQ = F.LN_SEQ
	AND C.LD_DLQ_OCC < F.LD_FOR_BEG
WHERE A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
	AND A.IC_LON_PGM <> 'TILP'
	AND A.LI_CON_PAY_STP_PUR <> 'Y'
	AND DAYS(CURRENT DATE) - DAYS(C.LD_DLQ_OCC) >= 330
	AND C.LC_STA_LON16 = '1'
	AND 
	(
		B.WC_DW_LON_STA NOT IN 
		(
			'08','12','06','16','17',
			'18','19','20','21','22',
			'23','88','98','10','11'
		)
		AND NOT EXISTS 
		(
			SELECT *
			FROM OLWHRM1.LN34_CU_RTT_HST X
			INNER JOIN OLWHRM1.DW01_DW_CLC_CLU Y
				ON X.BF_SSN = Y.BF_SSN
				AND X.LN_SEQ = Y.LN_SEQ
			WHERE X.BF_SSN = A.BF_SSN
			AND X.LN_SEQ = A.LN_SEQ
			AND DAYS(X.LD_CU_RTT) = DAYS(CURRENT DATE)-2
			AND Y.WC_DW_LON_STA = '03'
		)
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO37.LWO37RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA PCWDGT;
	SET WORKLOCL.PCWDGT;
RUN;
DATA PCWDGT;
	SET PCWDGT;
	LD_DLQ_OCC = LD_DLQ_OCC + 1;

	DO;
		IF LD_FOR_BEG <= TODAY() <= LD_FOR_END THEN 
			NET_FORB = TODAY() - COALESCE(LD_FOR_BEG,0);
		ELSE
			NET_FORB = COALESCE(LD_FOR_END,0) - COALESCE(LD_FOR_BEG,0);
	END;
	DCRIT = TODAY() - LD_DLQ_OCC - NET_FORB;
	DO;
		IF DCRIT > 340 THEN
			UIND = 'Y';
		ELSE 
			UIND = '';
	END;
RUN;
PROC SORT DATA=PCWDGT;
	BY BF_SSN DCRIT;
RUN;
DATA _NULL_;
SET PCWDGT ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = LD_DLQ_OCC + 330;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DM_PRS_LST,
	LN_SEQ,
	CLUID,
	DCRIT);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
