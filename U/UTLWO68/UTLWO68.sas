/*UTLWO68 - TILP BORROWERS DISQUALIFIED FOR DIRECT DEBIT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO68.LWO68R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DDQ AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	INTEGER(BR30.BF_SSN) AS BF_SSN
	,PD10.DM_PRS_1
	,PD10.DM_PRS_LST
	,AY10.LD_STA_ACTY10
	,AY10.PF_REQ_ACT 
	,AY10.LC_STA_ACTY10
	,PD10.DF_SPE_ACC_ID
FROM OLWHRM1.BR30_BR_EFT BR30 
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON BR30.BF_SSN = AY10.BF_SSN
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON BR30.BF_SSN = PD10.DF_PRS_ID
WHERE BR30.BC_EFT_STA = 'A'
AND AY10.PF_REQ_ACT = 'UNSFA'
AND AY10.LC_STA_ACTY10 = 'A'
AND AY10.IC_LON_PGM = 'TILP'
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA DDQ; 
SET WORKLOCL.DDQ; 
RUN;

PROC SORT DATA=DDQ;
BY DF_SPE_ACC_ID DESCENDING LD_STA_ACTY10;
RUN;
%MACRO IFNULL;
PROC CONTENTS DATA=DDQ OUT=NULLSET NOPRINT;
RUN;
DATA NULL;
SET NULLSET;
CALL SYMPUT('OB',NOBS);
RUN;
%IF &OB > 0 %THEN %DO;
PROC TRANSPOSE DATA=DDQ OUT=DDQ (KEEP=DF_SPE_ACC_ID BF_SSN PF_REQ_ACT LC_STA_ACTY10 DM_PRS_1 DM_PRS_LST ADATE1 ADATE2
	WHERE=(ADATE3 ^= . AND INTNX('YEAR',ADATE1,+1,'S') > TODAY())) PREFIX=ADATE;
VAR LD_STA_ACTY10;
BY DF_SPE_ACC_ID BF_SSN PF_REQ_ACT LC_STA_ACTY10 DM_PRS_1 DM_PRS_LST;
RUN;

PROC SORT DATA=DDQ;
BY ADATE1 DF_SPE_ACC_ID ;
RUN;


DATA DDQ;
SET DDQ;
IF ADATE3 >= ADATE1 - 365 THEN OUTPUT;
RUN;
%END;

%MEND IFNULL;
%IFNULL;
PROC SORT DATA=DDQ;
BY BF_SSN;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
TITLE 'BORROWERS DISQUALIFIED FOR DIRECT DEBIT';
TITLE2 "RUN DATE:  &RUNDATE";
PROC CONTENTS DATA=DDQ OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT ////////////////
		@46 "JOB = UTLWO68     REPORT = ULWO68.LWO68R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=DDQ WIDTH=UNIFORM WIDTH=MIN;
VAR DF_SPE_ACC_ID DM_PRS_1 DM_PRS_LST;
LABEL DF_SPE_ACC_ID='ACCT #' DM_PRS_1='FIRST NAME' DM_PRS_LST='LAST NAME';
FOOTNOTE 'JOB = UTLWO68     REPORT = ULWO68.LWO68R2';
RUN;
PROC PRINTTO;
RUN;
