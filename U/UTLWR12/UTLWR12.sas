/*UTLWR12 TILP PAYMENT ADJUSTMENT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR12.LWR12R2";
FILENAME REPORTZ "&RPTLIB/ULWR12.LWR12RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%macro sqlcheck(SQLRPT);
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE RILP_DAT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,E.DF_SPE_ACC_ID
	,E.DM_PRS_LST
	,A.LD_END_GRC_PRD
	,G.LD_RMT_PAY_EFF
	,G.LD_RMT_PST
	,G.LA_BR_RMT
	,H.LC_RMT_BCH
	,D.LC_DFR_TYP
	,J.LD_SCL_SPR 
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN90_FIN_ATY B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME E
	ON A.BF_SSN = E.DF_PRS_ID
INNER JOIN OLWHRM1.LN94_LON_PAY_FAT F
	ON B.BF_SSN = F.BF_SSN
	AND B.LN_SEQ = F.LN_SEQ
	AND B.LN_FAT_SEQ = F.LN_FAT_SEQ
INNER JOIN OLWHRM1.RM30_BR_RMT G
	 ON F.LD_RMT_BCH_INI = G.LD_RMT_BCH_INI
      AND F.LC_RMT_BCH_SRC_IPT = G.LC_RMT_BCH_SRC_IPT 
      AND F.LN_RMT_BCH_SEQ = G.LN_RMT_BCH_SEQ
      AND F.LN_RMT_SEQ = G.LN_RMT_SEQ
      AND F.LN_RMT_ITM = G.LN_RMT_ITM
      AND F.LN_RMT_ITM_SEQ = G.LN_RMT_ITM_SEQ
INNER JOIN OLWHRM1.RM10_RMT_BCH H
	ON G.LD_RMT_BCH_INI = H.LD_RMT_BCH_INI
	AND G.LC_RMT_BCH_SRC_IPT = H.LC_RMT_BCH_SRC_IPT
	AND G.LN_RMT_BCH_SEQ = H.LN_RMT_BCH_SEQ
INNER JOIN OLWHRM1.LN13_LON_STU_OSD I
	ON A.BF_SSN = I.BF_SSN
	AND A.LN_SEQ = I.LN_SEQ
INNER JOIN OLWHRM1.SD10_STU_SPR J
	ON I.LF_STU_SSN = J.LF_STU_SSN
	AND I.LN_STU_SPR_SEQ = J.LN_STU_SPR_SEQ
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,'X' AS TRX_IND
	FROM OLWHRM1.LN90_FIN_ATY
	WHERE PC_FAT_TYP = '10'
		AND PC_FAT_SUB_TYP IN ('40','45')
		AND LC_FAT_REV_REA = ''
		AND LC_STA_LON90 = 'A'
	) C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN (
	SELECT DF2.BF_SSN
		,DF2.LN_SEQ
		,DF1.LC_DFR_TYP
		,DF2.LD_DFR_BEG
		,DF2.LD_DFR_END
	FROM OLWHRM1.DF10_BR_DFR_REQ DF1
	INNER JOIN OLWHRM1.LN50_BR_DFR_APV DF2
		ON DF1.BF_SSN = DF2.BF_SSN
		AND DF1.LF_DFR_CTL_NUM = DF2.LF_DFR_CTL_NUM
	WHERE DF1.LC_DFR_TYP IN ('06','15','18','33')
		AND DF1.LC_STA_DFR10 = 'A'
		AND DF1.LC_DFR_STA = 'A'
		AND DF2.LC_STA_LON50 = 'A'
	) D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
	AND B.PC_FAT_TYP = '10'
	AND B.PC_FAT_SUB_TYP != '54'
	AND B.LC_FAT_REV_REA = ''
	AND B.LC_STA_LON90 = 'A'
	AND C.TRX_IND IS NULL
	AND A.LA_CUR_PRI <> 0 
	AND A.IC_LON_PGM = 'TILP'
	AND I.LC_STA_LON13 = 'A'
	AND J.LC_STA_STU10 = 'A'
	AND	
	(
		(
			B.LD_FAT_EFF <= A.LD_END_GRC_PRD
		)
	OR 
		(
			B.LD_FAT_EFF BETWEEN D.LD_DFR_BEG AND D.LD_DFR_END
		)
	)
	AND EXISTS 
	(
		SELECT 1
		FROM OLWHRM1.LN10_LON Z
		WHERE Z.BF_SSN = A.BF_SSN
			AND Z.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS','PLUSGB')
			AND Z.LA_CUR_PRI != 0
			AND Z.LC_STA_LON10 = 'R'
	)
	AND NOT EXISTS 
	(
		SELECT 1
		FROM OLWHRM1.LN75_BIL_LON_FAT X
		INNER JOIN OLWHRM1.LN80_LON_BIL_CRF Y
			ON X.BF_SSN = Y.BF_SSN
			AND X.LN_SEQ = Y.LN_SEQ
			AND X.LD_BIL_CRT = Y.LD_BIL_CRT
			AND X.LN_SEQ_BIL_WI_DTE = Y.LN_SEQ_BIL_WI_DTE
			AND X.LN_BIL_OCC_SEQ = Y.LN_BIL_OCC_SEQ
		WHERE X.BF_SSN = B.BF_SSN
			AND X.LN_SEQ = B.LN_SEQ
			AND Y.LC_STA_LON80 = 'A'
			AND Y.LC_BIL_TYP_LON IN ('I','C','P')  
			AND Y.LA_TOT_BIL_STS = X.LA_BIL_STS
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR12.LWR12RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA RILP_DAT;
	SET WORKLOCL.RILP_DAT;
RUN;
DATA RILP_DAT;
	SET RILP_DAT;
	IF LC_DFR_TYP NOT IN ('06','33') THEN DO;
		IF LD_SCL_SPR <= LD_RMT_PAY_EFF <= LD_END_GRC_PRD THEN 
			LC_DFR_TYP = '03';
		ELSE IF LD_SCL_SPR > TODAY() THEN
			LC_DFR_TYP = '04';
	END;
RUN;
PROC FORMAT;
	VALUE $LONSTA	
		'06' = 'GRAD FELLOWSHIP/TEACHING'
		'33' = 'INTERIM DEFERMENT'
		'03' = 'IN GRACE'
		'04' = 'IN SCHOOL';
QUIT;
PROC SORT DATA=RILP_DAT;
	BY DF_SPE_ACC_ID LN_SEQ;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER PAGENO=1 ;
TITLE 'OPS TILP PAYMENT ADJUSTMENT';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4  'JOB = UTLWR12     REPORT = UTLWR12.LWR12R2';
PROC CONTENTS DATA=RILP_DAT OUT=EMPTYSET NOPRINT; QUIT;
DATA _NULL_;
	SET EMPTYSET;
	FILE PRINT;
	IF  NOBS=0 AND _N_ =1 THEN DO;
		PUT // 126*'-';
		PUT      //////
			@51 '**** NO OBSERVATIONS FOUND ****';
		PUT //////
			@57 '-- END OF REPORT --';
		PUT //////////////
			@46 "JOB = UTLWR12     REPORT = UTLWR12.LWR12R2";
		END;
	RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=RILP_DAT WIDTH=MIN UNIFORM LABEL;
	FORMAT LC_DFR_TYP $LONSTA. LD_RMT_PAY_EFF LD_RMT_PST MMDDYY10.;
	VAR DF_SPE_ACC_ID LN_SEQ DM_PRS_LST LD_RMT_PAY_EFF LD_RMT_PST LA_BR_RMT LC_DFR_TYP LC_RMT_BCH;
	LABEL DF_SPE_ACC_ID = 'ACCOUNT'
		LN_SEQ = 'LOAN SEQ#'
		DM_PRS_LST = 'LAST NAME'
		LD_RMT_PAY_EFF = 'PAYMENT EFFECTIVE DATE'
		LD_RMT_PST = 'PAYMENT POSTED DATE'
		LA_BR_RMT = 'PAYMENT AMOUNT'
		LC_DFR_TYP = 'STATUS'
		LC_RMT_BCH = 'BATCH TYPE';
RUN;
PROC PRINTTO;
RUN;
