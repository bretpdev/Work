/*UTLWD20 -- CREDIT BUREAU RECONCILIATION*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD20.LWD20R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE REQD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.DF_PRS_ID
	,MAX(A.BF_CRT_DTS_AY01) AS REQD_DATE
	,B.DF_SPE_ACC_ID
FROM OLWHRM1.AY01_BR_ATY A
INNER JOIN OLWHRM1.PD01_PDM_INF B
	ON A.DF_PRS_ID = B.DF_PRS_ID
WHERE	A.PF_ACT = 'KRCRB'
AND DAYS((SELECT MAX(L.BF_CRT_DTS_AY01)
	   	  FROM OLWHRM1.AY01_BR_ATY L
		  WHERE A.DF_PRS_ID = L.DF_PRS_ID
		  AND L.PF_ACT = 'KRCRB'
		  GROUP BY DF_PRS_ID)) >= DAYS(CURRENT DATE) - 33
GROUP BY A.DF_PRS_ID
	,B.DF_SPE_ACC_ID
);

CREATE TABLE RETRND AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.DF_PRS_ID
	,A.PF_ACT
	,MAX(A.BF_CRT_DTS_AY01) AS RETRND_DATE
FROM	OLWHRM1.AY01_BR_ATY A
WHERE	A.PF_ACT IN ('KCTRL','KCLDG','KCNME','KCINQ')
OR (A.PF_ACT in ('KSPEN','KUPEN') AND A.BC_ATY_TYP = 'ED' AND A.BC_ATY_CNC_TYP = '84')
GROUP BY DF_PRS_ID, PF_ACT
);

DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA REQD;
SET WORKLOCL.REQD;
RUN;

DATA RETRND;
SET WORKLOCL.RETRND;
RUN;

PROC SQL;
CREATE TABLE CRBU AS
SELECT 
	A.DF_PRS_ID
	,DATEPART(A.REQD_DATE) AS REQD_DATE
	,B.PF_ACT
	,DATEPART(B.RETRND_DATE) AS RETRND_DATE
	,A.DF_SPE_ACC_ID
FROM REQD A 
LEFT OUTER JOIN RETRND B
ON A.DF_PRS_ID = B.DF_PRS_ID
AND B.RETRND_DATE > A.REQD_DATE
ORDER BY REQD_DATE, DF_SPE_ACC_ID
;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER PAGENO=1 ORIENTATION=PORTRAIT ;
OPTIONS LS=96 PS=52;
PROC PRINT NOOBS SPLIT='/' DATA=CRBU WIDTH=UNIFORM WIDTH=MIN;
VAR    DF_SPE_ACC_ID REQD_DATE PF_ACT RETRND_DATE;
LABEL  DF_SPE_ACC_ID = 'ACCT #' REQD_DATE = 'REQUESTED' PF_ACT = 'TYPE OF REPORT RETURNED' RETRND_DATE = 'RETURNED';
FORMAT REQD_DATE MMDDYY10. RETRND_DATE MMDDYY10.;
TITLE	'CREDIT BUREAU RECONCILIATION';
FOOTNOTE  'JOB = UTLWD20     REPORT = ULWD20.LWD20R2';
RUN;
PROC PRINTTO;
RUN;
