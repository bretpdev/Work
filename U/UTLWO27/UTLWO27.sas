/*UTLWO27 - SMALL BALANCE WRITE OFF QC REPORT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO27.LWO27R2";
FILENAME REPORT3 "&RPTLIB/ULWO27.LWO27R3";
FILENAME REPORTZ "&RPTLIB/ULWO27.LWO27RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SMWOQCR_A AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT DISTINCT A.BF_SSN AS SSN
		,C.DM_PRS_LST
		,A.LN_SEQ
		,B.LN_FAT_SEQ
		,B.LD_FAT_EFF
		,B.LD_FAT_APL
		,ABS(COALESCE(B.LA_FAT_CUR_PRI,0)) AS LA_FAT_CUR_PRI
		,ABS(COALESCE(B.LA_FAT_NSI,0)) AS LA_FAT_NSI
		,E.LD_ATY_RSP
		,C.DF_SPE_ACC_ID
		,F.CONSOL_IND 
	FROM OLWHRM1.LN10_LON A 
	INNER JOIN OLWHRM1.LN90_FIN_ATY B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	INNER JOIN OLWHRM1.PD10_PRS_NME C
		ON A.BF_SSN = C.DF_PRS_ID
	LEFT OUTER JOIN (
		SELECT X.BF_SSN
			,MAX(X.LD_ATY_RSP) AS LD_ATY_RSP
		FROM OLWHRM1.AY10_BR_LON_ATY X
		WHERE X.PF_REQ_ACT IN ('SMBWO','SPEWO')
		AND X.LC_STA_ACTY10 = 'A'
		GROUP BY X.BF_SSN
		 ) E
		ON E.BF_SSN = A.BF_SSN
	LEFT OUTER JOIN (
		SELECT DISTINCT BF_SSN	
			,LN_SEQ
			,'X' AS CONSOL_IND
		FROM OLWHRM1.LN90_FIN_ATY
		WHERE LC_STA_LON90 = 'A'
		AND LC_FAT_REV_REA = ''
		AND PC_FAT_TYP = '10'
		AND PC_FAT_SUB_TYP IN ('70','80')
		) F
	ON F.BF_SSN = A.BF_SSN
	AND F.LN_SEQ = A.LN_SEQ
	LEFT OUTER JOIN (
		SELECT BF_SSN
			,LN_SEQ
			,SUM(LA_FAT_CUR_PRI) AS PRI
			,SUM(LA_FAT_NSI) AS NSI
		FROM  OLWHRM1.LN90_FIN_ATY 
		WHERE PC_FAT_TYP='10'
			AND LC_FAT_REV_REA = ''
			AND LC_STA_LON90 = 'A'
			AND	PC_FAT_SUB_TYP='54'
			GROUP BY BF_SSN
					,LN_SEQ
		) G
			ON G.BF_SSN = A.BF_SSN
			AND G.LN_SEQ = A.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
	AND B.LC_STA_LON90 = 'A'
	AND B.LC_FAT_REV_REA = ''
	AND B.PC_FAT_TYP = '50'
	AND B.PC_FAT_SUB_TYP = '02'
	AND (A.IC_LON_PGM = 'TILP' AND (
		(COALESCE(B.LA_FAT_CUR_PRI,0) != 0) OR (   ABS(G.PRI)+ABS(G.NSI) = A.LA_LON_AMT_GTR	)

		)OR A.IC_LON_PGM != 'TILP')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO27.LWO27RZ);*/
/*QUIT;*/

ENDRSUBMIT;

DATA SMWOQCR_A; SET WORKLOCL.SMWOQCR_A; RUN;
DATA TO_DELETE; SET WORKLOCL.TO_DELETE; RUN;

PROC SQL;
CREATE TABLE BRLVSM AS 
SELECT SSN
	,LD_FAT_EFF
	,LD_ATY_RSP
	,CONSOL_IND 
	,SUM(LA_FAT_CUR_PRI)+SUM(LA_FAT_NSI) AS PI
FROM SMWOQCR_A
WHERE LD_FAT_EFF > LD_ATY_RSP
GROUP BY SSN
	,LD_FAT_EFF
	,LD_ATY_RSP
	,CONSOL_IND 
;
QUIT;

DATA BRLVSM;
SET BRLVSM;
IF CONSOL_IND = '' AND PI > 49.99 THEN OUTPUT;
ELSE IF CONSOL_IND = 'X' AND PI > 9.99 THEN OUTPUT;
ELSE DELETE;
RUN;

PROC SQL;
CREATE TABLE SMWOQCR AS 
SELECT A.SSN
	,A.DM_PRS_LST
	,A.LN_SEQ
	,A.LN_FAT_SEQ
	,A.LD_FAT_EFF
	,A.LD_FAT_APL
	,A.LA_FAT_CUR_PRI
	,A.LA_FAT_NSI 
	,A.LA_FAT_CUR_PRI + A.LA_FAT_NSI AS WOA
	,A.LD_ATY_RSP
	,A.DF_SPE_ACC_ID
	,A.CONSOL_IND 
FROM SMWOQCR_A A
INNER JOIN BRLVSM B
	ON A.SSN = B.SSN
	AND A.LD_FAT_EFF = B.LD_FAT_EFF
	AND A.CONSOL_IND = B.CONSOL_IND
ORDER BY SSN
	,LN_SEQ
;
QUIT;

DATA CNSL NONC;
SET SMWOQCR;
IF CONSOL_IND = 'X' THEN OUTPUT CNSL;
ELSE OUTPUT NONC;
RUN;

PROC SORT DATA=CNSL;BY DM_PRS_LST;RUN;
PROC SORT DATA=NONC;BY DM_PRS_LST;RUN;

DATA _NULL_;
SET NONC ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = LD_FAT_APL;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DM_PRS_LST,
	LN_SEQ,
	PUT(LD_FAT_EFF,MMDDYY10.),
	PUT(LD_FAT_APL,MMDDYY10.),
	LA_FAT_CUR_PRI,
	LA_FAT_NSI,
	WOA
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;

DATA _NULL_;
SET CNSL ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = LD_FAT_APL;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DM_PRS_LST,
	LN_SEQ,
	PUT(LD_FAT_EFF,MMDDYY10.),
	PUT(LD_FAT_APL,MMDDYY10.),
	LA_FAT_CUR_PRI,
	LA_FAT_NSI,
	WOA
);
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
