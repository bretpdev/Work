*--------------------------------------------------------------------------*
| UTLWA11 - ONELINK END OF YEAR REPORTING                                  |
*--------------------------------------------------------------------------*
**************************** NOTE TO PROGRAMMER ****************************
*--------------------------------------------------------------------------*
| THIS JOB ESSENTIALLY HAS TWO SEPARTE PARTS IN PROCESSING. THERE IS       |
| ONELINK PROCESSING (R2-R4) AND THE COMPASS PROCESSING (R5). NO           |
| CORRELLATION EXISTS BETWEEN THE TWO SO MODIFICATIONS SHOULD ONLY AFFECT  |
| EITHER THE ONELINK PROCESSING OR THE COMPASS PROCESSING                  |
*--------------------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWA11.LWA11RZ";
FILENAME REPORT2 "&RPTLIB/ULWA11.LWA11R2";
FILENAME REPORT3 "&RPTLIB/ULWA11.LWA11R3";
FILENAME REPORT4 "&RPTLIB/ULWA11.LWA11R4";
FILENAME REPORT5 "&RPTLIB/ULWA11.LWA11R5";
DATA _NULL_;
	CALL SYMPUT('YEARS_AGO_1',INTNX('YEAR',TODAY(),-1,'S'));
	CALL SYMPUT('BEGIN',INTNX('MONTHS',TODAY(),-12,'E'));
	CALL SYMPUT('END',INTNX('MONTHS',TODAY(),-1,'E'));
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYY10.));
RUN;
%SYSLPUT YEARS_AGO_1 = &YEARS_AGO_1;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
/*************************************************************************
* ONELINK QUERIES
**************************************************************************/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DATI AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
/*SHARED DATA FOR ONELINK PROCESSING*/
	GA01.DF_PRS_ID_BR AS BF_SSN
	,GA10.AF_APL_ID||GA10.AF_APL_ID_SFX AS CLUID
	,GA10.AA_GTE_LON_AMT
	,GA10.AA_TOT_RFD
	,COALESCE(GA10.AA_TOT_CAN,0) AS AA_TOT_CAN
	,GA14.AC_LON_STA_TYP
	,DC01.LC_AUX_STA
	,DC01.LC_STA_DC10
	,COALESCE(DC01.LA_CLM_PRI,0) 			AS LA_CLM_PRI
	,COALESCE(DC01.LA_TOT_ITL_CLM_PD,0) 	AS LA_TOT_ITL_CLM_PD
	,COALESCE(DC01.LA_COL_CST_ACR,0) 		AS LA_COL_CST_ACR
	,COALESCE(DC01.LA_COL_CST_COL,0) 		AS LA_COL_CST_COL
	,COALESCE(DC01.LA_INT_ACR,0) 			AS LA_INT_ACR
	,COALESCE(DC01.LA_INT_COL,0) 			AS LA_INT_COL
	,COALESCE(DC01.LA_LEG_CST_ACR,0)		AS LA_LEG_CST_ACR
	,COALESCE(DC01.LA_LEG_CST_COL,0) 		AS LA_LEG_CST_COL
	,COALESCE(DC01.LA_OTH_CHR_COL,0) 		AS LA_OTH_CHR_COL
	,COALESCE(DC01.LA_PRI_COL,0) 			AS LA_PRI_COL
	,COALESCE(DC01.LA_CLM_PRJ_COL_CST,0) 	AS LA_CLM_PRJ_COL_CST
	,COALESCE(DC01.LA_CLM_INT_ACR,0) 		AS LA_CLM_INT_ACR
	,COALESCE(DC19.LA_AGY_RIN_DOE,0) 		AS LA_AGY_RIN_DOE
/*R2 DATA*/
	,GA10.AC_LON_TYP
	,CASE 
		WHEN GA11.AD_DSB_ADJ < '10/01/1998' THEN '98%'
		ELSE '95%'
	 END AS REI_RATE	
	,GA14.AC_LON_STA_REA
	,GA15.AD_NDS_CLC_ENT_RPD
	,GA15.AC_STA_GA15
	,DC01.LD_LDR_POF
	,DC01.LC_PCL_REA
	,DAYS(DC01.LD_LDR_POF) - DAYS(GA15.AD_NDS_CLC_ENT_RPD) AS GPE2PIF
	,CASE
		WHEN DAYS(CURRENT DATE) - DAYS(GA15.AD_NDS_CLC_ENT_RPD) < 0 THEN 0
		ELSE DAYS(CURRENT DATE) - DAYS(GA15.AD_NDS_CLC_ENT_RPD)
	 END AS RPY2TDY
	,DC19.LC_DOE_LON_TYP
/*R3 DATA*/
	,GA10.AC_PRC_STA
	,GA10.AD_PRC
	,GA14.AC_STA_GA14
/*R4 DATA*/
	,GA10.AA_CUR_PRI
	,GA10.AA_OTS_ACR_INT
FROM OLWHRM1.GA01_APP GA01
INNER JOIN OLWHRM1.GA10_LON_APP GA10
	ON GA01.AF_APL_ID = GA10.AF_APL_ID
LEFT JOIN OLWHRM1.GA14_LON_STA GA14
	ON GA10.AF_APL_ID = GA14.AF_APL_ID
	AND GA10.AF_APL_ID_SFX = GA14.AF_APL_ID_SFX
	AND GA14.AC_STA_GA14 = 'A'
LEFT OUTER JOIN OLWHRM1.GA15_NDS_ID GA15
	ON GA14.AF_APL_ID = GA15.AF_APL_ID
	AND GA14.AF_APL_ID_SFX = GA15.AF_APL_ID_SFX
	AND GA15.AC_STA_GA15 = 'A'
LEFT OUTER JOIN (
	SELECT B.AF_APL_ID
		,B.AF_APL_ID_SFX
		,B.LF_CRT_DTS_DC10
		,B.LA_TOT_ITL_CLM_PD
		,B.LA_PRI_COL
		,B.LA_INT_ACR
		,B.LA_INT_COL
		,B.LA_LEG_CST_COL
		,B.LA_OTH_CHR_COL
		,B.LA_COL_CST_COL
		,B.LA_COL_CST_ACR
		,B.LA_LEG_CST_ACR
		,C.LA_CLM_PRJ_COL_CST
		,C.LA_CLM_INT_ACR
		,B.LA_CLM_PRI
		,B.LC_AUX_STA
		,B.LC_STA_DC10
		,B.LD_LDR_POF
		,B.LC_PCL_REA
	FROM OLWHRM1.DC01_LON_CLM_INF B
	INNER JOIN (
		SELECT AF_APL_ID
			,AF_APL_ID_SFX
			,MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
		FROM OLWHRM1.DC01_LON_CLM_INF 
		GROUP BY AF_APL_ID
			,AF_APL_ID_SFX
		) A
		ON A.AF_APL_ID = B.AF_APL_ID
		AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
		AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
	LEFT OUTER JOIN OLWHRM1.DC02_BAL_INT C
		ON B.AF_APL_ID = C.AF_APL_ID
		AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
		AND B.LF_CRT_DTS_DC10 = C.LF_CRT_DTS_DC10
	) DC01
	ON GA10.AF_APL_ID = DC01.AF_APL_ID
	AND GA10.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
LEFT JOIN (
	SELECT AF_APL_ID
		,AF_APL_ID_SFX
		,MIN(AD_DSB_ADJ) AS AD_DSB_ADJ
	FROM OLWHRM1.GA11_LON_DSB_ATY 
	WHERE AC_DSB_ADJ = 'A'
		AND AC_DSB_ADJ_STA = 'A'
	GROUP BY AF_APL_ID
		,AF_APL_ID_SFX
	) GA11
	ON GA10.AF_APL_ID = GA11.AF_APL_ID
	AND GA10.AF_APL_ID_SFX = GA11.AF_APL_ID_SFX
LEFT OUTER JOIN (
	SELECT AF_APL_ID
		,AF_APL_ID_SFX
		,LF_CRT_DTS_DC10
		,LC_DOE_LON_TYP
		,SUM(LA_AGY_RIN_DOE) AS LA_AGY_RIN_DOE
	FROM OLWHRM1.DC19_DOE_RIN 
	GROUP BY AF_APL_ID
		,AF_APL_ID_SFX
		,LF_CRT_DTS_DC10
		,LC_DOE_LON_TYP
	) DC19
	ON DC01.AF_APL_ID = DC19.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = DC19.AF_APL_ID_SFX
	AND DC01.LF_CRT_DTS_DC10 = DC19.LF_CRT_DTS_DC10

FOR READ ONLY WITH UR
);
/*IDENTIFY LOANS THAT ARE REINSURED 100% FOR EXCLUSION*/
CREATE TABLE REIN100P AS
	SELECT *
	FROM CONNECTION TO DB2 (
		SELECT DISTINCT AF_APL_ID||AF_APL_ID_SFX AS CLUID
		FROM OLWHRM1.GA11_LON_DSB_ATY
		WHERE AD_DSB_ADJ < '10/01/1993'
		FOR READ ONLY WITH UR
	);
DISCONNECT FROM DB2;
QUIT;
/*************************************************************************
* COMPASS QUERIES
**************************************************************************/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE COLNS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,D.LD_BIL_CRT
	,D.LN_SEQ_BIL_WI_DTE
	,D.LA_CUR_PRN_BIL
	,D.LR_INT_BIL
	,D.LA_BIL_CUR_DU
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,'Y' AS EXCLUDE
	FROM OLWHRM1.LN15_DSB
	WHERE LC_DSB_TYP = '2'
		AND LC_STA_LON15 IN ('1','3')
		AND LD_DSB <= '10/01/1993'
		AND LA_DSB != COALESCE(LA_DSB_CAN,0)
	) C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN (
	SELECT LN80.BF_SSN
		,LN80.LD_BIL_CRT
		,LN80.LN_SEQ_BIL_WI_DTE
		,LN80.LN_SEQ
		,LN80.LA_CUR_PRN_BIL
		,LN80.LR_INT_BIL
		,LN80.LA_BIL_CUR_DU
	FROM OLWHRM1.LN80_LON_BIL_CRF LN80
	INNER JOIN (
		SELECT BF_SSN
			,LC_BIL_TYP
			,MAX(LD_BIL_CRT) AS LD_BIL_CRT
		FROM OLWHRM1.BL10_BR_BIL
		WHERE LC_BIL_TYP = 'P'
		GROUP BY BF_SSN
			,LC_BIL_TYP
		) BL10
		ON LN80.BF_SSN = BL10.BF_SSN
		AND LN80.LD_BIL_CRT = BL10.LD_BIL_CRT
		AND LN80.LC_BIL_TYP_LON = BL10.LC_BIL_TYP
		WHERE LN80.LC_STA_LON80 = 'A'
	) D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
	AND B.WC_DW_LON_STA IN ('01','02','03')
	AND C.EXCLUDE IS NULL
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
CREATE TABLE COBIL(DROP=LN_SEQ_BIL_WI_DTE) AS 
	SELECT A.*
	FROM COLNS A
	INNER JOIN (
		SELECT BF_SSN
			,LN_SEQ
			,COUNT(*) AS C
		FROM COLNS
		GROUP BY BF_SSN
			,LN_SEQ
		HAVING COUNT(*) = 1
		) B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	ORDER BY A.BF_SSN
		,A.LN_SEQ
;
QUIT;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWA11.LWA11RZ);*/
/*QUIT;*/
/*************************************************************************
* ONELINK PROCESSING
**************************************************************************
* APPLY EXCLUSION CRITERIA
**************************************************************************/
PROC SQL;
	CREATE TABLE DATII AS
	SELECT A.*
	FROM DATI A
	LEFT OUTER JOIN REIN100P B
		ON A.CLUID = B.CLUID
	WHERE B.CLUID IS NULL;
QUIT;
/*************************************************************************
* CREATE DATA SETS OF DAY INTERVALS
**************************************************************************/
%MACRO SPLIT_SET2(DS,MAX_GROUP,LBND_INI,UBND_INI,PERCENT,CONCAT_DS);
DATA &DS (DROP=TUB GRPA GRPB);
LENGTH GRP $ 9. REI_RATE $3.;
RETAIN TUB;
DO SVAR=1 TO &MAX_GROUP;
	IF SVAR=1 THEN DO;
		LBOUND = &LBND_INI;
		UBOUND = &UBND_INI;
		TUB=UBOUND;
		GRPA = LEFT(TRIM(PUT(LBOUND,BEST12.)));
		GRPB = LEFT(TRIM(PUT(UBOUND,BEST12.)));
		GRP = LEFT(TRIM(GRPA))||'-'||LEFT(TRIM(GRPB));
		DO DAY_NUM=LBOUND TO UBOUND-1;
			REI_RATE = CATT("&PERCENT",'%');
			OUTPUT;
		END;
	END;
	ELSE DO;
		LBOUND=TUB+1;
		UBOUND=TUB+30;
		TUB=UBOUND;
		GRPA = LEFT(TRIM(PUT(LBOUND,BEST12.)));
		GRPB = LEFT(TRIM(PUT(UBOUND,BEST12.)));
		GRP = LEFT(TRIM(GRPA))||'-'||LEFT(TRIM(GRPB));
		DO DAY_NUM=LBOUND TO UBOUND-1;
			OUTPUT;
		END;
	END;
	REI_RATE = CATT("&PERCENT",'%');
 	OUTPUT;
END;
RUN;
%IF &CONCAT_DS %THEN %DO;
	DATA DINT;
		SET _95 _98;
	RUN;
%END;
%MEND;
%MACRO CALAMT(DS,RNO);
DATA &DS ;
SET &DS;
IF &RNO = 4 THEN DO;
	PRIN = AA_CUR_PRI;
	OINT = AA_OTS_ACR_INT;
	OTHR = 0;
END;
ELSE DO;
	PRIN = LA_TOT_ITL_CLM_PD - 
		LA_PRI_COL;
	OINT = LA_INT_ACR - 
		LA_INT_COL;
	OTHR = LA_LEG_CST_ACR - 
		LA_LEG_CST_COL + 
		LA_OTH_CHR_COL + 
		LA_COL_CST_ACR - 
		LA_COL_CST_COL + 
		LA_CLM_PRJ_COL_CST;
END;
OG_PRIN = AA_GTE_LON_AMT - 
	AA_TOT_CAN - 
	AA_TOT_RFD; 
TLO = PRIN + 
	OINT + 
	OTHR;
RUN;
%MEND CALAMT;
/*******************************************************
* BEGEIN ONELINK REPORT PROCSSING 
********************************************************
* REPORT2 PROCESSING
********************************************************/
DATA RINRTA;
SET DATII;
WHERE AC_LON_STA_TYP = 'CP'
	AND AC_LON_STA_REA IN 
	(
		'DF','DQ','DB','DU'
	)	
	AND LC_STA_DC10 IN 
	(
		'03','04'
	)
	AND LC_PCL_REA IN 
	(
		'DF','DQ','DB','DU'
	)
	AND LD_LDR_POF > &YEARS_AGO_1
	AND AC_STA_GA15 = 'A'
	AND LC_AUX_STA NOT IN 
	(
		'10','01'
	);
RUN;
%CALAMT(RINRTA,2);
/*CREATE DATE DATA SET FOR R2*/
%SPLIT_SET2(_95,144,90,119,95,0);
%SPLIT_SET2(_98,144,90,119,98,1);
PROC SQL;
	CREATE TABLE RINRTB AS 
	SELECT DISTINCT B.GRP
		,B.SVAR
		,B.REI_RATE
		,A.BF_SSN
		,A.CLUID
		,COALESCE(A.LA_CLM_PRI,0) AS LA_CLM_PRI
		,COALESCE(A.OG_PRIN,0) AS OG_PRIN
		,COALESCE(A.PRIN,0) AS PRIN
		,COALESCE(A.OINT,0) AS OINT
		,COALESCE(A.OTHR,0) AS OTHR
		,COALESCE(A.TLO,0) AS TLO
		,COALESCE(A.LA_AGY_RIN_DOE,0) AS LA_AGY_RIN_DOE
	FROM RINRTA A
	RIGHT OUTER JOIN DINT B
		ON A.GPE2PIF = B.DAY_NUM
		AND A.REI_RATE = B.REI_RATE
	;
QUIT;
PROC SORT DATA=RINRTB OUT=RINRTC;
	BY REI_RATE SVAR BF_SSN;
RUN;
DATA RINRTC;
	SET RINRTC;
	BY REI_RATE SVAR BF_SSN;
	IF FIRST.BF_SSN THEN DO;
		IF BF_SSN = '' THEN 
			BR_CT = 0;
		ELSE
			BR_CT = 1;
	END;
	ELSE 
		BR_CT = 0;
RUN;
/*REPORT3 PROCESSING*/
DATA TOBLCA;
SET DATII;
WHERE AC_PRC_STA = 'A'
	AND AD_PRC < &END
	AND AC_STA_GA14 = 'A'
	AND AC_LON_STA_TYP IN 
	(
		'CR','CP','DN','DA','FB','IA',
		'ID','IG','IM','RP','UA','UB'
	)
	AND LC_AUX_STA NOT IN 
	(
		'10','01'
	);
RUN;
%CALAMT(TOBLCA,3);
PROC SORT DATA=TOBLCA NODUPKEY;
	BY AD_PRC CLUID;
RUN;
DATA TOBLCA;
	SET TOBLCA;
	IF AD_PRC <= &BEGIN THEN DO;
		MO = PUT(MONTH(&BEGIN),Z2.);
		YEAR = PUT(YEAR(&BEGIN),4.);
	END;
	ELSE DO;
		MO = PUT(MONTH(AD_PRC),Z2.);
		YEAR = PUT(YEAR(AD_PRC),4.);
	END;
	PRIN_TOT+PRIN;
	OG_PRIN_TOT+OG_PRIN;
	OINT_TOT+OINT;
	OTHR_TOT+OTHR;
	TLO_TOT+TLO;
RUN;
DATA TOBLCB (KEEP= MO YEAR OG_PRIN_TOT PRIN_TOT OINT_TOT OTHR_TOT TLO_TOT);
	SET TOBLCA;
	BY YEAR MO;
	IF LAST.MO THEN OUTPUT;
RUN;
/*REPORT4 PROCESSING*/
DATA LAEDLA;
SET DATII;
WHERE LC_STA_DC10 NOT IN
	(
		'03'
	)
	AND AC_LON_STA_TYP IN 
	(
		'RP','ID','DA','IA','IG'
	)
	AND LC_AUX_STA NOT IN 
	(
		'10','01'
	);
RUN;
%CALAMT(LAEDLA,4);
/*CREATE DATE DATA SET FOR R4*/
%SPLIT_SET2(_95,147,0,30,95,0);
%SPLIT_SET2(_98,147,0,30,98,1);
PROC SQL;
	CREATE TABLE LAEDLB AS 
	SELECT DISTINCT B.GRP
		,B.SVAR
		,B.REI_RATE
		,A.BF_SSN
		,A.CLUID
		,COALESCE(A.LA_CLM_PRI,0) AS LA_CLM_PRI
		,COALESCE(A.OG_PRIN,0) AS OG_PRIN
		,COALESCE(A.PRIN,0) AS PRIN
		,COALESCE(A.OINT,0) AS OINT
		,COALESCE(A.OTHR,0) AS OTHR
		,COALESCE(A.TLO,0) AS TLO
		,COALESCE(A.LA_AGY_RIN_DOE,0) AS LA_AGY_RIN_DOE
	FROM LAEDLA A
	RIGHT OUTER JOIN DINT B
		ON COALESCE(A.RPY2TDY,0) = B.DAY_NUM
		AND A.REI_RATE = B.REI_RATE
	;
QUIT;
PROC SORT DATA=LAEDLB OUT=LAEDLC;
	BY REI_RATE SVAR BF_SSN;
RUN;
DATA LAEDLC;
	SET LAEDLC;
	BY REI_RATE SVAR BF_SSN;
	IF FIRST.BF_SSN THEN DO;
		IF BF_SSN = '' THEN 
			BR_CT = 0;
		ELSE
			BR_CT = 1;
	END;
	ELSE 
		BR_CT = 0;
RUN;
/*************************************************************************
* COMPASS PROCESSING
**************************************************************************/
/*SET UP ROWS FOR 12 MONTHS*/
DATA COBIL;
	SET COBIL;
	DO I=1 TO 12;
		OUTPUT;
	END;
RUN;
/*CALCULATE EXPECTED PRINCIPAL,INTEREST, AND TOTALS*/
DATA COBIL;
	SET COBIL;
	RETAIN PRIN_BAL tPRIN_BAL;
	IF I=1 THEN DO;
		EI = LA_CUR_PRN_BIL*(((LR_INT_BIL/100)/365)*30);
		EP = LA_BIL_CUR_DU - EI;
		ET = EI+EP;
		PRIN_BAL = LA_CUR_PRN_BIL - EP;
		tPRIN_BAL = PRIN_BAL;
	END;
	ELSE DO;
		EI = tPRIN_BAL*(((LR_INT_BIL/100)/365)*30);
		EP = LA_BIL_CUR_DU - EI;
		ET = EI+EP;
		PRIN_BAL = tPRIN_BAL - EP;
		tPRIN_BAL = PRIN_BAL;
	END;
RUN;
/*IF THE LOAN WILL BE PAID IN FULL ZERO THE SUMMARY AMOUNTS OUT ONCE PIF*/
DATA COBIL;
SET COBIL;
IF PRIN_BAL <= 0 THEN DO;
	EP = 0;
	EI = 0;
	ET = 0;
END;
RUN;
/*CREATE MONTH DATASET*/
DATA MONTH_NAMES;
	DO I=0 TO 11;
		IF I=0 THEN 
			MO = LEFT(PUT(INTNX('MONTH',TODAY(),I,'BEGINNING'), MONNAME10.));
		ELSE 
			MO = LEFT(PUT(INTNX('MONTH',TODAY(),I,'BEGINNING'), MONNAME10.));
		OUTPUT;
	END;
RUN;
/*PUT EVERYTHING TOGETHER*/
PROC SQL;
	CREATE TABLE SMRY_DATA AS
		SELECT A.I
			,B.MO
			,SUM(EP) AS EPT
			,SUM(EI) AS EIT
			,SUM(ET) AS ETT
			,'X' AS BVAR
		FROM COBIL A
		INNER JOIN MONTH_NAMES B
			ON A.I = B.I+1
		GROUP BY A.I
			,B.MO
	;
QUIT;
ENDRSUBMIT;
DATA RINRTC;
	SET WORKLOCL.RINRTC;
RUN;
DATA TOBLCB;
	SET WORKLOCL.TOBLCB;
RUN;
DATA LAEDLC;
	SET WORKLOCL.LAEDLC;
RUN;
DATA SMRY_DATA;
	SET WORKLOCL.SMRY_DATA;
RUN;
/*******************************************************
* CREATE REPORTS
********************************************************
* REPORT2 
********************************************************/
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER DATE;
TITLE 'DEFAULTED LOAN ANALYSIS';
TITLE2 'ALL LOANS THAT DEFAULTED IN THE TWELVE MONTHS ENDING WITH THE CURRENT MONTH';
TITLE3 'LOANS WITH REINSURANCE RATES OF < 100%';
TITLE4 'EXCLUDE BANKRUPTCIES, DEATH & DISABILITY, EXEMPT, REPURCHASED AND REHABILITATED LOANS';
TITLE5 'AGED FROM NSLDS CALCULATED DATE ENTERED REPAYMENT TO END OF CURRENT MONTH';
TITLE6 'BROKEN OUT BY REINSURANCE RATE AND 0 DAY INCREMENTS';
FOOTNOTE   "JOB = UTLWA11  	 REPORT = ULWA11.LWA11R2";
PROC REPORT DATA=RINRTC NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN REI_RATE SVAR GRP OG_PRIN LA_CLM_PRI PRIN OINT OTHR TLO LA_AGY_RIN_DOE BR_CT;
	DEFINE REI_RATE / GROUP "RATE" WIDTH=4 CENTER;
	DEFINE SVAR / GROUP NOPRINT;
	DEFINE GRP / GROUP "DATE~INTERVAL" WIDTH=10 CENTER;
	DEFINE OG_PRIN / ANALYSIS "ORIGINAL~PRINCIPAL" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE LA_CLM_PRI / ANALYSIS "CLAIM PAID AMOUNT" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE PRIN / ANALYSIS "CURRENT PRINCIPAL OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE OINT / ANALYSIS "INTEREST OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE OTHR / ANALYSIS "OTHER~CHARGES" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE TLO / ANALYSIS "TOTAL LOAN OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE LA_AGY_RIN_DOE / ANALYSIS "AMOUNT~REINSURED" FORMAT=DOLLAR14.2 CENTER;
	DEFINE BR_CT / ANALYSIS "NUMBER OF~BORROWERS" WIDTH=9 FORMAT=COMMA6. CENTER;
	BREAK AFTER REI_RATE / SUMMARIZE SUPPRESS OL SKIP;
	COMPUTE AFTER;
		LINE @1 130*'-' ;
		LINE @1 'GRAND TOTALS:' 
			@18 OG_PRIN.SUM DOLLAR14.2 
			@33 LA_CLM_PRI.SUM DOLLAR14.2
			@48 PRIN.SUM DOLLAR14.2
			@63 OINT.SUM DOLLAR14.2
			@78 OTHR.SUM DOLLAR14.2
			@93 TLO.SUM DOLLAR14.2
			@108 LA_AGY_RIN_DOE.SUM DOLLAR14.2
			@124 BR_CT.SUM COMMA6.;
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
/*REPORT3*/
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER NODATE;
TITLE "TOTAL PRINCIPAL BALANCE";
TITLE2 "&RUNDATE";
FOOTNOTE   "JOB = UTLWA11  	 REPORT = ULWA11.LWA11R3";
PROC REPORT DATA=TOBLCB NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN YEAR MO OG_PRIN_TOT PRIN_TOT OINT_TOT OTHR_TOT TLO_TOT;
	DEFINE YEAR / GROUP "YEAR" WIDTH=4 RIGHT ;
	DEFINE MO / GROUP "MONTH" WIDTH=5 RIGHT;
	DEFINE OG_PRIN_TOT / ANALYSIS "ORIGINAL~PRINCIPAL~AMOUNT" WIDTH=20 FORMAT=DOLLAR20.2 RIGHT;
	DEFINE PRIN_TOT / ANALYSIS "CURRENT~PRINCIPAL~OUTSTANDING" WIDTH=20 FORMAT=DOLLAR20.2 RIGHT;
	DEFINE OINT_TOT / ANALYSIS "INTEREST~OUTSTANDING" WIDTH=20 FORMAT=DOLLAR20.2 RIGHT;
	DEFINE OTHR_TOT / ANALYSIS "OTHER~CHARGES" WIDTH=20 FORMAT=DOLLAR20.2 RIGHT;
	DEFINE TLO_TOT / ANALYSIS "NET TOTAL LOAN~OUTSTANDING" WIDTH=20 FORMAT=DOLLAR20.2 RIGHT;
RUN;
PROC PRINTTO;
RUN;
/*REPORT4*/
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER DATE;
TITLE 'LOAN ANALYSIS EXCLUDING DEFAULTED LOANS';
TITLE2 'LOANS WITH REINSURANCE RATES < 100%';
TITLE3 'AGED FROM NSLDS CALCULATED DATE ENTERED REPOAYMENT TO END OF CURRENT MONTH';
TITLE4 'BROKEN OUT BY REINSURANCE RATE AND 30 DAY INCREMENTS';
FOOTNOTE   "JOB = UTLWA11  	 REPORT = ULWA11.LWA11R4";
PROC REPORT DATA=LAEDLC NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN REI_RATE SVAR GRP OG_PRIN LA_CLM_PRI PRIN OINT OTHR TLO LA_AGY_RIN_DOE BR_CT;
	DEFINE REI_RATE / GROUP "RATE" WIDTH=4 CENTER;
	DEFINE SVAR / GROUP NOPRINT;
	DEFINE GRP / GROUP "DATE~INTERVAL" WIDTH=10 CENTER;
	DEFINE OG_PRIN / ANALYSIS "ORIGINAL~PRINCIPAL" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE LA_CLM_PRI / ANALYSIS "CLAIM PAID AMOUNT" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE PRIN / ANALYSIS "CURRENT PRINCIPAL OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE OINT / ANALYSIS "INTEREST OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE OTHR / ANALYSIS "OTHER~CHARGES" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE TLO / ANALYSIS "TOTAL LOAN OUTSTANDING" WIDTH=14 FORMAT=DOLLAR14.2 CENTER;
	DEFINE LA_AGY_RIN_DOE / ANALYSIS "AMOUNT~REINSURED" FORMAT=DOLLAR14.2 CENTER;
	DEFINE BR_CT / ANALYSIS "NUMBER OF~BORROWERS" WIDTH=9 FORMAT=COMMA6. CENTER;
	BREAK AFTER REI_RATE / SUMMARIZE SUPPRESS OL SKIP;
	COMPUTE AFTER;
		LINE @1 130*'-' ;
		LINE @1 'GRAND TOTALS:' 
			@18 OG_PRIN.SUM DOLLAR14.2 
			@33 LA_CLM_PRI.SUM DOLLAR14.2
			@48 PRIN.SUM DOLLAR14.2
			@63 OINT.SUM DOLLAR14.2
			@78 OTHR.SUM DOLLAR14.2
			@93 TLO.SUM DOLLAR14.2
			@108 LA_AGY_RIN_DOE.SUM DOLLAR14.2
			@124 BR_CT.SUM COMMA6.;
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
/*REPORT5*/
PROC PRINTTO PRINT=REPORT5 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER DATE;
TITLE 'FUTURE CASH FLOW REPORT';
TITLE2 'ESTIMATED CASH FLOW FOR THE NEXT 12 MONTHS';
FOOTNOTE   "JOB = UTLWA11  	 REPORT = ULWA11.LWA11R5";
PROC REPORT DATA=SMRY_DATA NOWD HEADSKIP SPLIT='/' SPACING=1;
	COLUMN BVAR I MO EPT EIT ETT;
	DEFINE BVAR / GROUP NOPRINT;
	DEFINE I / GROUP NOPRINT;
	DEFINE MO / GROUP "MONTH" WIDTH=10 LEFT;
	DEFINE EPT / ANALYSIS "EXPECTED/AMOUNT TO/PRINCIPAL" WIDTH=20 FORMAT=DOLLAR18.2 RIGHT;
	DEFINE EIT / ANALYSIS "EXPECTED/AMOUNT TO/INTEREST" WIDTH=20 FORMAT=DOLLAR18.2 RIGHT;
	DEFINE ETT / ANALYSIS "TOTAL/EXPECTED" WIDTH=20 FORMAT=DOLLAR18.2 RIGHT;
	BREAK AFTER BVAR / SUMMARIZE SUPPRESS OL SKIP;
RUN;
PROC PRINTTO;
RUN;
