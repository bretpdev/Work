*---------------------------------------*
| UTLWU16 - TILP LITIGATION - QC REPORT |
*---------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU16.LWU16R2";
FILENAME REPORTZ "&RPTLIB/ULWU16.LWU16RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TILPLIT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT D.DF_SPE_ACC_ID
	,A.LN_SEQ
	,A.LA_CUR_PRI 
	,DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) AS LD_DLQ_MAX
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,MAX(LD_REQ_RSP_ATY_PRF) AS LD_REQ_RSP_ATY_PRF
	FROM OLWHRM1.AY10_BR_LON_ATY 
	WHERE PF_REQ_ACT = 'QTPLT'
	GROUP BY BF_SSN
	) C
	ON A.BF_SSN = C.BF_SSN
LEFT OUTER JOIN (
	SELECT X.BF_SSN
		,X.LA_BIL_PAS_DU
	FROM OLWHRM1.LN80_LON_BIL_CRF X
	INNER JOIN (
		SELECT BF_SSN
			,MAX(LD_BIL_CRT) AS LD_BIL_CRT
		FROM OLWHRM1.LN80_LON_BIL_CRF Y
		GROUP BY Y.BF_SSN
		) Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LD_BIL_CRT = Y.LD_BIL_CRT
	) E
	ON A.BF_SSN = E.BF_SSN
WHERE A.IC_LON_PGM = 'TILP'
	AND A.LA_CUR_PRI > 0
	AND B.LC_STA_LON16 = '1'
	AND DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) >= 160
	AND B.LC_DLQ_TYP = 'P'
	AND A.LC_STA_LON10 <> 'L'
	AND (
			(
				C.LD_REQ_RSP_ATY_PRF IS NULL
			)
		OR
			(
				C.LD_REQ_RSP_ATY_PRF < B.LD_DLQ_OCC 
			)
		)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU16.LWU16RZ);*/
/*QUIT;*/

ENDRSUBMIT;

DATA TILPLIT;
SET WORKLOCL.TILPLIT;
RUN;

PROC SORT DATA=TILPLIT; BY DF_SPE_ACC_ID ;RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'TILP LITIGATION - QC REPORT';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWU16  	 REPORT = ULWU16.LWU16R2';
PROC CONTENTS DATA=TILPLIT OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ = 1 THEN DO;
	PUT // 126*'-';
	PUT      /////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWU16  	 REPORT = ULWU16.LWU16R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=TILPLIT WIDTH=UNIFORM WIDTH=MIN;
FORMAT LA_CUR_PRI 10.2;
VAR DF_SPE_ACC_ID
	LD_DLQ_MAX
	LA_CUR_PRI
	;
LABEL DF_SPE_ACC_ID = 'ACCOUNT #'
	LD_DLQ_MAX = 'DAYS DELINQUENT'
	LA_CUR_PRI = 'CURRENT PRINCIPAL'
	;
RUN;

PROC PRINTTO;
RUN;
