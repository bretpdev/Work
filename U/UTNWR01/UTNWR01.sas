/*****************************************************************************
UTNWR01
******************************************************************************/
/*LIBNAME DNFPUTDL DB2 DATABASE=DNFPUTDL OWNER=pkub; */
/*LIBNAME FRDWODS DB2 DATABASE=DNFPUTDL OWNER=frdwods; */
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/UNWR01.NWR01R2";
FILENAME REPORT3 "&RPTLIB/UNWR01.NWR01R3";
FILENAME REPORT4 "&RPTLIB/UNWR01.NWR01R4";
FILENAME REPORT5 "&RPTLIB/UNWR01.NWR01R5";
FILENAME REPORT6 "&RPTLIB/UNWR01.NWR01R6";
FILENAME REPORT7 "&RPTLIB/UNWR01.NWR01R7";
FILENAME REPORT8 "&RPTLIB/UNWR01.NWR01R8";
FILENAME REPORTZ "&RPTLIB/UNWR01.NWR01RZ";
DATA _NULL_;
	REPORT_DT = TODAY()-1;
	CALL SYMPUTX('PRV_DT',"'"||PUT(INTNX('DAY',REPORT_DT,0,'beginning'), MMDDYY10.)||"'");
	CALL SYMPUTX('RUN_DT',PUT(INTNX('DAY',REPORT_DT,0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT PRV_DT = &PRV_DT;
LIBNAME  WORKLOCL  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL) ;
CREATE TABLE DailyPosting AS
SELECT 
	*
FROM CONNECTION TO DB2 
(
	SELECT DISTINCT 
		LN90.BF_SSN,
		LN90.LN_SEQ,
		LN90.LN_FAT_SEQ,
		LN90.PC_FAT_TYP||LN90.PC_FAT_SUB_TYP AS TRX_TYP,
		LN90.LD_FAT_EFF,
		LN90.LD_FAT_APL,
		LN90.LD_FAT_PST,
		COALESCE(LN90.LA_FAT_CUR_PRI,0) AS PRIN,
		COALESCE(LN90.LA_FAT_NSI,0) AS INTR,
		COALESCE(LN90.LA_FAT_DL_REB,0) AS REB,
		CASE WHEN LN90.PC_FAT_SUB_TYP = '14' THEN COALESCE(LN90.LA_FAT_CUR_PRI,0)
			 ELSE 0
		END AS ORIG_FEE,
		COALESCE(LN90.LA_FAT_MSC_FEE,0)+COALESCE(LN90.LA_FAT_LTE_FEE,0) AS OTR_FEE,
		PD10.DF_SPE_ACC_ID,
		RM10.LF_RMT_BCH_USR_INI,
		RM30.LF_BR_RMT_SCH_NUM,
		RM30.LD_RMT_BCH_INI,
		FR02.WD_FMS_SCH,
		FR02.PF_TRY_SCH_NUM,
		RM30.LF_LCK_BOX_TRC,
		CASE WHEN RM30.LC_RMT_PAY_SRC IN ('03','08') THEN '03'
			 WHEN RM30.LC_RMT_PAY_SRC IN ('01','04','09','13') THEN '01'
			 WHEN RM30.LC_RMT_PAY_SRC IN ('12') THEN '12'
			 WHEN RM30.LC_RMT_PAY_SRC IN ('02') AND RM31.LC_RMT_PST_SCH_TYP = '02' THEN '12'
			 WHEN RM30.LC_RMT_PAY_SRC IN ('02') AND RM31.LC_RMT_PST_SCH_TYP <> '02' THEN '02'
			 ELSE ''
		END AS LC_RMT_PAY_SRC,
		FS10.LF_FED_AWD||CHAR(FS10.LN_FED_AWD_SEQ) AS LID,
		CASE WHEN LN90.PC_FAT_TYP = '04' THEN OW30.IF_FLS_DEA
			 ELSE BT20.PF_FED_LON_PR_GRP 
		END AS DEAL_ID,
		FR03.WD_FMS_RPT,
		FR03.WN_FMS_RPT_SEQ,
		LN90.LI_FAT_RAP,
		CASE WHEN LN90.PC_FAT_TYP||LN90.PC_FAT_SUB_TYP = '0291' AND LN55.LC_LON_BBT_STA = 'A' AND LN55.LD_LON_BBT_STA >= &PRV_DT THEN 1
			 ELSE 0
		END AS KEEP_REB,
		FR02.WF_FMS_OTH_SER,
		LN10.IC_LON_PGM
	FROM 
		PKUB.LN90_FIN_ATY LN90
		INNER JOIN PKUB.PD10_PRS_NME PD10
			ON LN90.BF_SSN = PD10.DF_PRS_ID
		LEFT OUTER JOIN PKUB.LN94_LON_PAY_FAT LN94
			ON LN90.BF_SSN = LN94.BF_SSN
			AND LN90.LN_SEQ = LN94.LN_SEQ
			AND LN90.LN_FAT_SEQ = LN94.LN_FAT_SEQ
		LEFT OUTER JOIN PKUB.RM30_BR_RMT RM30
			ON LN94.LD_RMT_BCH_INI = RM30.LD_RMT_BCH_INI
			AND LN94.LC_RMT_BCH_SRC_IPT = RM30.LC_RMT_BCH_SRC_IPT
			AND LN94.LN_RMT_BCH_SEQ = RM30.LN_RMT_BCH_SEQ
			AND LN94.LN_RMT_SEQ = RM30.LN_RMT_SEQ
			AND LN94.LN_RMT_ITM = RM30.LN_RMT_ITM
			AND LN94.LN_RMT_ITM_SEQ = RM30.LN_RMT_ITM_SEQ
		LEFT JOIN PKUB.RM31_BR_RMT_PST RM31
			ON RM31.LD_RMT_BCH_INI = RM30.LD_RMT_BCH_INI
			AND RM31.LC_RMT_BCH_SRC_IPT = RM30.LC_RMT_BCH_SRC_IPT
			AND RM31.LN_RMT_BCH_SEQ = RM30.LN_RMT_BCH_SEQ
		LEFT OUTER JOIN PKUB.RM10_RMT_BCH RM10
			ON RM30.LD_RMT_BCH_INI = RM10.LD_RMT_BCH_INI
			AND RM30.LC_RMT_BCH_SRC_IPT = RM10.LC_RMT_BCH_SRC_IPT
			AND RM30.LN_RMT_BCH_SEQ = RM10.LN_RMT_BCH_SEQ
		INNER JOIN PKUB.LN10_LON LN10
			ON LN90.BF_SSN = LN10.BF_SSN
			AND LN90.LN_SEQ = LN10.LN_SEQ
		LEFT OUTER JOIN PKUB.BT20_MAJ_BCH BT20
			ON LN10.PF_MAJ_BCH = BT20.PF_MAJ_BCH
		LEFT OUTER JOIN PKUB.FR03_FMS_LON_FAT FR03
			ON LN90.BF_SSN = FR03.BF_SSN
			AND LN90.LN_SEQ = FR03.LN_SEQ
			AND LN90.LN_FAT_SEQ = FR03.LN_FAT_SEQ 
		LEFT OUTER JOIN PKUB.FR02_FMS_MST FR02
			ON FR03.WD_FMS_RPT = FR02.WD_FMS_RPT 
			AND FR03.WN_FMS_RPT_SEQ = FR02.WN_FMS_RPT_SEQ
		LEFT OUTER JOIN PKUB.FS10_DL_LON FS10
			ON LN10.BF_SSN = FS10.BF_SSN
			AND LN10.LN_SEQ = FS10.LN_SEQ
		LEFT OUTER JOIN PKUB.LN99_LON_SLE_FAT LN99
			ON LN90.BF_SSN = LN99.BF_SSN
			AND LN90.LN_SEQ = LN99.LN_SEQ
			AND LN90.LN_FAT_SEQ = LN99.LN_FAT_SEQ  
		LEFT OUTER JOIN PKUB.OW30_LON_SLE_CTL OW30
			ON LN99.IF_LON_SLE = OW30.IF_LON_SLE
		LEFT OUTER JOIN PKUB.LN55_LON_BBS_TIR LN55
			ON LN90.BF_SSN = LN55.BF_SSN
			AND LN90.LN_SEQ = LN55.LN_SEQ
			AND LN55.LC_STA_LN55 = 'A'
	WHERE 
		LN90.LD_FAT_APL = &PRV_DT
		AND 
		(
			LN90.PC_FAT_TYP = '10'
			OR
			(
				LN90.LC_FAT_REV_REA = '' 
				AND LN90.LC_STA_LON90 = 'A'
			)
		)
FOR READ ONLY WITH UR
);

CREATE TABLE ZC11 AS
SELECT 
	*
FROM CONNECTION TO DB2 
(
	SELECT 
		*
	FROM 
		FRDWODS.ZC11_FMS_GL_SUM

	FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

DATA DailyPosting;
	SET DailyPosting;
	IF KEEP_REB THEN REB = REB;
	ELSE REB = 0;
	TOT_AMT = SUM(PRIN,INTR,REB,ORIG_FEE,OTR_FEE);
RUN;

PROC SORT DATA=DailyPosting OUT=DailyPosting NODUPKEY;
	BY BF_SSN LN_SEQ LN_FAT_SEQ;
RUN;

PROC SORT DATA=DailyPosting OUT=R2;
	BY TRX_TYP BF_SSN;
RUN;

DATA R2;
	SET R2;
	*SET THE SCHEDULE DATE TO MISSING IF NO SCHED NUM;
	IF TRX_TYP = '2010' AND LF_BR_RMT_SCH_NUM = '' THEN WD_FMS_SCH = .; 
RUN;

PROC SORT DATA=DailyPosting(WHERE=(LC_RMT_PAY_SRC^='' AND LI_FAT_RAP^='Y')) OUT=R3;
	BY LC_RMT_PAY_SRC TRX_TYP;
RUN;
PROC SORT DATA=DailyPosting(WHERE=(LC_RMT_PAY_SRC^='' AND LI_FAT_RAP^='Y')) OUT=R4;
	BY LC_RMT_PAY_SRC WD_FMS_SCH LF_BR_RMT_SCH_NUM;
RUN;
PROC SORT DATA=DailyPosting OUT=R5(WHERE=(TRX_TYP IN ('0291','0496','0497','0498')));
	BY DEAL_ID LC_RMT_PAY_SRC WD_FMS_SCH LF_BR_RMT_SCH_NUM;
RUN;
DATA R6;
	SET DailyPosting;*THIS IS THE FILE THAT IS AT THE ZC11 TRX_ID LEVEL ;
	IF WD_FMS_RPT NE . THEN DO;
		TRX_ID = CATT('NC',PUT(WD_FMS_RPT,YYMMDDN8.),PUT(WN_FMS_RPT_SEQ,Z6.));
		OUTPUT;
	END;
RUN;
PROC SORT DATA=R6 ;
	BY BF_SSN LN_SEQ LN_FAT_SEQ;
RUN;

PROC SQL;
	CREATE TABLE R6TEMP AS 
	SELECT DISTINCT 
		R6.*,
		ZC11.WC_FMS_TRX_AMT_TYP,
		ZC11.WA_FMS_TRX
	FROM 
		R6 
		INNER JOIN ZC11
			ON R6.TRX_ID = ZC11.WF_FMS_TRX
	;
QUIT;

DATA R6TEMP(DROP=tPRIN tINTR tREB tORIG_FEE tOTR_FEE tTOT_AMT);
	SET R6TEMP;
/*	SET UP TEMPORARY CONTAINER VARIABLES*/
	tPRIN = PRIN ;
	tINTR = INTR;
	tREB = REB;
	tORIG_FEE = ORIG_FEE;
	tOTR_FEE = OTR_FEE;
	tTOT_AMT = TOT_AMT;
/*	CLEAR ALL VALUES*/
	PRIN = 0;
	INTR = 0;
	REB = 0;
	ORIG_FEE = 0;
	OTR_FEE = 0;
	TOT_AMT = 0;
/*MAKE ASSIGNMENTS DEPENDING ON WC_FMS_TRX_AMT_TYP*/
	IF WC_FMS_TRX_AMT_TYP = 'IRB_AMT' THEN INTR = tINTR;
	ELSE IF WC_FMS_TRX_AMT_TYP = 'ORIG_FEE_AMT' THEN ORIG_FEE = tORIG_FEE;
	ELSE IF WC_FMS_TRX_AMT_TYP = 'PBO_AMT' THEN PRIN = tPRIN;
	ELSE IF WC_FMS_TRX_AMT_TYP = 'REBATE_AMT' THEN REB = tREB;
	ELSE DELETE;
RUN;

PROC SORT DATA=R6TEMP OUT=R6 ;
	BY TRX_ID TRX_TYP LC_RMT_PAY_SRC WD_FMS_SCH LF_BR_RMT_SCH_NUM;
RUN;

DATA R6_ERRORS(KEEP=TRX_ID TRX_TOTAL WA_FMS_TRX);
	SET R6;
	BY TRX_ID;
	IF FIRST.TRX_ID THEN TRX_TOTAL = 0;
	TRX_TOTAL+SUM(PRIN,INTR,REB,ORIG_FEE,OTR_FEE,TOT_AMT);
	IF LAST.TRX_ID AND ROUND(TRX_TOTAL,.01) NE ROUND(WA_FMS_TRX,.01) THEN OUTPUT;
RUN;

/*include loans where a 1081 transaction code was posted*/
DATA TEN81;
	SET DailyPosting;
	WHERE TRX_TYP = '1081';
RUN;

/*information for any 1081, 4101, 4301, 5301, or 5401 transactions posted the same day as the 1081 transaction*/
PROC SQL;
	CREATE TABLE R8 AS
		SELECT
			D.*
		FROM
			DailyPosting D
			INNER JOIN TEN81 T
				ON D.BF_SSN = T.BF_SSN
				AND D.LN_SEQ = T.LN_SEQ
				AND D.LD_FAT_PST = T.LD_FAT_PST
		WHERE
			D.TRX_TYP IN ('1081', '4101', '4301', '5301', '5401')
		ORDER BY
			WF_FMS_OTH_SER,
			TRX_TYP,
			BF_SSN

	;
QUIT;

ENDRSUBMIT;

DATA R2; SET LEGEND.R2; RUN;
DATA R3; SET LEGEND.R3; RUN;
DATA R4; SET LEGEND.R4; RUN;
DATA R5; SET LEGEND.R5; RUN;
DATA R6; SET LEGEND.R6; RUN;
DATA R6_ERRORS; SET LEGEND.R6_ERRORS; RUN;
DATA R8; SET LEGEND.R8; RUN;

PROC FORMAT ;
VALUE $PaySrc '03' = 'Lockbox'
	'01' = 'Pay.gov'
	'12' = 'IPAC'
	'02' = 'REX'
;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=217 ;
TITLE 'CornerStone - Daily Servicing System Posting Report by Transaction Code';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R2';
PROC CONTENTS DATA=R2 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R2";
	END;
RETURN;
RUN;
PROC REPORT DATA=R2 NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN TRX_TYP LD_FAT_EFF LD_FAT_APL BF_SSN PRIN INTR REB ORIG_FEE OTR_FEE TOT_AMT
	DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI LF_BR_RMT_SCH_NUM WD_FMS_SCH LF_LCK_BOX_TRC;
	DEFINE TRX_TYP / ORDER WIDTH=10 "Tranaction~Type" right order=internal;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Effective~Date" right;
	DEFINE LD_FAT_APL / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Applied~Date" right;
	DEFINE BF_SSN / order WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Orig~Fee" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other~Fees" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total~Amount" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	DEFINE LF_BR_RMT_SCH_NUM / DISPLAY WIDTH=10 "Schedule~Number" right;
	DEFINE WD_FMS_SCH / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Schedule~Date" right;
	DEFINE LF_LCK_BOX_TRC / DISPLAY WIDTH=7 "Trace~Number" right;
	BREAK AFTER TRX_TYP / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;


PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=225;
TITLE 'CornerStone - Daily Servicing System Posting Report by Source Code';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R3';
PROC CONTENTS DATA=R3 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R3";
	END;
RETURN;
RUN;

PROC REPORT DATA=R3 NOWD HEADSKIP SPLIT='~' SPACING=1;
COLUMN LC_RMT_PAY_SRC TRX_TYP LD_FAT_EFF LD_FAT_APL BF_SSN PRIN INTR REB ORIG_FEE OTR_FEE TOT_AMT
DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI LF_BR_RMT_SCH_NUM WD_FMS_SCH LF_LCK_BOX_TRC;
	DEFINE LC_RMT_PAY_SRC / ORDER "Source~Code" ORDER=INTERNAL WIDTH=10 FORMAT=$PaySrc. right;
	DEFINE TRX_TYP / ORDER "Tranaction Type" ORDER=INTERNAL WIDTH=10 right;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Effective~Date" right;
	DEFINE LD_FAT_APL / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Applied~Date" right;
	DEFINE BF_SSN / DISPLAY WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Orig~Fee" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other~Fees" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total~Amount" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	DEFINE LF_BR_RMT_SCH_NUM / DISPLAY WIDTH=10 "Schedule~Number" right;
	DEFINE WD_FMS_SCH / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Schedule~Date" right;
	DEFINE LF_LCK_BOX_TRC / DISPLAY WIDTH=7 "Trace~Number" right;
	BREAK AFTER LC_RMT_PAY_SRC / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=225;
TITLE 'CornerStone - Daily Servicing System Report of Deposit Activity by Schedule Number';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R4';
PROC CONTENTS DATA=R4 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R4";
	END;
RETURN;
RUN;

PROC REPORT DATA=R4 NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN LC_RMT_PAY_SRC LD_RMT_BCH_INI TRX_TYP LD_FAT_EFF LD_FAT_APL BF_SSN PRIN INTR REB 
	ORIG_FEE OTR_FEE TOT_AMT DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI LF_BR_RMT_SCH_NUM 
	WD_FMS_SCH LF_LCK_BOX_TRC;
	DEFINE LC_RMT_PAY_SRC / ORDER "Source~Code" ORDER=INTERNAL WIDTH=10 FORMAT=$PaySrc. right;
	DEFINE LD_RMT_BCH_INI / ORDER "Receipt~Date" ORDER=INTERNAL WIDTH=10 FORMAT=MMDDYY10. right;
	DEFINE TRX_TYP / ORDER "Tranaction Type" ORDER=INTERNAL WIDTH=10 right;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Effective~Date" right;
	DEFINE LD_FAT_APL / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Applied~Date" right;
	DEFINE BF_SSN / DISPLAY WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Orig~Fee" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other~Fees" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total~Amount" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	DEFINE LF_BR_RMT_SCH_NUM / ORDER ORDER=INTERNAL WIDTH=10 "Schedule~Number" right;
	DEFINE WD_FMS_SCH / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Schedule~Date" right;
	DEFINE LF_LCK_BOX_TRC / DISPLAY WIDTH=7 "Trace~Number" right;
	BREAK AFTER LF_BR_RMT_SCH_NUM / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT5 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=217 ;
TITLE 'CornerStone - Daily Servicing System Posting Report by Deal Id';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R5';
PROC CONTENTS DATA=R5 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R5";
	END;
RETURN;
RUN;
PROC REPORT DATA=R5 NOWD HEADSKIP SPLIT='~' SPACING=1 MISSING;
	COLUMN DEAL_ID TRX_TYP LD_FAT_EFF LD_FAT_APL BF_SSN PRIN INTR REB ORIG_FEE OTR_FEE TOT_AMT
	DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI LF_BR_RMT_SCH_NUM WD_FMS_SCH LF_LCK_BOX_TRC;
	DEFINE DEAL_ID / ORDER WIDTH=10 "Deal ID" right order=internal;
	DEFINE TRX_TYP / ORDER WIDTH=10 "Tranaction~Type" right order=internal;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Effective~Date" right;
	DEFINE LD_FAT_APL / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Applied~Date" right;
	DEFINE BF_SSN / DISPLAY WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Orig~Fee" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other~Fees" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total~Amount" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	DEFINE LF_BR_RMT_SCH_NUM / DISPLAY WIDTH=10 "Schedule~Number" right;
	DEFINE WD_FMS_SCH / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Schedule~Date" right;
	DEFINE LF_LCK_BOX_TRC / DISPLAY WIDTH=7 "Trace~Number" right;
	BREAK AFTER DEAL_ID  / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT6 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=215 ;
TITLE 'CornerStone - Daily Servicing System Posting Report by Transaction Id';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R6';
PROC CONTENTS DATA=R6 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R6";
	END;
RETURN;
RUN;
PROC REPORT DATA=R6 NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN TRX_ID TRX_TYP LD_FAT_EFF LD_FAT_APL BF_SSN PRIN INTR REB ORIG_FEE OTR_FEE TOT_AMT
	DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI;
	DEFINE TRX_ID / ORDER WIDTH=16 "Tranaction ID" right order=internal;
	DEFINE TRX_TYP / ORDER WIDTH=10 "Tranaction~Type" right order=internal;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Effective~Date" right;
	DEFINE LD_FAT_APL / DISPLAY WIDTH=10 FORMAT=MMDDYY10. "Applied~Date" right;
	DEFINE BF_SSN / order WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Orig~Fee" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other~Fees" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total~Amount" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	BREAK AFTER TRX_ID / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT7 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER CENTER;
OPTIONS PS=39 LS=135 ;
TITLE 'CornerStone - Daily Servicing System Posting Report by Transaction Id';
TITLE2 'ERROR REPORT';
TITLE3 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R7';
PROC CONTENTS DATA=R6_ERRORS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@53 '**** NO ERRORS FOUND ****';
	PUT //////
		@56 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R7";
	END;
RETURN;
RUN;
PROC REPORT DATA=R6_ERRORS NOWD HEADSKIP SPLIT='~' SPACING=5;
	COLUMN TRX_ID TRX_TOTAL WA_FMS_TRX ;
	DEFINE TRX_ID / ORDER WIDTH=16 "Tranaction ID"  order=internal;
	DEFINE TRX_TOTAL / DISPLAY WIDTH=14 FORMAT=COMMA14.2 "Report Total"  order=internal;
	DEFINE WA_FMS_TRX / DISPLAY WIDTH=14 FORMAT=COMMA14.2 "ZC11 Total" ;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT8 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE NONUMBER;
OPTIONS PS=39 LS=217 ;
TITLE 'Daily Servicing System Report of Deposit Activity for Consolidation Payments';
TITLE2 "Run Date: &RUN_DT";
FOOTNOTE1  	"THIS DOCUMENT MAY CONTAIN BORROWERS' SENSITIVE INFORMATION THAT CORNERSTONE HAS PLEDGED TO PROTECT.";
FOOTNOTE2	'PLEASE TAKE APPROPRIATE PRECAUTIONS TO SAFEGUARD THIS INFORMATION.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTNWR01  	 REPORT = UNWR01.NWR01R8';
PROC CONTENTS DATA=R8 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTNWR01   	 REPORT = UNWR01.NWR01R8";
	END;
RETURN;
RUN;
PROC REPORT DATA=R8 NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN WF_FMS_OTH_SER IC_LON_PGM TRX_TYP LD_FAT_EFF LD_FAT_PST BF_SSN PRIN INTR REB ORIG_FEE OTR_FEE TOT_AMT
	DF_SPE_ACC_ID LID LF_RMT_BCH_USR_INI;

	DEFINE WF_FMS_OTH_SER / ORDER WIDTH=8 "Servicer~Code" right order=internal;
	DEFINE IC_LON_PGM / ORDER WIDTH=10 "Loan Program" right order=internal;
	DEFINE TRX_TYP / ORDER WIDTH=11 "Transaction~Type" right order=internal;
	DEFINE LD_FAT_EFF / DISPLAY WIDTH=11 FORMAT=MMDDYY10. "Transaction~Effective~Date" right;
	DEFINE LD_FAT_PST / DISPLAY WIDTH=11 FORMAT=MMDDYY10. "Transaction~Posted Date" right;
	DEFINE BF_SSN / ORDER WIDTH=9 'SSN' order = internal right;
	DEFINE PRIN / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Principal~Applied" right;
	DEFINE INTR / ANALYSIS WIDTH=14 FORMAT=COMMA14.2 "Interest~Applied" right;
	DEFINE REB / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Rebate~Applied" right;
	DEFINE ORIG_FEE / ANALYSIS WIDTH=11 FORMAT=COMMA10.2 "Origination~Fee Applied" right;
	DEFINE OTR_FEE / ANALYSIS WIDTH=10 FORMAT=COMMA10.2 "Other Fees~Applied" right;
	DEFINE TOT_AMT / ANALYSIS WIDTH=17 FORMAT=COMMA17.2 "Total Amount~of Transaction" right;
	DEFINE DF_SPE_ACC_ID / DISPLAY WIDTH=10 "Account~Number" right;
	DEFINE LID / DISPLAY WIDTH=23 "Loan ID" right;
	DEFINE LF_RMT_BCH_USR_INI / DISPLAY WIDTH=9 "Processor~Code/~User ID" right;
	BREAK AFTER WF_FMS_OTH_SER / SUMMARIZE SUPPRESS SKIP DOL;
RUN;

PROC PRINTTO;
RUN;
