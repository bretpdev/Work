*--------------------------------------------*
| UTLWU23 - QC LVCs                          |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU23.LWU23R2";
FILENAME REPORT3 "&RPTLIB/ULWU23.LWU23R3";
FILENAME REPORT4 "&RPTLIB/ULWU23.LWU23R4";
/*FILENAME REPORTZ "&RPTLIB/ULWU23.LWU23RZ";*/
DATA _NULL_;
	CALL SYMPUT('PRV_DAY',INTNX('DAY',TODAY(),-1,'BEGINNING'));
	CALL SYMPUT('DAYS_AGO_30',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;
%SYSLPUT DAYS_AGO_30 = &DAYS_AGO_30;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE QCLVC AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT DISTINCT B.DF_SPE_ACC_ID
		,MAX(A.BD_ATY_PRF) AS BD_ATY_PRF
		,A.BF_LST_USR_AY01
		,SUBSTR(A.BX_CMT,28,5) AS DOC_ID
		,C.DAC
	FROM OLWHRM1.AY01_BR_ATY A
		INNER JOIN OLWHRM1.PD01_PDM_INF B
			ON A.DF_PRS_ID = B.DF_PRS_ID
		LEFT OUTER JOIN (SELECT DF_PRS_ID
					,MAX(BD_ATY_PRF) AS DAC
				FROM OLWHRM1.AY01_BR_ATY
				WHERE PF_ACT IN ('DFDLC','DSLVC','STPCN')
				GROUP BY DF_PRS_ID) C
			ON C.DF_PRS_ID = B.DF_PRS_ID
	WHERE A.PF_ACT = 'MDCID'
		AND SUBSTR(A.BX_CMT,1,9) = 'LVC BLANK'
		AND A.BD_ATY_PRF >= &DAYS_AGO_30
	GROUP BY B.DF_SPE_ACC_ID
	,SUBSTR(A.BX_CMT,28,5) 
	,C.DAC
	,A.BF_LST_USR_AY01
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU23.LWU23RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA QCLVC ;SET WORKLOCL.QCLVC;RUN;
/***********************************************************************************************
* HOLIDAY PROCESSING - THIS INFO GATHERED FROM http://aa.usno.navy.mil/faq/docs/holidays.html
************************************************************************************************
* CREATE MACRO VARIALBES FOR DAY SPECIFIC HOLIDAYS
***********************************************************************************************
* New Year's Day                                    January 1 FOR CURRENT AND NEXT YEAR
* Independence Day                                  July 4
* Pioneer Day										July 24
* Christmas Day                                     December 25
***********************************************************************************************/
DATA _NULL_;
	CALL SYMPUT('THIS_YR',YEAR(INTNX('YEAR',TODAY(),0,'BEGINNING')));
	CALL SYMPUT('NEXT_YR',YEAR(INTNX('YEAR',TODAY(),+1,'BEGINNING')));
RUN;
DATA _NULL_;	
	CALL SYMPUT('CUR_NEW_YR',"01JAN&THIS_YR"d);
	CALL SYMPUT('NXT_NEW_YR',"01JAN&NEXT_YR"d);
	CALL SYMPUT('JUL4TH',"04JUL&THIS_YR"d);
	CALL SYMPUT('JUL24TH',"24JUL&THIS_YR"d);
	CALL SYMPUT('CMAS',"25DEC&THIS_YR"d);
RUN;
DATA DSPEC;FORMAT HOL MMDDYY10.;RUN;
DATA DSPEC;
FORMAT HOL MMDDYY10.;
	HOL=&NXT_NEW_YR;
RUN;
PROC SQL NOPRINT;
INSERT INTO DSPEC
	VALUES (&CUR_NEW_YR)
	VALUES (&JUL4TH)
	VALUES (&JUL24TH)
	VALUES (&CMAS);
QUIT;
/**********************************************************************************************
* CALCULATE HOLIDAYS THAT NEED TO BE CALCULATED
***********************************************************************************************
* Martin Luther King's Birthday		Third Monday in January
* Washington's Birthday				Third Monday in February
* Memorial Day						Last Monday in May
* Labor Day							First Monday in September
* Thanksgiving						Fourth Thursday in November
* Day after Thanksgiving			Friday after the fourth Thursday in November
***********************************************************************************************/
DATA _NULL_; 
	CALL SYMPUT('FRST_DAY',INTNX('YEAR',TODAY(),0,'BEGINNING'));
	CALL SYMPUT('DIY', INTNX('YEAR',TODAY(),0,'END') - INTNX('YEAR',TODAY(),0,'BEGINNING'));
RUN;
DATA CALDAY;
HOL = &FRST_DAY;
RUN;
DATA CALDAY (DROP=I);
FORMAT HOL MMDDYY10.;
SET CALDAY;
RETAIN HOL;
	DO I=0 TO &DIY;
		IF I=0 THEN HOL=HOL;
			ELSE HOL + 1;
		OUTPUT;
	END;
RUN;

DATA CALDAY;
SET CALDAY;
	MNTH = MONTH(HOL);
	WKDY = WEEKDAY(HOL);
RUN;

PROC SORT DATA=CALDAY;
	BY MNTH WKDY;
RUN;
DATA CALDAY;
SET CALDAY;
BY MNTH WKDY;
	IF FIRST.WKDY THEN C=1;
		ELSE C+1;
RUN;
/*SIMPLE CALCULATIONS FOR HOLIDAYS*/
DATA CALDAYS2;
SET CALDAY;
IF MNTH = 1 AND WKDY = 2 AND C=3 THEN OUTPUT;
ELSE IF MNTH = 2 AND WKDY = 2 AND C=3 THEN OUTPUT;
ELSE IF MNTH = 11 AND WKDY = 5 AND C=4 THEN OUTPUT;
ELSE DELETE;
RUN;
/*SLIGHTLY MORE COMPLEX CALCULATIONS FOR HOLIDAYS*/
PROC SQL;
CREATE TABLE CALDAYS3 AS 
	SELECT MAX(HOL) AS HOL FORMAT=MMDDYY10.
	FROM CALDAY 
	WHERE MNTH = 5
	AND WKDY = 2
UNION	
	SELECT MIN(HOL) AS HOL FORMAT=MMDDYY10.
	FROM CALDAY 
	WHERE MNTH = 9
	AND WKDY = 2
UNION 
	SELECT HOL+1 AS HOL FORMAT=MMDDYY10.
	FROM CALDAY 
	WHERE MNTH = 11 AND WKDY = 5 AND C=4
;
QUIT;
/*PUT ALL HOLIDAYS TOGETHER*/
DATA HOLIDAYS (KEEP=HOL);
SET DSPEC CALDAYS2 CALDAYS3 ;
	HWD = WEEKDAY(HOL);
RUN;
PROC SORT DATA=HOLIDAYS;BY HOL;RUN;
DATA HOLIDAYS;
SET HOLIDAYS;
FORMAT OBHOL MMDDYY10.;
	HWD = WEEKDAY(HOL);
	IF HWD = 1 THEN OBHOL = HOL+1;
	ELSE IF HWD = 7 THEN OBHOL = HOL-1;
	ELSE OBHOL = HOL;
	OWD = WEEKDAY(OBHOL);
RUN;
/************************************************
* CALCULATE 8 AND 10 BUSINESS DAYS FROM TODAY 
* ACCOUNT FOR HOLIDAYS AND WEEKENDS
*************************************************/
DATA BUSDAY;
FORMAT BD MMDDYY10.;
	BD = TODAY();
RUN;

DATA BUSDAY;
SET BUSDAY;
FORMAT BD MMDDYY10.;
RETAIN BD;
	I=1;
	DO UNTIL (I=30);
		IF I=30 THEN BD=BD;
			ELSE BD = BD - 1;
		OUTPUT;
		I=I+1;
	END;
RUN;

PROC SQL;
CREATE TABLE BUS_DAYS AS 
SELECT A.BD
	,CASE 
		WHEN WEEKDAY(BD) IN (2 3 4 5 6) AND B.OBHOL = . THEN 1
		ELSE 0
	 END AS D
FROM BUSDAY A
LEFT OUTER JOIN HOLIDAYS B
	ON A.BD = B.OBHOL
ORDER BY A.BD DESC
;
QUIT;

DATA BUS_DAYS;
SET BUS_DAYS;
	BD_NUM+D;
RUN;

PROC SORT DATA = BUS_DAYS; 
	BY DESCENDING BD BD_NUM ; 
RUN;

DATA _NULL_;
SET BUS_DAYS;
BY BD_NUM;
	IF FIRST.BD_NUM AND BD_NUM = 8 THEN DO;
		CALL SYMPUT('BUS_DAYS_AGO_8',BD);
	END;
	ELSE IF FIRST.BD_NUM AND BD_NUM = 10 THEN DO;
		CALL SYMPUT('BUS_DAYS_AGO_10',BD);
	END;
RUN;

DATA QCLVC;
SET QCLVC;
	DYS_ELPSD = TODAY()-DAC;
RUN;
DATA REPDS2;
SET QCLVC;
	WHERE BD_ATY_PRF = &PRV_DAY;
RUN;
PROC SORT DATA=REPDS2;
	BY DF_SPE_ACC_ID;
RUN;
PROC SQL;
CREATE TABLE REPDS3 AS
SELECT *
FROM QCLVC
WHERE DAC IS NULL
	AND BD_ATY_PRF >= &BUS_DAYS_AGO_8
	AND BD_ATY_PRF < &BUS_DAYS_AGO_10
;
QUIT;

DATA REPDS4;
SET QCLVC;
	WHERE DAC = &PRV_DAY 
	AND BD_ATY_PRF < &BUS_DAYS_AGO_10;
RUN;
/*****************************************
* CREATE REPORTS
******************************************/
PROC PRINTTO PRINT=REPORT2;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   "JOB = UTLWU23  	 REPORT = UTLWU23.LWU23R2";

PROC CONTENTS DATA=REPDS2 OUT=EMPTYSET NOPRINT;RUN;
TITLE 'Incoming LVCs Received Yesterday';

DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWU23  	 REPORT = UTLWU23.LWU23R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=REPDS2 WIDTH=UNIFORM WIDTH=MIN;
VAR DF_SPE_ACC_ID
	DOC_ID
	;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	DOC_ID = 'DOC ID'
	;
RUN;

PROC PRINTTO;
RUN;
DATA _NULL_;
SET REPDS3 ;
LENGTH DESCRIPTION $600.;
USER = BF_LST_USR_AY01;
ACT_DT = BD_ATY_PRF;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DOC_ID,
	PUT(BD_ATY_PRF,MMDDYY10.)
);
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
DATA _NULL_;
SET REPDS4 ;
LENGTH DESCRIPTION $600.;
USER = BF_LST_USR_AY01;
ACT_DT = BD_ATY_PRF;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DOC_ID,
	PUT(BD_ATY_PRF,MMDDYY10.),
	PUT(DAC,MMDDYY10.),
	DYS_ELPSD
);
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
