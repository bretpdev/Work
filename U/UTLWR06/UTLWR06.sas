*------------------------------------*
| UTLWR06 -LATE SCHOOL CANCELLATION  |
*------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR06.LWR06R2";
FILENAME REPORT3 "&RPTLIB/ULWR06.LWR06R3";
FILENAME REPORT4 "&RPTLIB/ULWR06.LWR06R4";
FILENAME REPORTZ "&RPTLIB/ULWR06.LWR06RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LTSCANA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT I.DF_SPE_ACC_ID
	,I.DM_PRS_LST
	,D.BF_SSN
	,D.LN_SEQ
	,D.LN_BR_DSB_SEQ
	,D.LA_DSB_ORG_CHK_EFT
	,A.LD_FAT_EFF
	,ABS(A.LA_FAT_CUR_PRI) AS LA_FAT_CUR_PRI
	,F.IF_DOE_SCL
	,F.IM_SCL_FUL
	,G.LF_LON_CUR_OWN
	,LR10.IM_LDR_FUL
	,H.LD_FAT_EFF AS LD_FAT_EFF_1021
	,ABS(H.LA_FAT_NSI) AS LA_FAT_NSI
	,ABS(H2.LA_FAT_NSI) AS LA_FAT_NSI_5001
	,I.WC_DW_LON_STA
	,D.LD_DSB
	,CASE
		WHEN H.LD_FAT_EFF IS NOT NULL THEN 'Y'
		ELSE 'N'
	 END AS TRX_1021_IND
FROM (
	SELECT Z.BF_SSN
		,Z.LN_SEQ
		,Z.LN_FAT_SEQ
		,Z.LD_FAT_EFF
		,Z.LA_FAT_CUR_PRI
	FROM OLWHRM1.LN90_FIN_ATY Z
	WHERE Z.LC_STA_LON90 = 'A'
	AND Z.LC_FAT_REV_REA = ''
	AND Z.PC_FAT_TYP = '10'
	AND Z.PC_FAT_SUB_TYP = '40'
	) A
INNER JOIN OLWHRM1.LN94_LON_PAY_FAT B
      ON A.BF_SSN = B.BF_SSN
      AND A.LN_SEQ = B.LN_SEQ
      AND A.LN_FAT_SEQ = B.LN_FAT_SEQ
INNER JOIN OLWHRM1.RM40_RMT_LON_TGT C
      ON B.LD_RMT_BCH_INI = C.LD_RMT_BCH_INI   
      AND B.LC_RMT_BCH_SRC_IPT = C.LC_RMT_BCH_SRC_IPT
      AND B.LN_RMT_BCH_SEQ = C.LN_RMT_BCH_SEQ
      AND B.LN_RMT_SEQ = C.LN_RMT_SEQ
      AND B.LN_RMT_ITM = C.LN_RMT_ITM
      AND B.LN_RMT_ITM_SEQ = C.LN_RMT_ITM_SEQ
      AND B.BF_SSN = C.BF_SSN
      AND B.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.LN15_DSB D
      ON C.BF_SSN = D.BF_SSN
      AND C.LN_BR_DSB_SEQ = D.LN_BR_DSB_SEQ
	  AND A.LD_FAT_EFF > LD_DSB + 120 DAYS
INNER JOIN OLWHRM1.AP03_MASTER_APL E
	ON D.AF_APL_ID = E.AF_APL_ID
INNER JOIN OLWHRM1.SC10_SCH_DMO F
	ON E.AF_DOE_SCL = F.IF_DOE_SCL
INNER JOIN OLWHRM1.LN10_LON G
	ON C.BF_SSN = G.BF_SSN
	AND C.LN_SEQ = G.LN_SEQ
INNER JOIN OLWHRM1.LR10_LDR_DMO LR10
	ON G.LF_LON_CUR_OWN = LR10.IF_DOE_LDR
INNER JOIN OLWHRM1.PD10_PRS_NME I
	ON C.BF_SSN = I.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,MAX(LD_FAT_EFF) AS LD_FAT_EFF
		,COALESCE(SUM(LA_FAT_NSI),0) +
		 	COALESCE(SUM(LA_FAT_CUR_PRI),0) AS LA_FAT_NSI
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	AND PC_FAT_TYP = '10'
	AND PC_FAT_SUB_TYP = '21'
	GROUP BY BF_SSN
		,LN_SEQ
	) H
	ON C.BF_SSN = H.BF_SSN
	AND C.LN_SEQ = H.LN_SEQ
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,COALESCE(SUM(LA_FAT_NSI),0) +
		 	COALESCE(SUM(LA_FAT_CUR_PRI),0) AS LA_FAT_NSI
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	AND PC_FAT_TYP = '50'
	AND PC_FAT_SUB_TYP = '01'
	GROUP BY BF_SSN
		,LN_SEQ
	) H2
	ON C.BF_SSN = H2.BF_SSN
	AND C.LN_SEQ = H2.LN_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU I
	ON G.BF_SSN = I.BF_SSN
	AND G.LN_SEQ = I.LN_SEQ
WHERE D.LC_DSB_TYP = '2'
	AND D.LC_STA_LON15 IN ('1','3')
	AND COALESCE(D.LA_DSB,0) = COALESCE(D.LA_DSB_CAN,0)
	AND COALESCE(G.LA_CUR_PRI,0) + COALESCE(G.LA_NSI_OTS,0) > 0
	AND G.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS','PLUSGB')
	AND D.LD_DSB > '12/31/2008'
	AND G.LC_STA_LON10 = 'R'
);
DISCONNECT FROM DB2;

CREATE TABLE LTSCANB AS 
SELECT DISTINCT 
	A.BF_SSN
	,A.LN_SEQ
	,A.LN_BR_DSB_SEQ
	,A.LA_DSB_ORG_CHK_EFT 
	,B.SUMX
FROM LTSCANA A
INNER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,LN_BR_DSB_SEQ
		,ABS(SUM(LA_FAT_CUR_PRI)) AS SUMX
	FROM LTSCANA 
	GROUP BY BF_SSN
		,LN_SEQ
		,LN_BR_DSB_SEQ
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND A.LN_BR_DSB_SEQ = B.LN_BR_DSB_SEQ 
WHERE ROUND(A.LA_DSB_ORG_CHK_EFT,.01) = ROUND(B.SUMX,.01)
;

CREATE TABLE LTSCAN AS
SELECT DISTINCT A.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.LN_SEQ
	,A.DM_PRS_LST
	,A.LD_FAT_EFF
	,A.LA_FAT_CUR_PRI
	,A.LD_FAT_EFF_1021
	,COALESCE(A.LA_FAT_NSI,0) AS LA_FAT_NSI
	,COALESCE(A.LA_FAT_NSI_5001,0) AS LA_FAT_NSI_5001
	,A.LF_LON_CUR_OWN
	,A.IM_LDR_FUL
	,A.IF_DOE_SCL
	,A.IM_SCL_FUL
	,A.WC_DW_LON_STA
	,A.LD_DSB
	,A.TRX_1021_IND
FROM LTSCANA A
INNER JOIN LTSCANB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
;
ENDRSUBMIT;
DATA LTSCAN;
	SET WORKLOCL.LTSCAN;
RUN;
PROC SORT DATA=LTSCAN;
	BY IF_DOE_SCL DF_SPE_ACC_ID TRX_1021_IND;
RUN;
DATA LTSCAN;
	SET LTSCAN;
	BY IF_DOE_SCL DF_SPE_ACC_ID TRX_1021_IND;
	IF FIRST.DF_SPE_ACC_ID THEN 
		F = 1;
	ELSE 
		F = 0;
	IF FIRST.TRX_1021_IND THEN 
		I=1;
	ELSE 
		I=0;
	J=1;
RUN;
PROC SORT DATA=LTSCAN;
BY IF_DOE_SCL DM_PRS_LST;
RUN;
PROC FORMAT;
VALUE $STAT '01'='IN GRACE' 
	'02'='IN SCHOOL'
	'03'='IN REPAYMENT' 
	'04'='IN DEFERMENT' 
	'05'='IN FORBEARANCE' 
	'06'='CURE' 
	'07'='CLAIM PENDING' 
	'08'='CLAIM SUBMITTED' 
	'09'='CLAIM CANCLED' 
	'10'='CLAIM REJECT' 
	'11'='CLAIM RETURNED' 
	'12'='CLAIM PAID' 
	'13'='PRE-CLAIM PENDING' 
	'14'='PRE-CLAIM SUBMITTED'
	'15'='PRE-CLAIM CANCELLED' 
	'16'='DEATH ALLEGED' 
	'17'='DEATH VERIFIED' 
	'18'='DISABILITY ALLEGED' 
	'19'='DISABILITY VERIFIED' 
	'20'='BANKRUPTCY ALLEGED' 
	'21'='BANKRUPTCY VERIFIED' 
	'22'='PAID IN FULL' 
	'23'='NOT FULLY ORIGINATED';
RUN;
DATA R2 R4;
	SET LTSCAN;
	IF TRX_1021_IND = 'Y' THEN 
		OUTPUT R2;
	ELSE 
		OUTPUT R4;
RUN;

PROC SORT DATA=R2;BY IF_DOE_SCL DM_PRS_LST;RUN;
PROC SORT DATA=R4;BY IF_DOE_SCL DM_PRS_LST;RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 DATE CENTER; 
TITLE 'LATE CANCELLATION LOANS WITH A 1021 TRANSACTION';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2';
PROC CONTENTS DATA=R2 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      /////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT /////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2";
	END;
RETURN;
RUN;
PROC REPORT DATA=R2 NOWD HEADSKIP SPLIT= '@' SPACING=1;
COLUMN IF_DOE_SCL IM_SCL_FUL DM_PRS_LST DF_SPE_ACC_ID LN_SEQ WC_DW_LON_STA LD_DSB LD_FAT_EFF LA_FAT_CUR_PRI 
	LF_LON_CUR_OWN LD_FAT_EFF_1021 LA_FAT_NSI LA_FAT_NSI_5001 I X ;
DEFINE IF_DOE_SCL / GROUP 'SCHOOL@CODE' WIDTH=8;
DEFINE IM_SCL_FUL / GROUP 'SCHOOL NAME' WIDTH=15 FLOW ;
DEFINE DM_PRS_LST / ORDER NOPRINT;
DEFINE DF_SPE_ACC_ID / DISPLAY 'ACCT #' WIDTH=10;
DEFINE LN_SEQ / DISPLAY 'LOAN SEQ' WIDTH=4;
DEFINE WC_DW_LON_STA / DISPLAY 'LOAN STATUS' FORMAT=$STAT. WIDTH=10 FLOW;
DEFINE LD_DSB / DISPLAY 'DISB DATE' WIDTH=10 FORMAT=MMDDYY10.;
DEFINE LD_FAT_EFF / DISPLAY 'PAYMENT EFFICTIVE DATE' WIDTH=10 FORMAT=MMDDYY10.;
DEFINE LA_FAT_CUR_PRI / DISPLAY 'PAYMENT AMT' FORMAT=10.2 ;
DEFINE LF_LON_CUR_OWN / DISPLAY 'CURRENT OWNER' WIDTH=8;
DEFINE LD_FAT_EFF_1021 / DISPLAY '1021 EFFECTIVE DATE' WIDTH=10 FORMAT=MMDDYY10. ;
DEFINE LA_FAT_NSI / DISPLAY '1021 PAYMENT AMT' FORMAT=DOLLAR10.2 ;
DEFINE LA_FAT_NSI_5001 / DISPLAY '5001 PAYMENT AMT' FORMAT=DOLLAR10.2 ;
DEFINE I / NOPRINT;
DEFINE X / COMPUTED NOPRINT;
COMPUTE X;
	X = I.SUM;
ENDCOMP;
COMPUTE AFTER IM_SCL_FUL;
	LINE @35 90*'-';
	LINE @35 'TOTAL BORROWERS FOR SCHOOL: ' @120 X COMMA5. I;
	LINE ' ';
ENDCOMP;
COMPUTE AFTER;
	LINE ' ';
	LINE @1 'TOTAL BORROWERS FOR ALL SCHOOL: ' @35 X COMMA5. I;
	LINE ' ';
	LINE ' ';
ENDCOMP;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 DATE CENTER; 
TITLE 'LATE CANCELLATION SUMMARY REPORT';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2';
PROC CONTENTS DATA=LTSCAN OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      /////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT /////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2";
	END;
RETURN;
RUN;
PROC REPORT DATA=LTSCAN NOWD HEADSKIP SPLIT= '@' SPACING=1;
COLUMN IF_DOE_SCL IM_SCL_FUL LF_LON_CUR_OWN IM_LDR_FUL LA_FAT_NSI LA_FAT_NSI_5001 I J X X2;
DEFINE IF_DOE_SCL / GROUP 'SCHOOL@CODE' WIDTH=8;
DEFINE IM_SCL_FUL / GROUP 'SCHOOL NAME' WIDTH=30 FLOW ;
DEFINE LF_LON_CUR_OWN / GROUP 'OWNER@CODE' WIDTH=8;
DEFINE IM_LDR_FUL / GROUP 'CURRENT OWNER' WIDTH=30 FLOW;
DEFINE I / NOPRINT;
DEFINE J / NOPRINT;
DEFINE LA_FAT_NSI / SUM 'TOTAL PENALTY';
DEFINE LA_FAT_NSI_5001 / SUM 'TOTAL WRITE OFF';
DEFINE X / COMPUTED 'TOTAL BORROWERS';
DEFINE X2 / COMPUTED 'TOTAL LOANS';

COMPUTE X;
	X = I.SUM;
ENDCOMP;
COMPUTE X2;
	X2 = J.SUM;
ENDCOMP;
BREAK AFTER IF_DOE_SCL / SUMMARIZE SUPPRESS SKIP OL;

COMPUTE AFTER;
	LINE ' ';
	LINE @35 'TOTAL BORROWERS: ' @60 X COMMA16.;
	LINE @35 'TOTAL LOANS: ' @60 X2 COMMA16. ;
	LINE @35 'TOTAL PENALTY AMOUNT: ' @60 LA_FAT_NSI.SUM DOLLAR16.2 ;
	LINE @35 'TOTAL WRITE OFF AMOUNT: ' @60 LA_FAT_NSI_5001.SUM DOLLAR16.2 ;
	LINE ' ';
	LINE ' ';
ENDCOMP;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 DATE CENTER; 
TITLE 'LATE CANCELLATION WITH OUTSTANDING BALANCE (W/O 1021)';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2';
PROC CONTENTS DATA=R4 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      /////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT /////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR06  	 REPORT = ULWR06.LWR06R2";
	END;
RETURN;
RUN;
PROC REPORT DATA=R4 NOWD HEADSKIP SPLIT= '@' SPACING=1;
COLUMN IF_DOE_SCL IM_SCL_FUL DM_PRS_LST DF_SPE_ACC_ID LN_SEQ WC_DW_LON_STA LD_DSB LD_FAT_EFF LA_FAT_CUR_PRI 
	LF_LON_CUR_OWN LA_FAT_NSI_5001 I X ;
DEFINE IF_DOE_SCL / GROUP 'SCHOOL@CODE' WIDTH=8;
DEFINE IM_SCL_FUL / GROUP 'SCHOOL NAME' WIDTH=15 FLOW ;
DEFINE DM_PRS_LST / DISPLAY 'LAST NAME' WIDTH=15 ;
DEFINE DF_SPE_ACC_ID / DISPLAY 'ACCT #' WIDTH=10;
DEFINE LN_SEQ / DISPLAY 'LOAN SEQ' WIDTH=4;
DEFINE WC_DW_LON_STA / DISPLAY 'LOAN STATUS' FORMAT=$STAT. WIDTH=10 FLOW;
DEFINE LD_DSB / DISPLAY 'DISB DATE' WIDTH=10 FORMAT=MMDDYY10.;
DEFINE LD_FAT_EFF / DISPLAY 'PAYMENT EFFICTIVE DATE' WIDTH=10 FORMAT=MMDDYY10.;
DEFINE LA_FAT_CUR_PRI / DISPLAY 'PAYMENT AMT' FORMAT=10.2 ;
DEFINE LF_LON_CUR_OWN / DISPLAY 'CURRENT OWNER' WIDTH=8;
DEFINE LA_FAT_NSI_5001 / DISPLAY '5001 PAYMENT AMT' FORMAT=DOLLAR10.2 ;
DEFINE I / NOPRINT;
DEFINE X / COMPUTED NOPRINT;
COMPUTE X;
	X = I.SUM;
ENDCOMP;
COMPUTE AFTER IM_SCL_FUL;
	LINE @35 90*'-';
	LINE @35 'TOTAL BORROWERS FOR SCHOOL: ' @120 X COMMA5. I;
	LINE ' ';
ENDCOMP;
COMPUTE AFTER;
	LINE ' ';
	LINE @1 'TOTAL BORROWERS FOR ALL SCHOOL: ' @35 X COMMA5. I;
	LINE ' ';
	LINE ' ';
ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
