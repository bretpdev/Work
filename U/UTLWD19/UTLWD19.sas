/*UTLWD19 - BORROWER PAYMENTS RECEIVED FOR SKIP BORROWERS*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD19.LWD19R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BRSKP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	INTEGER(A.BF_SSN) AS BF_SSN
	,RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_LST) AS NAME
	,PD03.DX_STR_ADR_1
	,PD03.DX_STR_ADR_2
	,PD03.DM_CT
	,PD03.DC_DOM_ST
	,PD03.DF_ZIP
	,PD03.DI_VLD_ADR
	,PD03.DN_PHN
	,PD03.DI_PHN_VLD
	,A.LD_DFL_LST_CNC
	,A.LD_DFL_LST_ATT_CNC
	,A.LC_AUX_STA
	,D.DF_SPE_ACC_ID
FROM OLWHRM1.DC01_LON_CLM_INF A 
INNER JOIN OLWHRM1.DC11_LON_FAT B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN PD03
	ON A.BF_SSN = PD03.DF_PRS_ID
WHERE B.LC_TRX_TYP = 'BR'
AND A.LC_STA_DC10 = '03'
AND A.LD_CLM_ASN_DOE IS NULL
AND DAYS(B.LD_TRX_ADJ) = DAYS(CURRENT DATE) - 1
AND PD03.DC_ADR = 'L'
AND (PD03.DI_VLD_ADR = 'N' OR PD03.DI_PHN_VLD = 'N')

);
DISCONNECT FROM DB2;

ENDRSUBMIT;
DATA BRSKP; 
SET WORKLOCL.BRSKP; 
RUN;

PROC SORT DATA=BRSKP;
BY BF_SSN;
RUN;

DATA BRSKP;
SET BRSKP;
IF DX_STR_ADR_2 = ' ' 
	THEN ADDRESS = TRIM(DX_STR_ADR_1)||'/'||TRIM(DM_CT)
		||', '||TRIM(DC_DOM_ST)||' '||TRIM(DF_ZIP);
	ELSE ADDRESS = TRIM(DX_STR_ADR_1)||'/'||TRIM(DX_STR_ADR_2)||'/'||
		TRIM(DM_CT)||', '||TRIM(DC_DOM_ST)||' '||TRIM(DF_ZIP);
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
TITLE 'BORROWER PAYMENT RECEIVED FOR SKIP BORROWER';
TITLE2 "RUN DATE:  &RUNDATE";

PROC CONTENTS DATA=BRSKP OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132 *'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT ////////////////
		@46 "JOB = UTLWD19     REPORT = ULWD19.LWD19R2";
	END;
RETURN;
run;

PROC REPORT DATA=BRSKP NOWD SPACING=1 HEADSKIP SPLIT='/';
FOOTNOTE  'JOB = UTLWD19     REPORT = ULWD19.LWD19R2';
COLUMN DF_SPE_ACC_ID NAME ADDRESS DI_VLD_ADR DN_PHN DI_PHN_VLD LD_DFL_LST_CNC
	LD_DFL_LST_ATT_CNC LC_AUX_STA;
DEFINE DF_SPE_ACC_ID / "ACCT #" WIDTH=10;
DEFINE NAME / "BORROWER NAME" WIDTH=24;
DEFINE ADDRESS / "LEGAL ADDRESS" FORMAT=$100. WIDTH=30 FLOW;
DEFINE DI_VLD_ADR / "ADDR/VALID" WIDTH=5;
DEFINE DN_PHN / "HOME PHONE";
DEFINE DI_PHN_VLD / "PHONE/VALID" WIDTH=5;
DEFINE LD_DFL_LST_CNC / "LAST/CONTACT" FORMAT=MMDDYY10.;
DEFINE LD_DFL_LST_ATT_CNC / "LAST/CONTACT/ATTEMPT" FORMAT=MMDDYY10.;
DEFINE LC_AUX_STA / "AUX/STATUS" WIDTH=6;
RUN;

PROC PRINTTO;
RUN;
