*----------------------------*
|   UTLWO39 BB HUMAN ERROR 	 |
*----------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO39.LWO39R2";
FILENAME REPORTZ "&RPTLIB/ULWO39.LWO39RZ";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BASE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT LN10.BF_SSN
	,LN10.LN_SEQ
FROM OLWHRM1.LN10_LON LN10
/*REWRITE*/
INNER JOIN OLWHRM1.LN54_LON_BBS LN54
	ON LN10.BF_SSN = LN54.BF_SSN
	AND LN10.LN_SEQ = LN54.LN_SEQ
	AND LN54.LC_BBS_ELG = 'Y'
/**/
WHERE LN10.LD_LON_1_DSB < '05/01/2006'
AND LN10.LF_LON_CUR_OWN = '828476'
AND LN10.LA_CUR_PRI > 0
AND LN10.LC_STA_LON10 = 'R'
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ
	WHERE Y.BF_SSN = LN10.BF_SSN
	AND Y.LN_SEQ = LN10.LN_SEQ
	AND X.PF_REQ_ACT = 'U48TP'
	AND X.LC_STA_ACTY10 = 'A'
	)
FOR READ ONLY WITH UR
);

CREATE TABLE BILL_A AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT BL10.BF_SSN
	,LN10B.LN_SEQ
	,BL10.LD_BIL_CRT	
	,BL10.LN_SEQ_BIL_WI_DTE
	,BL10.LD_BIL_DU
	,'A' AS SCN_STA
FROM OLWHRM1.LN80_LON_BIL_CRF LN80
INNER JOIN OLWHRM1.BL10_BR_BIL BL10
	ON BL10.BF_SSN = LN80.BF_SSN
	AND BL10.LD_BIL_CRT = LN80.LD_BIL_CRT
	AND BL10.LN_SEQ_BIL_WI_DTE = LN80.LN_SEQ_BIL_WI_DTE
INNER JOIN OLWHRM1.LN10_LON LN10B
 	ON LN80.BF_SSN = LN10B.BF_SSN
	AND LN80.LN_SEQ = LN10B.LN_SEQ
WHERE BL10.LC_BIL_TYP = 'P'
AND BL10.LC_STA_BIL10 = 'A'
AND DAYS(LN80.LD_BIL_STS_RIR_TOL) > DAYS(LN80.LD_BIL_DU_LON) + 15
AND LN10B.LA_CUR_PRI > 0
AND LN80.LI_BIL_DLQ_OVR_RIR <> 'Y'

FOR READ ONLY WITH UR
);

CREATE TABLE BILL_I AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT BL10.BF_SSN
	,LN10B.LN_SEQ
	,BL10.LD_BIL_CRT	
	,BL10.LN_SEQ_BIL_WI_DTE
	,BL10.LD_BIL_DU
	,'I' AS SCN_STA
FROM OLWHRM1.LN80_LON_BIL_CRF LN80
INNER JOIN OLWHRM1.BL10_BR_BIL BL10
	ON BL10.BF_SSN = LN80.BF_SSN
	AND BL10.LD_BIL_CRT = LN80.LD_BIL_CRT
	AND BL10.LN_SEQ_BIL_WI_DTE = LN80.LN_SEQ_BIL_WI_DTE
INNER JOIN OLWHRM1.LN10_LON LN10B
 	ON LN80.BF_SSN = LN10B.BF_SSN
	AND LN80.LN_SEQ = LN10B.LN_SEQ
INNER JOIN OLWHRM1.LN50_BR_DFR_APV LN50
	ON LN10B.BF_SSN = LN50.BF_SSN
	AND LN10B.LN_SEQ = LN50.LN_SEQ
INNER JOIN OLWHRM1.DF10_BR_DFR_REQ DF10
	ON DF10.BF_SSN = LN50.BF_SSN
	AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM
WHERE BL10.LC_BIL_TYP = 'P'
AND BL10.LC_STA_BIL10 = 'I'
AND DAYS(LN50.LD_DFR_APL) > DAYS(LN80.LD_BIL_DU_LON) + 15
AND LN10B.LA_CUR_PRI > 0
AND LN80.LI_BIL_DLQ_OVR_RIR <> 'Y'
AND LN50.LC_STA_LON50 = 'A'
AND DF10.LC_DFR_TYP NOT IN ('15','18')
AND LN50.LD_DFR_APL >= '01/20/2006'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO39.LWO39RZ);*/
/*QUIT;*/
DATA BILL;
SET BILL_A BILL_I;
RUN;

PROC SQL;
CREATE TABLE BBHER AS 
SELECT DISTINCT B.BF_SSN
	,B.LN_SEQ
	,B.LD_BIL_CRT	
	,B.LN_SEQ_BIL_WI_DTE
	,B.LD_BIL_DU
	,B.SCN_STA
FROM BASE A
INNER JOIN BILL B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
;
QUIT;

ENDRSUBMIT;

DATA BBHER;SET WORKLOCL.BBHER;RUN;

PROC SORT DATA=BBHER;
BY BF_SSN LN_SEQ LD_BIL_CRT	LN_SEQ_BIL_WI_DTE LD_BIL_DU;
RUN;

PROC SQL;
CREATE TABLE BBHER2 AS
SELECT A.*
FROM BBHER A
INNER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,COUNT(*) AS COUNT
	FROM BBHER
	GROUP BY BF_SSN
		,LN_SEQ
	HAVING COUNT(*) > 3
	 ) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
ORDER BY BF_SSN
	,LN_SEQ
	,LD_BIL_DU
;
QUIT;

PROC CONTENTS DATA=BBHER2 OUT=OBS_SET NOPRINT;RUN;

DATA _NULL_;
SET OBS_SET;
CALL SYMPUT('OBS_IN_DS',NOBS);
RUN;

/*--------------------------------------------------------*
|MACRO DEFINITION AND FINAL STEP PROCESSING               |
*---------------------------------------------------------*
| CHANGE THE DATE TO A CHARACTER FORMAT FOR CONCATINATION |
*--------------------------------------------------------*/
%MACRO DT_CONCAT(DS,CRT_DT);
DATA &DS (DROP=MONTH DAY YEAR);
SET &DS;
IF LENGTH(LEFT(MONTH(&CRT_DT))) EQ 1 THEN MONTH = '0'||TRIM(LEFT(MONTH(&CRT_DT)));
ELSE MONTH = TRIM(LEFT(MONTH(&CRT_DT)));
IF LENGTH(LEFT(DAY(&CRT_DT))) EQ 1 THEN DAY = '0'||TRIM(LEFT(DAY(&CRT_DT)));
ELSE DAY = TRIM(LEFT(DAY(&CRT_DT)));
YEAR = TRIM(LEFT(YEAR(&CRT_DT)));
CHAR_&CRT_DT = LEFT(TRIM(MONTH)||TRIM(DAY)||TRIM(YEAR));
RUN;
%MEND DT_CONCAT;
/*----------------------------------------------------------*
|DETERMINE IF ANY OBS WERE RETURNED AND PROCESS ACCORDINGLY |
*----------------------------------------------------------*/
%MACRO FPROC;
%PUT &OBS_IN_DS; 
%IF &OBS_IN_DS NE 0 %THEN %DO;
	%DT_CONCAT(BBHER2,LD_BIL_DU);

	PROC TRANSPOSE DATA=BBHER2 OUT=BBHER2 PREFIX=PST_DUE;
		VAR CHAR_LD_BIL_DU;
		BY BF_SSN LN_SEQ;
	RUN;
/*------------------------------------------------------*
| CONCAT THE CHARACTER DATES INTO ONE VARIABLE FOR FILE |
*------------------------------------------------------*/
	DATA BBHER2;
		SET BBHER2 (KEEP=BF_SSN LN_SEQ PST_DUE1-PST_DUE3);
		DATE_LIST = LEFT(TRIM(PST_DUE1))||' '||LEFT(TRIM(PST_DUE2))||' '||LEFT(TRIM(PST_DUE3));
	RUN;

	DATA _NULL_;
		SET BBHER2;
		FILE REPORT2 DLM=',' DROPOVER LRECL=32767;
		FORMAT BF_SSN $9. 
			LN_SEQ 6. 
			DATE_LIST $122.;
		DO;
			PUT BF_SSN $ @;
			PUT LN_SEQ @ ;
			PUT DATE_LIST ;
		END;
	RUN;
	%END;
%ELSE %DO;
	DATA BBHER2;
		SET BBHER2; 
		DATE_LIST = '';
	RUN;

	DATA _NULL_;
		SET BBHER2;
		FILE REPORT2 DLM=',' DROPOVER LRECL=32767;
		FORMAT BF_SSN $9. 
			LN_SEQ 6. 
			DATE_LIST $122.;
		DO;
			PUT BF_SSN $ @;
			PUT LN_SEQ @ ;
			PUT DATE_LIST ;
		END;
	RUN;
	%END;
%MEND FPROC;
%FPROC;
