/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWM45.LWM45RZ";
FILENAME REPORT2 "&RPTLIB/ULWM45.LWM45R2";
FILENAME REPORT3 "&RPTLIB/ULWM45.LWM45R3";
FILENAME REPORT4 "&RPTLIB/ULWM45.LWM45R4";
FILENAME REPORT5 "&RPTLIB/ULWM45.LWM45R5";
FILENAME REPORT6 "&RPTLIB/ULWM45.LWM45R6";
FILENAME REPORT7 "&RPTLIB/ULWM45.LWM45R7";

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT DUSTER;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
data _null_;
call symput ('minus226',"'" || put(today()-226,mmddyy10.) || "'");
run;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.DF_SPE_ACC_ID
	,B.BF_SSN
	,B.LD_DCO
	,B.LA_CLM_PRI
	,B.LA_CLM_INT
	,B.LD_PCL_SUP_LST_CNC
	,B.LD_PCL_SUP_LST_ATT
FROM OLWHRM1.PD01_PDM_INF A
inner JOIN OLWHRM1.DC01_LON_CLM_INF B
	ON A.DF_PRS_ID = B.BF_SSN
WHERE B.LC_PCL_REA IN ('DF','RS','DB','DQ')
	AND B.LC_STA_DC10 = '01'
	AND B.LD_DCO > &minus226
	and b.if_ops_ldr = '834529'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
QUIT;
PROC SORT DATA=DEMO; BY DF_SPE_ACC_ID LD_DCO; RUN;
DATA DEMO(DROP=LD_PCL_SUP_LST_ATT LD_PCL_SUP_LST_CNC);
SET DEMO;
BY DF_SPE_ACC_ID;
IF FIRST.DF_SPE_ACC_ID;
DLC = MAX(LD_PCL_SUP_LST_CNC,LD_PCL_SUP_LST_ATT);
RUN;
ENDRSUBMIT;
DATA DEMO;
	SET DUSTER.DEMO;
RUN;
PROC SORT DATA=DEMO;
	BY LD_DCO;
RUN;
ods listing;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

/*FOR PORTRAIT REPORTS;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=96;
TITLE 'DEFAULT AVERSION CONDUIT ACCOUNTS';

PROC CONTENTS DATA=DEMO OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 96*'-';
	PUT      //////
		@34 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@40 '-- END OF REPORT --';
	PUT //////////////
		@29 "JOB = UTLWM45  	 REPORT = ULWM45.LWM45R2";
	END;
RETURN;
RUN;

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LA_CLM_PRI LA_CLM_INT DOLLAR15.2 LD_DCO MMDDYY10.;
VAR DF_SPE_ACC_ID LD_DCO LA_CLM_PRI LA_CLM_INT;
LABEL DF_SPE_ACC_ID = 'ACCOUNT NUMBER'
	LD_DCO = 'DATE CONDITION OCCURRED'
	LA_CLM_PRI = 'PRINCIPAL AMOUNT OF CLAIM'
	LA_CLM_INT = 'INTEREST AMOUNT OF CLAIM';
RUN;

PROC PRINTTO;
RUN;
PROC SORT DATA=DEMO; BY DLC BF_SSN; RUN;

%MACRO QUE(QUEUE,R,LOW,HIGH);
DATA _NULL_;
SET DEMO ;
WHERE TODAY() - &LOW <= LD_DCO <= TODAY() - &HIGH;
QNAME = &QUEUE;
ARRAY MISS{5} $ INST_ID INST_TYPE DUE_DATE DUE_TIME TEXT('' '' '' '' '');
FILE REPORT&r DELIMITER=',' DSD DROPOVER LRECL=32767;
   PUT BF_SSN @;
   PUT QNAME @;
DO I = 1 TO 4;
   PUT MISS(I) @;
END;
   PUT MISS(5) $ ;

RUN;
%MEND;
%que("PRECON60",3,90,60);
%que("PRECON90",4,120,91);
%que("PRECO120",5,150,121);
%que("PRECO150",6,180,151);
%que("PRECO180",7,225,181);
