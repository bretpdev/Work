/*	This report prints the total number of outstanding default
    prevention accounts and total dollar amount.*/

LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWDPD.LWDPDR2";

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132 SYMBOLGEN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	
		A.bf_ssn							as SSN,
		A.af_apl_id||A.af_apl_id_sfx		as CLUID,
		A.ld_pcl_rcv						as PCLRCV,
		A.la_clm_pri + A.la_clm_int			as TOTDFL,
		CASE
			WHEN A.lc_clm_lon_typ IN ('SF','SU') 
				THEN 'S'
				ELSE A.lc_clm_lon_typ END 	as LONTYP,
		CASE
			WHEN A.ld_org_dco is not null 
				THEN A.ld_org_dco
				ELSE A.ld_dco END			as DCODT,
		A.lf_cur_lon_ser_agy				as CURSER
FROM  OLWHRM1.DC01_LON_CLM_INF A
WHERE A.LC_PCL_REA IN ('DF','DQ')
AND A.lc_sta_dc10 = '01'
ORDER BY A.BF_SSN, A.af_apl_id||A.af_apl_id_sfx
);
DISCONNECT FROM DB2;

DATA DEMO;
SET DEMO;
DELDAYS = TODAY() - DCODT;
IF DELDAYS >= 1 AND DELDAYS <= 30 THEN INTERVAL = '001-030';
ELSE IF DELDAYS >= 31 AND DELDAYS <= 60 THEN INTERVAL = '031-060';
ELSE IF DELDAYS >= 61 AND DELDAYS <= 90 THEN INTERVAL = '061-090';
ELSE IF DELDAYS >= 91 AND DELDAYS <= 120 THEN INTERVAL = '091-120';
ELSE IF DELDAYS >= 121 AND DELDAYS <= 150 THEN INTERVAL = '121-150';
ELSE IF DELDAYS >= 151 AND DELDAYS <= 180 THEN INTERVAL = '151-180';
ELSE IF DELDAYS >= 181 AND DELDAYS <= 210 THEN INTERVAL = '181-210';
ELSE IF DELDAYS >= 211 AND DELDAYS <= 240 THEN INTERVAL = '211-240';
ELSE IF DELDAYS >= 241 AND DELDAYS <= 270 THEN INTERVAL = '241-270';
ELSE IF DELDAYS >= 271 AND DELDAYS <= 300 THEN INTERVAL = '271-300';
ELSE IF DELDAYS >= 301 AND DELDAYS <= 330 THEN INTERVAL = '301-330';
ELSE IF DELDAYS >= 331 AND DELDAYS <= 360 THEN INTERVAL = '331-360';
ELSE IF DELDAYS > 360 THEN INTERVAL = '360+';
ELSE INTERVAL = '0';
RUN;

PROC SQL;
CREATE TABLE DEMOLON AS
SELECT 	INTERVAL, 
		COUNT(INTERVAL) AS LONCNT,
		SUM(TOTDFL)		AS TOTDFL
FROM DEMO
GROUP BY INTERVAL;

CREATE TABLE DEMOPCA AS
SELECT 	INTERVAL,
		COUNT(INTERVAL) AS PCACNT
FROM 
	(SELECT DISTINCT  	INTERVAL, 
						SSN, 
						LONTYP, 
						PCLRCV, 
						DCODT, 
						CURSER
	FROM DEMO A)
GROUP BY INTERVAL;

CREATE TABLE DEMOSUM AS
SELECT A.*,B.PCACNT /*"*"*/
FROM DEMOLON A JOIN DEMOPCA B
ON A.INTERVAL = B.INTERVAL;
QUIT;

endrsubmit  ;

DATA DEMOSUM; 
SET WORKLOCL.DEMOSUM; 
RUN;


%macro fdate(fmt);
   %global fdate;
   data _null_;
      call symput("fdate",left(put("&sysdate"d,&fmt)));
   run;
%mend fdate;
%fdate(worddate.);

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/

OPTIONS CENTER NODATE NONUMBER LS=120 SYMBOLGEN;
PROC PRINT DATA = DEMOSUM NOOBS LABEL;
VAR INTERVAL LONCNT PCACNT TOTDFL;
SUM LONCNT PCACNT TOTDFL ;
LABEL INTERVAL= 'Number of Days Delinquent'
LONCNT= 'Number of Loans' PCACNT= 'Number of PCAs'
TOTDFL= 'Total Dollar Amount';
FORMAT LONCNT COMMA8. PCACNT COMMA8. TOTDFL DOLLAR15.2;
TITLE 'Outstanding Default Prevention Accounts';
TITLE2 "Run date:  &FDATE";
FOOTNOTE  'JOB = UTLWDPD     REPORT = ULWDPD.LWDPDR2 (53)';
RUN;
