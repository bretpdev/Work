/*
PIF PROM NOTE AND FILE PULL LISTS

This program select loans closed by other than rehabilitation or repurchase within
the preceeding month and generates a promissory note and a file pull list.
The lists are used by the file room to pull and archive promissory notes and files
for closed loans.

*/


/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWPF1.LWPF1R2";
FILENAME REPORT3 "&RPTLIB/ULWPF1.LWPF1R3";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
OPTIONS MPRINT SYMBOLGEN;
DATA _NULL_;
     BEGDATE = PUT(INTNX('MONTH',TODAY(),-1), YYMMDD10.);	/*RESOLVES TO 1ST OF PRIOR MONTH*/
     ENDDATE = PUT(INTNX('MONTH',TODAY(),0)-1 , YYMMDD10.);	/*RESOLVES TO END OF PRIOR MONTH*/
     CALL SYMPUT('BEGIN',"'"||BEGDATE||"'");	            /*CREATES MACRO VARIABLE WITH IN FORMAT  'YYYY/MM/DD'*/
     CALL SYMPUT('END',"'"||ENDDATE||"'");              	/* WILL BE USED AS REPLACEMENTS IN CODE*/
RUN;

%MACRO  MYSQL1 (DATECOL);			                        /* CREATE MACRO MYSQL1 WITH A MACRO VARIABLE DATECOL*/

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PNOTE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.af_apl_id,
		A.af_apl_id_sfx,
		A.bf_ssn,
		A.LF_CLM_ID,
		C.DM_PRS_LST,
		C.DM_PRS_1,
		B.AX_SCL_DSB_1_IAD,
		B.AA_GTE_LON_AMT
FROM	OLWHRM1.DC01_LON_CLM_INF A INNER JOIN
		OLWHRM1.GA10_LON_APP B ON
	 		A.AF_APL_ID = b.AF_APL_ID AND
			A.AF_APL_ID_SFX = B.AF_APL_ID_SFX AND
			A.LC_STA_DC10 ='04' INNER JOIN
		OLWHRM1.PD01_PDM_INF C ON
			A.BF_SSN = C.DF_PRS_ID
WHERE	B.AC_PRC_STA = 'A' AND
		A.LC_STA_DC10 ='04' AND
		A.LC_AUX_STA NOT IN ('01', '10') AND
		&DATECOL BETWEEN &BEGIN AND &END     /* MACRO VARIABLES WILL BE RESOLVED AT EXECUTION  */

);
DISCONNECT FROM DB2;
QUIT;
%MEND;                                       /* END MACRO CREATION FOR MYSQL1*/
%MYSQL1 (A.LD_STA_UPD_DC10);                 /* EXECUTE STATEMENTS IN MACRO MYSQL1 WITH*/

ENDRSUBMIT;
DATA WORKLOCL.PNOTE;
	SET WORKLOCL.PNOTE;
	CLID = AF_APL_ID||AF_APL_ID_SFX;
	NAME = TRIM(DM_PRS_LST)||', '||TRIM(DM_PRS_1);
	SSN = BF_SSN;
	DISB_DT = SUBSTR(AX_SCL_DSB_1_IAD,5,2)||'/'||SUBSTR(AX_SCL_DSB_1_IAD,7,2)||'/'||SUBSTR(AX_SCL_DSB_1_IAD,1,4);
RUN;
DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;
PROC SORT DATA=WORKLOCL.PNOTE;
BY SSN NAME;
RUN;
/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS PAGENO=1 LS=80;
PROC PRINT NOOBS SPLIT='/' DATA=WORKLOCL.PNOTE;
BY SSN NAME;
VAR CLID
	DISB_DT
	AA_GTE_LON_AMT
	;
LABEL	CLID = 'UNIQUE ID'
		DISB_DT = 'DISBURSEMENT DATE'
		AA_GTE_LON_AMT = 'LOAN AMOUNT'	
		;

TITLE1  'PROMISSORY NOTE PULL LIST';
TITLE2	"LOANS SATISFIED DURING THE MONTH OF &EFFDATE";
FOOTNOTE  'JOB = UTLWPF1     REPORT = ULWPF1.LWPF1R2';
RUN;
PROC SORT DATA=WORKLOCL.PNOTE;
BY NAME SSN;
RUN;
/*PROC PRINTTO PRINT=REPORT3;
RUN;*/
OPTIONS PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=WORKLOCL.PNOTE;
BY NAME SSN;
VAR CLID
	DISB_DT
	AA_GTE_LON_AMT
	;
LABEL	CLID = 'UNIQUE ID'
		DISB_DT = 'DISBURSEMENT DATE'
		AA_GTE_LON_AMT = 'LOAN AMOUNT'	
		;

TITLE1  'FILE PULL LIST';
TITLE2	"LOANS SATISFIED DURING THE MONTH OF &EFFDATE";
FOOTNOTE  'JOB = UTLWPF1     REPORT = ULWPF1.LWPF1R3';
RUN;
