*------------------------------------------------------------------*
| UTLWO87 QC - DELINQUENT BORROWERS ON REDUCED PAYMENT FORBEARANCE |
*------------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO87.LWO87R2";
FILENAME REPORTZ "&RPTLIB/ULWO87.LWO87RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT; 
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DBRPF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_SPE_ACC_ID
	,C.LD_FOR_BEG
	,C.LD_FOR_END
	,G.LD_BIL_CRT 
	,G.LD_BIL_DU
	,i.payment
	,L.LA_BIL_DU_PRT
FROM OLWHRM1.PD10_PRS_NME A
INNER JOIN OLWHRM1.LN10_LON B
	ON A.DF_PRS_ID = B.BF_SSN
INNER JOIN OLWHRM1.LN60_BR_FOR_APV C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST M
	ON B.BF_SSN = M.BF_SSN
	AND B.LN_SEQ = M.LN_SEQ
INNER JOIN OLWHRM1.FB10_BR_FOR_REQ D
	ON C.BF_SSN = D.BF_SSN
	AND C.LF_FOR_CTL_NUM = D.LF_FOR_CTL_NUM
INNER JOIN OLWHRM1.BL10_BR_BIL G
	ON B.BF_SSN = G.BF_SSN
INNER JOIN (SELECT BF_SSN
			,LD_BIL_CRT
			,SUM(COALESCE(LA_BIL_CUR_DU,0)) AS LA_BIL_DU_PRT
		FROM OLWHRM1.LN80_LON_BIL_CRF
		WHERE LC_STA_LON80 = 'A'
		GROUP BY BF_SSN, LD_BIL_CRT
) L
	ON G.BF_SSN = L.BF_SSN
	AND G.LD_BIL_CRT = L.LD_BIL_CRT 
LEFT OUTER JOIN (select bf_ssn
				,i.ld_fat_eff
				,SUM(COALESCE(I.LA_FAT_NSI,0) + COALESCE(I.LA_FAT_CUR_PRI,0)) AS PAYMENT
				from OLWHRM1.LN90_FIN_ATY I
				where i.pc_fat_typ = '10'
					and i.pc_fat_sub_typ = '10'
				group by i.bf_ssn, i.ld_fat_eff
) I
	ON G.BF_SSN = I.BF_SSN
	AND DAYS(I.LD_FAT_EFF) BETWEEN DAYS(G.LD_BIL_DU)-15 AND DAYS(G.LD_BIL_DU)+25
WHERE B.LC_STA_LON10 = 'R'
	AND B.LA_CUR_PRI > 0
	AND C.LC_STA_LON60 = 'A'
	AND C.LC_FOR_RSP <> '003'
	AND C.LD_FOR_END > CURRENT DATE
	AND G.LD_BIL_CRT >= C.LD_FOR_BEG
	AND G.LC_BIL_TYP IN ('P','C')
	AND D.LC_FOR_TYP = '31'
	AND G.LD_BIL_CRT >= C.LD_FOR_BEG
	AND M.LC_DLQ_TYP = 'I'
	AND G.LC_IND_BIL_SNT = 'T'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO87.LWO87RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA DBRPF ;
	SET WORKLOCL.DBRPF;
RUN; 
PROC SORT DATA=DBRPF;
BY DF_SPE_ACC_ID LD_BIL_DU;
RUN;

DATA DBRPF(DROP=A B LD_BIL_CRT LD_BIL_DU PAYMENT LA_BIL_DU_PRT BALANCE DELINQ);
SET DBRPF;
BY DF_SPE_ACC_ID LD_BIL_DU;
RETAIN A B;
FORMAT DELINQ MMDDYY10.;
IF FIRST.DF_SPE_ACC_ID THEN DO;
	A = 0;
	B = 0;
END;
IF A = 0 THEN DO;
	IF FIRST.LD_BIL_DU THEN B = COALESCE(PAYMENT,0);
	IF FIRST.LD_BIL_DU = 0 THEN B = B + COALESCE(PAYMENT,0);
	IF LAST.LD_BIL_DU THEN BALANCE = COALESCE(B,0) + COALESCE(LA_BIL_DU_PRT,0);
/*The business unit requested that we give a $5 tollerance*/
	IF BALANCE > 5 THEN DO;
		DELINQ = LD_BIL_DU + 1;
		DAYS_DELQ = TODAY() - DELINQ;
		IF DAYS_DELQ >=20 THEN DO;
			OUTPUT;
			A=1;
		END;
	END;	
END;
RUN;

PROC SORT DATA=DBRPF ;
	BY DAYS_DELQ DF_SPE_ACC_ID;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGNO=1 CENTER DATE;
TITLE 'DELINQUENT BORROWERS ON A REDUCED PAYMENT FORBEARANCE';
FOOTNOTE   'JOB = UTLWO87  	 REPORT = ULWO87.LWO87R2';
PROC CONTENTS DATA=DBRPF OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWO87  	 REPORT = ULWO87.LWO87R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=DBRPF WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_FOR_BEG LD_FOR_END MMDDYY10.;
VAR DF_SPE_ACC_ID LD_FOR_BEG LD_FOR_END DAYS_DELQ;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LD_FOR_BEG = 'FORBEARANCE/BEGIN DATE' 
	LD_FOR_END = 'FORBEARANCE/END DATE' 
	DAYS_DELQ = '# DAYS/DELINQUENT' 
	;
RUN;
PROC PRINTTO;
RUN;
