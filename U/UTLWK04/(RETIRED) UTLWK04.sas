*-----------------------------------------------*
|UTLWK04 SKIP WITH OTHER VALID PHONE OR ADDRESS |
*-----------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET PROGREVW = /sas/whse/progrevw;*/
%LET RPTLIB = T:\SAS;
%LET PROGREVW = Q:\Support Services\Test Files\SAS\PROGREVW;

FILENAME REPORT2 "&RPTLIB/ULWK04.LWK04R2";
FILENAME REPORTZ "&RPTLIB/ULWK04.LWK04RZ";

LIBNAME PROGREVW "&PROGREVW";

PROC SQL NOPRINT;
	SELECT 
		"'"||STRIP(LENDER_ID)||"'" INTO :EXCL_LIST SEPARATED BY ","
	FROM 
		PROGREVW.LENDER_GROUP_LENDERS
	WHERE 
		LENDER_GROUP_ID = 1
	;
QUIT;
%PUT &EXCL_LIST;

%SYSLPUT EXCL_LIST = &EXCL_LIST;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*---------------- ONELINK ----------------*/

	/*	get the most recently created DC01 record for loans*/
	CREATE TABLE MAX_DC01 AS
		SELECT
			AF_APL_ID,
			AF_APL_ID_SFX,
			MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
		FROM
			OLWHRM1.DC01_LON_CLM_INF
		GROUP BY
			AF_APL_ID,
			AF_APL_ID_SFX
	;

	/*	get LC_STA_DC10 for the most recently created DC01 record for the loan*/
	CREATE TABLE DC01 AS
		SELECT
			DC01A.AF_APL_ID,
			DC01A.AF_APL_ID_SFX,
			DC01A.LC_STA_DC10
		FROM
			OLWHRM1.DC01_LON_CLM_INF DC01A
			JOIN MAX_DC01 DC01M
				ON DC01A.AF_APL_ID = DC01M.AF_APL_ID
				AND DC01A.AF_APL_ID_SFX = DC01M.AF_APL_ID_SFX
				AND DC01A.LF_CRT_DTS_DC10 = DC01M.LF_CRT_DTS_DC10
	;


	CREATE TABLE OLINK AS
		SELECT DISTINCT 
			A.DF_PRS_ID_BR AS DF_PRS_ID
			,A.DC_SKP_STA
		/*ADDRESS INFO*/
			,B.DC_ADR
			,B.DI_VLD_ADR
			,B.DF_ZIP
			,B.DM_CT
			,B.DX_STR_ADR_1
			,B.DX_STR_ADR_2
			,B.DC_DOM_ST	
		/*PHONE INFO*/
			,B.DI_PHN_VLD
			,B.DN_PHN
			,B.DI_ALT_PHN_VLD
			,B.DN_ALT_PHN
		FROM
			OLWHRM1.PD26_PRS_SKP_PRC A
			INNER JOIN 
				(
					SELECT 
						DF_PRS_ID_BR
						,MAX(DD_SKP_STA_EFF) AS DD_SKP_STA_EFF
					FROM
						OLWHRM1.PD26_PRS_SKP_PRC
					GROUP BY 
						DF_PRS_ID_BR
				) X
					ON A.DF_PRS_ID_BR = X.DF_PRS_ID_BR
					AND A.DD_SKP_STA_EFF = X.DD_SKP_STA_EFF
			INNER JOIN OLWHRM1.GA01_APP Y
				ON A.DF_PRS_ID_BR = Y.DF_PRS_ID_BR
			INNER JOIN OLWHRM1.GA10_LON_APP Z
				ON Y.AF_APL_ID = Z.AF_APL_ID
				AND Z.AA_CUR_PRI > 0
			INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN B
				ON A.DF_PRS_ID_BR = B.DF_PRS_ID
			LEFT JOIN DC01
				ON Z.AF_APL_ID = DC01.AF_APL_ID
				AND Z.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
			JOIN OLWHRM1.GA14_LON_STA GA14
				ON Z.AF_APL_ID = GA14.AF_APL_ID
				AND Z.AF_APL_ID_SFX = GA14.AF_APL_ID_SFX
			LEFT JOIN OLWHRM1.CT30_CALL_QUE CT30
				ON A.DF_PRS_ID_BR = CT30.DF_PRS_ID_BR
				AND CT30.IF_WRK_GRP = 'KOTHDEMO'
			LEFT JOIN OLWHRM1.AY01_BR_ATY AY01
				ON A.DF_PRS_ID_BR = AY01.DF_PRS_ID
				AND AY01.PF_ACT = 'KOTHR'
				AND AY01.BD_ATY_PRF >= DATE() - 45
		WHERE 
			A.DC_SKP_STA IN ('P','W')
			AND Z.AC_GTE_TRF <> 'O'
			AND DC01.LC_STA_DC10 <> '03'
			AND GA14.AC_STA_GA14 = 'A'
			AND GA14.AC_LON_STA_TYP <> 'DN'
			AND CT30.DF_PRS_ID_BR IS NULL
			AND AY01.DF_PRS_ID IS NULL
	;


/*---------------- COMPASS ----------------*/
	CREATE TABLE COMPA AS
	SELECT *
	FROM CONNECTION TO DB2
		(
			SELECT DISTINCT 
				A.DF_PRS_ID
			/*ADDRESS INFO*/
				,D.DC_ADR
				,D.DI_VLD_ADR
				,D.DF_ZIP_CDE
				,D.DM_CT
				,D.DX_STR_ADR_1
				,D.DX_STR_ADR_2
				,D.DX_STR_ADR_3
				,D.DC_DOM_ST
				,D.DC_FGN_CNY
				,D.DM_FGN_CNY
				,D.DM_FGN_ST
			/*PHONE INFO*/
				,C.DC_PHN
				,C.DI_PHN_VLD
				,C.DN_DOM_PHN_ARA||C.DN_DOM_PHN_XCH||C.DN_DOM_PHN_LCL AS PHN
			FROM 
				OLWHRM1.PD27_PRS_SKP_PRC A
				INNER JOIN 
					(
						SELECT 
							DF_PRS_ID
							,MAX(DD_STA_SKP) AS DD_STA_SKP
						FROM 
							OLWHRM1.PD27_PRS_SKP_PRC
						GROUP BY 
							DF_PRS_ID
					)	X
					ON A.DF_PRS_ID = X.DF_PRS_ID
					AND A.DD_STA_SKP = X.DD_STA_SKP
				INNER JOIN OLWHRM1.LN10_LON B
					ON A.DF_PRS_ID = B.BF_SSN
				INNER JOIN OLWHRM1.PD42_PRS_PHN C
					ON A.DF_PRS_ID = C.DF_PRS_ID
				INNER JOIN OLWHRM1.PD30_PRS_ADR D
					ON A.DF_PRS_ID = D.DF_PRS_ID 
				LEFT JOIN OLWHRM1.CT30_CALL_QUE CT30
					ON A.DF_PRS_ID = CT30.DF_PRS_ID_BR
					AND CT30.IF_WRK_GRP = 'KOTHDEMO'
				LEFT JOIN OLWHRM1.AY10_BR_LON_ATY AY10
					ON A.DF_PRS_ID = AY10.BF_SSN
					AND AY10.PF_REQ_ACT = 'KOTHR'
					AND DAYS(AY10.LD_ATY_REQ_RCV) >= DAYS(CURRENT DATE) - 45
			WHERE 
				A.DC_STA_SKP = '2'
				AND B.LF_LON_CUR_OWN NOT IN (&EXCL_LIST)
				AND B.LC_STA_LON10 = 'R'
				AND B.LA_CUR_PRI > 0  
				AND CT30.DF_PRS_ID_BR IS NULL
				AND AY10.BF_SSN IS NULL

			FOR READ ONLY WITH UR
		)
	;
	DISCONNECT FROM DB2;
	/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
	/*%SQLCHECK (SQLRPT=ULWK04.LWK04RZ);*/
QUIT;
ENDRSUBMIT;

DATA OLINK;SET WORKLOCL.OLINK;RUN;
DATA COMPA;SET WORKLOCL.COMPA;RUN;

PROC SORT DATA=OLINK;BY DF_PRS_ID;RUN;
PROC SORT DATA=COMPA;BY DF_PRS_ID;RUN;
/*-------------------- ONELINK PROCESSING --------------------*/
DATA OLEGAL(KEEP=DF_PRS_ID DC_ADR DI_VLD_ADR WHERE=(DC_ADR='L' AND DI_VLD_ADR='N')) 
	OALT(KEEP=DF_PRS_ID DC_ADR DI_VLD_ADR DF_ZIP DM_CT DX_STR_ADR_1 DX_STR_ADR_2
		DC_DOM_ST WHERE=(DC_ADR^='L' AND DI_VLD_ADR='Y' AND DX_STR_ADR_1^=''))
	OHPHN(KEEP=DF_PRS_ID DI_PHN_VLD WHERE=(DI_PHN_VLD='N')) 
	OAPHN(KEEP=DF_PRS_ID DN_ALT_PHN DI_ALT_PHN_VLD WHERE=(DN_ALT_PHN^=''
		AND DI_ALT_PHN_VLD = 'Y'));
SET OLINK;
RUN;

PROC SQL;
CREATE TABLE OLADD AS
SELECT DISTINCT B.DF_PRS_ID
	,'ONELINK'||','||
	TRIM(B.DC_ADR)||','||
	TRIM(B.DX_STR_ADR_1)||','||
	TRIM(B.DX_STR_ADR_2)||','|| 
	TRIM(B.DM_CT)||','||
	TRIM(B.DC_DOM_ST)||','||
	TRIM(B.DF_ZIP) AS OLAADD 
FROM OLEGAL A
INNER JOIN OALT B
	ON A.DF_PRS_ID = B.DF_PRS_ID
ORDER BY DF_PRS_ID
;
QUIT;
PROC SQL;
CREATE TABLE OLPHN AS
SELECT DISTINCT B.DF_PRS_ID
	,'ONELINK'||','||'A'||','||TRIM(B.DN_ALT_PHN) AS OLAPHN 
FROM OHPHN A
INNER JOIN OAPHN B
	ON A.DF_PRS_ID = B.DF_PRS_ID
ORDER BY DF_PRS_ID
;
QUIT;

DATA ONELINK (KEEP=DF_PRS_ID OLCOM) ;
MERGE OLADD (IN=A) OLPHN (IN=B);
BY DF_PRS_ID;
IF A AND B THEN OLCOM = TRIM(LEFT(OLAADD))||','||TRIM(LEFT(OLAPHN));
ELSE IF A AND NOT B THEN OLCOM=TRIM(LEFT(OLAADD));
ELSE IF B AND NOT A THEN OLCOM=TRIM(LEFT(OLAPHN));
RUN;

/*-------------------- COMPASS PROCESSING --------------------*/
DATA CLEGAL(KEEP=DF_PRS_ID DC_ADR DI_VLD_ADR WHERE=(DC_ADR='L' AND DI_VLD_ADR='N')) 
	CALT(KEEP=DF_PRS_ID DC_ADR DI_VLD_ADR DC_ADR DI_VLD_ADR DF_ZIP_CDE DM_CT 
		DX_STR_ADR_1 DX_STR_ADR_2 DX_STR_ADR_3 DC_DOM_ST WHERE=(DC_ADR^='L' 
		AND DI_VLD_ADR='Y' AND DX_STR_ADR_1^=''))
	CHPHN(KEEP=DF_PRS_ID DC_PHN DI_PHN_VLD WHERE=(DC_PHN='H' AND DI_PHN_VLD='N')) 
	CAPHN(KEEP=DF_PRS_ID DC_PHN PHN DI_PHN_VLD WHERE=(PHN^='' AND DI_PHN_VLD='Y' 
		AND DC_PHN^='H'));
SET COMPA;
RUN;

PROC SQL;
CREATE TABLE COADD AS
SELECT DISTINCT B.DF_PRS_ID
	,'COMPASS'||','||
	TRIM(B.DC_ADR)||','||
	TRIM(B.DX_STR_ADR_1)||','||
	TRIM(B.DX_STR_ADR_2)||','|| 
	TRIM(B.DX_STR_ADR_3)||','|| 
	TRIM(B.DM_CT)||','||
	TRIM(B.DC_DOM_ST)||','||
	TRIM(B.DF_ZIP_CDE ) AS COAADD 
FROM CLEGAL A
INNER JOIN CALT B
	ON A.DF_PRS_ID = B.DF_PRS_ID
ORDER BY DF_PRS_ID
;
QUIT;
PROC SQL;
CREATE TABLE COPHN AS
SELECT DISTINCT B.DF_PRS_ID
	,'COMPASS'||','||TRIM(B.DC_PHN)||','||TRIM(B.PHN) AS COAPHN 
FROM CHPHN A
INNER JOIN CAPHN B
	ON A.DF_PRS_ID = B.DF_PRS_ID
ORDER BY DF_PRS_ID
;
QUIT;

DATA COMPASS (KEEP=DF_PRS_ID COCOM) ;
MERGE COADD (IN=A) COPHN (IN=B);
BY DF_PRS_ID;
IF A AND B THEN COCOM = TRIM(LEFT(COAADD))||','||TRIM(LEFT(COAPHN));
ELSE IF A AND NOT B THEN COCOM=TRIM(LEFT(COAADD));
ELSE IF B AND NOT A THEN COCOM=TRIM(LEFT(COAPHN));
RUN;

/*-------------------- COMBINE FILES AND INITIALIZE VARIABLES --------------------*/
PROC SORT DATA=ONELINK;BY DF_PRS_ID;RUN;
PROC SORT DATA=COMPASS;BY DF_PRS_ID;RUN;

DATA BTHSYS (KEEP=TARGET_ID QUEUE_NAME INSTITUTION_ID INSTITUTION_TYPE DATE_DUE TIME_DUE COMMENTS) ;
LENGTH COMMENTS $ 600;
MERGE ONELINK (IN=A) COMPASS (IN=B);
BY DF_PRS_ID;
IF A AND B THEN COMMENTS = TRIM(LEFT(OLCOM))||','||TRIM(LEFT(COCOM));
ELSE IF A AND NOT B THEN COMMENTS=TRIM(LEFT(OLCOM));
ELSE IF B AND NOT A THEN COMMENTS=TRIM(LEFT(COCOM));
TARGET_ID = DF_PRS_ID;
QUEUE_NAME = 'KOTHDEMO';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
RUN;

DATA _NULL_;
SET  WORK.BTHSYS; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;

