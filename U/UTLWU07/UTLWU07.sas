*-----------------------------------------------------------------------------*
| UTLWU07 QC DEFERMENT-FORBEARANCE ADDED BUT DID NOT COVER FULL DELINQUENCY V2|
*-----------------------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU07.LWU07R2";
FILENAME REPORTZ "&RPTLIB/ULWU07.LWU07RZ";
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DFADNCD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_SPE_ACC_ID
	,C.DM_PRS_LST
	,A.LN_SEQ 
	,B.LD_DLQ_OCC
	,B.LN_DLQ_MAX
	,DATE(DAYS(CURRENT DATE) - B.LN_DLQ_MAX) AS MXVAL
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME C
	ON B.BF_SSN = C.DF_PRS_ID
WHERE B.LC_STA_LON16 = '1'
	AND A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
	AND B.LN_DLQ_MAX != DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) - 1
/* NO 'QFBOK' ARC WHERE LD_ATY_REQ_RCV > LD_DFR_CER */
	AND NOT EXISTS ( 
		SELECT *
		FROM OLWHRM1.AY10_BR_LON_ATY X
		INNER JOIN (
			SELECT BF_SSN
				,LD_DFR_CER
			FROM OLWHRM1.DF10_BR_DFR_REQ
			UNION
			SELECT BF_SSN
				,LD_FOR_INF_CER AS LD_DFR_CER
			FROM OLWHRM1.FB10_BR_FOR_REQ
			) Y
			ON X.BF_SSN = Y.BF_SSN
		WHERE X.BF_SSN = A.BF_SSN
		AND X.PF_REQ_ACT = 'QFBOK'
		AND X.LD_ATY_REQ_RCV > Y.LD_DFR_CER
		)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU07.LWU07RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA DFADNCD;
SET WORKLOCL.DFADNCD;
RUN;

PROC SORT DATA=DFADNCD;BY DF_SPE_ACC_ID LN_SEQ;RUN;

DATA _NULL_;
SET DFADNCD ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = TODAY();
DESCRIPTION = CATX(',',
	'BORROWER ACCOUNT #'||DF_SPE_ACC_ID,
	'LAST NAME = '||DM_PRS_LST,
	'LOAN SEQ #'||TRIM(LEFT(LN_SEQ)),   
	'DATE DELINQUENCY OCCURRED: '||PUT(LD_DLQ_OCC,MMDDYY10.),
	'MAX DATE OF DELINQUENCY: '||PUT(MXVAL,MMDDYY10.) 
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
