/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWS57.LWS57RZ";
FILENAME REPORT2 "&RPTLIB/ULWS57.LWS57R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;

RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
PROC SQL;
CREATE TABLE BASE AS
SELECT A.BF_SSN
	,A.LN_SEQ
FROM DLGSUTWH.LN10_LON A
INNER JOIN DLGSUTWH.DW01_DW_CLC_CLU B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
	AND B.WC_DW_LON_STA NOT IN ('17','21','22')
	AND A.LI_CON_PAY_STP_PUR ^= 'Y'
	AND A.LC_STP_PUR ^= 'Y';

CREATE TABLE RPS AS
SELECT DAY(LD_RPS_1_PAY_DU) AS DUE_DAY
	,COUNT(DISTINCT A.BF_SSN) AS BORROWERS
FROM BASE A
INNER JOIN DLGSUTWH.LN65_LON_RPS B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN DLGSUTWH.RS10_BR_RPD C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_RPS_SEQ = C.LN_RPS_SEQ
WHERE B.LC_STA_LON65 = 'A'
	AND C.LC_STA_RPST10 = 'A'
GROUP BY DUE_DAY;

CREATE TABLE DLQ AS
SELECT COUNT(DISTINCT A.BF_SSN) AS BORROWERS
	,DAY(LD_RPS_1_PAY_DU) AS due_DAY
	,CASE 
		WHEN TODAY() > LD_DLQ_OCC + 300 THEN 11
		ELSE CEIL(DATDIF(LD_DLQ_OCC,TODAY(),'ACT/ACT')/30)
	END AS DLQ_BKT
FROM BASE A
INNER JOIN DLGSUTWH.LN16_LON_DLQ_HST C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
inner join DLGSUTWH.LN65_LON_RPS B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN DLGSUTWH.RS10_BR_RPD d
	ON B.BF_SSN = d.BF_SSN
	AND B.LN_RPS_SEQ = d.LN_RPS_SEQ
WHERE c.lc_sta_lon16 = '1'
	AND C.LD_DLQ_OCC < TODAY() - 15
	and B.LC_STA_LON65 = 'A'
	AND d.LC_STA_RPST10 = 'A'
GROUP BY due_DAY, DLQ_BKT;

QUIT;

ENDRSUBMIT;
DATA DLQ; SET WORKLOCL.DLQ; RUN;
DATA RPS; SET WORKLOCL.RPS; RUN;

PROC SORT DATA=DLQ; BY DUE_DAY DLQ_BKT; RUN;
DATA DLQ(DROP=I BORROWERS DLQ_BKT);
SET DLQ;
LABEL DUE_DAY = 'DUE DAY'
	C1 = '15-30'
	C2 = '31-60'
	C3 = '61-90'
	C4 = '91-120'
	C5 = '121-150'
	C6 = '151-180'
	C7 = '181-210'
	C8 = '211-240'
	C9 = '241-270'
	C10 = '271-300'
	C11 = '300+';
BY DUE_DAY;
RETAIN C1-C11 0;
ARRAY BKT{11} C1-C11 ;
IF FIRST.DUE_DAY THEN DO I=1 TO 11;
	BKT{I} = 0;
END;
DO I = 1 TO 11;
	IF DLQ_BKT = I THEN BKT{I} = BORROWERS;
END;
IF LAST.DUE_DAY THEN DO;
	TOTAL = SUM(OF C1-C11);
	OUTPUT;
END;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
/*FOR PORTRAIT REPORTS;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=96;
TITLE 'BORROWERS PER DUE DAY';
FOOTNOTE4   'JOB = UTLWS57  	 REPORT = ULWS57.LWS57R2';
PROC PRINT NOOBS SPLIT='/' DATA=RPS WIDTH=UNIFORM WIDTH=MIN LABEL;
RUN;
TITLE2 'BY DELINQUENCY BUCKETS';

PROC PRINT NOOBS SPLIT='/' DATA=DLQ WIDTH=UNIFORM WIDTH=MIN LABEL;
VAR DUE_DAY;
SUM C1 - C11 TOTAL;
LABEL DUE_DAY = 'DUE DAY'
	C1 = '15-30'
	C2 = '31-60'
	C3 = '61-90'
	C4 = '91-120'
	C5 = '121-150'
	C6 = '151-180'
	C7 = '181-210'
	C8 = '211-240'
	C9 = '241-270'
	C10 = '271-300'
	C11 = '300+';
RUN;

PROC PRINTTO;
RUN;

