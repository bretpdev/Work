*------------------------------------------------*
| UTLWA31 Federal Default (Guarantee) Fee Report |
*------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWA31.LWA31R2";
FILENAME REPORT3 "&RPTLIB/ULWA31.LWA31R3";
FILENAME REPORT4 "&RPTLIB/ULWA31.LWA31R4";
FILENAME REPORTZ "&RPTLIB/ULWA31.LWA31RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FDGFR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT C.DF_SPE_ACC_ID
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,B.AA_GTE_LON_AMT
	,COALESCE(B.AA_TOT_CAN,0) AS AA_TOT_CAN
	,B.AA_TOT_RFD
	,SUM(COALESCE(D.AA_DSB_ADJ,0)) AS AA_DSB_ADJ
	,E.FIRST_DISB
	,A.AD_APL_DSB_2
	,A.AD_APL_DSB_3
	,A.AD_APL_DSB_4
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
INNER JOIN OLWHRM1.GA11_LON_DSB_ATY D
	ON B.AF_APL_ID = D.AF_APL_ID
	AND B.AF_APL_ID_SFX = D.AF_APL_ID_SFX
INNER JOIN (
	SELECT AF_APL_ID
		,AF_APL_ID_SFX 
		,MIN(AD_DSB_ADJ) AS FIRST_DISB
	FROM OLWHRM1.GA11_LON_DSB_ATY 
	WHERE AC_DSB_ADJ = 'A'
		AND AC_DSB_ADJ_STA = 'A'
	GROUP BY AF_APL_ID
		,AF_APL_ID_SFX 
	) E
	ON B.AF_APL_ID = E.AF_APL_ID
	AND B.AF_APL_ID_SFX = E.AF_APL_ID_SFX
WHERE B.AC_LON_TYP IN ('GB','PL','SF','SU')
	AND E.FIRST_DISB BETWEEN '07/01/2006' AND '06/30/2009'
	AND D.AC_DSB_ADJ = 'A'
	AND D.AC_DSB_ADJ_STA = 'A'
GROUP BY C.DF_SPE_ACC_ID
	,B.AF_APL_ID||B.AF_APL_ID_SFX 
	,B.AA_GTE_LON_AMT
	,COALESCE(B.AA_TOT_CAN,0) 
	,B.AA_TOT_RFD
	,E.FIRST_DISB
	,A.AD_APL_DSB_2
	,A.AD_APL_DSB_3
	,A.AD_APL_DSB_4
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWA31.LWA31RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA FDGFR ;
	SET WORKLOCL.FDGFR;
RUN;
DATA FDGFR;
	SET FDGFR;
	FORMAT SUB_DISB_DT MMDDYY10.;
	DISB = COALESCE(ROUND(AA_DSB_ADJ - AA_TOT_CAN - AA_TOT_RFD,.01),0);
	BOGLND = COALESCE(ROUND(AA_GTE_LON_AMT - AA_TOT_CAN - AA_TOT_RFD - DISB,.01),0);
	SUB_DISB_DT = MAX(OF AD_APL_DSB_2-AD_APL_DSB_4);
RUN;
/*R2 DATA SET*/
PROC SQL;
	CREATE TABLE FDGFR_TOT AS 
		SELECT SUM(AA_GTE_LON_AMT) AS TOT_GUAR
			,SUM(AA_TOT_CAN) AS TOT_CAN
			,SUM(AA_TOT_RFD) AS TOT_RFD
			,SUM(DISB) AS TOT_DISB
			,SUM(BOGLND) AS BOGLND_SUM
		FROM FDGFR
		;
QUIT;
/*R3 DATA SET*/
PROC SORT DATA=FDGFR OUT=DNG(WHERE=(BOGLND < 0));
	BY DF_SPE_ACC_ID;
RUN;
/*R4 DATA SET*/
PROC SQL;
	CREATE TABLE SUB_DISB_TOT AS 
		SELECT COUNT(*) AS LOAN_COUNT
			,SUM(BOGLND) AS BOGLND_SUM
		FROM FDGFR
		WHERE SUB_DISB_DT >= MDY(7,1,2009)
		;
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'FEDERAL LOANS GUARANTEED AND DISBURSED ';
TITLE2 'FOR THE PERIOD JULY 1, 2006 AND JUNE 30, 2009';
FOOTNOTE   'JOB = UTLWA31  	 REPORT = ULWA31.LWA31R2';
PROC PRINT NOOBS SPLIT='/' DATA=FDGFR_TOT WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT TOT_GUAR TOT_CAN TOT_RFD TOT_DISB BOGLND_SUM DOLLAR20.2;
VAR TOT_GUAR TOT_CAN TOT_RFD TOT_DISB BOGLND_SUM ;
LABEL TOT_GUAR = 'TOTAL/GUARANTEED'
	TOT_CAN = 'TOTAL/CANCELLATIONS' 
	TOT_RFD = 'TOTAL/REFUNDS'
	TOT_DISB = 'TOTAL/DISBURSED' 
	BOGLND_SUM = 'REMAINING NET/AMOUNT GUARANTEED';
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'NEGATIVE GUARANTEE';
TITLE2 'FEDERAL LOANS GUARANTEED AND DISBURSED';
TITLE3 'FOR THE PERIOD JULY 1, 2006 AND JUNE 30, 2009';
FOOTNOTE4   'JOB = UTLWA31  	 REPORT = ULWA31.LWA31R3';
PROC CONTENTS DATA=DNG OUT=EMPTYSET NOPRINT; QUIT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWA31  	 REPORT = ULWA31.LWA31R3";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=DNG WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT BOGLND DOLLAR20.2;
VAR DF_SPE_ACC_ID BOGLND;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	BOGLND = 'REMAINING NET/AMOUNT GUARANTEED';
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'SUBSEQUENT DISBURSEMENTS';
TITLE2 'FIRST DISBURSED BETWEEN JULY 1, 2006 AND JUNE 30, 2009';
FOOTNOTE4   'JOB = UTLWA31  	 REPORT = ULWA31.LWA31R4';
PROC CONTENTS DATA=SUB_DISB_TOT OUT=EMPTYSET NOPRINT; QUIT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWA31  	 REPORT = ULWA31.LWA31R4";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=SUB_DISB_TOT WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LOAN_COUNT COMMA8. BOGLND_SUM DOLLAR20.2;
VAR LOAN_COUNT BOGLND_SUM;
LABEL LOAN_COUNT = 'LOANS TO BE/DISBURSED'
	BOGLND_SUM = 'BALANCE OF/GUARANTEED/LOANS NOT/DISBURSED';
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE - FOR TESTING ONLY*/
DATA _NULL_;
	SET  WORK.FDGFR;
	FILE 'T:\SAS\UTLWA31.Detail.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT AA_GTE_LON_AMT BEST12.;
	FORMAT AA_TOT_CAN  BEST12.;
	FORMAT AA_TOT_RFD  BEST12.;
	FORMAT AA_DSB_ADJ  BEST12.;
	FORMAT FIRST_DISB  MMDDYY10.;
	FORMAT AD_APL_DSB_2  MMDDYY10.;
	FORMAT AD_APL_DSB_3  MMDDYY10.;
	FORMAT AD_APL_DSB_4  MMDDYY10.;   
	FORMAT SUB_DISB_DT  MMDDYY10.;
	IF _N_ = 1 THEN        
	DO;
		PUT
		'DF_SPE_ACC_ID'
		','
		'CLUID' 
		','
		'AA_GTE_LON_AMT'
		','
		'AA_TOT_CAN'  
		','
		'AA_TOT_RFD'  
		','
		'AA_DSB_ADJ'  
		','
		'FIRST_DISB'  
		','
		'AD_APL_DSB_2' 
		','
		'AD_APL_DSB_3' 
		','
		'AD_APL_DSB_4'     
		','
		'SUB_DISB_DT';
	END;
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT CLUID $ @;
		PUT AA_GTE_LON_AMT @;
		PUT AA_TOT_CAN  @;
		PUT AA_TOT_RFD  @;
		PUT AA_DSB_ADJ  @;
		PUT FIRST_DISB  @;
		PUT AD_APL_DSB_2  @;
		PUT AD_APL_DSB_3  @;
		PUT AD_APL_DSB_4  @;   
		PUT SUB_DISB_DT  ;
	END;
RUN;
