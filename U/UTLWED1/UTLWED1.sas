/*
ASSIGNED TO ED PROM NOTE AND FILE PULL LISTS

This program selects loans assigned to ED and generates a promissory note and a
file pull list.
The lists are used by the file room to pull promissory notes and files to send to ED.

*/


LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWED1.LWED1R2";
FILENAME REPORT3 "&RPTLIB/ULWED1.LWED1R3";

/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;*/
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SUB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.af_apl_id,
		A.af_apl_id_sfx,
		A.bf_ssn,
		A.LF_CLM_ID,
		C.DM_PRS_LST,
		C.DM_PRS_1,
		B.AX_SCL_DSB_1_IAD,
		B.AA_GTE_LON_AMT
FROM	OLWHRM1.DC01_LON_CLM_INF A INNER JOIN
		OLWHRM1.GA10_LON_APP B ON
	 		A.AF_APL_ID = b.AF_APL_ID AND
			A.AF_APL_ID_SFX = B.AF_APL_ID_SFX AND
			A.LC_REA_CLM_ASN_DOE ='08' AND
			B.AC_PRC_STA = 'A' INNER JOIN
		OLWHRM1.PD01_PDM_INF C ON
			A.BF_SSN = C.DF_PRS_ID

);
DISCONNECT FROM DB2;
QUIT;
/*ENDRSUBMIT;*/
DATA WORK.SUB;
	SET WORK.SUB;
	CLID = AF_APL_ID||AF_APL_ID_SFX;
	NAME = TRIM(DM_PRS_LST)||', '||TRIM(DM_PRS_1);
	SSN = BF_SSN;
	DISB_DT = SUBSTR(AX_SCL_DSB_1_IAD,5,2)||'/'||SUBSTR(AX_SCL_DSB_1_IAD,7,2)||'/'||SUBSTR(AX_SCL_DSB_1_IAD,1,4);
RUN;
PROC SORT DATA=WORK.SUB;
BY SSN NAME;
RUN;
PROC PRINTTO PRINT=REPORT2;
RUN;
OPTIONS PAGENO=1 LS=80;
PROC PRINT NOOBS SPLIT='/' DATA=WORK.SUB;
BY SSN NAME;
VAR CLID
	DISB_DT
	AA_GTE_LON_AMT
	;
LABEL	CLID = 'UNIQUE ID'
		DISB_DT = 'DISBURSEMENT DATE'
		AA_GTE_LON_AMT = 'LOAN AMOUNT'	
		;

TITLE1	' ';
TITLE2	' ';
TITLE3  'PROMISSORY NOTE PULL LIST';
TITLE4	'LOANS ASSIGNED TO ED';
RUN;
PROC SORT DATA=WORK.SUB;
BY NAME SSN;
RUN;
PROC PRINTTO PRINT=REPORT3;
RUN;
OPTIONS PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=WORK.SUB;
BY NAME SSN;
VAR CLID
	DISB_DT
	AA_GTE_LON_AMT
	;
LABEL	CLID = 'UNIQUE ID'
		DISB_DT = 'DISBURSEMENT DATE'
		AA_GTE_LON_AMT = 'LOAN AMOUNT'	
		;

TITLE1	' ';
TITLE2	' ';
TITLE3  'FILE PULL LIST';
TITLE4	'LOANS ASSIGNED TO ED';
RUN;
