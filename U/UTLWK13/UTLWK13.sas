********************************************;
*UTLWK13 BANKRUPTCY CLAIM NOT IN A BK STATUS;
********************************************;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWK13.LWK13R2";
FILENAME REPORTZ "&RPTLIB/ULWK13.LWK13RZ";

DATA _NULL_;
	CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYS10.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BKCNBS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,C.DF_SPE_ACC_ID
	,RTRIM(A.AF_APL_ID)||A.AF_APL_ID_SFX AS CLUID
	,A.LC_AUX_STA
	,A.LD_LDR_POF
	,B.LC_BKR_STA
	,B.BKR_IND
	,B.LD_BKR_FIL
	,C.DM_PRS_LST
	,A.LF_CLM_ID
	,A.LC_PCL_REA
FROM OLWHRM1.DC01_LON_CLM_INF A
LEFT OUTER JOIN 
	(SELECT AF_APL_ID
		,AF_APL_ID_SFX
		,LF_CRT_DTS_DC10
		,LC_BKR_STA
		,LD_BKR_FIL
		,'Y' AS BKR_IND
	 FROM OLWHRM1.DC18_BKR
	 ) B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.BF_SSN = C.DF_PRS_ID 
WHERE A.LC_STA_DC10 = '03'
AND A.LC_PCL_REA IN ('BC','BO','BH')
AND A.LD_CLM_ASN_DOE IS NULL
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWK13.LWK13RZ);*/
/*QUIT;*/

ENDRSUBMIT;

DATA BKCNBS;
SET WORKLOCL.BKCNBS;
RUN;

PROC SORT DATA=BKCNBS;BY BF_SSN LF_CLM_ID DESCENDING LD_BKR_FIL;RUN;

DATA BKCNBS;
SET BKCNBS;
BY BF_SSN LF_CLM_ID;
IF FIRST.LF_CLM_ID THEN OUTPUT;
RUN;

DATA BKCNBS;
SET BKCNBS;
WHERE BKR_IND = '' 
OR (LC_BKR_STA NOT IN ('01','04','06','07') 
	AND	LD_LDR_POF LE TODAY()); 
RUN;

PROC SORT DATA=BKCNBS NODUPKEY;
BY DF_SPE_ACC_ID;
RUN;
DATA _NULL_;
SET BKCNBS ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = LD_LDR_POF ;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID
	,DM_PRS_LST
	,LF_CLM_ID
	,LC_PCL_REA
	,PUT(LD_LDR_POF,MMDDYY10.)
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
