/*MONTHLY DISBURSEMENT TOTALS BY SCHOOL - Lender reports*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWL04.LWL04RZ";
FILENAME REPORT2 "&RPTLIB/ULWL04.LWL04R2";
FILENAME REPORT3 "&RPTLIB/ULWL04.LWL04R3";
FILENAME REPORT4 "&RPTLIB/ULWL04.LWL04R4";
FILENAME REPORT5 "&RPTLIB/ULWL04.LWL04R5";
FILENAME REPORT7 "&RPTLIB/ULWL04.LWL04R7";
FILENAME REPORT8 "&RPTLIB/ULWL04.LWL04R8";
FILENAME REPORT9 "&RPTLIB/ULWL04.LWL04R9";
OPTIONS SYMBOLGEN;
/*----------------------------------------------------*
| DATE RANGE FOR ROSTER DATE: LAST WORKING DAY OF PVS |
| MONTH TO NEXT-TO-LAST WORKING DAY OF CURRENT MONTH  |
*----------------------------------------------------*/
DATA _NULL_;
/*TODAY()+3 IS USED IN CASE EOM HAPPENS PRIOR TO THE LAST CALENDAR DAY*/
BEGIN = INTNX('MONTH',TODAY()+3,-2,'end');
IF WEEKDAY(BEGIN) = 1 THEN BEGIN = BEGIN - 2;
ELSE IF WEEKDAY(BEGIN) = 7 THEN BEGIN = BEGIN - 1;
	CALL SYMPUT('BEGIN',"'"||PUT(BEGIN,MMDDYY10.)||"'");
END = INTNX('MONTH',TODAY()+3,-1,'end');
IF WEEKDAY(END) = 1 THEN END = END - 3;
ELSE IF WEEKDAY(END) = 2 THEN END = END - 3;
ELSE IF WEEKDAY(END) = 3 THEN END = END - 1;
ELSE IF WEEKDAY(END) = 4 THEN END = END - 1;
ELSE IF WEEKDAY(END) = 5 THEN END = END - 1;
ELSE IF WEEKDAY(END) = 6 THEN END = END - 1;
ELSE IF WEEKDAY(END) = 7 THEN END = END - 2;
	CALL SYMPUT('END',"'"||PUT(END,MMDDYY10.)||"'");
	CALL SYMPUT('EFFDATE',TRIM(LEFT(UPCASE(
		PUT(INTNX('MONTH',TODAY()+3,-1), MONNAME9.)||' '||
		PUT(INTNX('MONTH',TODAY()+3,-1), YEAR4.)))));
RUN;

%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE MODSBSC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT C.AF_DOE_LDR
	,E.IM_LDR_FUL
    ,A.IC_LON_PGM
	,COALESCE(A.LA_DSB,0) AS LA_DSB
	,COALESCE(A.LA_DSB_CAN,0) AS LA_DSB_CAN 
	,C.AF_DOE_SCL 
	,F.IM_SCL_FUL
	,C.AC_SCL_ACA_GDE_LEV
	,C.AF_RGL_CAT_LP20
	,H.LC_DSB_FEE AS LC_DSB_FEE_02
	,H.LA_DSB_FEE AS LA_DSB_FEE_02
	,I.LC_DSB_FEE AS LC_DSB_FEE_21
	,I.LA_DSB_FEE AS LA_DSB_FEE_21
	,G.IF_TIR_PCE
	,CASE 
		WHEN A.IC_LON_PGM IN ('PLUS','PLUSGB') AND A.LD_DSB >= '08/08/2006' THEN 'X'
		ELSE ''
	 END AS I_IND  
	,A.IC_LON_PGM
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL C
	ON A.AF_APL_ID = C.AF_APL_ID
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON E.IF_DOE_LDR = C.AF_DOE_LDR
INNER JOIN OLWHRM1.SC10_SCH_DMO F
	ON F.IF_DOE_SCL = C.AF_DOE_SCL 
INNER JOIN OLWHRM1.LN10_LON G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE H
	ON A.BF_SSN = H.BF_SSN
	AND A.LN_BR_DSB_SEQ = H.LN_BR_DSB_SEQ
	AND H.LC_DSB_FEE = '02'
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE I
	ON A.BF_SSN = I.BF_SSN
	AND A.LN_BR_DSB_SEQ = I.LN_BR_DSB_SEQ
	AND I.LC_DSB_FEE = '21'
WHERE A.LD_DSB_ROS_PRT BETWEEN &BEGIN AND &END
AND (A.LA_DSB_CAN IS NULL
    OR A.LA_DSB_CAN <> A.LA_DSB)
AND A.LC_DSB_TYP = '2'
AND A.LC_LDR_DSB_MDM NOT IN ('F','M','E')
AND A.LC_STA_LON15 IN ('1','3')
ORDER BY C.AF_DOE_LDR, F.IM_SCL_FUL, A.IC_LON_PGM
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWL04.LWL04RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA MODSBSC ; 
	SET WORKLOCL.MODSBSC; 
RUN;

DATA MODSBSC;
SET MODSBSC;
LENGTH LOC1 $ 4. LOC2 $ 35.;
IF I_IND = 'X' THEN 
	LOC1 = '3%  ';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) <= 1999030 THEN
	LOC1 = '3%  ';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2006020 AND I_IND = '' THEN
	LOC1 = '2%  ';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2007020 AND I_IND = '' THEN
	LOC1 = '1.5%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2008020 AND I_IND = '' THEN
	LOC1 = '1%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 AND I_IND = '' THEN DO;
	IF IC_LON_PGM IN ('PLUS','PLUSGB') THEN
		LOC1 = '3%';
	ELSE 
		LOC1 = '0.5%';
END;

IF LA_DSB_FEE_02 = 0 THEN DO;
	IF INPUT(AF_RGL_CAT_LP20,BEST12.) => 2008020 THEN
		LOC2 = 'UHEAA PAID ORIGINATION FEE';
	ELSE 
		LOC2 = 'LOAN WITH ZERO ORIGINATION FEE';
END;

IF LA_DSB_FEE_02 > 0 THEN DO;
	IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 AND IC_LON_PGM IN ('PLUS', 'PLUSGB') THEN DO;
		IF LA_DSB_FEE_21 = . THEN
			LOC2 = 'LOAN WITH ORIGINATION FEE 3%';
		ELSE IF LA_DSB_FEE_21 > 0 THEN
			LOC2 = 'LOAN WITH ORIG/GUAR FEE 3%/1%';
	END;
	ELSE DO;
		IF LA_DSB_FEE_21 = . THEN
			LOC2 = 'LOAN WITH ORIGINATION FEE 0.5%';
		ELSE IF LA_DSB_FEE_21 > 0 THEN
			LOC2 = 'LOAN WITH ORIG/GUAR FEE 0.5%/1%';
	END;
END;
RUN;

PROC SQL;
CREATE TABLE MODSBSC2 AS 
SELECT AF_DOE_LDR
	,IM_LDR_FUL
    ,IC_LON_PGM
	,COUNT(*) AS COUNT
	,SUM(LA_DSB - LA_DSB_CAN) AS DISBAMT
	,AF_DOE_SCL 
	,IM_SCL_FUL
	,AC_SCL_ACA_GDE_LEV
	,LOC1
	,LOC2
FROM MODSBSC
GROUP BY AF_DOE_LDR
	,IM_LDR_FUL
    ,IC_LON_PGM
	,AF_DOE_SCL 
	,IM_SCL_FUL
	,AC_SCL_ACA_GDE_LEV
	,LOC1
	,LOC2
;
QUIT;

/*******************************************
* CREATE DATASET FOR WELLS REQUESTED REPORT
*******************************************/
PROC SQL;
CREATE TABLE TIERTB AS 
SELECT AF_DOE_LDR
	,IM_LDR_FUL
    ,IC_LON_PGM
	,COUNT(*) AS COUNT
	,SUM(LA_DSB - LA_DSB_CAN) AS DISBAMT
	,AF_DOE_SCL 
	,IM_SCL_FUL
	,AC_SCL_ACA_GDE_LEV
	,IF_TIR_PCE 
FROM MODSBSC
WHERE AF_DOE_LDR = '813894'
GROUP BY AF_DOE_LDR
	,IM_LDR_FUL
    ,IC_LON_PGM
	,AF_DOE_SCL 
	,IM_SCL_FUL
	,AC_SCL_ACA_GDE_LEV
	,IF_TIR_PCE
ORDER BY IM_SCL_FUL
;
QUIT;
RUN;

/***************************
* MACRO FOR REGULAR REPORTS
***************************/
%MACRO PRNT(LENDER=,REPNO=);

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 CENTER NODATE PAGENO=1;

%LET TTL = ;

DATA REPDS;
SET MODSBSC2 ;
WHERE AF_DOE_LDR = "&LENDER";
RUN;

PROC SORT DATA=REPDS;
BY LOC2 AF_DOE_SCL IM_SCL_FUL IC_LON_PGM LOC1 LOC2;
RUN;

DATA _NULL_;
SET REPDS;
CALL SYMPUT('TTL',TRIM(AF_DOE_LDR)||' - '||TRIM(IM_LDR_FUL));
RUN;

PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
/*----------------------------------------------------------------------*
| THIS PART OF THE MACRO WILL EITHER PRINT A REPORT FOR WELLS THAT WILL |
| INCLUDE GRADE LEVEL OR PRINT A REPORT FOR ALL OTHER LENDERS THAT WONT |
| INCLUDE THE GRADE LEVEL                                               |
*----------------------------------------------------------------------*/
%IF "&LENDER" = "813894" %THEN %DO;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE3 "LENDER: &LENDER";
	TITLE4 "&EFFDATE";
	PROC CONTENTS DATA=REPDS OUT=EMPTYSET NOPRINT;RUN;	
	DATA _NULL_;
	SET EMPTYSET;
	FILE PRINT;
	IF  NOBS=0 AND _N_ =1 THEN DO;
	   PUT // 90*'-';
	   PUT      ////////
	       @41 '**** NO DISBURSEMENTS FOUND ****';
	   PUT ////////
	       @47 '-- END OF REPORT --';
	   PUT //////////
	   		@36 "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
	   END;
	RETURN;
	RUN;

	PROC REPORT DATA=REPDS NOWD HEADSKIP SPLIT='~' SPACING=2;
	COLUMN LOC2 LOC1 IM_SCL_FUL AF_DOE_SCL AC_SCL_ACA_GDE_LEV IC_LON_PGM COUNT DISBAMT ;
	DEFINE LOC2 / GROUP "CATEGORY" WIDTH=15 FLOW;
	DEFINE LOC1 / GROUP "ED ORIGINATION FEE %" WIDTH=11;
	DEFINE IM_SCL_FUL / GROUP "SCHOOL NAME" WIDTH=40 FLOW;
	DEFINE AF_DOE_SCL / GROUP "SCHOOL CODE" WIDTH=8;
	DEFINE AC_SCL_ACA_GDE_LEV / GROUP "GRADE LEVEL" WIDTH=5;
	DEFINE IC_LON_PGM / GROUP "LOAN PROGRAM" WIDTH=7;
	DEFINE COUNT / ANALYSIS "NUMBER OF DISBURSEMENTS" WIDTH=13 ;
	DEFINE DISBAMT / ANALYSIS "TOTAL DISBURSEMENT AMOUNT" WIDTH=15 FORMAT=DOLLAR14.2 ;
	BREAK AFTER LOC1 / SUMMARIZE OL SUPPRESS;
	COMPUTE AFTER;
	LINE ' ';
	LINE @1 'TOTAL NUMBER OF DISBURSEMENTS:' @40 COUNT.SUM COMMA7.;
	LINE @1 'TOTAL DISBURSEMENT AMOUNT:' @33 DISBAMT.SUM DOLLAR14.2;
	ENDCOMP;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE1 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE2 "LENDER: &TTL";
	TITLE3 "&EFFDATE";
	FOOTNOTE "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
	RUN;
%END;
%ELSE %DO;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE3 "LENDER: &LENDER";
	TITLE4 "&EFFDATE";
	PROC CONTENTS DATA=REPDS OUT=EMPTYSET NOPRINT;RUN;	
	DATA _NULL_;
	SET EMPTYSET;
	FILE PRINT;
	IF  NOBS=0 AND _N_ =1 THEN DO;
	   PUT // 90*'-';
	   PUT      ////////
	       @41 '**** NO DISBURSEMENTS FOUND ****';
	   PUT ////////
	       @47 '-- END OF REPORT --';
	   PUT //////////
	   		@36 "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
	   END;
	RETURN;
	RUN;

	PROC REPORT DATA=REPDS NOWD HEADSKIP SPLIT='~' SPACING=2;
	COLUMN LOC2 LOC1 IM_SCL_FUL AF_DOE_SCL IC_LON_PGM COUNT DISBAMT ;
	DEFINE LOC2 / GROUP "CATEGORY" WIDTH=15 FLOW;
	DEFINE LOC1 / GROUP "ED ORIGINATION FEE %" WIDTH=11;
	DEFINE IM_SCL_FUL / GROUP "SCHOOL NAME" WIDTH=40 FLOW;
	DEFINE AF_DOE_SCL / GROUP "SCHOOL CODE" WIDTH=8;
	DEFINE IC_LON_PGM / GROUP "LOAN PROGRAM" WIDTH=7;
	DEFINE COUNT / ANALYSIS "NUMBER OF DISBURSEMENTS" WIDTH=13 ;
	DEFINE DISBAMT / ANALYSIS "TOTAL DISBURSEMENT AMOUNT" WIDTH=15 FORMAT=DOLLAR14.2 ;
	BREAK AFTER LOC1 / SUMMARIZE OL SUPPRESS SKIP;
	COMPUTE AFTER;
	LINE ' ';
	LINE @1 'TOTAL NUMBER OF DISBURSEMENTS:' @40 COUNT.SUM COMMA7.;
	LINE @1 'TOTAL DISBURSEMENT AMOUNT:' @33 DISBAMT.SUM DOLLAR14.2;
	ENDCOMP;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE1 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE2 "LENDER: &TTL";
	TITLE3 "&EFFDATE";
	FOOTNOTE "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
	RUN;
%END;
PROC PRINTTO;
RUN;
%MEND PRNT;

/*******************************
* MACROS FOR WELLS ONLY REPORTS
*******************************/
/*this will clear the file of old data*/
%MACRO FIRST(REPNO=);
PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
%MEND FIRST;

%MACRO PRINT_TIER(LENDER=,REPNO=,TIER=,ABV=);
%LET TTL = ;
DATA TIERTB2;
SET TIERTB;
WHERE AF_DOE_LDR = "&LENDER"
AND IF_TIR_PCE = &ABV;
RUN;

DATA _NULL_;
SET TIERTB2;
CALL SYMPUT('TTL',TRIM(AF_DOE_LDR)||' - '||TRIM(IM_LDR_FUL));
RUN;

DATA TEMP NONE (DROP=IF_TIR_PCE);
SET TIERTB2;
LENGTH TIER $ 10.;
IF IF_TIR_PCE = &ABV THEN TIER = &TIER;
IF IF_TIR_PCE = &ABV THEN OUTPUT TEMP;
ELSE OUTPUT NONE;
RUN;

PROC PRINTTO PRINT=REPORT&REPNO;
RUN;

/*REPORT FOR TEMP*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 NOBYLINE;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=100;
PROC CONTENTS DATA=TEMP OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
   PUT // 90*'-';
   PUT      ////////
       @31 '**** NO DISBURSEMENTS FOUND ****';
   PUT ////////
       @37 '-- END OF REPORT --';
   PUT //////////////
   		@26 "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
   END;
RETURN;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
TITLE3 "LENDER: &LENDER";
TITLE4 "TIER: &TIER";
TITLE5 "&EFFDATE";
run;

PROC PRINT NOOBS SPLIT='~' DATA=TEMP WIDTH=UNIFORM WIDTH=MIN;
VAR IM_SCL_FUL AF_DOE_SCL AC_SCL_ACA_GDE_LEV IC_LON_PGM COUNT DISBAMT;
BY TIER;
PAGEBY TIER;
SUM COUNT DISBAMT;
LABEL IM_SCL_FUL="SCHOOL NAME" AF_DOE_SCL="SCHOOL~CODE" AC_SCL_ACA_GDE_LEV = "GRADE~LEVEL"
IC_LON_PGM="LOAN PROGRAM" COUNT="NUMBER OF DISBURSEMENTS"
DISBAMT= "TOTAL DISBURSEMENT AMOUNT";;
FORMAT COUNT COMMA6. DISBAMT DOLLAR18.2;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
TITLE3 "LENDER: &TTL";
TITLE4  #BYVAL1	;
TITLE5 "&EFFDATE";
FOOTNOTE "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
RUN;

PROC PRINTTO;
RUN;
%MEND PRINT_TIER;

/*PRINT NO TIER BY LENDER*/
/*PRINT NO TIER BY LENDER*/
%MACRO PRINT_TIER_NONE(LENDER=,REPNO=,NI=);
DATA _NULL_;
	CALL SYMPUT('NI2',TRANSLATE(&NI,"'",'|'));
RUN;
DATA TIERTB2;
	SET TIERTB;
	WHERE AF_DOE_LDR = "&LENDER"
	AND IF_TIR_PCE NOT IN (&NI2);
RUN;

DATA _NULL_;
	SET TIERTB2;
	CALL SYMPUT('TTL',TRIM(AF_DOE_LDR)||' - '||TRIM(IM_LDR_FUL));
RUN;
%IF %SYMEXIST(TTL) = 0 %THEN %DO;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE3 "LENDER: &LENDER";
	TITLE4 'NO TIER';
	TITLE5 "&EFFDATE";
%END;
%ELSE %DO;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 "MONTHLY DISBURSEMENT TOTALS BY SCHOOL";
	TITLE3 "LENDER: &TTL";
	TITLE4 'NO TIER';
	TITLE5 "&EFFDATE";
%END;
PROC PRINTTO PRINT=REPORT&REPNO;
RUN;
/*REPORT FOR NO TIER*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 NOBYLINE;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=100;
PROC CONTENTS DATA=TIERTB2 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
   PUT // 90*'-';
   PUT      ////////
       @31 '**** NO DISBURSEMENTS FOUND ****';
   PUT ////////
       @37 '-- END OF REPORT --';
  PUT //////////////
   		@26 "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
   END;
RETURN;

run;

PROC PRINT NOOBS SPLIT='~' DATA=TIERTB2 WIDTH=UNIFORM WIDTH=MIN;
VAR IM_SCL_FUL AF_DOE_SCL AC_SCL_ACA_GDE_LEV IC_LON_PGM COUNT DISBAMT;
SUM COUNT DISBAMT;
LABEL IM_SCL_FUL="SCHOOL NAME" AF_DOE_SCL="SCHOOL~CODE" AC_SCL_ACA_GDE_LEV = "GRADE~LEVEL"
IC_LON_PGM="LOAN PROGRAM" COUNT="NUMBER OF DISBURSEMENTS"
DISBAMT= "TOTAL DISBURSEMENT AMOUNT";;
FORMAT COUNT COMMA6. DISBAMT DOLLAR18.2;
FOOTNOTE "JOB = UTLWL04     REPORT = ULWL04.LWL04R&REPNO";
RUN;

PROC PRINTTO;
RUN;
%MEND PRINT_TIER_NONE;

%PRNT(LENDER=811698,REPNO=2);
%PRNT(LENDER=813894,REPNO=3);
%PRNT(LENDER=817455,REPNO=4);
%PRNT(LENDER=819628,REPNO=5);
%PRNT(LENDER=822373,REPNO=7);
%PRNT(LENDER=828476,REPNO=9);

%FIRST(REPNO=8);/*THIS WILL CLEAR THE FILE OF OLD DATA*/
%PRINT_TIER(LENDER=813894,REPNO=8,TIER='CALIFORNIA',ABV='WCA');
%PRINT_TIER(LENDER=813894,REPNO=8,TIER='COLORADO',ABV='WCO');
%PRINT_TIER(LENDER=813894,REPNO=8,TIER='NEVADA',ABV='WNV');
%PRINT_TIER(LENDER=813894,REPNO=8,TIER='OFD',ABV='OFD');
%PRINT_TIER_NONE(LENDER=813894,REPNO=8,NI='|WCA|,|WCO|,|WNV|,|OFD|');


/*run the code below to create a file for testing*/
/*libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;*/
/*RSUBMIT;*/
/*PROC SQL;*/
/*CONNECT TO DB2 (DATABASE=DLGSUTWH);*/
/*CREATE TABLE TEST AS*/
/*SELECT **/
/*FROM CONNECTION TO DB2 (*/
/*SELECT A.BF_SSN*/
/*	,C.AF_DOE_LDR*/
/*	,E.IM_LDR_FUL*/
/*    ,A.IC_LON_PGM*/
/*	,COALESCE(A.LA_DSB,0) AS LA_DSB*/
/*	,COALESCE(A.LA_DSB_CAN,0) AS LA_DSB_CAN */
/*	,C.AF_DOE_SCL */
/*	,F.IM_SCL_FUL*/
/*	,C.AC_SCL_ACA_GDE_LEV*/
/*	,H.LA_DSB_FEE*/
/*	,C.AF_RGL_CAT_LP20*/
/*FROM OLWHRM1.LN15_DSB A*/
/*INNER JOIN OLWHRM1.AP03_MASTER_APL C*/
/*	ON A.AF_APL_ID = C.AF_APL_ID*/
/*INNER JOIN OLWHRM1.LR10_LDR_DMO E*/
/*	ON E.IF_DOE_LDR = C.AF_DOE_LDR*/
/*INNER JOIN OLWHRM1.SC10_SCH_DMO F*/
/*	ON F.IF_DOE_SCL = C.AF_DOE_SCL */
/*INNER JOIN OLWHRM1.LN10_LON G*/
/*	ON A.BF_SSN = G.BF_SSN*/
/*	AND A.LN_SEQ = G.LN_SEQ*/
/*LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE H*/
/*	ON A.BF_SSN = H.BF_SSN*/
/*	AND A.LN_BR_DSB_SEQ = H.LN_BR_DSB_SEQ*/
/*	AND H.LC_DSB_FEE = '02'*/
/*WHERE A.LD_DSB_ROS_PRT BETWEEN &BEGIN AND &END*/
/*AND (A.LA_DSB_CAN IS NULL*/
/*    OR A.LA_DSB_CAN <> A.LA_DSB)*/
/*AND A.LC_DSB_TYP = '2'*/
/*AND A.LC_LDR_DSB_MDM NOT IN ('F','M','E')*/
/*AND A.LC_STA_LON15 IN ('1','3')*/
/*ORDER BY C.AF_DOE_LDR, F.IM_SCL_FUL, A.IC_LON_PGM*/
/*FOR READ ONLY WITH UR*/
/*);*/
/*DISCONNECT FROM DB2;*/
/*ENDRSUBMIT;*/
/*DATA TEST;*/
/*SET WORKLOCL.TEST;*/
/*RUN;*/
/**/
/*PROC SORT DATA=TEST;*/
/*BY AF_DOE_SCL AF_DOE_LDR BF_SSN IC_LON_PGM;*/
/*RUN;*/
/**/
/*PROC EXPORT DATA= TEST*/
/*            OUTFILE= "T:\SAS\UTLWL04_DETAIL.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
