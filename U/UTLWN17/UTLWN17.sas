*--------------------------------------------------------------*
| WELLS FARGO NEW GUARANTEE REPORT WITH DEMOGRAPHICS - UTLWN17 |
*--------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWN17.LWN17R2";
FILENAME REPORTZ "&RPTLIB/ULWN17.LWN17RZ";
/*****************************************
* DEFINE MACRO VARIABLES FOR ONE TIME RUN
******************************************
%LET BEGIN = '04/15/2007';
******************************************/
DATA _NULL_;
	CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'END'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE WFNGRD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID_BR
	,C.DF_SPE_ACC_ID
	,C.DM_PRS_1
	,C.DM_PRS_LST
	,C.DX_STR_ADR_1
	,C.DX_STR_ADR_2
	,C.DM_CT
	,C.DC_DOM_ST
	,C.DF_ZIP
	,C.DM_FGN_CNY
	,C.DN_PHN
	,A.AF_APL_OPS_SCL
	,B.AF_CUR_LON_OPS_LDR
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
WHERE B.AC_PRC_STA = 'A'
	AND B.AD_PRC BETWEEN &BEGIN AND &END
	AND C.DI_PHN_VLD = 'Y'
	AND C.DI_VLD_ADR = 'Y'
	AND B.AF_CUR_LON_OPS_LDR = '813894'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWN17.LWN17RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA WFNGRD;
SET WORKLOCL.WFNGRD;
RUN;

PROC SORT DATA = WFNGRD;BY DF_PRS_ID_BR;RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 NODATE CENTER PAGNO=1;
TITLE 'WELLS FARGO NEW GUARANTEE REPORT WITH DEMOGRAPHICS';
TITLE2	"&RUNDATE - &RUNTIME";
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWN17  	 REPORT = ULWN17.LWN17R2';
PROC CONTENTS DATA=WFNGRD OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWN17  	 REPORT = ULWN17.LWN17R2";
	END;
RETURN;
RUN;
PROC REPORT NOWD DATA=WFNGRD  SPLIT='\' SPACING=2 HEADSKIP;
COLUMN DF_PRS_ID_BR DF_SPE_ACC_ID DM_PRS_1 DM_PRS_LST DX_STR_ADR_1 DX_STR_ADR_2 DM_CT 
	DC_DOM_ST DF_ZIP DM_FGN_CNY DN_PHN BLIDOC AF_APL_OPS_SCL ;
DEFINE DF_PRS_ID_BR / GROUP 'SSN' WIDTH=9 FORMAT=$9.;
DEFINE DF_SPE_ACC_ID / GROUP 'ACCT #' WIDTH=10 FORMAT=$10.;
DEFINE DM_PRS_1 / DISPLAY 'FIRST NAME' WIDTH=12 FORMAT=$12. ;
DEFINE DM_PRS_LST / DISPLAY 'LAST NAME' WIDTH=15 FORMAT=$12. ;
DEFINE DX_STR_ADR_1 / DISPLAY NOPRINT ;
DEFINE DX_STR_ADR_2 / DISPLAY NOPRINT;
DEFINE DM_CT / DISPLAY NOPRINT;
DEFINE DC_DOM_ST / DISPLAY NOPRINT;
DEFINE DF_ZIP / DISPLAY NOPRINT;
DEFINE DM_FGN_CNY / DISPLAY NOPRINT ;
DEFINE DN_PHN / DISPLAY NOPRINT ;
DEFINE BLIDOC / COMPUTED FLOW WIDTH=40 'BORROWER\ADDRESS';
DEFINE AF_APL_OPS_SCL / DISPLAY 'SCHOOL CODE' WIDTH=8;
COMPUTE BLIDOC / CHAR LENGTH=200 ;
	BLIDOC = 'ADD 1: '||INPUT(DX_STR_ADR_1,$20.)
	||'\ADD 2: '||INPUT(DX_STR_ADR_2,$15.)
	||'\CITY: '||INPUT(DM_CT,$20.)
	||'\STATE: '||INPUT(DC_DOM_ST,$2.)
	||'\ZIP: '||INPUT(DF_ZIP,$10.)
	||'\FOREIGN COUNTRY: '||INPUT(DM_FGN_CNY,$20.)
	||'\PHONE: '||INPUT(DN_PHN,$10.)
	||'\\';
ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
