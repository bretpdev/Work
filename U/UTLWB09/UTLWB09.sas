/*%LET RPTLIB = Q:\Account Services\UHEAA Reports\Error Reports\TILP loans with different separation dates;*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWB09.LWB09RZ";
FILENAME REPORT2 "&RPTLIB/ULWB09.LWB09R2";
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
PROC SQL;
CREATE TABLE MULT_SEP AS
		SELECT
			*
		FROM	
			CONNECTION TO DB2 
				(
				SELECT DISTINCT 
					PD10.DF_SPE_ACC_ID,
					TRIM(PD10.DM_PRS_LST) || ', ' || PD10.DM_PRS_1 AS NAME,
					LN10.LN_SEQ,
					SD10.LD_SCL_SPR
				FROM 
					OLWHRM1.SD10_STU_SPR SD10
					INNER JOIN OLWHRM1.SC10_SCH_DMO SC10
						ON SD10.LF_DOE_SCL_ENR_CUR = SC10.IF_DOE_SCL
					INNER JOIN OLWHRM1.LN13_LON_STU_OSD LN13
						ON SD10.LF_STU_SSN = LN13.LF_STU_SSN
						AND SD10.LN_STU_SPR_SEQ = LN13.LN_STU_SPR_SEQ
					INNER JOIN OLWHRM1.LN10_LON LN10
						ON LN13.BF_SSN = LN10.BF_SSN
						AND LN13.LN_SEQ = LN10.LN_SEQ
					INNER JOIN OLWHRM1.PD10_PRS_NME PD10
						ON LN10.BF_SSN = PD10.DF_PRS_ID
				WHERE 
					SD10.LC_STA_STU10 = 'A'
					AND LN13.LC_STA_LON13 = 'A'
					AND LN10.IC_LON_PGM = 'TILP'
					AND LN10.LC_STA_LON10 = 'R'

				FOR READ ONLY WITH UR
				)
;
DISCONNECT FROM DB2;
QUIT;

PROC SQL;
CREATE TABLE DEMO AS
	SELECT DISTINCT 
		MS.*
	FROM 
		MULT_SEP MS
		INNER JOIN 
		(
			SELECT 
				DF_SPE_ACC_ID,
				COUNT(DISTINCT LD_SCL_SPR) AS SEPS
			FROM 
				MULT_SEP
			GROUP BY 
				DF_SPE_ACC_ID
			HAVING 
				SEPS > 1
) TotalSeps
	ON MS.DF_SPE_ACC_ID = TotalSeps.DF_SPE_ACC_ID
;
QUIT;
ENDRSUBMIT;

DATA DEMO;
	SET DUSTER.DEMO;
RUN;


PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
/*FOR LANDSCAPE REPORTS:*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'TILP BORROWERS WITH MULTIPLE SEPARATION DATES';

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
VAR DF_SPE_ACC_ID NAME LN_SEQ LD_SCL_SPR;
LABEL DF_SPE_ACC_ID = 'ACCOUNT NUMBER'
	LN_SEQ = 'LOAN SEQUENCE'
	LD_SCL_SPR = 'SEPARATION DATE';
RUN;

PROC PRINTTO;
RUN;
