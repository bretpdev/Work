*------------------------------------*
| UTLWQ30 Gross Guaranty Volume FYTD |
*------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWQ30.LWQ30R2";
FILENAME REPORTZ "&RPTLIB/ULWQ30.LWQ30RZ";
DATA _NULL_;
 	CALL SYMPUT('BEGIN',"'"||PUT(INTNX('YEAR.7',TODAY(),0,'BEGINNING'), MMDDYY10.)||"'");
	CALL SYMPUT('END',"'"||PUT(INTNX('YEAR.7',TODAY(),0,'END'), MMDDYY10.)||"'");
	CALL SYMPUT('FY',COMPRESS(YEAR(INTNX('YEAR.7',TODAY(),0,'END'))));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE GGVFYTD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.DF_PRS_ID_BR
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,B.AC_PRC_STA
	,B.AD_PRC
	,B.AA_GTE_LON_AMT 
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
WHERE B.AC_PRC_STA = 'A'
	AND B.AD_PRC BETWEEN &BEGIN AND &END 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWQ30.LWQ30RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA GGVFYTD;
SET WORKLOCL.GGVFYTD;
RUN;
PROC SORT DATA=GGVFYTD NODUPKEY;
	BY CLUID;
RUN;
PROC SQL;
CREATE TABLE GGA_SMRY AS
SELECT A.YR
	,A.MO
	,COUNT(DISTINCT DF_PRS_ID_BR) AS BORS
	,COUNT(*) AS LOANS
	,SUM(A.AA_GTE_LON_AMT) AS GGA
FROM (
	SELECT YEAR(AD_PRC) AS YR
		,MONTH(AD_PRC) AS MO
		,DF_PRS_ID_BR
		,CLUID
		,AA_GTE_LON_AMT
	FROM GGVFYTD
	) A
GROUP BY A.YR
	,A.MO
ORDER BY A.YR
	,A.MO
;
QUIT;
PROC FORMAT;
	VALUE MNUCS	
		1 = 'JANUARY'
		2 = 'FEBRUARY'
		3 = 'MARCH'
		4 = 'APRIL'
		5 = 'MAY'
		6 = 'JUNE'
		7 = 'JULY'
		8 = 'AUGUST'
		9 = 'SEPTEMBER'
		10 = 'OCTOBER'
		11 = 'NOVEMBER'
		12 = 'DECEMBER';
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'GROSS GUARANTY VOLUME FYTD';
TITLE2 "FISCAL YEAR &FY";
FOOTNOTE   'JOB = UTLWQ30  	 REPORT = ULWQ30.LWQ30R2';
PROC PRINT NOOBS SPLIT='/' DATA=GGA_SMRY WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT MO MNUCS. BORS LOANS COMMA8. GGA DOLLAR18.2;
VAR MO LOANS BORS GGA;
LABEL MO = 'MONTH'
	LOANS = '#/LOANS'
	BORS = '#/BORROWERS'
	GGA = '$/GUARANTY';
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE*/
/*DATA _NULL_;*/
/*	FILE 'T:\SAS\GrossGuarantyVolumeFYTD.Detail.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;*/
/*	IF _N_ = 1 THEN        */
/*		DO;*/
/*			PUT "DF_PRS_ID_BR,CLUID,AD_PRC,AA_GTE_LON_AMT";*/
/*		END;*/
/*	SET  WORK.GGVFYTD ;*/
/*	   FORMAT CLUID $19. ;*/
/*	   FORMAT AA_GTE_LON_AMT 10.2 ;*/
/*	   FORMAT AC_PRC_STA $1. ;*/
/*	   FORMAT AD_PRC MMDDYY10. ;*/
/*	DO;*/
/*	   PUT DF_PRS_ID_BR $ @;*/
/*	   PUT CLUID $ @;*/
/*	   PUT AD_PRC @;*/
/*	   PUT AA_GTE_LON_AMT ;*/
/*	END;*/
/*RUN;*/
