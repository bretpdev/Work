/*

OUTSTANDING CLAIMS

Lists claims logged on but not paid.

*/


/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWC01.LWC01R2";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE OSCLM AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.bf_ssn						AS SSN,
	A.af_apl_id||A.af_apl_id_sfx	AS CLID,
	A.if_ops_ldr					AS HOLDER,
	A.lf_cur_lon_ser_agy			AS SERVICER,
	C.ac_lon_typ					AS LOAN_TYPE,
	A.bd_clm_pkg_rcv				AS RECEIVED,
	A.lc_pcl_rea					AS CLAIM_TYPE,
	RTRIM(B.dm_prs_lst)||', '||RTRIM(B.dm_prs_1)	AS NAME
FROM	OLWHRM1.DC01_LON_CLM_INF A INNER JOIN
		OLWHRM1.PD01_PDM_INF B ON
			A.bf_ssn = B.df_prs_id INNER JOIN
		OLWHRM1.GA10_LON_APP C ON
			A.af_apl_id = C.af_apl_id AND
			A.af_apl_id_sfx = C.af_apl_id_sfx
WHERE	A.lc_sta_dc10 = '01' AND
		A.bd_clm_pkg_rcv IS NOT NULL AND
		A.bd_clm_pkg_rtn IS NULL

);
DISCONNECT FROM DB2;

DATA OSCLM;
	SET OSCLM;
	IF LOAN_TYPE IN ('SF', 'SU') THEN LOAN_TYPE = 'GL';
	DAYS = TODAY() - RECEIVED;
RUN;
PROC SORT DATA=OSCLM;
BY CLAIM_TYPE DAYS SSN;
RUN;
ENDRSUBMIT;
/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS PAGENO=1 LS=132;
PROC PRINT NOOBS SPLIT='/' DATA=WORKLOCL.OSCLM N='TOTAL LOANS FOR CLAIM TYPE = ';
BY CLAIM_TYPE;
VAR RECEIVED
	DAYS
	CLAIM_TYPE
	SSN
	NAME
	CLID
	HOLDER
	SERVICER
	LOAN_TYPE;
LABEL	CLID = 'UNIQUE ID'
		CLAIM_TYPE = 'CLAIM TYPE'
		LOAN_TYPE  = 'LOAN TYPE';
FORMAT RECEIVED MMDDYY10.;
TITLE	'OUTSTANDING CLAIMS';
FOOTNOTE 'JOB = UTLWC01     REPORT = ULWC01.LWC01R2';
RUN;
