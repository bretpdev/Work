/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWM40.LWM40RZ";
FILENAME REPORT2 "&RPTLIB/ULWM40.LWM40R2";
FILENAME REPORT3 "&RPTLIB/ULWM40.LWM40R3";
FILENAME REPORT4 "&RPTLIB/ULWM40.LWM40R4";
FILENAME REPORT5 "&RPTLIB/ULWM40.LWM40R5";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT; 
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT D.DF_SPE_ACC_ID
	,D.DF_PRS_ID
	,D.DC_ADR
	,TRIM(D.DM_PRS_1) || ' ' || TRIM(D.DM_PRS_LST) AS NAME
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP
	,(A.LA_CLM_PRI + A.LA_CLM_INT - A.LA_PRI_COL + A.LA_INT_ACR + C.LA_CLM_INT_ACR 
		- A.LA_INT_COL + A.LA_LEG_CST_ACR - A.LA_LEG_CST_COL + A.LA_OTH_CHR_ACR
		- A.LA_OTH_CHR_COL + A.LA_COL_CST_ACR - A.LA_COL_CST_COL 
		+ C.LA_CLM_PRJ_COL_CST) AS OUTSTANDING
	,(A.LA_COL_CST_ACR - A.LA_COL_CST_COL) / 5 AS WAIVED
	,CASE 
		WHEN DAYS(CURRENT DATE) - DAYS(A.LD_LDR_POF) BETWEEN 60 AND 239 THEN 'R2'
		WHEN DAYS(CURRENT DATE) - DAYS(A.LD_LDR_POF) BETWEEN 240 AND 419 THEN 'R3'
		WHEN DAYS(CURRENT DATE) - DAYS(A.LD_LDR_POF) BETWEEN 420 AND 599 THEN 'R4'
		WHEN DAYS(CURRENT DATE) - DAYS(A.LD_LDR_POF) >= 600 THEN 'R5'
	END AS LD_LDR_POF
	,F.PF_ACT
		,A.LD_LST_PAY
		,B.LC_TRX_TYP
		 ,A.LI_DFL_LTR_1_IVL
FROM OLWHRM1.PD01_PDM_INF D
INNER JOIN OLWHRM1.DC01_LON_CLM_INF A
	ON D.DF_PRS_ID = A.BF_SSN
INNER JOIN OLWHRM1.DC02_BAL_INT C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT B.LC_TRX_TYP
			,B.AF_APL_ID
			,B.AF_APL_ID_SFX
		FROM OLWHRM1.DC11_LON_FAT B
		WHERE DAYS(B.LD_TRX_EFF) = (SELECT MAX(DAYS(E.LD_TRX_EFF)) AS LD_TRX_EFF
					FROM OLWHRM1.DC11_LON_FAT E
					WHERE B.AF_APL_ID = E.AF_APL_ID
						AND B.AF_APL_ID_SFX = E.AF_APL_ID_SFX
					GROUP BY E.AF_APL_ID
						,E.AF_APL_ID_SFX)
			AND B.LC_TRX_TYP IN ('BR','CS','EP')
)	B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT F.DF_PRS_ID
			,SUBSTR(F.PF_ACT,5,1) AS PF_ACT
		FROM OLWHRM1.AY01_BR_ATY F 
		WHERE ((F.PF_ACT IN ('DLMN1') AND DAYS(CURRENT DATE) - DAYS(BD_ATY_PRF) BETWEEN 60 AND 239)
			OR (F.PF_ACT IN ('DLMN2') AND DAYS(CURRENT DATE) - DAYS(BD_ATY_PRF) BETWEEN 240 AND 419)
			OR (F.PF_ACT IN ('DLMN3') AND DAYS(CURRENT DATE) - DAYS(BD_ATY_PRF) BETWEEN 420 AND 599)
			OR (F.PF_ACT IN ('DLMN4') AND DAYS(CURRENT DATE) - DAYS(BD_ATY_PRF) >= 600))
) F
	ON A.BF_SSN = F.DF_PRS_ID
WHERE A.LC_STA_DC10 = '03'
	AND A.LC_AUX_STA = ''
	AND A.LC_PCL_REA IN ('DF','DQ','DB')
	AND A.LD_CLM_ASN_DOE IS NULL
	AND (COALESCE(A.LA_CLM_PRI,0) + COALESCE(A.LA_CLM_INT,0) - COALESCE(A.LA_PRI_COL,0) + COALESCE(A.LA_INT_ACR,0) 
	+ COALESCE(C.LA_CLM_INT_ACR,0) - COALESCE(A.LA_INT_COL,0) + COALESCE(A.LA_LEG_CST_ACR,0) 
	- COALESCE(A.LA_LEG_CST_COL,0) + COALESCE(A.LA_OTH_CHR_ACR,0) - COALESCE(A.LA_OTH_CHR_COL,0) 
	+ COALESCE(A.LA_COL_CST_ACR,0) - COALESCE(A.LA_COL_CST_COL,0) + COALESCE(LA_CLM_PRJ_COL_CST,0)) > 25
	AND A.LI_COL_CST_ASS = 'Y'
		AND A.LA_COL_CST_ACR
		-A.LA_COL_CST_COL
		+C.LA_CLM_PRJ_COL_CST > 25

	AND A.LI_DFL_LTR_1_IVL != 'Y'
	AND (A.LD_LST_PAY IS NULL 
		OR B.LC_TRX_TYP NOT IN ('BR','CS','EP'))
	AND DI_VLD_ADR = 'Y'
	AND DC_ADR <> 'T'
	AND A.LC_GRN != '02'
	AND A.BF_SSN NOT IN (SELECT DF_PRS_ID_BR
						FROM OLWHRM1.LA11_LEG_ACT_ATY
						WHERE BC_LEG_ACT_ATY_TYP IN ('GG','JD')
							AND BC_ATY_WDR_REA = '')
	AND A.BF_SSN NOT IN (SELECT DF_PRS_ID_BR
						FROM OLWHRM1.CT30_CALL_QUE 
						WHERE IF_WRK_GRP = 'WG000016')
 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/

ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;
PROC SORT DATA=DEMO NODUPKEY;
	BY DF_SPE_ACC_ID;
RUN;

*CALCULATE KEYLINE;
DATA DEMO (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET DEMO;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

%MACRO REPORTS(R,X);
DATA _NULL_;
SET DEMO ;
WHERE LD_LDR_POF = "R&R" AND PF_ACT ^= &X ;
FILE REPORT&R DELIMITER=',' DSD DROPOVER LRECL=32767;
PAYOFF = OUTSTANDING - WAIVED;
DEADLINE = TODAY() + 45;
CCC = 'MA2329';
FORMAT DEADLINE MMDDYY10. ;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNTNUMBER,NAME,STREET1,STREET2,CITY,STATE,ZIP,OUTSTANDING,WAIVED,PAYOFF,DEADLINE,STATECODE,CCC,ACSKEY";
END;
DO;
   PUT DF_SPE_ACC_ID @;
   PUT NAME @;
   PUT DX_STR_ADR_1	@;
   PUT DX_STR_ADR_2 @;
   PUT DM_CT @;
   PUT DC_DOM_ST @;
   PUT DF_ZIP @;
   PUT OUTSTANDING @;
   PUT WAIVED @;
   PUT PAYOFF @;
   PUT DEADLINE @;
   PUT DC_DOM_ST @;
   PUT CCC @;
   PUT ACSKEY $;
END;
RUN;
%MEND;
%REPORTS(2,'1');
%REPORTS(3,'2');
%REPORTS(4,'3');
%REPORTS(5,'4');
