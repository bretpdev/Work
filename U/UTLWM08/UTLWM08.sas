/*================================*/
/*UTLWM08 Activity History Entries*/
/*================================*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWM08.LWM08RZ";
FILENAME REPORT2 "&RPTLIB/ULWM08.LWM08R2";
FILENAME REPORT3 "&RPTLIB/ULWM08.LWM08R3";
FILENAME REPORT4 "&RPTLIB/ULWM08.LWM08R4";

DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.BF_LST_USR_AY01	AS USER_ID,
	A.PF_ACT			AS ACTION_CODE,
	RTRIM(B.PX_ACT_DSC)	AS ACTION_CODE_DESCRIPTION,  
	A.BC_ATY_TYP		AS ACTIVITY_TYPE,
	A.BC_ATY_CNC_TYP	AS CONTACT_TYPE, 
	D.DF_SPE_ACC_ID		AS ACCTNUM,
	A.BD_ATY_PRF		AS DATE,
	A.BX_CMT			AS COMMENTS,
	CASE 
		WHEN HOUR(A.BT_ATY_PRF) < 19 
		THEN 'DAY  '
		ELSE 'NIGHT'
	END					AS TIME		
FROM OLWHRM1.AY01_BR_ATY A
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.DF_PRS_ID = D.DF_PRS_ID 
INNER JOIN OLWHRM1.AC01_ACT B
	ON A.PF_ACT = B.PF_ACT
INNER JOIN OLWHRM1.US10_USR_PFL C
	ON A.BF_LST_USR_AY01 = C.PF_USR
WHERE DAYS(A.BD_ATY_PRF) = DAYS(CURRENT DATE) - 1
AND C.PC_SER_DPT = 'LMN'
AND C.PD_USR_IN_DPT_END IS NULL

);

CREATE TABLE DEMO2 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.IF_LST_USR_AY05	AS USER_ID,
	A.PF_ACT			AS ACTION_CODE,
	RTRIM(B.PX_ACT_DSC)	AS ACTION_CODE_DESCRIPTION,  
	A.IC_ATY_TYP		AS ACTIVITY_TYPE,
	A.IC_ATY_CNC_TYP	AS CONTACT_TYPE, 
	A.IF_IST			AS ACCTNUM,
	A.ID_ATY_PRF		AS DATE,
	A.IX_CMT			AS COMMENTS,
	CASE 
		WHEN HOUR(A.IF_LST_DTS_AY05) < 19
		THEN 'DAY  '
		ELSE 'NIGHT'
	END					AS TIME
FROM OLWHRM1.AY05_IST_ATY A 
INNER JOIN OLWHRM1.AC01_ACT B
	ON A.PF_ACT = B.PF_ACT
INNER JOIN OLWHRM1.US10_USR_PFL C
	ON A.IF_LST_USR_AY05 = C.PF_USR
WHERE DAYS(A.ID_ATY_PRF) = DAYS(CURRENT DATE) - 1
AND A.IC_IST_TYP = '006'
AND C.PC_SER_DPT = 'LMN'
AND C.PD_USR_IN_DPT_END IS NULL
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWM08.LWM08RZ);
QUIT;
ENDRSUBMIT;
DATA DEMO;SET WORKLOCL.DEMO;RUN;
DATA DEMO2;SET WORKLOCL.DEMO2;RUN;

DATA DEMO;
SET DEMO DEMO2;
RUN;

PROC SORT DATA = DEMO;
BY USER_ID DESCENDING ACTION_CODE;
RUN;

PROC SQL;
CREATE TABLE DEMOSUM AS
SELECT 
	USER_ID
	,ACTION_CODE
	,ACTION_CODE_DESCRIPTION
	,COUNT(ACTION_CODE) AS TTL_BY_CD
FROM DEMO
WHERE ACTION_CODE <> 'AD101'
GROUP BY USER_ID, ACTION_CODE, ACTION_CODE_DESCRIPTION;

PROC SORT DATA=DEMOSUM;
BY USER_ID ACTION_CODE;
RUN;

PROC SQL;
CREATE TABLE BYTIME AS
SELECT DISTINCT
	(SELECT COUNT(ACTION_CODE)
	 FROM DEMO
	 WHERE TIME = 'DAY'
	 AND ACTION_CODE <> 'AD101') AS DAY
   ,(SELECT COUNT(ACTION_CODE)
	 FROM DEMO
	 WHERE TIME = 'NIGHT'
	 AND ACTION_CODE <> 'AD101') AS NIGHT
FROM DEMO;
QUIT;
RUN;

OPTIONS ORIENTATION = LANDSCAPE PAGENO=1;
OPTIONS PS = 39 LS = 128 SYMBOLGEN NODATE CENTER ;
TITLE	'LOAN MANAGEMENT SERVICES';
TITLE2	"ACTIVITY HISTORY ENTRIES - ONELINK";
TITLE3 "&RUNDATE - &RUNTIME";
FOOTNOTE  'JOB = UTLWM08     REPORT = ULWM08.LWM08R2';
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
PROC PRINT SPLIT='/' DATA=DEMO N="TOTAL:  ";
BY USER_ID;
LABEL USER_ID = 'USER ID'
ACTION_CODE = 'ACTION CODE'
ACTION_CODE_DESCRIPTION = 'ACTION CODE DESCRIPTION'
ACTIVITY_TYPE = 'ACTIVITY TYPE'
CONTACT_TYPE = 'CONTACT TYPE'
ACCTNUM = 'ACCOUNT NUMBER'
DATE = 'DATE'
COMMENTS = 'COMMENTS';
VAR USER_ID ACTION_CODE ACTION_CODE_DESCRIPTION ACTIVITY_TYPE CONTACT_TYPE ACCTNUM DATE;
FORMAT DATE MMDDYY.;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS PAGENO=1;
PROC PRINT SPLIT='/' DATA=DEMO N="TOTAL:  ";
BY USER_ID;
LABEL COMMENTS = 'COMMENTS';
VAR COMMENTS;
FORMAT COMMENTS $122.;
TITLE	'LOAN MANAGEMENT SERVICES';
TITLE2	"ONELINK COMMENTS";
TITLE3 "&RUNDATE - &RUNTIME";
FOOTNOTE  'JOB = UTLWM08     REPORT = ULWM08.LWM08R3';
RUN;

PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS = 52 LS = 96 PAGENO=1;
OPTIONS PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=DEMOSUM;
BY 		USER_ID;
SUM 	TTL_BY_CD;
LABEL 	USER_ID = 'USER ID'
		ACTION_CODE = 'ACTION CODE'
		ACTION_CODE_DESCRIPTION = 'ACTION CODE DESCRIPTION'
		TTL_BY_CD = 'TOTAL USED';
VAR 	ACTION_CODE ACTION_CODE_DESCRIPTION TTL_BY_CD;
TITLE	'LOAN MANAGEMENT SERVICES';
TITLE2	"ACTION CODE USAGE SUMMARY";
TITLE3 "&RUNDATE - &RUNTIME";
FOOTNOTE  'JOB = UTLWM08     REPORT = ULWM08.LWM08R4';
RUN;

PROC PRINTTO;
RUN;
