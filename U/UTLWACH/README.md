/*DPA (ACH)*/
/*This program creates reports which list borrowers making payments by direct payment.*/
/*Collections uses this report to review accounts before the monthly draw.*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWACH.LWACHRZ";
FILENAME REPORT2 "&RPTLIB/ULWACH.LWACHR2";
FILENAME REPORT3 "&RPTLIB/ULWACH.LWACHR3";
FILENAME REPORT4 "&RPTLIB/ULWACH.LWACHR4";


LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DPA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.BF_SSN								AS SSN
	,RTRIM(A.AF_APL_ID)||(A.AF_APL_ID_SFX) 	AS CLUID
	,D.LC_BKR_STA							AS BNK_STA
	,A.LD_MIN_PAY_BEG
	,A.LD_MIN_PAY_END
	,A.LA_MIN_PAY
	,A.LA_PAY_XPC	 
	,RTRIM(B.DM_PRS_LST)||', '||B.DM_PRS_1	AS NAME
	,LA_CLM_BAL								AS BAL
	,DAY(A.LD_NXT_PAY_DUE) 					AS "DAY TOTALS"
	,B.DF_SPE_ACC_ID

FROM OLWHRM1.DC01_LON_CLM_INF A 
INNER JOIN OLWHRM1.PD01_PDM_INF B 
	ON A.BF_SSN = B.DF_PRS_ID 
LEFT OUTER JOIN	OLWHRM1.DC02_BAL_INT C 
	ON A.AF_APL_ID = C.AF_APL_ID 
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX 
LEFT OUTER JOIN	OLWHRM1.DC18_BKR D 
	ON A.AF_APL_ID = D.AF_APL_ID 
	AND	A.AF_APL_ID_SFX = D.AF_APL_ID_SFX 
	AND	D.LC_BKR_STA IN ('01', '07')

WHERE A.LI_DIR_PAY = 'Y' 
AND (
		(A.LC_STA_DC10 = '03' AND A.LC_REA_CLM_ASN_DOE <> '03') 
	 	 OR 
		(A.LC_STA_DC10 = '04' AND DAYS(CURRENT DATE) - 45 < DAYS(A.LD_STA_UPD_DC10))
	)
);
DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
ENDRSUBMIT;

DATA DPA;
SET WORKLOCL.DPA;
RUN;

PROC SORT DATA=DPA;BY SSN;RUN;

DATA DPA1A DPA2A;
SET DPA;
IF TODAY() >= LD_MIN_PAY_BEG AND TODAY() <= LD_MIN_PAY_END THEN OUTPUT DPA1A;
ELSE OUTPUT DPA2A;
RUN;

%MACRO PCALC(TBL,PY_VAR,QTBL);
PROC SQL;
CREATE TABLE &TBL AS 
SELECT SSN
	,BNK_STA
	,NAME
	,DAY_TOTALS
	,SUM(BAL) AS BAL
	,SUM(&PY_VAR) AS PMT
	,DF_SPE_ACC_ID
FROM &QTBL
GROUP BY SSN
	,BNK_STA
	,NAME
	,DAY_TOTALS
	,DF_SPE_ACC_ID;
QUIT;
RUN;
%MEND PCALC;
%PCALC(DPA1,LA_MIN_PAY,DPA1A);
%PCALC(DPA2,LA_PAY_XPC,DPA2A);

DATA DPA;
SET DPA1 DPA2;
RUN;

PROC SQL;
CREATE TABLE DPAOPEN AS
SELECT *
FROM DPA
WHERE BAL IS NOT NULL;
PROC SQL;
CREATE TABLE DPACLOSED AS
SELECT *
FROM DPA
WHERE BAL IS NULL;

PROC SQL;
CREATE TABLE DPALOW AS
SELECT *
FROM DPA
WHERE	BAL < PMT + 10 AND
		BAL IS NOT NULL;	/* LIST BORROWERS WHOSE ACCOUNT WILL OVERPAY WITH THE NEXT DRAW. */
PROC SORT DATA=DPAOPEN;
BY DAY_TOTALS DF_SPE_ACC_ID;
RUN;
PROC SORT DATA=DPACLOSED;
BY DAY_TOTALS DF_SPE_ACC_ID;
RUN;
PROC SORT DATA=DPALOW;
BY DAY_TOTALS BAL DF_SPE_ACC_ID;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW; 
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER BYLINE PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=DPAOPEN WIDTH = MIN;
VAR DF_SPE_ACC_ID
	NAME
	BAL
	PMT
	BNK_STA
	DAY_TOTALS;
FORMAT BAL DOLLAR12.2 PMT DOLLAR10.2 ;
LABEL	BAL = 'BALANCE'
		PMT = 'BILLED AMOUNT'
		BNK_STA = 'BANKRUPTCY STATUS'
		DAY_TOTALS = 'DUE DATE'
		DF_SPE_ACC_ID = 'ACCT #';
BY DAY_TOTALS;
SUM PMT;
TITLE1 'OPEN DPA ACCOUNTS';
TITLE2 'BY SSN';
FOOTNOTE  'JOB = UTLWACH     REPORT = ULWACH.LWACHR2';
RUN;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER BYLINE PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=DPACLOSED WIDTH = MIN;
VAR DF_SPE_ACC_ID
	NAME
	DAY_TOTALS;
LABEL DF_SPE_ACC_ID = 'ACCT #' DAY_TOTALS = 'DUE DATE';
BY DAY_TOTALS;
*SUM PMT;
TITLE1 'CLOSED DPA ACCOUNTS';
TITLE2 'BY ACCT #';
FOOTNOTE  'JOB = UTLWACH     REPORT = ULWACH.LWACHR3';
RUN;
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER BYLINE PAGENO=1;
PROC PRINT NOOBS SPLIT='/' DATA=DPALOW WIDTH = MIN;
VAR DF_SPE_ACC_ID
	NAME
	BAL
	PMT
	BNK_STA
	DAY_TOTALS;
FORMAT BAL DOLLAR12.2 PMT DOLLAR10.2 ;
LABEL	BAL = 'BALANCE'
		PMT = 'BILLED AMOUNT'
		BNK_STA = 'BANKRUPTCY STATUS'
		DAY_TOTALS = 'DUE DATE'
		DF_SPE_ACC_ID = 'ACCT #';
BY DAY_TOTALS;
SUM PMT;
TITLE1 'LOW BALANCE DPA ACCOUNTS';
TITLE2 'BY BALANCE';
FOOTNOTE  'JOB = UTLWACH     REPORT = ULWACH.LWACHR4';
RUN;

PROC PRINTTO;
RUN;
