/*UTLWQ23 - DELINQUENCY BY SCHOOLS - ALL SCHOOLS REPORT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = C:/WINDOWS/TEMP;
FILENAME REPORT2 "&RPTLIB/ULWQ23.LWQ23R2";
FILENAME REPORTZ "&RPTLIB/ULWQ23.LWQ23RZ";

DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DET AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
		A.BF_SSN
		,A.LF_DOE_SCL_ORG
		,C.IM_SCL_FUL
		,A.LA_CUR_PRI + B.WA_TOT_BRI_OTS AS BALANCE
		,E.LD_DLQ_OCC
FROM 	OLWHRM1.LN10_LON A 
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B ON
			A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
			AND A.LC_STA_LON10 = 'R'
			AND B.WC_DW_LON_STA NOT IN ('01','02','04','05','12','17','19','22','23','88','98')
		INNER JOIN OLWHRM1.SC10_SCH_DMO C ON
			A.LF_DOE_SCL_ORG = C.IF_DOE_SCL
		INNER JOIN (SELECT	DISTINCT X.BF_SSN
					FROM	OLWHRM1.LN65_LON_RPS X
					WHERE X.LC_STA_LON65 = 'A'
					) D 
			ON A.BF_SSN = D.BF_SSN
		LEFT OUTER JOIN (SELECT	DISTINCT W.BF_SSN, LN_SEQ, MAX(W.LD_DLQ_OCC) AS LD_DLQ_OCC
					  	 FROM	OLWHRM1.LN16_LON_DLQ_HST W
						 WHERE	DAYS(CURRENT DATE) - DAYS(W.LD_DLQ_OCC) >= 30
						 		AND W.LC_STA_LON16 = '1'
						 GROUP BY W.BF_SSN, LN_SEQ) E 
			ON D.BF_SSN = E.BF_SSN
			AND E.LN_SEQ = A.LN_SEQ
WHERE	A.LA_CUR_PRI + B.WA_TOT_BRI_OTS > 0
AND DAYS(CURRENT DATE) - DAYS(B.WD_LON_RPD_SR) >= 75
AND A.LI_CON_PAY_STP_PUR <> 'Y'
ORDER BY LF_DOE_SCL_ORG
);
DISCONNECT FROM DB2;

ENDRSUBMIT;
DATA DET; SET WORKLOCL.DET; RUN;

PROC SQL;
CREATE TABLE DBSASR AS 
SELECT 	A.LF_DOE_SCL_ORG
		,A.IM_SCL_FUL
		,COUNT(DISTINCT A.BF_SSN) AS TOT_BWR_CT
		,COALESCE(B.BALANCE,0) AS BALANCE
		,COALESCE(B.DLQ_BWR_CT,0) AS DLQ_BWR_CT
FROM DET A
LEFT OUTER JOIN (
	SELECT 	LF_DOE_SCL_ORG
			,IM_SCL_FUL
			,SUM(BALANCE) AS BALANCE
			,COUNT(DISTINCT BF_SSN) AS DLQ_BWR_CT
	FROM 	DET
	WHERE	LD_DLQ_OCC IS NOT NULL
	GROUP BY LF_DOE_SCL_ORG, IM_SCL_FUL
	) B
	ON A.LF_DOE_SCL_ORG = B.LF_DOE_SCL_ORG
GROUP BY A.LF_DOE_SCL_ORG
	,A.IM_SCL_FUL
ORDER BY A.LF_DOE_SCL_ORG
;
QUIT;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=127 PS=39;
PROC REPORT DATA=DBSASR NOWD SPACING=1 HEADSKIP SPLIT='/';
TITLE "IN-HOUSE SERVICING";
TITLE2 "SCHOOL DELINQUENCY REPORT";
TITLE3 "&RUNDATE";
FOOTNOTE  'JOB = UTLWQ23     REPORT = ULWQ23.LWQ23R2';
COLUMN LF_DOE_SCL_ORG IM_SCL_FUL DLQ_BWR_CT TOT_BWR_CT PCT_BWR BALANCE AVG_BAL ;
DEFINE LF_DOE_SCL_ORG / GROUP DISPLAY "SCHOOL/ID";
DEFINE IM_SCL_FUL / DISPLAY "SCHOOL NAME";
DEFINE DLQ_BWR_CT / ANALYSIS SUM "BORROWERS/DELINQUENT/30+ DAYS" WIDTH = 15 FORMAT=COMMA5.;
DEFINE TOT_BWR_CT / ANALYSIS SUM "BORROWERS/IN REPAYMENT/30+ DAYS BEYOND/FIRST DUE DATE" WIDTH = 16 FORMAT=COMMA5.;
DEFINE PCT_BWR / COMPUTED "DELINQUENCY/PERCENTAGE" FORMAT=PERCENT8.1 WIDTH=15;
DEFINE BALANCE / ANALYSIS NOPRINT;
DEFINE AVG_BAL / COMPUTED "AVERAGE/DELINQUENT/ACCOUNT/BALANCE" FORMAT=DOLLAR15.2;
COMPUTE AVG_BAL;
	IF BALANCE.SUM = 0 THEN DO;
		AVG_BAL = 0;
		END;
	ELSE
		AVG_BAL = BALANCE.SUM / DLQ_BWR_CT.SUM;
	ENDCOMP;
COMPUTE PCT_BWR;
	PCT_BWR = DLQ_BWR_CT.SUM / TOT_BWR_CT.SUM;
	ENDCOMP;
RBREAK AFTER / SUMMARIZE SKIP OL;
RUN;
PROC PRINTTO;
RUN;
/*PROC EXPORT DATA=WORK.DET*/
/*            OUTFILE= "T:\SAS\DET.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
