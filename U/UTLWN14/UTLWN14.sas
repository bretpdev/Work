*-------------------------------------------------*
|UTLWN14 CANCELLATION - REFUND VOLUME FOR LENDERS |
*-------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWN14.LWN14R2";
FILENAME REPORT3 "&RPTLIB/ULWN14.LWN14R3";
FILENAME REPORT4 "&RPTLIB/ULWN14.LWN14R4";
FILENAME REPORT5 "&RPTLIB/ULWN14.LWN14R5";
FILENAME REPORT6 "&RPTLIB/ULWN14.LWN14R6";
FILENAME REPORT7 "&RPTLIB/ULWN14.LWN14R7";
FILENAME REPORT8 "&RPTLIB/ULWN14.LWN14R8";
FILENAME REPORT9 "&RPTLIB/ULWN14.LWN14R9";
FILENAME REPORT10 "&RPTLIB/ULWN14.LWN14R10";
FILENAME REPORT11 "&RPTLIB/ULWN14.LWN14R11";
FILENAME REPORT12 "&RPTLIB/ULWN14.LWN14R12";
FILENAME REPORT13 "&RPTLIB/ULWN14.LWN14R13";
FILENAME REPORT14 "&RPTLIB/ULWN14.LWN14R14";
FILENAME REPORT15 "&RPTLIB/ULWN14.LWN14R15";
FILENAME REPORT16 "&RPTLIB/ULWN14.LWN14R16";
FILENAME REPORT17 "&RPTLIB/ULWN14.LWN14R17";
FILENAME REPORT18 "&RPTLIB/ULWN14.LWN14R18";
FILENAME REPORT19 "&RPTLIB/ULWN14.LWN14R19";
FILENAME REPORT20 "&RPTLIB/ULWN14.LWN14R20";
FILENAME REPORT21 "&RPTLIB/ULWN14.LWN14R21";
FILENAME REPORT22 "&RPTLIB/ULWN14.LWN14R22";
FILENAME REPORT23 "&RPTLIB/ULWN14.LWN14R23";
FILENAME REPORT24 "&RPTLIB/ULWN14.LWN14R24";
FILENAME REPORT25 "&RPTLIB/ULWN14.LWN14R25";
FILENAME REPORT26 "&RPTLIB/ULWN14.LWN14R26";
FILENAME REPORT27 "&RPTLIB/ULWN14.LWN14R27";
FILENAME REPORT28 "&RPTLIB/ULWN14.LWN14R28";
FILENAME REPORT29 "&RPTLIB/ULWN14.LWN14R29";
FILENAME REPORTZ "&RPTLIB/ULWN14.LWN14RZ";
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'END'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CRVFL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN||CHAR(A.LN_SEQ) AS LN_ID
	,B.PC_FAT_TYP||B.PC_FAT_SUB_TYP AS TRX
	,COALESCE(ABS(B.LA_FAT_CUR_PRI),0) AS LA_FAT_CUR_PRI
	,A.LF_LON_CUR_OWN
	,A.IC_LON_PGM
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN90_FIN_ATY B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE B.PC_FAT_TYP = '10'
	AND B.PC_FAT_SUB_TYP IN ('40','45')
	AND B.LC_FAT_REV_REA = ''
	AND B.LC_STA_LON90 = 'A'
	AND B.LD_FAT_APL BETWEEN &BEGIN AND &END
FOR READ ONLY WITH UR
);

CREATE TABLE LNDRS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT IF_DOE_LDR
	,IM_LDR_SHO
FROM OLWHRM1.LR10_LDR_DMO 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWN14.LWN14RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA CRVFL;SET WORKLOCL.CRVFL;RUN;
DATA LNDRS;SET WORKLOCL.LNDRS;RUN;

PROC SQL;
/*CREATE TABLE OF LENDERS AND EXISTING LOAN TYPES*/
CREATE TABLE LDNR_LON AS 
SELECT DISTINCT A.IF_DOE_LDR
	,A.IM_LDR_SHO
	,B.IC_LON_PGM
FROM LNDRS A
LEFT OUTER JOIN CRVFL B
	ON A.IF_DOE_LDR = B.LF_LON_CUR_OWN
;
/*SUMMARIZE DATA FOR EACH LENDER AND LOAN TYPE*/
CREATE TABLE CRVFL2 AS 
SELECT DISTINCT A.IF_DOE_LDR
	,A.IM_LDR_SHO
	,A.IC_LON_PGM
	,COALESCE(B._1045_COUNT,0) AS _1045_COUNT
	,COALESCE(B.LA_FIN_TRX_DSB_CAN,0) AS LA_FIN_TRX_DSB_CAN
	,COALESCE(C._1040_COUNT,0) AS _1040_COUNT
	,COALESCE(C.LA_FAT_CUR_PRI,0) AS LA_FAT_CUR_PRI
FROM LDNR_LON A
LEFT OUTER JOIN (
	SELECT LF_LON_CUR_OWN
		,IC_LON_PGM
		,COUNT(*) AS _1045_COUNT
		,SUM(LA_FAT_CUR_PRI) AS LA_FIN_TRX_DSB_CAN
	FROM CRVFL
	WHERE TRX = '1045'
	GROUP BY LF_LON_CUR_OWN
		,IC_LON_PGM
	) B 
	ON A.IF_DOE_LDR = B.LF_LON_CUR_OWN
	AND A.IC_LON_PGM = B.IC_LON_PGM
LEFT OUTER JOIN (
	SELECT LF_LON_CUR_OWN
		,IC_LON_PGM
		,COUNT(*) AS _1040_COUNT
		,SUM(LA_FAT_CUR_PRI) AS LA_FAT_CUR_PRI
	FROM CRVFL
	WHERE TRX = '1040'
	GROUP BY LF_LON_CUR_OWN
		,IC_LON_PGM
	) C 
	ON A.IF_DOE_LDR = C.LF_LON_CUR_OWN
	AND A.IC_LON_PGM = C.IC_LON_PGM
;
QUIT;

%MACRO PRINT_REP(LDR,RNO);
DATA REP;
SET CRVFL2;
WHERE IF_DOE_LDR = "&LDR";
CALL SYMPUT('LDR_NM',TRIM(IM_LDR_SHO));
RUN;
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'CANCELLATION/REFUND VOLUME FOR LENDER';
TITLE2 "&LDR_NM" ;
FOOTNOTE  "JOB = UTLWN14  	 REPORT = ULWN14.LWN14R&RNO";
PROC PRINT NOOBS SPLIT='/' DATA=REP WIDTH=UNIFORM WIDTH=MIN;
FORMAT _1045_COUNT _1040_COUNT COMMA7. LA_FIN_TRX_DSB_CAN LA_FAT_CUR_PRI 18.2;
VAR  IC_LON_PGM _1045_COUNT LA_FIN_TRX_DSB_CAN _1040_COUNT LA_FAT_CUR_PRI;
LABEL IC_LON_PGM = 'LOAN TYPE'
	_1045_COUNT = 'TOTAL # LOANS CANCELLED'
	LA_FIN_TRX_DSB_CAN = 'TOTAL AMOUNT CANCELLED'
	_1040_COUNT = 'TOTAL # LOANS REFUNDED'
	LA_FAT_CUR_PRI = 'TOTAL AMOUNT REFUNDED';
SUM _1045_COUNT LA_FIN_TRX_DSB_CAN _1040_COUNT LA_FAT_CUR_PRI;
RUN;
PROC PRINTTO;
RUN;
%MEND PRINT_REP;

%PRINT_REP(817575,2);
%PRINT_REP(822373,3);
%PRINT_REP(833577,4);
%PRINT_REP(828476,5);
%PRINT_REP(819628,6);
%PRINT_REP(833828,7);
%PRINT_REP(817440,8);
%PRINT_REP(828476,9);
%PRINT_REP(817545,10);
%PRINT_REP(834265,11);
%PRINT_REP(830791,12);
%PRINT_REP(813760,13);
%PRINT_REP(834370,14);
%PRINT_REP(817546,15);
%PRINT_REP(834122,16);
%PRINT_REP(832241,17);
%PRINT_REP(820200,18);
%PRINT_REP(830132,19);
%PRINT_REP(829123,20);
%PRINT_REP(811698,21);
%PRINT_REP(830146,22);
%PRINT_REP(829158,23);
%PRINT_REP(813894,24);
%PRINT_REP(817455,25);
%PRINT_REP(834437,26);
%PRINT_REP(834396,27);
%PRINT_REP(82847601,28);
%PRINT_REP(834493,29);

/*DETAIL FILE*/
PROC EXPORT DATA=CRVFL
            OUTFILE= "T:\SAS\UTLWN14_Detail.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;
