/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
FILENAME REPORT2 "&RPTLIB/ULWGG2.LWGG2R2";
RSUBMIT;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DAY75 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID	AS SSN
	,C.BC_HLD_REA
	,C.BD_HLD
	,AY01.PF_ACT
	,AY01.BD_ATY_PRF
FROM OLWHRM1.PD01_PDM_INF A 
INNER JOIN OLWHRM1.IN01_LGS_IDM_MST B 
	ON A.BF_EMP_ID_1 = B.IF_IST 
INNER JOIN OLWHRM1.LA10_LEG_ACT C 
	ON A.DF_PRS_ID = C.DF_PRS_ID_BR 
INNER JOIN OLWHRM1.DC01_LON_CLM_INF D 
	ON A.DF_PRS_ID = D.BF_SSN 
INNER JOIN OLWHRM1.DC02_BAL_INT E 
	ON D.AF_APL_ID = E.AF_APL_ID 
	AND D.AF_APL_ID_SFX = E.AF_APL_ID_SFX 
LEFT OUTER JOIN (
	SELECT Z.DF_PRS_ID,
		PF_ACT, 
		MAX(Z.BD_ATY_PRF) AS BD_ATY_PRF
	FROM OLWHRM1.AY01_BR_ATY Z
	WHERE Z.PF_ACT IN ('LUBEI','LSBEI')
	GROUP BY Z.DF_PRS_ID, PF_ACT
) AY01
	ON AY01.DF_PRS_ID = A.DF_PRS_ID
WHERE C.BC_WDR_REA = '' 
	AND C.BC_INA_REA = ''  
	AND C.BC_LEG_ACT_REC_TYP = '1' 		
	AND C.BD_NCP_FWP_LTR_1 is NOT NULL
	AND C.BD_HLD < CURRENT DATE	
	AND DAYS(CURRENT DATE) >= DAYS(C.BD_NCP_FWP_LTR_2) + 7
	AND D.LC_AUX_STA = '' 
	AND	D.LC_STA_DC10 = '03' 
	AND D.LC_GRN = '02' 
	AND E.LA_CLM_BAL > 25 
	AND (DAYS(D.LD_LST_PAY) < DAYS(CURRENT DATE) - 60 OR D.LD_LST_PAY IS NULL) 
	AND NOT EXISTS (SELECT 1
		FROM OLWHRM1.CT30_CALL_QUE X
		WHERE X.DF_PRS_ID_BR = D.BF_SSN
			AND X.IF_WRK_GRP = 'DEMCMP75') 
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA WORK.DAY75; 
	SET WORKLOCL.DAY75; 
RUN;
PROC SORT DATA=DAY75;
	BY SSN;
RUN;

DATA DAY75 (KEEP=SSN);
	SET DAY75 ;
	BY SSN;
	RETAIN A;
	IF FIRST.SSN THEN A=0;
	IF BC_HLD_REA = '' THEN DO;
		IF PF_ACT = 'LUBEI' 
			AND BD_ATY_PRF < TODAY() - 30 THEN A=1;
		IF PF_ACT = 'LSBEI'
			AND BD_ATY_PRF < TODAY() - 90 THEN A=1;
	END;
	ELSE IF PF_ACT = '' THEN A = 1;
	IF LAST.SSN AND A THEN OUTPUT;
RUN;

DATA _NULL_;
SET  WORK.DAY75;
	TARGET_ID = SSN;
	QUEUE_NAME = 'DEMCMP75';
	INSTITUTION_ID = '';
	INSTITUTION_TYPE = '';
	DATE_DUE = '';
	TIME_DUE = '';
	COMMENTS = '';
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
