/*LIBNAME DNFPRQUT DB2 DATABASE=DNFPRQUT OWNER=PKUB;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS12.NWS12RZ";
FILENAME REPORT2 "&RPTLIB/UNWS12.NWS12R2";
FILENAME REPORT3 "&RPTLIB/UNWS12.NWS12R3";
FILENAME REPORT4 "&RPTLIB/UNWS12.NWS12R4";
FILENAME REPORT5 "&RPTLIB/UNWS12.NWS12R5";
FILENAME REPORT6 "&RPTLIB/UNWS12.NWS12R6";
FILENAME REPORT7 "&RPTLIB/UNWS12.NWS12R7";
FILENAME REPORT8 "&RPTLIB/UNWS12.NWS12R8";
FILENAME REPORT9 "&RPTLIB/UNWS12.NWS12R9";
FILENAME REPORT10 "&RPTLIB/UNWS12.NWS12R10";
FILENAME REPORT11 "&RPTLIB/UNWS12.NWS12R11";
FILENAME REPORT12 "&RPTLIB/UNWS12.NWS12R12";
FILENAME REPORT13 "&RPTLIB/UNWS12.NWS12R13";
FILENAME REPORT14 "&RPTLIB/UNWS12.NWS12R14";
FILENAME REPORT15 "&RPTLIB/UNWS12.NWS12R15";
FILENAME REPORT16 "&RPTLIB/UNWS12.NWS12R16";
FILENAME REPORT17 "&RPTLIB/UNWS12.NWS12R17";
FILENAME REPORT18 "&RPTLIB/UNWS12.NWS12R18";

LIBNAME  WORKLOCL  REMOTE  SERVER=LEGEND SLIBREF=WORK  ;
RSUBMIT;
LIBNAME PKUB DB2 DATABASE=DNFPUTDL OWNER=PKUB;
DATA ARCS;
	INFILE CARDS DSD MISSOVER;
	INFORMAT PF_REQ_ACT $5.;
	INPUT PF_REQ_ACT;
	CARDS;
DQE05
DQE10
DQE15
DQE20
DQE25
DQE30
DQE35
DQE40
DQE45
DQE50
DQE55
DQE60
DQE65
DQE70
DQE75
DQE80
;

PROC SQL;
	CREATE TABLE INIT_POP AS
		SELECT DISTINCT 
			D.DF_SPE_ACC_ID
			,A.BF_SSN
			,LN16.DAYS_DELQ + 1 AS DAYS_DELQ
			,LN20.LF_EDS
		FROM
			PKUB.PD10_PRS_NME D
			INNER JOIN PKUB.LN10_LON A
				ON A.BF_SSN = D.DF_PRS_ID
			INNER JOIN
				(
					SELECT 
						A.BF_SSN
						,A.LN_SEQ
						,MAX(A.LN_DLQ_MAX) AS DAYS_DELQ
					FROM 
						PKUB.LN16_LON_DLQ_HST A
					WHERE 
						A.LC_STA_LON16 = '1'
						AND	A.LN_DLQ_MAX >= 14
					GROUP BY 
						A.BF_SSN
				) LN16
				ON A.BF_SSN = LN16.BF_SSN
				AND A.LN_SEQ = LN16.LN_SEQ
			INNER JOIN PKUB.DW01_DW_CLC_CLU B
				ON A.BF_SSN = B.BF_SSN
				AND A.LN_SEQ = B.LN_SEQ
			LEFT JOIN PKUB.LN20_EDS LN20
				ON A.BF_SSN = LN20.BF_SSN
				AND A.LN_SEQ = LN20.LN_SEQ
				AND LN20.LC_REL_TO_BR IS NOT NULL
			LEFT OUTER JOIN 
				(
					SELECT 
						G.BF_SSN
					FROM 
						PKUB.AY10_BR_LON_ATY G
					WHERE 
						G.PF_REQ_ACT = 'EMBWR'
						AND G.LD_ATY_REQ_RCV <= TODAY() - 15
				) F
				ON A.BF_SSN = F.BF_SSN
			LEFT OUTER JOIN 
				(
					SELECT 
						LN60.BF_SSN
					FROM 
						PKUB.LN60_BR_FOR_APV LN60
						INNER JOIN PKUB.FB10_BR_FOR_REQ FB10
							ON LN60.BF_SSN = FB10.BF_SSN
							AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
					WHERE 	
						FB10.LC_FOR_TYP = '28'
						AND	FB10.LC_FOR_STA = 'A'
						AND	FB10.LC_STA_FOR10 = 'A'
						AND LN60.LC_STA_LON60 = 'A'
						AND	LN60.LD_FOR_END > TODAY()
				) CS_FORB
				ON A.BF_SSN = CS_FORB.BF_SSN
		WHERE 
			A.LA_CUR_PRI > 0
			AND A.LC_STA_LON10 = 'R'
			AND B.WC_DW_LON_STA NOT IN ('12','16','17','18','19','20','21','22')
			AND F.BF_SSN IS NULL
			AND CS_FORB.BF_SSN IS NULL
	;

CREATE TABLE AY10 AS
	SELECT B.BF_SSN
		,MAX(B.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
		,B.PF_REQ_ACT
	FROM INIT_POP A
	INNER JOIN PKUB.AY10_BR_LON_ATY B
		ON A.BF_SSN = B.BF_SSN
	INNER JOIN ARCS C
		ON B.PF_REQ_ACT = C.PF_REQ_ACT
	GROUP BY B.BF_SSN
			,B.PF_REQ_ACT;

	CREATE TABLE DEMOS AS
		SELECT DISTINCT
			PD10.DF_PRS_ID,
			PD10.DM_PRS_1,
			PD10.DM_PRS_LST,
			EML.DX_ADR_EML
		FROM
			PKUB.PD10_PRS_NME PD10
			JOIN 
				( 
					SELECT 
						XX.DF_PRS_ID,
						COALESCE(X.DX_ADR_EML,COALESCE(Y.DX_ADR_EML,Z.DX_ADR_EML))  AS DX_ADR_EML
					FROM 
						PKUB.PD32_PRS_ADR_EML XX
						LEFT OUTER JOIN PKUB.PD32_PRS_ADR_EML X
							ON XX.DF_PRS_ID = X.DF_PRS_ID
							AND X.DC_ADR_EML = 'H'
							AND X.DC_STA_PD32 = 'A'
							AND X.DI_VLD_ADR_EML = 'Y'
						LEFT OUTER JOIN PKUB.PD32_PRS_ADR_EML Y
							ON XX.DF_PRS_ID = Y.DF_PRS_ID
							AND Y.DC_ADR_EML = 'A'
							AND Y.DC_STA_PD32 = 'A'
							AND Y.DI_VLD_ADR_EML = 'Y'
						LEFT OUTER JOIN PKUB.PD32_PRS_ADR_EML Z
							ON XX.DF_PRS_ID = Z.DF_PRS_ID
							AND Z.DC_ADR_EML = 'W'
							AND Z.DC_STA_PD32 = 'A'
							AND Z.DI_VLD_ADR_EML = 'Y'
					) EML
				ON PD10.DF_PRS_ID = EML.DF_PRS_ID
		WHERE
			EML.DX_ADR_EML IS NOT NULL
	;

	CREATE TABLE BRWS AS
		SELECT DISTINCT
			I.DF_SPE_ACC_ID,
			D.DM_PRS_1,
			D.DM_PRS_LST,
			D.DX_ADR_EML,
			I.BF_SSN,
			I.DAYS_DELQ
		FROM
			INIT_POP I
			JOIN DEMOS D
				ON I.BF_SSN = D.DF_PRS_ID
	;

	CREATE TABLE ENDS AS
		SELECT DISTINCT
			I.DF_SPE_ACC_ID,
			D.DM_PRS_1,
			D.DM_PRS_LST,
			D.DX_ADR_EML,
			I.BF_SSN,
			I.DAYS_DELQ
		FROM
			INIT_POP I
			JOIN DEMOS D
				ON I.LF_EDS = D.DF_PRS_ID
	;
QUIT;

DATA DEMO;
	SET BRWS ENDS;
RUN;

ENDRSUBMIT;
DATA DEMO;	SET WORKLOCL.DEMO;RUN;
DATA AY10;	SET WORKLOCL.AY10;RUN;
DATA ARCS;	SET WORKLOCL.ARCS;RUN;

PROC SORT DATA=DEMO; BY DF_SPE_ACC_ID; RUN;

/*STACKING DEMO AND ARCS */
DATA STACKED;
	SET DEMO(KEEP = BF_SSN) ARCS;
RUN;

PROC FREQ DATA = STACKED NOPRINT;
	TABLE BF_SSN*PF_REQ_ACT/
	SPARSE OUT = SSNARC;
RUN;

DATA SSNARC;
	SET SSNARC (KEEP = BF_SSN PF_REQ_ACT);
	WHERE BF_SSN NE ' ' 
	AND PF_REQ_ACT NE ' ';
RUN;

PROC SQL;
	CREATE TABLE SSNARC2 AS
	SELECT DISTINCT A.*
					,TODAY() - COALESCE(B.LD_ATY_REQ_RCV,0) AS LD_ATY_REQ_RCV
	FROM SSNARC A
	LEFT OUTER JOIN AY10 B
		ON A.BF_SSN = B.BF_SSN
		AND A.PF_REQ_ACT = B.PF_REQ_ACT
	ORDER BY A.BF_SSN, A.PF_REQ_ACT;
QUIT;

PROC TRANSPOSE DATA=SSNARC2 OUT=SSNARC3 (DROP=_NAME_ _LABEL_) PREFIX=DAYS_SINCE_DQARC;
VAR LD_ATY_REQ_RCV;
BY BF_SSN;
RUN;

PROC SQL;
	CREATE TABLE FINAL AS
	SELECT A.*, B.*
	FROM DEMO A
		INNER JOIN SSNARC3 B
			ON A.BF_SSN = B.BF_SSN;
QUIT;

%MACRO BOUNDS(LB,UB,RNUM,UB2,ARC_DT);

DATA _NULL_;
	SET FINAL ;
	%IF &RNUM NE 2 %THEN %DO; 
		WHERE &LB <= DAYS_DELQ <= &UB 
			AND &UB2 < &ARC_DT ;
	%END;
	FILE REPORT&RNUM DELIMITER=',' DSD DROPOVER LRECL=32767;
	IF _N_ = 1 THEN DO;
		PUT "DF_SPE_ACC_ID,DM_PRS_1,DM_PRS_LST,DX_ADR_EML";
	END;
	DO;
	   PUT DF_SPE_ACC_ID @;
	   PUT DM_PRS_1 @;
	   PUT DM_PRS_LST @;
	   PUT DX_ADR_EML $ ;
	END;
RUN;
%MEND BOUNDS;
%BOUNDS(,,2,,);/*NO FILTERS FOR R2*/
%BOUNDS(15,29,3,13,DAYS_SINCE_DQARC1);
%BOUNDS(30,44,4,13,DAYS_SINCE_DQARC2);
%BOUNDS(45,59,5,13,DAYS_SINCE_DQARC3);
%BOUNDS(60,89,6,27,DAYS_SINCE_DQARC4);
%BOUNDS(90,119,7,27,DAYS_SINCE_DQARC5);
%BOUNDS(120,149,8,27,DAYS_SINCE_DQARC6);
%BOUNDS(150,179,9,27,DAYS_SINCE_DQARC7);
%BOUNDS(180,209,10,27,DAYS_SINCE_DQARC8);
%BOUNDS(210,239,11,27,DAYS_SINCE_DQARC9);
%BOUNDS(240,254,12,27,DAYS_SINCE_DQARC10);
%BOUNDS(270,284,13,13,DAYS_SINCE_DQARC11);
%BOUNDS(285,299,14,13,DAYS_SINCE_DQARC12);
%BOUNDS(300,314,15,13,DAYS_SINCE_DQARC13);
%BOUNDS(315,329,16,13,DAYS_SINCE_DQARC14);
%BOUNDS(330,344,17,13,DAYS_SINCE_DQARC15);
%BOUNDS(345,359,18,13,DAYS_SINCE_DQARC16);
