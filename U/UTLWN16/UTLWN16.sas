/*UTLWN16 - Percent of loans esigned*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWN16.LWN16RZ";
FILENAME REPORT2 "&RPTLIB/ULWN16.LWN16R2";		/*Alliance Credit Union*/
FILENAME REPORT3 "&RPTLIB/ULWN16.LWN16R3";		/*America First Credit Union*/
FILENAME REPORT4 "&RPTLIB/ULWN16.LWN16R4";		/*American United Family of Credit Unions FCU*/
FILENAME REPORT5 "&RPTLIB/ULWN16.LWN16R5";		/*UHEAA*/
FILENAME REPORT6 "&RPTLIB/ULWN16.LWN16R6";		/*JP Morgan Chase Bank/Bank One*/
FILENAME REPORT7 "&RPTLIB/ULWN16.LWN16R7";		/*Deseret First Credit Union*/
FILENAME REPORT8 "&RPTLIB/ULWN16.LWN16R8";		/*Family First Credit Union*/
FILENAME REPORT10 "&RPTLIB/ULWN16.LWN16R10";	/*Granite Credit Union*/
FILENAME REPORT11 "&RPTLIB/ULWN16.LWN16R11";	/*Intermountain Credit Union*/
FILENAME REPORT12 "&RPTLIB/ULWN16.LWN16R12";	/*Jordan Credit Union*/
FILENAME REPORT13 "&RPTLIB/ULWN16.LWN16R13";	/*KeyBank NA*/
FILENAME REPORT14 "&RPTLIB/ULWN16.LWN16R14";	/*Liberty Bank Inc.*/
FILENAME REPORT15 "&RPTLIB/ULWN16.LWN16R15";	/*Mountain America Credit Union*/
FILENAME REPORT16 "&RPTLIB/ULWN16.LWN16R16";	/*Mountain High Federal Credit Union*/
FILENAME REPORT17 "&RPTLIB/ULWN16.LWN16R17";	/*Salt Lake City Credit Union*/
FILENAME REPORT18 "&RPTLIB/ULWN16.LWN16R18";	/*Tooele Federal Credit Union*/
FILENAME REPORT19 "&RPTLIB/ULWN16.LWN16R19";	/*University of Utah Credit Union*/
FILENAME REPORT20 "&RPTLIB/ULWN16.LWN16R20";	/*Utah Community Credit Union*/
FILENAME REPORT21 "&RPTLIB/ULWN16.LWN16R21";	/*U.S. Bank*/
FILENAME REPORT22 "&RPTLIB/ULWN16.LWN16R22";	/*USU Charter Credit Union*/
FILENAME REPORT23 "&RPTLIB/ULWN16.LWN16R23";	/*Weber State Credit Union*/
FILENAME REPORT24 "&RPTLIB/ULWN16.LWN16R24";	/*Wells Fargo*/
FILENAME REPORT25 "&RPTLIB/ULWN16.LWN16R25";	/*Zions First National Bank*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ORIGLN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT C.IM_IST_FUL AS NAME,
	A.AF_ORG_APL_OPS_LDR AS ID,
	B.AC_LON_TYP AS TYPE,
	A.AC_ELS_LON AS ESIGN
FROM OLWHRM1.GA01_APP A
	INNER JOIN OLWHRM1.GA10_LON_APP B
		ON A.AF_APL_ID = B.AF_APL_ID
	INNER JOIN OLWHRM1.LR01_LGS_LDR_INF C
		ON A.AF_ORG_APL_OPS_LDR = C.IF_IST
WHERE B.AC_PRC_STA = 'A'
	AND B.AD_PRC >= '06/01/2001'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

ENDRSUBMIT;
DATA ORIGLN; SET WORKLOCL.ORIGLN; RUN;

/*THIS SORT IS USEFUL FOR DETAIL FILES. IT DOESN'T HURT TO LEAVE IT IN PRODUCTION.*/
PROC SORT DATA = ORIGLN;
BY ID TYPE ESIGN;
RUN;

PROC SQL;
CREATE TABLE MASTER AS
SELECT A.NAME, A.ID, A.TYPE, COUNT(*) AS TOTAL_LOANS, COALESCE(B.ECOUNT, 0) AS ECOUNT
FROM ORIGLN A
LEFT OUTER JOIN (
	SELECT ID, TYPE, COUNT(*) AS ECOUNT
	FROM ORIGLN
	WHERE ESIGN = 'E'
	GROUP BY ID, TYPE
	) B
	ON A.ID = B.ID
	AND A.TYPE = B.TYPE
GROUP BY A.NAME, A.ID, A.TYPE, B.ECOUNT
;
QUIT;

DATA MASTER;
SET MASTER;
EPERCENT = ECOUNT / TOTAL_LOANS;
RUN;

%MACRO PRINT(LENDER=,REPNO=);
PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=39 LS=132 NOBYLINE;
DATA ORIGLENDER;
	SET MASTER;
	WHERE ID = "&LENDER";
RUN;
PROC CONTENTS DATA=ORIGLENDER OUT=EMPTYSET NOPRINT; RUN;
DATA _NULL_;
	SET EMPTYSET;
	CALL SYMPUT('HOBS',NOBS);
RUN;
%IF &HOBS = 0 %THEN %DO;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 'LENDER E-SIGN PERCENTAGES';
	TITLE3 "&LENDER";
	DATA _NULL_;
		SET EMPTYSET;
		FILE PRINT;
		IF  NOBS=0 AND _N_ =1 THEN DO;
			PUT // 131*'-';
			PUT      ////////
				@55 '**** NO LOANS FOUND ****';
			PUT ////////
				@57 '-- END OF REPORT --';
			PUT //////////
				@46 "JOB = UTLWN16     REPORT = ULWN16.LWN16R&REPNO";
			END;
		RETURN;
	RUN;
%END;
%ELSE %DO;
	DATA _NULL_;
		SET ORIGLENDER;
		I + TOTAL_LOANS;
		E + ECOUNT;
		CALL SYMPUT('LENDER_TOTAL', I);
		CALL SYMPUT('TOTAL_ESIGNED', E);
		CALL SYMPUT('NAME', TRIM(NAME));
	RUN;
	DATA _NULL_;
		ARBNAME = &TOTAL_ESIGNED / &LENDER_TOTAL;
		CALL SYMPUT('PERCENT_ESIGNED', PUT(ARBNAME, PERCENT10.2));
	RUN;
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 'LENDER E-SIGN PERCENTAGES';
	TITLE3 "&NAME - &LENDER";
	FOOTNOTE "JOB = UTLWN16     REPORT = ULWN16.LWN16R&REPNO";
	PROC REPORT DATA=ORIGLENDER NOWD HEADSKIP SPLIT='/';
		COLUMN TYPE TOTAL_LOANS EPERCENT;
		DEFINE TYPE / DISPLAY "LOAN TYPE" WIDTH=4;
		DEFINE TOTAL_LOANS / DISPLAY "NUMBER OF LOANS";
		DEFINE EPERCENT / DISPLAY "PERCENT E-SIGNED" FORMAT=PERCENT10.2;
		COMPUTE AFTER;
			LINE ' ';
			LINE @46 "TOTAL NUMBER OF LOANS: &LENDER_TOTAL";
			LINE @46 "PERCENT E-SIGNED:         &PERCENT_ESIGNED";
		ENDCOMP;
	RUN;
%END;
OPTIONS BYLINE;
PROC PRINTTO;
RUN;
%MEND PRINT;

%PRINT(LENDER=817575,REPNO= 2);
%PRINT(LENDER=822373,REPNO= 3);
%PRINT(LENDER=833577,REPNO= 4);
%PRINT(LENDER=828476,REPNO= 5);
%PRINT(LENDER=819628,REPNO= 6);
%PRINT(LENDER=833828,REPNO= 7);
%PRINT(LENDER=817440,REPNO= 8);
%PRINT(LENDER=817545,REPNO= 10);
%PRINT(LENDER=834265,REPNO= 11);
%PRINT(LENDER=830791,REPNO= 12);
%PRINT(LENDER=813760,REPNO= 13);
%PRINT(LENDER=834370,REPNO= 14);
%PRINT(LENDER=817546,REPNO= 15);
%PRINT(LENDER=834122,REPNO= 16);
%PRINT(LENDER=832241,REPNO= 17);
%PRINT(LENDER=820200,REPNO= 18);
%PRINT(LENDER=830132,REPNO= 19);
%PRINT(LENDER=829123,REPNO= 20);
%PRINT(LENDER=811698,REPNO= 21);
%PRINT(LENDER=830146,REPNO= 22);
%PRINT(LENDER=829158,REPNO= 23);
%PRINT(LENDER=813894,REPNO= 24);
%PRINT(LENDER=817455,REPNO= 25);
