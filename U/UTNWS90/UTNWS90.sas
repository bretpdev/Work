/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS90.NWS90RZ";
FILENAME REPORT2 "&RPTLIB/UNWS90.NWS90R2";
FILENAME REPORT3 "&RPTLIB/UNWS90.NWS90R3";
LIBNAME  LEGEND  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
RSUBMIT;

/*%let DB = DNFPRQUT;  *This is test;*/
/*%let DB = DNFPRUUT;  *This is VUK3 test;*/
%let DB = DNFPUTDL;  *This is live;

LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;

PROC SQL;
	CONNECT TO DB2 (DATABASE=&DB);

	CREATE TABLE BDS0 AS
		SELECT	
			*
		FROM	
			CONNECTION TO DB2 
				(
					SELECT DISTINCT
						LN10.BF_SSN,
						LN10.LA_CUR_PRI,
						LN10.LF_LON_CUR_OWN,
						LN16.LD_DLQ_ITL_MIN,
						LN16.LN_DLQ_MAX + 1 AS LN_DLQ_MAX,
						TRIM(PD10.DM_PRS_1) || ', ' || PD10.DM_PRS_LST AS BOR_NAME,
						PD30.DX_STR_ADR_1,
						PD30.DX_STR_ADR_2,
						PD30.DX_STR_ADR_3,
						PD30.DM_CT,
						PD30.DC_DOM_ST,
						PD30.DF_ZIP_CDE,
						PD26.DC_STA_SKP,
						DW01.WC_DW_LON_STA
					FROM 
						PKUB.PD10_PRS_NME PD10
						LEFT OUTER JOIN PKUB.PD30_PRS_ADR PD30
							ON PD10.DF_PRS_ID = PD30.DF_PRS_ID
							AND PD30.DC_ADR = 'L'
						LEFT JOIN PKUB.PD26_PRS_SKP_PRC PD26
							ON PD26.BF_SSN = PD10.DF_PRS_ID
						INNER JOIN PKUB.LN90_FIN_ATY LN90
							ON LN90.BF_SSN = PD10.DF_PRS_ID
							AND (LN90.PC_FAT_TYP = '01' AND LN90.PC_FAT_SUB_TYP = '01' AND LN90.LC_FAT_REV_REA = '' AND LN90.LC_STA_LON90 = 'A') 
						INNER JOIN  /*GETS THE TOTAL BALANCE*/ 
						(
							SELECT 
								LN10.BF_SSN,
								LN10.LF_LON_CUR_OWN,
								SUM(LN10.LA_CUR_PRI + COALESCE(DW01.WA_TOT_BRI_OTS,0)) * 100 AS LA_CUR_PRI
							FROM 
								PKUB.LN10_LON LN10
								LEFT OUTER JOIN PKUB.DW01_DW_CLC_CLU DW01
									ON LN10.BF_SSN = DW01.BF_SSN
									AND LN10.LN_SEQ = DW01.LN_SEQ
							WHERE LN10.LC_STA_LON10 = 'R'
								AND LN10.LA_CUR_PRI > 0
							GROUP BY
								LN10.BF_SSN,
								LN10.LF_LON_CUR_OWN
							) LN10
								ON PD10.DF_PRS_ID = LN10.BF_SSN
						LEFT JOIN 
						(
							SELECT 
								LN16.BF_SSN,
								MIN(LN16.LD_DLQ_OCC) AS LD_DLQ_ITL_MIN,
								LN16.LN_DLQ_MAX
							FROM
								PKUB.LN16_LON_DLQ_HST LN16
								INNER JOIN PKUB.LN10_LON LN10
									ON LN16.BF_SSN = LN10.BF_SSN
									AND LN16.LN_SEQ = LN10.LN_SEQ
							WHERE LN16.LC_STA_LON16 = '1'
								AND LN10.LC_STA_LON10 = 'R'
								AND LN10.LA_CUR_PRI > 0
								AND DAYS(LN16.LD_DLQ_MAX) = DAYS(CURRENT_DATE) - 1
							GROUP BY 
								LN16.BF_SSN,
								LN16.LN_DLQ_MAX
							) LN16
								ON LN10.BF_SSN = LN16.BF_SSN
						LEFT OUTER JOIN 
						(
							SELECT DISTINCT 
								BF_SSN,
								WC_DW_LON_STA
							FROM
								PKUB.DW01_DW_CLC_CLU
							WHERE WC_DW_LON_STA NOT IN ('21','19','16','17','04','05','18')
						) DW01
							ON PD10.DF_PRS_ID = DW01.BF_SSN
					WHERE DW01.BF_SSN IS NOT NULL
				);
			DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CREATE TABLE BDS AS/*	select higher delinquency in cases where borrower has split delinquency*/
		SELECT
			BDS.*
		FROM 
			BDS0 BDS
			INNER JOIN
				(
					SELECT
						BF_SSN,
						MAX(LN_DLQ_MAX) AS LN_DLQ_MAX
					FROM
						BDS0
					GROUP BY
						BF_SSN
				) DLQ
					ON BDS.BF_SSN = DLQ.BF_SSN
					AND BDS.LN_DLQ_MAX = DLQ.LN_DLQ_MAX
;

	CREATE TABLE PHN AS
		SELECT DISTINCT
			BDS.BF_SSN,
			PD40.DN_DOM_PHN_ARA || PD40.DN_DOM_PHN_XCH || PD40.DN_DOM_PHN_LCL AS PHN,
			PD40.DC_PHN,
			CASE
				WHEN PD40.DC_ALW_ADL_PHN IN ('L','Q','P') THEN 'Y'
				ELSE 'N'
			 END AS DC_ALW_ADL_PHN
		FROM 
			BDS 
			INNER JOIN PKUB.PD40_PRS_PHN PD40
				ON BDS.BF_SSN = PD40.DF_PRS_ID
		WHERE pd40.DI_PHN_VLD = 'Y'
;

	CREATE TABLE DELQ AS
		SELECT DISTINCT 
			PHN.*,
			COALESCE(BILL_DATA.AMTDU,0) * 100 AS AMTDU
		FROM PHN 
			LEFT OUTER JOIN /*GETS THE AMOUNT DUE*/
			(
				SELECT 
					LN80.BF_SSN,
					SUM(COALESCE(LN80.LA_BIL_PAS_DU,0) + COALESCE(LN80.LA_BIL_DU_PRT,0) +	COALESCE(LN80.LA_LTE_FEE_OTS_PRT,0)) AS AMTDU
				FROM PKUB.LN80_LON_BIL_CRF LN80
					INNER JOIN 
					(
						SELECT
							BIL_SEQ.BF_SSN,
							BIL_SEQ.LD_BIL_CRT,
							MAX(LN_SEQ_BIL_WI_DTE) AS LN_SEQ_BIL_WI_DTE
						FROM PKUB.LN80_LON_BIL_CRF BIL_SEQ
							INNER JOIN 
							(
								SELECT 
									BF_SSN,
									MAX(LD_BIL_CRT) AS LD_BIL_CRT
								FROM PKUB.LN80_LON_BIL_CRF 
								WHERE LC_STA_LON80 = 'A'
									AND LC_BIL_TYP_LON = 'P'
								GROUP BY
									BF_SSN
							) BIL_DT
								ON BIL_DT.BF_SSN = BIL_SEQ.BF_SSN
								AND BIL_DT.LD_BIL_CRT = BIL_SEQ.LD_BIL_CRT
						WHERE BIL_SEQ.LC_STA_LON80 = 'A'
							AND BIL_SEQ.LC_BIL_TYP_LON = 'P'
						GROUP BY 
							BIL_SEQ.BF_SSN,
							BIL_SEQ.LD_BIL_CRT
					) THE_BILL
						ON LN80.BF_SSN = THE_BILL.BF_SSN
						AND LN80.LD_BIL_CRT = THE_BILL.LD_BIL_CRT
						AND LN80.LN_SEQ_BIL_WI_DTE = THE_BILL.LN_SEQ_BIL_WI_DTE
			LEFT JOIN PKUB.PD10_PRS_NME PD10
				ON LN80.BF_SSN = PD10.DF_PRS_ID
			LEFT JOIN PKUB.LN10_LON LN10
				ON LN80.BF_SSN = LN10.BF_SSN
				AND LN80.LN_SEQ = LN10.LN_SEQ
			WHERE LN80.LC_STA_LON80 = 'A'
				AND LN10.LA_CUR_PRI > 0
				AND LN10.LC_STA_LON10 = 'R'
			GROUP BY
				LN80.BF_SSN
			) BILL_DATA
			ON PHN.BF_SSN = BILL_DATA.BF_SSN
;

	/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
	/*%SQLCHECK;*/
QUIT;


PROC SORT DATA=PHN NODUPRECS; BY BF_SSN; RUN;
PROC SORT DATA=BDS; BY BF_SSN; RUN;
PROC SORT DATA=DELQ; BY BF_SSN; RUN;

DATA PHN(KEEP=BF_SSN PHN_H PHN_A PHN_W PHN_H_CON PHN_A_CON PHN_W_CON);
	SET PHN;
	BY BF_SSN;
	LENGTH PHN_H PHN_A PHN_W $10. PHN_H_CON PHN_A_CON PHN_W_CON $1.;
	RETAIN PHN_H PHN_A PHN_W PHN_H_CON PHN_A_CON PHN_W_CON;
	IF FIRST.BF_SSN THEN DO;
		PHN_A = '';
		PHN_W = '';
		PHN_H = '';
		PHN_H_CON = '';
		PHN_W_CON = '';
		PHN_A_CON = '';
	END;
	IF DC_PHN = 'H' THEN DO;
		PHN_H = PHN;
		PHN_H_CON = DC_ALW_ADL_PHN;
	END;
	ELSE IF DC_PHN = 'A' THEN DO;
		PHN_A = PHN;
		PHN_A_CON = DC_ALW_ADL_PHN;
	END;
	ELSE IF DC_PHN = 'W' THEN DO;
		PHN_W = PHN;
		PHN_W_CON = DC_ALW_ADL_PHN;
	END;
	IF LAST.BF_SSN THEN OUTPUT;
RUN;

DATA BDS(DROP=DC_PHN PHN DC_ALW_ADL_PHN);
MERGE BDS PHN(IN=A) DELQ;
BY BF_SSN;
IF A;
RUN;

PROC SORT DATA=BDS OUT=BDS NODUPKEY; BY BF_SSN; RUN;  

DATA R2;
	SET BDS;
	WHERE ((LN_DLQ_MAX > 0 AND LN_DLQ_MAX < 6) OR (WC_DW_LON_STA IN ('01','03') AND LN_DLQ_MAX < 1 )) AND DC_STA_SKP ^= '2';
RUN;

DATA R3;
	SET BDS;
	WHERE ((LN_DLQ_MAX > 0 AND LN_DLQ_MAX < 6) OR (WC_DW_LON_STA IN ('01','03') AND LN_DLQ_MAX < 1 )) AND DC_STA_SKP = '2';
RUN;

ENDRSUBMIT;

DATA R2; 
	SET LEGEND.R2;  
RUN;

DATA R3; 
	SET LEGEND.R3;  
RUN;

DATA _NULL_;
SET R2;
FILE REPORT2 DROPOVER LRECL=32767;
DO;
	PUT @1 BF_SSN   			
		@15 bor_name
		@70 LN_DLQ_MAX
		@159 PHN_H_CON
		@160 PHN_H
		@175 PHN_A_CON
		@176 PHN_A
		@191 PHN_W_CON
		@192 PHN_W
		@260 dx_str_adr_1
		@290 dx_str_adr_2
		@320 dx_str_adr_3
		@350 dm_ct
		@370 dc_dom_st
		@372 df_zip_cde
		@414 LF_LON_CUR_OWN
		@422 AMTDU
		@429 LA_CUR_PRI;	
	END;
RUN;

DATA _NULL_;
SET R3;
FILE REPORT3 DROPOVER LRECL=32767;
DO;
	PUT @1 BF_SSN   			
		@15 bor_name
		@70 LN_DLQ_MAX
		@159 PHN_H_CON
		@160 PHN_H
		@175 PHN_A_CON
		@176 PHN_A
		@191 PHN_W_CON
		@192 PHN_W
		@260 dx_str_adr_1
		@290 dx_str_adr_2
		@320 dx_str_adr_3
		@350 dm_ct
		@370 dc_dom_st
		@372 df_zip_cde
		@414 LF_LON_CUR_OWN
		@422 AMTDU
		@429 LA_CUR_PRI;	
	END;
RUN;
