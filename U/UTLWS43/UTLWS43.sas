/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWS43.LWS43RZ";
FILENAME REPORT2 "&RPTLIB/ULWS43.LWS43R2";
FILENAME REPORT3 "&RPTLIB/ULWS43.LWS43R3";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AMF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID
	,A.DF_SPE_ACC_ID
	,TRIM(A.DM_PRS_1) || ' ' || TRIM(A.DM_PRS_LST) AS NAME
	,E.DX_ADR_EML
	,COALESCE(F.BF_SSN,G.BF_SSN) AS NOT_2
	,D.BF_SSN AS NOT_3
	,CASE
		WHEN (DAYS(CURRENT DATE)- 1) - DAYS(C.LD_DLQ_OCC) >= 210 THEN '2'
		WHEN DAYS(CURRENT DATE) - DAYS(C.LD_DLQ_OCC) - 1 <= 209 THEN '3'
	END AS DAYS_DLQ
				,C.LD_DLQ_MAX
	,DAYS(CURRENT DATE) - DAYS(C.LD_DLQ_OCC) - 1 AS CALC_DELINQ
FROM OLWHRM1.PD10_PRS_NME A
INNER JOIN OLWHRM1.AY10_BR_LON_ATY B
	ON A.DF_PRS_ID = B.BF_SSN
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST C
	ON B.BF_SSN = C.BF_SSN
LEFT OUTER JOIN (SELECT E.DF_PRS_ID
				,E.DX_ADR_EML
			FROM OLWHRM1.PD32_PRS_ADR_EML E
			WHERE E.DI_VLD_ADR_EML = 'Y'
				AND DC_STA_PD32 = 'A'
) E
	ON A.DF_PRS_ID = E.DF_PRS_ID
LEFT OUTER JOIN (SELECT BF_SSN
			FROM OLWHRM1.AY10_BR_LON_ATY AY10
			WHERE PF_REQ_ACT = 'EMFLW'
				AND DAYS(LD_ATY_REQ_RCV) >= DAYS(CURRENT DATE) - 6
) D
	ON E.DF_PRS_ID = D.BF_SSN
LEFT OUTER JOIN (SELECT BF_SSN
			FROM OLWHRM1.AY10_BR_LON_ATY AY10
			WHERE (PF_REQ_ACT = 'FLLWP' 
					AND DAYS(LD_ATY_REQ_RCV) >= DAYS(CURRENT DATE) - 6)
				OR (PF_RSP_ACT = 'NOCTC' 
					AND DAYS(LD_ATY_RSP) >= DAYS(CURRENT DATE) - 3)
				OR (PF_RSP_ACT = 'CNTCT'
					AND DAYS(LD_ATY_RSP) >= DAYS(CURRENT DATE) - 5)
) G
	ON A.DF_PRS_ID = G.BF_SSN
LEFT OUTER JOIN (SELECT BF_SSN
			FROM OLWHRM1.WQ20_TSK_QUE 
			WHERE WF_QUE = 'DQ'
				AND WF_SUB_QUE = '01'
				AND WC_STA_WQUE20 NOT IN ('X','C')
) F
	ON A.DF_PRS_ID = F.BF_SSN

WHERE DAYS(C.LD_DLQ_OCC) <= DAYS(CURRENT DATE) - 15
	AND C.LC_STA_LON16 = '1'
	AND DAYS(C.LD_DLQ_MAX) = DAYS(CURRENT DATE) - 1
	AND B.PF_REQ_ACT IN ('G7077','G708C','H742A','THFSC','TPECL','TPUNL')
	AND DAYS(B.LD_ATY_REQ_RCV ) BETWEEN DAYS(CURRENT DATE) - 15 AND DAYS(CURRENT DATE) - 10
 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/

ENDRSUBMIT;
DATA AMF;
	SET WORKLOCL.AMF;
RUN;
PROC SORT DATA=AMF;
	BY DF_SPE_ACC_ID DESCENDING DAYS_DLQ ;
RUN;


/*The exclusion criteria were picked up with the values 'N3' */
/*and 'N2' to signify not report 2 or not report 3.  a or b*/
/*are set to '1' if the exclusions are encountered for the */
/*borrower.  also, if a borrower is in report 2, they should */
/*be excluded from report 3.*/
DATA AMF2 (KEEP=DF_PRS_ID DF_SPE_ACC_ID) AMF3 (KEEP=DF_PRS_ID DF_SPE_ACC_ID NAME DX_ADR_EML);
SET AMF;
	BY DF_SPE_ACC_ID DESCENDING DAYS_DLQ ;
RETAIN A;
RETAIN B;
IF FIRST.DF_SPE_ACC_ID THEN DO;
	A = 0;
	B = 0;
END;
IF DAYS_DLQ = '2' THEN DO;
	IF NOT_2 = DF_PRS_ID THEN A = 1;
	IF LAST.DAYS_DLQ AND A = 0 THEN DO;
		A = 2;
		OUTPUT AMF2;
	END;
END;
IF DAYS_DLQ = '3' AND A ^= 2 THEN DO;
	IF NOT_3 = DF_PRS_ID THEN B = 1;
	IF LAST.DAYS_DLQ AND B = 0 THEN DO;
		IF DX_ADR_EML ^= '' THEN OUTPUT AMF3;
	END;
END;
RUN;

DATA _NULL_;
SET AMF2 ;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT COMMENTS $600. ;
ARC = 'FLLWP';
FR_DTE = '';
TO_DTE = '';
NEED_DTE = '';
REC_ID = '';
REG_CDE = '';
REG_ID = '';
LN_SEQ_NUM = 'ALL';
COMMENTS = 'Review account to determine if follow up call is needed regarding deferment or forbearance form that was sent.' ;
DO;
   PUT DF_PRS_ID $ @;
   PUT ARC @;
   PUT FR_DTE @;
   PUT TO_DTE @;
   PUT NEED_DTE @;
   PUT REC_ID @;
   PUT REG_CDE @;
   PUT REG_ID @;
   PUT LN_SEQ_NUM @;
   PUT COMMENTS $;
END;
RUN;

DATA _NULL_;
SET AMF3 ;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNTNUMBER,NAME,RECIPIENT";
END;
DO;
   PUT DF_SPE_ACC_ID $ @;
   PUT NAME @;
   PUT DX_ADR_EML $ ;
END;
RUN;
