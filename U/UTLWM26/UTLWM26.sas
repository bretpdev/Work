/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWM26.LWM26RZ";
FILENAME REPORT2 "&RPTLIB/ULWM26.LWM26R2";
FILENAME REPORT3 "&RPTLIB/ULWM26.LWM26R3";

DATA _NULL_  ;
	CALL SYMPUT  ( 'BEGIN', "'"||PUT(INTNX('MONTH',TODAY(),-1,'BEGINNING'),YYMMDD10.)||"'" )   ;
	CALL SYMPUT  ( 'END', "'"||PUT(INTNX('MONTH',TODAY(),-1,'ENDING'),YYMMDD10.)||"'" )   ;
RUN;

%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SCALL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,D.DF_SPE_ACC_ID
	,E.WD_LON_RPD_SR 
	,C.LD_RPS_1_PAY_DU
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.LN65_LON_RPS B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.RS10_BR_RPD C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_RPS_SEQ = C.LN_RPS_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU E
	ON B.BF_SSN = E.BF_SSN
	AND B.LN_SEQ = E.LN_SEQ
WHERE A.LA_CUR_PRI > 0
	AND A.LC_STA_LON10 = 'R'
	AND A.IC_LON_PGM IN ('STFFRD', 'UNSTFD', 'PLUS', 'PLUSGB')
	AND C.LD_RPS_1_PAY_DU BETWEEN &BEGIN AND &END
	AND A.LI_CON_PAY_STP_PUR != 'Y'
	AND E.WD_LON_RPD_SR < C.LD_RPS_1_PAY_DU
	AND E.WC_LON_STA_RPY = '03'
	AND E.WC_DW_LON_STA = '03'
	AND E.WC_LON_STA_PIF != '22'
	AND C.LC_STA_RPST10 = 'A'
	AND (DAYS(C.LD_RPS_1_PAY_DU) BETWEEN DAYS(A.LD_END_GRC_PRD) AND DAYS(A.LD_END_GRC_PRD) + 60
		OR A.LD_END_GRC_PRD IS NULL)
	AND A.BF_SSN NOT IN (SELECT BF_SSN
		FROM OLWHRM1.LN40_LON_CLM_PCL 
		WHERE LC_REA_CLP_LON = '05'
			AND LD_CAN_CLM_PCL IS NULL)
	AND A.BF_SSN NOT IN (SELECT LN65.BF_SSN
		FROM OLWHRM1.LN65_LON_RPS LN65
		INNER JOIN OLWHRM1.LN80_LON_BIL_CRF LN80 
			ON LN65.BF_SSN = LN80.BF_SSN
			AND LN65.LN_SEQ = LN80.LN_SEQ
		WHERE LN65.LC_TYP_SCH_DIS IN ('IS','IB')
			AND LN80.LA_BIL_DU_PRT = 0)
	AND A.BF_SSN NOT IN (SELECT BF_SSN
		FROM OLWHRM1.FB10_BR_FOR_REQ FB10
		WHERE LC_FOR_STA = 'A'
			AND LC_STA_FOR10 = 'A'
			AND DAYS(LD_FOR_REQ_END) > 	DAYS(C.LD_RPS_1_PAY_DU) - 45)
	AND A.BF_SSN NOT IN (SELECT BF_SSN
		FROM OLWHRM1.DF10_BR_DFR_REQ DF10
		WHERE LC_DFR_STA = 'A'
			AND LC_STA_DFR10 = 'A'
			AND DAYS(LD_DFR_REQ_END) > 	DAYS(C.LD_RPS_1_PAY_DU) - 45)


FOR READ ONLY WITH UR
);

CREATE TABLE SCPAY AS
SELECT 	DISTINCT A.BF_SSN
	,A.LN_SEQ
	,B.LD_FAT_EFF
FROM SCALL A
LEFT OUTER JOIN CONNECTION TO DB2 (
	SELECT DISTINCT C.BF_SSN, 
		C.LN_SEQ,
		C.LD_FAT_EFF 
	FROM OLWHRM1.LN90_FIN_ATY C
	WHERE C.PC_FAT_TYP = '10'
		AND C.PC_FAT_SUB_TYP = '10'
		AND C.LC_FAT_REV_REA = ''
		AND C.LC_STA_LON90 = 'A'
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LD_RPS_1_PAY_DU - 30 <= COALESCE(B.LD_FAT_EFF,0) <= A.LD_RPS_1_PAY_DU + 30

;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA SCALL; SET WORKLOCL.SCALL; RUN;
DATA SCPAY; SET WORKLOCL.SCPAY; RUN;
PROC SORT DATA=SCPAY NODUPKEY ;
BY BF_SSN ;
RUN;
PROC SORT DATA=SCALL NODUPKEY ;
BY BF_SSN LD_RPS_1_PAY_DU;
RUN;
PROC SQL;
CREATE TABLE SCALLCNT AS
SELECT	'A' AS ID
		,COUNT(DISTINCT BF_SSN) AS ALL
FROM	SCALL
;

CREATE TABLE SCPAYCNT AS
SELECT	'A' AS ID
		,COUNT(DISTINCT BF_SSN) AS PAY
FROM	SCPAY
;

CREATE TABLE SCRESCNT AS
SELECT	A.ID
		,A.ALL
		,B.PAY
		,A.ALL - B.PAY AS NOPAY
FROM	SCALLCNT A
		INNER JOIN SCPAYCNT B
			ON A.ID = B.ID
;
QUIT;
DATA SCRESCNT;
SET SCRESCNT;
PCNTPAY = FLOOR(PAY/ALL * 100);
PCNTNOPAY = 100 - FLOOR(PAY/ALL * 100);
RUN;

PROC SQL;
CREATE TABLE DF AS
SELECT DISTINCT DF_SPE_ACC_ID
	,LD_RPS_1_PAY_DU
	,CASE
		WHEN PUT(COALESCE(B.LD_FAT_EFF,0),MMDDYY10.) = '01/01/1960' THEN 'N/A'
		ELSE PUT(B.LD_FAT_EFF,MMDDYY10.)
	 END AS LD_FAT_EFF
FROM SCALL A
LEFT OUTER JOIN SCPAY B
	ON A.BF_SSN = B.BF_SSN
;
QUIT;	

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER PAGENO=1 ORIENTATION=PORTRAIT ;
OPTIONS LS=96 PS=52;

PROC PRINT NOOBS SPLIT='/' DATA=SCRESCNT WIDTH=UNIFORM WIDTH=MIN;
VAR 	ALL
		PAY
		PCNTPAY
		NOPAY
		PCNTNOPAY;
LABEL	ALL = 'TOTAL ENTERED REPAYMENT'
		PAY = 'FIRST PMT MADE'
		PCNTPAY = '%'
		NOPAY = 'FIRST PMT NOT MADE'
		PCNTNOPAY = '%';
TITLE	'SPECIAL CAMPAIGN';
TITLE2	'FIRST PAYMENT DELINQUENCY';

FOOTNOTE3	;
FOOTNOTE4 	'JOB = UTLWM26     REPORT = ULWM26.LWM26R2';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS CENTER PAGENO=1 ORIENTATION=PORTRAIT ;
OPTIONS LS=96 PS=52;
PROC PRINT NOOBS SPLIT='/' DATA=DF WIDTH=UNIFORM WIDTH=MIN;
FORMAT LD_RPS_1_PAY_DU MMDDYY10.;
VAR DF_SPE_ACC_ID LD_RPS_1_PAY_DU LD_FAT_EFF;
LABEL	DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LD_RPS_1_PAY_DU = 'DUE/DATE'
	LD_FAT_EFF = 'DATE/PAYMENT/MADE'
	;
TITLE	'Special Campaign DETAIL REPORT';
FOOTNOTE 	'JOB = UTLWM26     REPORT = ULWM26.LWM26R3';
RUN;

PROC PRINTTO;
RUN;

