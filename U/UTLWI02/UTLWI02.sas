/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWI02.LWI02R2";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LPCONDA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.DF_PRS_ID
	,A.LF_CLM_ID
	,B.DM_PRS_LST
	,A.LD_LDR_POF
	,A.LC_STA_DC10
	,A.LC_AUX_STA
	,A.LD_SPC_ASN_PRC
	,B.DD_PMN_DSA
	,A.AF_APL_ID || A.AF_APL_ID_SFX AS CLUID
	,CASE
		WHEN C.AF_BS_MPN_APL_ID IS NULL THEN 'BASE'
		ELSE 'SERIAL'
	END AS MPNTYPE
FROM OLWHRM1.DC01_LON_CLM_INF A	
INNER JOIN OLWHRM1.PD01_PDM_INF B
		ON A.BF_SSN = B.DF_PRS_ID
		AND A.LC_STA_DC10 = '03'
INNER JOIN OLWHRM1.GA01_APP C
		ON A.AF_APL_ID = C.AF_APL_ID
WHERE DAYS(A.LD_LDR_POF) <= DAYS(CURRENT DATE) - 60
AND A.LC_REA_CLM_ASN_DOE NOT IN ('20','23','24','25','03')
AND A.LD_SPC_ASN_PRC IS NOT NULL /*DATE OF DETERMINATION*/
AND B.DD_PMN_DSA IS NOT NULL /*DATE OF DISABILITY*/
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA LPCONDA;
SET WORKLOCL.LPCONDA;
RUN;

DATA _NULL_;                                      
CURRENT=TODAY();                                  
CALL SYMPUT('CURDATE',PUT(CURRENT,MMDDYY10.));       
RUN;  

PROC SORT DATA=LPCONDA;
BY DM_PRS_LST;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

OPTIONS PAGENO=1 LS=132 CENTER;
PROC PRINT NOOBS SPLIT='/' DATA=LPCONDA WIDTH=MIN;
FORMAT LD_LDR_POF MMDDYY10. LD_SPC_ASN_PRC MMDDYY10. DD_PMN_DSA MMDDYY10.;
VAR DF_PRS_ID
	DM_PRS_LST 
	LD_LDR_POF
	LC_STA_DC10
	LC_AUX_STA
	LD_SPC_ASN_PRC
	DD_PMN_DSA
	LF_CLM_ID
	CLUID
	MPNTYPE;
LABEL DF_PRS_ID = 'SSN'
	  DM_PRS_LST = 'LAST NAME'
	  LD_LDR_POF = 'CLAIM PAID DATE'
	  LC_STA_DC10 = 'STATUS CODE'
	  LC_AUX_STA = 'AUX STATUS'
	  LD_SPC_ASN_PRC = 'DATE OF DETERMINATION'
	  DD_PMN_DSA = 'DATE OF DISABILITY'
	  LF_CLM_ID = 'CLAIM ID'
	  CLUID = 'UNIQUE ID'
	  MPNTYPE = 'MPN TYPE';
TITLE	'LOANS PENDING CONDITIONAL DISABILITY ASSIGNMENT';
TITLE2	"RUN DATE: &CURDATE";
FOOTNOTE  'JOB = UTLWIO2  	 REPORT = ULWIO2.LWIO2R2';
RUN;

PROC PRINTTO;
RUN;
