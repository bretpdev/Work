/*  RETURNS AND REJECTS

Lists claims returned or rejected in the past week.

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWC03.LWC03R2";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB\ULWC03.LWC03R2.txt";
RSUBMIT;

data _null_;
   begin1=intnx('month',today(),-1)  ;
   call symput  ( 'begin', "'"||put(begin1,yymmdd10.)||"'" )   ;

   end1=intnx('month',today(),0)-1  ;
   call symput  ( 'end', "'"||put(end1,yymmdd10.)||"'" )   ;
run;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE RTRNS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT  INTEGER(A.bf_ssn)				AS SSN,
        A.AF_APL_ID||A.AF_APL_ID_SFX	AS CLID,
		B.ac_lon_typ					AS LNTYPE,
        A.IF_OPS_LDR                  	AS HOLDER,
        A.LF_CUR_LON_SER_AGY    	    AS SERVIC,
		A.bc_clm_pkg_rtn_rea			AS RTNRSN,
		A.bd_clm_pkg_rtn				AS RTNDT,
		A.bd_clm_pkg_rcv				AS RCVDT,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
										AS NAME,
		D.DF_SPE_ACC_ID					AS ACCTNUM
FROM    OLWHRM1.DC01_LON_CLM_INF A INNER JOIN
		OLWHRM1.GA10_LON_APP B ON
			A.AF_APL_ID = B.AF_APL_ID AND
			A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
		left outer join OLWHRM1.PD01_PDM_INF D
			on A.bf_ssn = D.DF_PRS_ID
WHERE   DATE(A.bd_clm_pkg_rtn) BETWEEN &begin AND &end
ORDER BY A.bc_clm_pkg_rtn_rea, A.bf_ssn, A.IF_OPS_LDR, A.AF_APL_ID||A.AF_APL_ID_SFX);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS NODATE CENTER PAGENO=1 PS=41 LS=126 ERRORS=0 MISSING=0;
PROC PRINT DATA=WORKLOCL.RTRNS N="TOTAL FOR RETURN REASON = " NOOBS SPLIT='/' 
WIDTH=UNIFORM WIDTH=MIN;
   BY RTNRSN;
   VAR ACCTNUM NAME CLID LNTYPE HOLDER SERVIC RCVDT RTNDT;
   FORMAT RTNDT MMDDYY10. RCVDT MMDDYY10.;
   LABEL RTNRSN = "RETURN REASON"
   		 LNTYPE = "LOAN/TYPE"
		 CLID = 'UNIQUE ID'
         RTNDT='DATE/RETURNED'
         HOLDER='HOLDER/CODE'
         SERVIC='SERVICER/CODE'
		 NAME='BORROWER/NAME'
		 RCVDT='DATE/RECEIVED';
   TITLE1 'CLAIMS RETURNED OR REJECTED';
   TITLE2 "FOR THE MONTH OF &EFFDATE";
   FOOTNOTE	'JOB = UTLWC03     REPORT = ULWC03.LWC03R2';
RUN;
