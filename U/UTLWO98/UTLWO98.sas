*-----------------------------------------------------*
| UTLWO98 - INTEREST PENALTIES - MONTHLY FROM ONELINK |
*-----------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO98.LWO98R2";
FILENAME REPORT3 "&RPTLIB/ULWO98.LWO98R3";
FILENAME REPORT4 "&RPTLIB/ULWO98.LWO98R4";
FILENAME REPORTZ "&RPTLIB/ULWO98.LWO98RZ";
DATA _NULL_;
	CALL SYMPUT('BEGIN',INTNX('MONTH',TODAY(),-1,'BEGINNING'));
	CALL SYMPUT('END',INTNX('MONTH',TODAY(),-1,'END'));
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE IPMFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT B.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.AF_APL_ID||A.AF_APL_ID_SFX AS CLUID
	,A.LF_CRT_DTS_DC10
	,A.LA_ACR_NOT_PD_INT 
	,A.LA_CLM_UNS
	,A.LD_LDR_POF
	,A.BD_VER_CLM_PAY
	,C.AF_CUR_LON_SER_AGY
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.PD01_PDM_INF B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.GA10_LON_APP C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
WHERE A.LD_LDR_POF >= '01/01/2006'
	AND A.LA_ACR_NOT_PD_INT <> A.LA_CLM_UNS
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO98.LWO98RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA IPMFO;
	SET WORKLOCL.IPMFO ;
RUN ;
PROC SORT DATA=IPMFO;
	BY AF_CUR_LON_SER_AGY DF_SPE_ACC_ID;
RUN;
DATA IPMFO;
	SET IPMFO;
	BY AF_CUR_LON_SER_AGY DF_SPE_ACC_ID;
	IF FIRST.DF_SPE_ACC_ID THEN 
		BOR_CT = 1;
	ELSE 
		BOR_CT = 0;
RUN;
%MACRO IPREP(CRIT,RNO,TITLE);
DATA RDS;
	SET IPMFO;
	WHERE &CRIT;
	TIAP = LA_ACR_NOT_PD_INT - LA_CLM_UNS;
RUN;
PROC SORT DATA=RDS;
	BY AF_CUR_LON_SER_AGY LD_LDR_POF;
RUN;
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE "INTEREST PENALTIES &TITLE";
FOOTNOTE   "JOB = UTLWO98  	 REPORT = ULWO98.LWO98R&RNO";
PROC CONTENTS DATA=RDS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWO98  	 REPORT = ULWO98.LWO98R&RNO";
	END;
RETURN;
RUN;
%MEND IPREP;
%IPREP(%STR(&BEGIN <= LD_LDR_POF <= &END),2,MONTHLY);
PROC REPORT DATA=RDS NOWD HEADSKIP SPLIT='/';
	COLUMN AF_CUR_LON_SER_AGY LD_LDR_POF DF_SPE_ACC_ID CLUID TIAP BOR_CT;
	DEFINE AF_CUR_LON_SER_AGY / ORDER 'SERVICER/CODE' WIDTH=10;
	DEFINE LD_LDR_POF / ORDER 'PAYOFF/DATE' FORMAT=MMDDYY10. WIDTH=10;
	DEFINE DF_SPE_ACC_ID / ORDER 'ACCOUNT/NUMBER' WIDTH=10;
	DEFINE CLUID / ORDER 'UNIQUE ID' WIDTH=19 ;
	DEFINE TIAP / ANALYSIS 'TOTAL INTEREST/AMOUNT PENALTY' WIDTH=14 FORMAT=DOLLAR14.2;
	DEFINE BOR_CT / ANALYSIS NOPRINT ;
	COMPUTE AFTER AF_CUR_LON_SER_AGY;
		LINE @29 71*'_';
		LINE @29 "TOTAL BORROWERS:" @54 BOR_CT.SUM COMMA9. @86 TIAP.SUM DOLLAR14.2;
	ENDCOMP;
;
RUN;
PROC PRINTTO;
RUN;
%IPREP(%STR(LD_LDR_POF >= MDY(1,1,2006)),3,JANUARY 2006 - PRESENT);
PROC PRINT NOOBS SPLIT='/' DATA=RDS WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT TIAP DOLLAR14.2 LD_LDR_POF MMDDYY10.;
VAR DF_SPE_ACC_ID TIAP CLUID AF_CUR_LON_SER_AGY LD_LDR_POF;
LABEL DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	TIAP = 'TOTAL INTEREST/AMOUNT PENALTY'
	CLUID = 'UNIQUE ID'
	AF_CUR_LON_SER_AGY = 'SERVICER/CODE'
	LD_LDR_POF = 'PAYOFF/DATE';
RUN;
PROC PRINTTO;
RUN;
/*************************************************************************
* NOTE: THE UNDERLYING PROCESSING MUST DIRECTLY FOLLOW THE CREATION AND 
* REOPRTING OF R3 BECAUSE THIS CODE WILL CREATE A SUMMARY OF THE R3 DATA
**************************************************************************/
DATA RDS;
	SET RDS;
	YR = YEAR(BD_VER_CLM_PAY);
	MO = MONTH(BD_VER_CLM_PAY);
RUN;
PROC SORT DATA=RDS;
	BY AF_CUR_LON_SER_AGY YR MO DF_SPE_ACC_ID;
RUN;
DATA RDS;
	SET RDS;
	BY AF_CUR_LON_SER_AGY YR MO DF_SPE_ACC_ID;
	IF FIRST.DF_SPE_ACC_ID THEN 
		BOR_CT = 1;
	ELSE 
		BOR_CT = 0;
RUN;
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=96 PAGENO=1 CENTER DATE;
TITLE "INTEREST PENALTIES BY BORROWER";
FOOTNOTE   "JOB = UTLWO98  	 REPORT = ULWO98.LWO98R4";
PROC REPORT DATA=RDS NOWD HEADSKIP SPLIT='/';
	COLUMN AF_CUR_LON_SER_AGY YR MO DF_SPE_ACC_ID TIAP BOR_CT;
	DEFINE AF_CUR_LON_SER_AGY / GROUP 'SERVICER/CODE' WIDTH=10;
	DEFINE YR / GROUP 'YEAR' WIDTH=10;
	DEFINE MO / GROUP 'MONTH' WIDTH=10;
	DEFINE DF_SPE_ACC_ID / GROUP 'ACCOUNT/NUMBER' WIDTH=10;
	DEFINE TIAP / ANALYSIS 'TOTAL INTEREST/AMOUNT PENALTY' WIDTH=14 FORMAT=DOLLAR14.2;
	DEFINE BOR_CT / ANALYSIS NOPRINT ;
	COMPUTE AFTER MO;
		LINE @54 26*'_';
		LINE @55 BOR_CT.SUM COMMA9. @65 TIAP.SUM DOLLAR14.2;
		LINE '';
	ENDCOMP;
;
RUN;
PROC PRINTTO;
RUN;
