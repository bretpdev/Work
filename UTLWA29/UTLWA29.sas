/*UTLWA29 Special Write Off Target Bond Prelim*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET TBLLIB = /sas/whse/progrevw';*/
%LET RPTLIB = T:\SAS;
%LET TBLLIB = Q:\Process Automation\TabSAS;
FILENAME REPORT2 "&RPTLIB/ULWA29.LWA29R2";
FILENAME REPORT3 "&RPTLIB/ULWA29.LWA29R3";
FILENAME REPORTZ "&RPTLIB/ULWA29.LWA29RZ";
DATA _NULL_;
	INFILE "&TBLLIB/SWOP_VAR.txt" DLM=',' DSD MISSOVER LRECL=67000 FIRSTOBS=2;
	INFORMAT BOND $10. AMOUNT BEST12.;
	INPUT BOND $ AMOUNT;
	CALL SYMPUT('BOND',BOND);
	CALL SYMPUT('AMOUNT',AMOUNT);
RUN;
%SYSLPUT AMOUNT = &AMOUNT;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
PROC SQL ;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SWOTB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,A.LC_STA_LON10
	,A.LF_LON_CUR_OWN
	,A.LA_CUR_PRI
	,B.BOR_BAL
	,C.LN_BR_DSB_SEQ
	,C.LC_DSB_TYP
	,C.LC_STA_LON15
	,C.LD_DSB
	,COALESCE(C.LA_DSB,0) 			AS LA_DSB
	,COALESCE(C.LA_DSB_CAN,0) 		AS LA_DSB_CAN
	,D.WC_DW_LON_STA
	,E.IF_BND_ISS
	,COALESCE(A.LA_CUR_PRI,0) 		AS LA_CUR_PRI
	,COALESCE(A.LA_LTE_FEE_OTS,0) 	AS LA_LTE_FEE_OTS
	,COALESCE(D.LA_NSI_OTS,0) 		AS LA_NSI_OTS
	,COALESCE(A.LA_CUR_PRI,0) +
		COALESCE(A.LA_LTE_FEE_OTS,0) + 	
		COALESCE(D.LA_NSI_OTS,0) 		AS LN_BAL
	,PD10.DM_PRS_LST
	,PD10.DF_SPE_ACC_ID
	,OL.OLIND
FROM OLWHRM1.LN10_LON A
INNER JOIN (
	SELECT IJ1.BF_SSN
		,SUM(COALESCE(IJ1.LA_CUR_PRI,0)) +
		 SUM(COALESCE(IJ1.LA_LTE_FEE_OTS,0)) +
		 SUM(COALESCE(IJ2.LA_NSI_OTS,0)) AS BOR_BAL
	FROM OLWHRM1.LN10_LON IJ1
	LEFT OUTER JOIN OLWHRM1.DW01_DW_CLC_CLU IJ2
		ON IJ1.BF_SSN = IJ2.BF_SSN
		AND IJ1.LN_SEQ = IJ2.LN_SEQ
	WHERE IJ1.LC_STA_LON10 = 'R'
	GROUP BY IJ1.BF_SSN
	) B
	ON A.BF_SSN = B.BF_SSN
INNER JOIN OLWHRM1.LN15_DSB C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN OLWHRM1.DW01_DW_CLC_CLU D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON PD10.DF_PRS_ID = A.BF_SSN
INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
	ON PD30.DF_PRS_ID = A.BF_SSN
	AND PD30.DC_ADR = 'L'
LEFT OUTER JOIN (
	SELECT IJ3.BF_SSN
		,IJ3.LN_SEQ
		,IJ3.IF_BND_ISS
	FROM OLWHRM1.LN35_LON_OWN IJ3
	INNER JOIN (
		SELECT BF_SSN
			,LN_SEQ
			,MAX(LD_OWN_EFF_SR) AS MXDT
		FROM OLWHRM1.LN35_LON_OWN
		GROUP BY BF_SSN
			,LN_SEQ
		) IJ4
		ON IJ3.BF_SSN = IJ4.BF_SSN
		AND IJ3.LN_SEQ = IJ4.LN_SEQ
		AND IJ3.LD_OWN_EFF_SR = IJ4.MXDT
	) E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
LEFT OUTER JOIN (
	SELECT DISTINCT O1.DF_PRS_ID_BR
		,'Y' AS OLIND
	FROM OLWHRM1.GA01_APP O1
	INNER JOIN OLWHRM1.GA10_LON_APP O2
		ON O1.AF_APL_ID = O2.AF_APL_ID
	INNER JOIN OLWHRM1.GA14_LON_STA O3
		ON O2.AF_APL_ID = O3.AF_APL_ID
		AND O2.AF_APL_ID_SFX = O3.AF_APL_ID_SFX
	WHERE O2.AC_PRC_STA = 'A'
		AND O2.AF_CUR_LON_SER_AGY = '700121'
		AND O3.AC_STA_GA14 = 'A'
		AND O3.AC_LON_STA_TYP IN 
		(
			'ID','IG','DA','FB','RP','CR','IM'
		)
	) OL
	ON A.BF_SSN = OL.DF_PRS_ID_BR
WHERE B.BOR_BAL < &AMOUNT
	AND B.BOR_BAL > 0 
	AND A.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);
CREATE TABLE OLNEL AS
SELECT DISTINCT A.BF_SSN 
	,A.DM_PRS_LST
	,A.DF_SPE_ACC_ID
	,B.CLUID
	,B.CBAL
	,B.AD_CUR_PRI_RPT
FROM SWOTB A
INNER JOIN CONNECTION TO DB2 (
	SELECT O1.DF_PRS_ID_BR
		,O2.AF_APL_ID||O2.AF_APL_ID_SFX AS CLUID
		,COALESCE(O2.AA_CUR_PRI,0) + COALESCE(O2.AA_OTS_ACR_INT,0) AS CBAL
		,O2.AD_CUR_PRI_RPT
	FROM OLWHRM1.GA01_APP O1
	INNER JOIN OLWHRM1.GA10_LON_APP O2
		ON O1.AF_APL_ID = O2.AF_APL_ID
	INNER JOIN OLWHRM1.GA14_LON_STA O3
		ON O2.AF_APL_ID = O3.AF_APL_ID
		AND O2.AF_APL_ID_SFX = O3.AF_APL_ID_SFX
	WHERE O2.AC_PRC_STA = 'A'
		AND O2.AF_CUR_LON_SER_AGY = '700121'
		AND O3.AC_STA_GA14 = 'A'
		AND O3.AC_LON_STA_TYP IN 
		(
			'ID','IG','DA','FB','RP','CR','IM'
		)
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.DF_PRS_ID_BR
;
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWA29.LWA29RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA SWOTB ;
	SET WORKLOCL.SWOTB;
RUN;
DATA OLNEL;
	SET WORKLOCL.OLNEL;
RUN;
PROC SORT DATA = SWOTB NODUPKEY; 
	BY BF_SSN LN_SEQ LN_BR_DSB_SEQ; 
RUN;
PROC SQL;
CREATE TABLE FIN AS 
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,A.IC_LON_PGM
	,A.LC_STA_LON10
	,A.LF_LON_CUR_OWN
	,A.IF_BND_ISS
	,A.BOR_BAL
	,A.LN_BAL
	,A.LA_CUR_PRI
	,A.LA_LTE_FEE_OTS
	,A.LA_NSI_OTS
	,A.DM_PRS_LST
	,A.DF_SPE_ACC_ID
	,A.OLIND
FROM SWOTB A
/*EXCLUSION CATEGORIES*/
LEFT OUTER JOIN ( 
		SELECT DISTINCT A.BF_SSN /*LOANS NOT OWNED BY UHEAA*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE EXISTS (
			SELECT *
			FROM SWOTB X
			WHERE X.BF_SSN = A.BF_SSN
				AND X.LN_SEQ = A.LN_SEQ
				AND X.LF_LON_CUR_OWN ^= '828476'
				AND X.LN_BAL > 0
			)
	UNION
		SELECT DISTINCT A.BF_SSN /*NOT FULLY DISBURSED*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE NOT EXISTS (
			SELECT *
			FROM SWOTB X
			WHERE X.BF_SSN = A.BF_SSN
				AND X.LN_SEQ = A.LN_SEQ
				AND X.LC_STA_LON15 IN ('1','3')
				AND X.LC_DSB_TYP = '2'
			)
	UNION
		SELECT DISTINCT BF_SSN /*INTEREST ONLY BALANCE*/
			,'X' AS EXCLD
		FROM (
			SELECT DISTINCT A.BF_SSN
				,LN_SEQ
				,LA_CUR_PRI
				,SUM(LA_DSB) 
				,SUM(LA_DSB_CAN)
			FROM SWOTB A
			GROUP BY BF_SSN
				,LN_SEQ
				,LA_CUR_PRI
			HAVING SUM(LA_DSB) = SUM(LA_DSB_CAN) 
				AND LA_CUR_PRI > 0
			)
	UNION
		SELECT DISTINCT A.BF_SSN /*CREDIT BALANCE*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE LA_CUR_PRI < 0
	UNION
		SELECT DISTINCT A.BF_SSN /*CLAIM SUBMITTED OR CLAIM PAID*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE WC_DW_LON_STA IN ('08', '12')
	UNION
		SELECT DISTINCT A.BF_SSN /*BOND ID*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE IF_BND_ISS ^= "&BOND"
			AND LN_BAL > 0
	UNION
		SELECT DISTINCT A.BF_SSN /*LOAN STATUS*/
			,'X' AS EXCLD
		FROM SWOTB A
		WHERE WC_DW_LON_STA IN ('01','02')
	) B
	ON A.BF_SSN = B.BF_SSN
WHERE B.EXCLD ^= 'X'
	AND LN_BAL > 0
;
QUIT;
PROC SORT DATA=FIN NODUPKEY;
	BY BF_SSN LN_SEQ;
RUN;
DATA FIN NEL;
	SET FIN;
	IF OLIND = 'Y' THEN 
		OUTPUT NEL;
	ELSE 
		OUTPUT FIN;
RUN;
PROC SQL;
	CREATE TABLE RNEL AS 
	SELECT DISTINCT B.*
	FROM NEL A
	INNER JOIN OLNEL B
		ON A.BF_SSN = B.BF_SSN
	;
QUIT;
PROC SQL;
CREATE TABLE SFIN AS
SELECT COUNT(DISTINCT BF_SSN) AS BORS
	,SUM(LA_CUR_PRI) AS PRIN
	,SUM(LA_NSI_OTS) AS INTR
	,SUM(LA_LTE_FEE_OTS) AS LTFE
FROM FIN
;
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER DATE PAGENO=1;
TITLE 'SPECIAL WRITE OFF TARGET BOND PRELIM';
FOOTNOTE   'JOB = UTLWA29  	 REPORT = ULWA29.LWA29R2';
PROC CONTENTS DATA=SFIN OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWA29  	 REPORT = ULWA29.LWA29R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=SFIN WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT BORS COMMA8. PRIN INTR LTFE DOLLAR20.2;
VAR BORS PRIN INTR LTFE;
LABEL BORS = '#/BORROWERS'
	PRIN = '$/PRINCIPAL'
	INTR = '$/INTEREST'
	LTFE= '$/LATE FEES';
RUN;
PROC PRINTTO;
RUN;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER;
TITLE 'SPECIAL WRITE OFF  - TARGET BOND';
TITLE2 'NELNET BORROWERS EXCLUDED';
FOOTNOTE 'JOB = UTLWA29  	 REPORT = ULWA29.LWA29R3';
PROC CONTENTS DATA=RNEL OUT=EMPTYSET NOPRINT;QUIT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = JOB = UTLWA29  	 REPORT = ULWA29.LWA29R3";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=RNEL WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT CBAL 14.2 AD_CUR_PRI_RPT MMDDYY10.;
VAR DM_PRS_LST DF_SPE_ACC_ID CLUID CBAL AD_CUR_PRI_RPT;
LABEL DM_PRS_LST = 'LAST NAME'
	DF_SPE_ACC_ID = 'ACCOUNT #'
	CLUID = 'CLUID'
	CBAL = 'CURRENT BALANCE'
	AD_CUR_PRI_RPT = 'BALANCE DATE';
RUN;
PROC PRINTTO;
RUN;
