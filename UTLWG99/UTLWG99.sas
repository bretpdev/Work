*----------------------------------------------------------*
| UTLWG99 TILP ACCTS RECEIVABLE AND BAD DEBT YEARLY REPORT |
*----------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG99.LWG99R2";
FILENAME REPORTZ "&RPTLIB/ULWG99.LWG99RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TARABDYR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.BF_SSN
	,B.LN_SEQ
	,C.WC_DW_LON_STA
	,E.LC_DFR_TYP
	,B.WA_CUR_PRI
	,CASE 
		WHEN B.WN_DAY_DLQ_ISL > 0 THEN 'Y'
		ELSE 'N'
	 END AS DELQ
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.MR5A_MR_LON_MTH_01 B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN OLWHRM1.DW01_DW_CLC_CLU C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN OLWHRM1.LN50_BR_DFR_APV D
	ON B.BF_SSN = D.BF_SSN
	AND B.LN_SEQ = D.LN_SEQ
	AND D.LC_STA_LON50 = 'A'
	AND DAYS(CURRENT DATE) BETWEEN DAYS(D.LD_DFR_BEG) AND DAYS(D.LD_DFR_END)
LEFT OUTER JOIN OLWHRM1.DF10_BR_DFR_REQ E
	ON D.BF_SSN = E.BF_SSN
	AND D.LF_DFR_CTL_NUM = E.LF_DFR_CTL_NUM
	AND E.LC_STA_DFR10 = 'A'
	AND E.LC_DFR_TYP IN ('06','15','18','33')
WHERE A.IC_LON_PGM = 'TILP'
	AND A.LA_CUR_PRI > 0
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWG99.LWG99RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA TARABDYR ;
	SET WORKLOCL.TARABDYR;
RUN;
DATA TARABDYR ;
	SET TARABDYR;
	JVAR = 1;
	IF WC_DW_LON_STA IN ('02','01') THEN 
		DELETE;
	ELSE IF WC_DW_LON_STA = '04' AND LC_DFR_TYP ^= '' THEN 
		DELETE;
	ELSE 
		OUTPUT;
RUN;
PROC SQL;
CREATE TABLE STARABDYR AS 
SELECT COALESCE(A.TAR,0) AS TAR
	,COALESCE(B.TBD,0) AS TBD
FROM (
	SELECT JVAR
		,SUM(WA_CUR_PRI) AS TAR
	FROM TARABDYR
	WHERE DELQ = 'N'
	GROUP BY JVAR
	) A
FULL OUTER JOIN (
	SELECT JVAR
		,SUM(WA_CUR_PRI) AS TBD
	FROM TARABDYR
	WHERE DELQ = 'Y'
	GROUP BY JVAR
	) B
	ON A.JVAR = B.JVAR
;
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER PAGENO=1 DATE;
TITLE 'TILP ACCOUNTS RECEIVABLE AND BAD DEBT REPORT';
FOOTNOTE   'JOB = UTLWG99  	 REPORT = ULWG99.LWG99R2';
PROC PRINT NOOBS SPLIT='/' DATA=STARABDYR WIDTH=UNIFORM WIDTH=MIN LABEL;
	FORMAT TAR TBD DOLLAR20.2;
	VAR TAR TBD;
	LABEL TAR = 'TOTAL/ACCOUNTS/RECEIVABLE'
		TBD = 'TOTAL/BAD/DEBT';
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE*/
PROC EXPORT DATA=TARABDYR
	OUTFILE= "T:\SAS\UTLWG99.Detail.xls" 
	DBMS=EXCEL2000 REPLACE;
RUN;
