*---------------------------------------------*
| UTLWU08 - ALTERNATE ADDRESS USAGE QC REPORT |
*---------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU08.LWU08R2";
FILENAME REPORT3 "&RPTLIB/ULWU08.LWU08R3";
FILENAME REPORTZ "&RPTLIB/ULWU08.LWU08RZ";
DATA _NULL_;
     CALL SYMPUT('PREV_DY',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT PREV_DY = &PREV_DY;
LIBNAME WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ALTAD AS
SELECT *
FROM CONNECTION TO DB2 (
/*********** COMPASS ***********/
	SELECT DISTINCT C.DF_SPE_ACC_ID
		,C.DM_PRS_LST
		,B.DC_ADR
		,B.DX_STR_ADR_1 AS AD1
		,B.DX_STR_ADR_2 AS AD2
		,B.DM_CT
		,B.DC_DOM_ST
		,B.DF_ZIP_CDE AS DF_ZIP
		,B.DF_LST_USR_PD30 AS UID
		,DATE(B.DF_LST_DTS_PD30) AS UDATE
		,'CO' AS SYS
	FROM OLWHRM1.LN10_LON A
	INNER JOIN OLWHRM1.PD30_PRS_ADR B
		ON A.BF_SSN = B.DF_PRS_ID
	INNER JOIN OLWHRM1.PD10_PRS_NME C
		ON B.DF_PRS_ID = C.DF_PRS_ID 
	LEFT JOIN (
			 	SELECT AA.LF_STU_SSN, CC.DC_ADR, 'X' AS NOTAKEEPER
				FROM OLWHRM1.SD10_STU_SPR AA
				INNER JOIN OLWHRM1.LN13_LON_STU_OSD BB
						ON AA.LF_STU_SSN = BB.BF_SSN
				INNER JOIN OLWHRM1.PD30_PRS_ADR CC
						ON AA.LF_STU_SSN = CC.DF_PRS_ID
				WHERE AA.LC_STA_STU10 = 'A' AND BB.LC_STA_LON13 = 'A' AND AA.LD_SCL_SPR > CC.DD_VER_ADR
				) D 
					ON D.LF_STU_SSN = A.BF_SSN 
					AND D.DC_ADR = B.DC_ADR
	WHERE A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
	AND B.DC_ADR <> 'L'
	AND B.DI_VLD_ADR = 'Y'
	AND SUBSTR(B.DF_LST_USR_PD30,1,2) IN ('PH','UT')
	AND DATE(B.DF_LST_DTS_PD30) = &PREV_DY
	AND D.NOTAKEEPER IS NULL
UNION
/*********** ONELINK ***********/
	SELECT DISTINCT D.DF_SPE_ACC_ID
		,D.DM_PRS_LST
		,C.DC_ADR
		,C.DX_STR_ADR_1 AS AD1
		,C.DX_STR_ADR_2	AS AD2
		,C.DM_CT
		,C.DC_DOM_ST
		,C.DF_ZIP
		,C.DF_USR_UPD_ADR AS UID
		,C.DD_LST_UPD_ADR AS UDATE
		,'OL' AS SYS
	FROM OLWHRM1.GA01_APP A
	INNER JOIN OLWHRM1.GA10_LON_APP B
		ON A.AF_APL_ID = B.AF_APL_ID 
	INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN C
		ON A.DF_PRS_ID_BR = C.DF_PRS_ID
	INNER JOIN OLWHRM1.PD01_PDM_INF D
		ON C.DF_PRS_ID = D.DF_PRS_ID
	LEFT JOIN (
				SELECT AA.DF_PRS_ID_STU, BB.DC_ADR, 'X' AS NOTAKEEPER
				FROM OLWHRM1.SD02_STU_ENR AA
				INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN BB
					ON AA.DF_PRS_ID_STU = BB.DF_PRS_ID
				WHERE AA.LC_STA_SD02 = 'A' AND AA.LC_STU_ENR_STA = 'I' AND BB.DD_LST_UPD_ADR < AA.LD_XPC_GRD
				) E
					ON C.DF_PRS_ID = E.DF_PRS_ID_STU
					AND C.DC_ADR = E.DC_ADR
	WHERE B.AA_CUR_PRI > 0
	AND C.DC_ADR <> 'L' 
	AND C.DI_VLD_ADR = 'Y'
	AND SUBSTR(C.DF_USR_UPD_ADR,1,2) IN ('PH','UT')
	AND C.DD_LST_UPD_ADR = &PREV_DY
	AND E.NOTAKEEPER IS NULL
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU08.LWU08RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA ALTAD;
SET WORKLOCL.ALTAD;
RUN;
PROC SORT DATA=ALTAD;
	BY DF_SPE_ACC_ID;
RUN;

DATA _NULL_;
SET ALTAD;
WHERE SYS = 'CO';
LENGTH DESCRIPTION $600.;
USER = UID;
ACT_DT = UDATE;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DM_PRS_LST,
	DC_ADR,
	AD1,
	AD2,
	DM_CT,
	DC_DOM_ST,
	DF_ZIP
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;

DATA _NULL_;
SET ALTAD;
WHERE SYS = 'OL';
LENGTH DESCRIPTION $600.;
USER = UID;
ACT_DT = UDATE;
DESCRIPTION = CATX(',',
	DF_SPE_ACC_ID,
	DM_PRS_LST,
	DC_ADR,
	AD1,
	DM_CT,
	DC_DOM_ST,
	DF_ZIP,
	UID
);
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
