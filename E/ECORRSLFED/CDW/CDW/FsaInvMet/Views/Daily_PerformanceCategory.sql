
CREATE VIEW [FsaInvMet].[Daily_PerformanceCategory]

AS

-- set performance category
SELECT
	BD.BF_SSN,
	BD.LN_SEQ,
	PF.PerformanceCategory
FROM
	[FsaInvMet].[Daily_BasePopulation] BD
	INNER JOIN
	(
		SELECT
			BD.BF_SSN,
			BD.LN_SEQ,
			CASE
				WHEN BD.ORD = 0 THEN
					CASE 
						WHEN M.ActiveMilitaryIndicator = 1 THEN '04'
						WHEN BD.WC_DW_LON_STA IN ('02','23') AND BD.LN_DLQ_MAX < 1 THEN '01'
						WHEN BD.WC_DW_LON_STA = '01' AND BD.LN_DLQ_MAX < 1 THEN '02'
						WHEN BD.WC_DW_LON_STA IN ('03','09','10','11','15','16','17','18','19','20','21','22') THEN 
							CASE
								WHEN BD.LN_DLQ_MAX = 0 AND BD.BILL_SATISFIED = 1 THEN '03'
								WHEN BD.SPEC_FORB_IND = 1 AND BD.LN_DLQ_MAX = 0 THEN '06'
								WHEN BD.DefermentIndicator = 1 AND BD.LN_DLQ_MAX = 0 THEN '05'
								WHEN BD.LN_DLQ_MAX = 0 AND (BD.LC_CAM_LON_STA = '02' OR BD.SepDate >= CAST(DATEADD(SECOND,-1,DATEADD(MONTH, DATEDIFF(MONTH,0,GETDATE())+1,0)) AS DATE)) THEN '01'
								WHEN BD.LN_DLQ_MAX = 0 AND (BD.LC_CAM_LON_STA = '01' OR (BD.SepDate < CAST(DATEADD(SECOND,-1,DATEADD(MONTH, DATEDIFF(MONTH,0,GETDATE())+1,0)) AS DATE) AND BD.GraceEnd >= CAST(DATEADD(SECOND,-1,DATEADD(MONTH, DATEDIFF(MONTH,0,GETDATE())+1,0)) AS DATE))) THEN '02'
								WHEN BD.LN_DLQ_MAX BETWEEN 0 AND 5 THEN '03'
								WHEN BD.LN_DLQ_MAX <= 30 THEN '07'
								WHEN BD.LN_DLQ_MAX <= 90 THEN '08'
								WHEN BD.LN_DLQ_MAX <= 150 THEN '09'
								WHEN BD.LN_DLQ_MAX <= 270 THEN '10'
								WHEN BD.LN_DLQ_MAX <= 360 THEN '11'
								WHEN BD.LN_DLQ_MAX > 360 THEN '12'
								ELSE '99'
							END
						WHEN BD.WC_DW_LON_STA IN ('04', '05') THEN 
							CASE
								WHEN BD.LA_OTS_PRI_ELG <= 0 THEN RIGHT('00'+ CAST(CAST(BD.WC_DW_LON_STA AS TINYINT) + 1 AS VARCHAR),2) --Paid off, but showing defer/forb (needs BU review)
								WHEN BD.LN_DLQ_MAX = 0 AND BD.BILL_SATISFIED = 1 THEN '03'
								WHEN BD.LN_DLQ_MAX = 0 THEN RIGHT('00'+ CAST(CAST(BD.WC_DW_LON_STA AS TINYINT) + 1 AS VARCHAR),2)--status 4 is category 5 etc.
								WHEN BD.LN_DLQ_MAX BETWEEN 1 AND 5 THEN '03'
								WHEN BD.LN_DLQ_MAX <= 30 THEN '07'
								WHEN BD.LN_DLQ_MAX <= 90 THEN '08'
								WHEN BD.LN_DLQ_MAX <= 150 THEN '09'
								WHEN BD.LN_DLQ_MAX <= 270 THEN '10'
								WHEN BD.LN_DLQ_MAX <= 360 THEN '11'
								WHEN BD.LN_DLQ_MAX > 360 THEN '12'
								ELSE '99'
							END
						WHEN BD.LN_DLQ_MAX <= 30 THEN '07'
						WHEN BD.LN_DLQ_MAX <= 90 THEN '08'
						WHEN BD.LN_DLQ_MAX <= 150 THEN '09'
						WHEN BD.LN_DLQ_MAX <= 270 THEN '10'
						WHEN BD.LN_DLQ_MAX <= 360 THEN '11'
						WHEN BD.LN_DLQ_MAX > 360 THEN '12'
						ELSE '99'
					END
				WHEN BD.ORD = 1 AND LD_PIF_RPT BETWEEN CAST(DATEADD(MONTH, DATEDIFF(MONTH, 0, GETDATE()), 0) AS DATE) AND CAST(DATEADD(SECOND,-1,DATEADD(MONTH, DATEDIFF(MONTH,0,GETDATE())+1,0)) AS DATE) THEN 'PIF'
				WHEN BD.ORD = 2 AND LC_STA_LON10 = 'D' AND LA_OTS_PRI_ELG = 0 AND LD_STA_LON10 BETWEEN CAST(DATEADD(MONTH, DATEDIFF(MONTH, 0, GETDATE()), 0) AS DATE) AND CAST(DATEADD(SECOND,-1,DATEADD(MONTH, DATEDIFF(MONTH,0,GETDATE())+1,0)) AS DATE) THEN 'TRN'
				WHEN BD.ORD = 1 THEN 'PIFPRV'
				WHEN BD.ORD = 2 THEN 'TRNPRV'
				ELSE 'PRV'
			END [PerformanceCategory]
		FROM
			[FsaInvMet].[Daily_BasePopulation] BD
			LEFT OUTER JOIN [FsaInvMet].[Daily_Military] M
				ON M.BF_SSN = BD.BF_SSN
				AND M.LN_SEQ = BD.LN_SEQ
	) PF 
		ON PF.BF_SSN = BD.BF_SSN
		AND PF.LN_SEQ = BD.LN_SEQ