/*TEST*/
%LET RPTLIB = Y:\BATCH\FTP;
%LET RPTLIBA = Y:\ARCHIVE\SAS;
%LET CTFILE = Q:\CS Loan Servicing\CornerStone Reports\Pending IDR apps-tasks to close\Test\;

/*LIVE*/
/*%LET RPTLIB = Z:\BATCH\FTP;*/
/*%LET RPTLIBA = Z:\ARCHIVE\SAS;*/
/*%LET CTFILE = Q:\CS Loan Servicing\CornerStone Reports\Pending IDR apps-tasks to close\;*/

%let guid = %sysfunc(uuidgen());/*THIS WILL GIVE THE FILE A UNIQUE NAME*/

FILENAME REPORT2 "&RPTLIB/IDRAOPFED_R2.&guid._&sysdate";
FILENAME REPORT3 "&RPTLIB/IDRAOPFED_R3.&guid._&sysdate";
FILENAME REPORT4 "&RPTLIB/IDRAOPFED_R4.&guid._&sysdate";
FILENAME REPORT5 "&RPTLIB/IDRAOPFED_R5.&guid._&sysdate";
FILENAME REPORT6 "&CTFILE/IDRAOPFED_R6.&guid._&sysdate..xlsx";
FILENAME REPORT2A "&RPTLIBA/IDRAOPFED_R2.&guid._&sysdate";
FILENAME REPORT3A "&RPTLIBA/IDRAOPFED_R3.&guid._&sysdate";
FILENAME REPORT4A "&RPTLIBA/IDRAOPFED_R4.&guid._&sysdate";
FILENAME REPORT5A "&RPTLIBA/IDRAOPFED_R5.&guid._&sysdate";

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=work  ;
RSUBMIT LEGEND;

/*%let DB = DNFPRQUT;  *This is test;*/
/*%let DB = DNFPRUUT;  *This is VUK3 test;*/
%let DB = DNFPUTDL;  *This is live;

LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;
LIBNAME AES DB2 DATABASE=&DB OWNER=AES; 
LIBNAME SESSION DB2 DATABASE=&DB  SCHEMA=SESSION CONNECTION=GLOBAL;

/*USING TEMP TABLE SO AI CAN JOIN TO MULTIPKE DB2 QUERIES*/
PROC SQL;
	CONNECT TO DB2 (DATABASE=&DB CONNECTION=GLOBAL);
	EXECUTE ( DECLARE GLOBAL TEMPORARY TABLE SESSION.POP
	          ( 
				BF_SSN  CHAR(9),
				WD_ACT_REQ DATE,
				PF_REQ_ACT CHAR(5),
				LD_ATY_REQ_RCV DATE,
				DF_SPE_ACC_ID CHAR(10)
	          )
			  ON COMMIT PRESERVE ROWS
	        ) BY DB2;
	INSERT INTO SESSION.POP
	SELECT DISTINCT
		WQ20.BF_SSN,
		WQ20.WD_ACT_REQ,
		ARC.PF_REQ_ACT,
		ARC.LD_ATY_REQ_RCV,
		PD10.DF_SPE_ACC_ID
	FROM 
		PKUB.WQ20_TSK_QUE WQ20
		INNER JOIN
		(
			SELECT
				WF_QUE,
				WF_SUB_QUE,
				WN_CTL_TSK,
				PF_REQ_ACT,
				MIN(WF_CRT_DTS_WQ20) AS WF_CRT_DTS_WQ20
			FROM
				PKUB.WQ20_TSK_QUE
			GROUP BY
				WF_QUE,
				WF_SUB_QUE,
				WN_CTL_TSK,
				PF_REQ_ACT
		)MAX_QUEUE
			ON MAX_QUEUE.WF_QUE = WQ20.WF_QUE
			AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
			AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
			AND MAX_QUEUE.PF_REQ_ACT = WQ20.PF_REQ_ACT
		INNER JOIN PKUB.LN10_LON LN10
			ON LN10.BF_SSN = WQ20.BF_SSN
		INNER JOIN PKUB.PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = LN10.BF_SSN
		LEFT JOIN /*GETS EXCLUSION ARCS*/
		(
			SELECT
				AY10.BF_SSN,
				AY10.PF_REQ_ACT,
				AY10.LD_ATY_REQ_RCV
			FROM
				PKUB.AY10_BR_LON_ATY AY10
				INNER JOIN
				(
					SELECT
						BF_SSN,
						LN_ATY_SEQ,
						MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
					FROM
						PKUB.AY10_BR_LON_ATY
					WHERE
						PF_REQ_ACT IN ('DIBNK','DIINC','DITAX','IBAPV','ICAPV','PEAPV','REAPV','IBDN2','IBDNY','ICDNY','PEDNY','REDNY','NIDR2')
					GROUP BY
						BF_SSN,
						LN_ATY_SEQ
				)POP
					ON POP.BF_SSN = AY10.BF_SSN
					AND POP.LN_ATY_SEQ = AY10.LN_ATY_SEQ
					AND POP.LD_ATY_REQ_RCV = AY10.LD_ATY_REQ_RCV
		)ARC
			ON WQ20.BF_SSN = ARC.BF_SSN
		LEFT JOIN /*GETS ALL IDR SCHEDULES*/
		(
			SELECT DISTINCT
				BF_SSN
			FROM
				PKUB.LN65_LON_RPS
			WHERE
				LC_TYP_SCH_DIS IN ('AG','CA','CP','CQ','C1','C2','C3','IA','IB','IL','IP','I3','I5')/*IDR schedules*/
		)HAD_IDR
			ON HAD_IDR.BF_SSN = LN10.BF_SSN
		WHERE
			WQ20.WF_QUE = 'NP'	
			AND WQ20.WC_STA_WQUE20 IN ('A','H','P','U','W')
			AND LN10.LA_CUR_PRI > 0
			AND LN10.LC_STA_LON10 = 'R'
			AND HAD_IDR.BF_SSN IS NULL
	;
	DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CONNECT TO DB2 (DATABASE=&DB CONNECTION=GLOBAL);
	CREATE TABLE DIALER_FILES AS
		SELECT	
			*
		FROM	
			CONNECTION TO DB2 
				(
					SELECT DISTINCT
						PD10.DF_SPE_ACC_ID,
						PD10.DF_PRS_ID,
						PD10.DM_PRS_1,
						PD10.DM_PRS_LST,
						PD30.DX_STR_ADR_1,
						PD30.DX_STR_ADR_2,
						PD30.DX_STR_ADR_3,
						PD30.DM_CT,
						PD30.DC_DOM_ST,
						PD30.DF_ZIP_CDE,
						PD30.DM_FGN_CNY,
						PD30.DM_FGN_ST,
						PD30.DI_VLD_ADR,
						COALESCE(PAST_DUE.DAYS_DELQ, 0) AS DAYS_DELQ,
						PD40_HOME.HOME_CONSENT,
						PD40_HOME.HOME_PHONE,
						PD40_ALT.ALT_CONSENT,
						PD40_ALT.ALT_PHONE,
						PD40_WORK.WORK_CONSENT,
						PD40_WORK.WORK_PHONE,
						LN10.LF_LON_CUR_OWN,
						COALESCE(AMT_DUE.AMOUNT_DUE, 0) AS AMOUNT_DUE,
						SUM(LN10.LA_CUR_PRI + COALESCE(DW01.LA_NSI_OTS,0)) AS TOTAL_BALANCE,
						CASE
							WHEN ((DAYS(CURRENT_DATE) - DAYS(WD_ACT_REQ)) BETWEEN 1 AND 7) AND COALESCE(ARC_POP_FILE1.CALL_COUNT, 0) < 2  AND P.WD_ACT_REQ > COALESCE(ARC_POP_FILE2.LD_ATY_REQ_RCV, '01/01/1960') THEN 1
							WHEN ((DAYS(CURRENT_DATE) - DAYS(WD_ACT_REQ)) BETWEEN 14 AND 21) AND COALESCE(ARC_POP_FILE1.CALL_COUNT,0) < 4  AND P.WD_ACT_REQ > COALESCE(ARC_POP_FILE2.LD_ATY_REQ_RCV, '01/01/1960') THEN 2
							ELSE 0
						END AS FILE
					FROM 
						SESSION.POP P
						INNER JOIN PKUB.PD10_PRS_NME PD10
							ON PD10.DF_PRS_ID = P.BF_SSN
						INNER JOIN PKUB.LN10_LON LN10
							ON PD10.DF_PRS_ID = LN10.BF_SSN
						INNER JOIN PKUB.DW01_DW_CLC_CLU DW01
							ON DW01.BF_SSN = LN10.BF_SSN
							AND DW01.LN_SEQ = LN10.LN_SEQ
							AND DW01.WC_DW_LON_STA  IN('01','02','03','05')
						INNER JOIN PKUB.PD30_PRS_ADR PD30
							ON PD30.DF_PRS_ID = PD10.DF_PRS_ID
							AND PD30.DC_ADR = 'L'
						LEFT JOIN
						(
							SELECT
								PD40.DF_PRS_ID,
								(PD40.DN_DOM_PHN_ARA || PD40.DN_DOM_PHN_XCH || PD40.DN_DOM_PHN_LCL) AS HOME_PHONE,
								CASE
									WHEN PD40.DC_ALW_ADL_PHN IN ('L','Q','P') THEN 'Y'
									ELSE 'N'
								END AS HOME_CONSENT
							FROM 
								PKUB.PD40_PRS_PHN PD40
							WHERE
								PD40.DC_PHN = 'H'
								AND PD40.DI_PHN_VLD = 'Y'
						)PD40_HOME
							ON PD40_HOME.DF_PRS_ID = PD10.DF_PRS_ID
						LEFT JOIN
						(
							SELECT
								PD40.DF_PRS_ID,
								(PD40.DN_DOM_PHN_ARA || PD40.DN_DOM_PHN_XCH || PD40.DN_DOM_PHN_LCL) AS ALT_PHONE,
								CASE
									WHEN PD40.DC_ALW_ADL_PHN IN ('L','Q','P') THEN 'Y'
									ELSE 'N'
								END AS ALT_CONSENT
							FROM 
								PKUB.PD40_PRS_PHN PD40
							WHERE
								PD40.DC_PHN = 'A'
								AND PD40.DI_PHN_VLD = 'Y'
						)PD40_ALT
							ON PD40_ALT.DF_PRS_ID = PD10.DF_PRS_ID
						LEFT JOIN
						(
							SELECT
								PD40.DF_PRS_ID,
								(PD40.DN_DOM_PHN_ARA || PD40.DN_DOM_PHN_XCH || PD40.DN_DOM_PHN_LCL) AS WORK_PHONE,
								CASE
									WHEN PD40.DC_ALW_ADL_PHN IN ('L','Q','P') THEN 'Y'
									ELSE 'N'
								END AS WORK_CONSENT
							FROM 
								PKUB.PD40_PRS_PHN PD40
							WHERE
								PD40.DC_PHN = 'W'
								AND PD40.DI_PHN_VLD = 'Y'

						)PD40_WORK
							ON PD40_WORK.DF_PRS_ID = PD10.DF_PRS_ID
						LEFT JOIN /*GETS DAYS PAST DUE*/
						(
							SELECT
								LN16.BF_SSN,
								(MAX(LN16.LN_DLQ_MAX) + 1) AS DAYS_DELQ
							FROM
								PKUB.LN16_LON_DLQ_HST LN16
							WHERE
								LC_STA_LON16 = '1'
							GROUP BY
								LN16.BF_SSN
						)PAST_DUE
							ON PAST_DUE.BF_SSN = PD10.DF_PRS_ID
						LEFT JOIN /*GETS THE BORROWERS CURRENT AMOUNT DUE*/
						(
							SELECT
								LN80.BF_SSN,
								SUM(LN80.LA_BIL_PAS_DU + LN80.LA_BIL_CUR_DU + LN80.LA_LTE_FEE_OTS_PRT) AS AMOUNT_DUE
							FROM
								PKUB.LN80_LON_BIL_CRF LN80
							WHERE 
								LN80.LC_BIL_TYP_LON = 'P'
								AND LN80.LC_STA_LON80 = 'A'
								AND DAYS(LN80.LD_BIL_CRT) > DAYS(CURRENT_DATE) - 31 
							GROUP BY
								LN80.BF_SSN
						) AMT_DUE
							ON AMT_DUE.BF_SSN = PD10.DF_PRS_ID
						LEFT JOIN /*GETS THE MAX INSTANCE OF AN EXCLUSION ARC*/
						(
							SELECT
								AY10.BF_SSN,
								COUNT(*) AS  CALL_COUNT
							FROM
								PKUB.AY10_BR_LON_ATY AY10
								INNER JOIN SESSION.POP P
									ON P.BF_SSN = AY10.BF_SSN
							WHERE
								AY10.PF_REQ_ACT = 'NIDRA'
								AND  AY10.LD_ATY_REQ_RCV > P.WD_ACT_REQ 
						GROUP BY
								AY10.BF_SSN
						)ARC_POP_FILE1
							ON ARC_POP_FILE1.BF_SSN = P.BF_SSN
						LEFT JOIN /*GET THE MAX INSTANCE ON AN EXCLSUION ARC*/
						(
							SELECT
								BF_SSN,
								MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
							FROM
								PKUB.AY10_BR_LON_ATY
							WHERE
								PF_REQ_ACT IN('DIBNK','DIINC','DITAX','IBAPV','ICAPV','PEAPV','REAPV','IBDN2','IBDNY','ICDNY','PEDNY','REDNY','NIDR2')
							GROUP BY
								BF_SSN
						)ARC_POP_FILE2
							ON ARC_POP_FILE2.BF_SSN = P.BF_SSN
					WHERE
						(PD40_HOME.DF_PRS_ID IS NOT NULL OR PD40_ALT.DF_PRS_ID IS NOT NULL OR PD40_WORK.DF_PRS_ID IS NOT NULL)/*HAS AT LEAST 1 VALID PHONE*/
					GROUP BY 
						PD10.DF_SPE_ACC_ID,
						PD10.DF_PRS_ID,
						PD10.DM_PRS_1,
						PD10.DM_PRS_LST,
						PD30.DX_STR_ADR_1,
						PD30.DX_STR_ADR_2,
						PD30.DX_STR_ADR_3,
						PD30.DM_CT,
						PD30.DC_DOM_ST,
						PD30.DF_ZIP_CDE,
						PD30.DM_FGN_CNY,
						PD30.DM_FGN_ST,
						PD30.DI_VLD_ADR,
						PAST_DUE.DAYS_DELQ,
						PD40_HOME.HOME_CONSENT,
						PD40_HOME.HOME_PHONE,
						PD40_ALT.ALT_CONSENT,
						PD40_ALT.ALT_PHONE,
						PD40_WORK.WORK_CONSENT,
						PD40_WORK.WORK_PHONE,
						LN10.LF_LON_CUR_OWN,
						AMT_DUE.AMOUNT_DUE,
						CASE
							WHEN ((DAYS(CURRENT_DATE) - DAYS(WD_ACT_REQ)) BETWEEN 1 AND 7) AND COALESCE(ARC_POP_FILE1.CALL_COUNT,0) < 2  AND P.WD_ACT_REQ > COALESCE(ARC_POP_FILE2.LD_ATY_REQ_RCV, '01/01/1960') THEN 1
							WHEN ((DAYS(CURRENT_DATE) - DAYS(WD_ACT_REQ)) BETWEEN 14 AND 21) AND COALESCE(ARC_POP_FILE1.CALL_COUNT,0) < 4  AND P.WD_ACT_REQ > COALESCE(ARC_POP_FILE2.LD_ATY_REQ_RCV, '01/01/1960') THEN 2
							ELSE 0
						END
						
					FOR READ ONLY WITH UR
				);
	DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CONNECT TO DB2 (DATABASE=&DB CONNECTION=GLOBAL);
	CREATE TABLE LETTER_FILES AS
		SELECT	
			*
		FROM	
			CONNECTION TO DB2 
				(
					SELECT DISTINCT
						PD10.DF_SPE_ACC_ID,
						PD10.DF_PRS_ID,
						PD10.DM_PRS_1,
						PD10.DM_PRS_LST,
						PD30.DX_STR_ADR_1,
						PD30.DX_STR_ADR_2,
						PD30.DM_CT,
						PD30.DC_DOM_ST,
						PD30.DF_ZIP_CDE,
						PD30.DM_FGN_CNY,
						PD30.DM_FGN_ST,
						CASE
							WHEN (DAYS(CURRENT_DATE) - DAYS(P.WD_ACT_REQ)) BETWEEN 10 AND 17 AND P.WD_ACT_REQ > COALESCE(P.LD_ATY_REQ_RCV, '01/01/1960') AND COALESCE(ARC.LD_ATY_REQ_RCV, '01/01/1960') < P.WD_ACT_REQ THEN 1
							WHEN (DAYS(CURRENT_DATE) - DAYS(P.WD_ACT_REQ)) BETWEEN 25 AND 33 AND P.WD_ACT_REQ > COALESCE(P.LD_ATY_REQ_RCV, '01/01/1960') THEN 2
							ELSE 0
						END AS FILE
					FROM
						SESSION.POP P
						INNER JOIN PKUB.PD10_PRS_NME PD10
							ON PD10.DF_PRS_ID = P.BF_SSN
						INNER JOIN PKUB.PD30_PRS_ADR PD30
							ON PD30.DF_PRS_ID = PD10.DF_PRS_ID
							AND PD30.DC_ADR = 'L'
						LEFT JOIN AES.PH05_CNC_EML PH05
							ON PH05.DF_SPE_ID = PD10.DF_SPE_ACC_ID
							AND PH05.DI_VLD_CNC_EML_ADR = 'Y'
							AND PH05.DI_CNC_ELT_OPI = 'Y'
						LEFT JOIN /*GET THE MAX INSTANCE ON AN EXCLSUION ARC*/
						(
							SELECT
								BF_SSN,
								MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
							FROM
								PKUB.AY10_BR_LON_ATY
							WHERE
								PF_REQ_ACT = 'NIDR1'
							GROUP BY
								BF_SSN
						)ARC
							on ARC.BF_SSN = P.BF_SSN
					WHERE
						(PD30.DI_VLD_ADR = 'Y' OR PH05.DF_SPE_ID IS NOT NULL)/*HAS A VALID MAILING ADDRESS OR IS ON ECORR*/
						
					FOR READ ONLY WITH UR
				);
	DISCONNECT FROM DB2;
QUIT;

PROC SQL;
	CONNECT TO DB2 (DATABASE=&DB CONNECTION=GLOBAL);
	CREATE TABLE QUEUE_TASKS_FILE AS
		SELECT	
			*
		FROM	
			CONNECTION TO DB2 
				(
					SELECT DISTINCT
						CURRENT_DATE AS DATE,
						DF_SPE_ACC_ID AS ACCOUNT,
						ARC.PF_REQ_ACT AS ARC,
						WQ20.WN_CTL_TSK,
						CASE
							WHEN ARC.PF_REQ_ACT in ('DIBNK','DIINC','DITAX') THEN 'Documentation sent in, stop contact'
							WHEN ARC.PF_REQ_ACT in ('IBDN2','IBDNY','PEDNY','PEDNY','REDNY','ICDNY') THEN 'IDR Application denied'
							WHEN ARC.PF_REQ_ACT = 'NIDR2' THEN '25 day letter sent'
							WHEN ARC.PF_REQ_ACT in ('IBAPV','ICAPV','PEAPV', 'REAPV') THEN 'IDR Application approved'
						END AS COMMENT
					FROM
						PKUB.WQ20_TSK_QUE WQ20
						INNER JOIN
						(
							SELECT
								WF_QUE,
								WF_SUB_QUE,
								WN_CTL_TSK,
								PF_REQ_ACT,
								MIN(WF_CRT_DTS_WQ20) AS WF_CRT_DTS_WQ20
							FROM
								PKUB.WQ20_TSK_QUE
							GROUP BY
								WF_QUE,
								WF_SUB_QUE,
								WN_CTL_TSK,
								PF_REQ_ACT
						)MAX_QUEUE
							ON MAX_QUEUE.WF_QUE = WQ20.WF_QUE
							AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
							AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
							AND MAX_QUEUE.PF_REQ_ACT = WQ20.PF_REQ_ACT
						INNER JOIN PKUB.PD10_PRS_NME PD10
							ON PD10.DF_PRS_ID = WQ20.BF_SSN
						LEFT JOIN /*GETS EXCLUSION ARCS*/
						(
							SELECT
								AY10.BF_SSN,
								AY10.PF_REQ_ACT,
								AY10.LD_ATY_REQ_RCV
							FROM
								PKUB.AY10_BR_LON_ATY AY10
								INNER JOIN
								(
									SELECT
										BF_SSN,
										LN_ATY_SEQ,
										MAX(LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
									FROM
										PKUB.AY10_BR_LON_ATY
									WHERE
										PF_REQ_ACT IN ('DIBNK','DIINC','DITAX','IBAPV','ICAPV','PEAPV','REAPV','IBDN2','IBDNY','ICDNY','PEDNY','REDNY','NIDR2')
									GROUP BY
										BF_SSN,
										LN_ATY_SEQ
								)POP
									ON POP.BF_SSN = AY10.BF_SSN
									AND POP.LN_ATY_SEQ = AY10.LN_ATY_SEQ
									AND POP.LD_ATY_REQ_RCV = AY10.LD_ATY_REQ_RCV
						)ARC
							ON WQ20.BF_SSN = ARC.BF_SSN
						WHERE
							ARC.PF_REQ_ACT IS NOT NULL 
							AND LD_ATY_REQ_RCV > WD_ACT_REQ
							AND WQ20.WF_QUE = 'NP'	
							AND WQ20.WC_STA_WQUE20 IN ('A','H','P','U','W')
					UNION ALL
						SELECT DISTINCT
							CURRENT_DATE AS DATE,
							DF_SPE_ACC_ID AS ACCOUNT,
							'N/A' AS ARC,
							WQ20.WN_CTL_TSK,
							'DUPLICATE NP QUEUE TASK' AS COMMENT
						FROM
							PKUB.WQ20_TSK_QUE WQ20
							INNER JOIN PKUB.PD10_PRS_NME PD10
								ON PD10.DF_PRS_ID = WQ20.BF_SSN
							LEFT JOIN
							(
								SELECT
									WF_QUE,
									WF_SUB_QUE,
									WN_CTL_TSK,
									PF_REQ_ACT,
									MIN(WF_CRT_DTS_WQ20) AS WF_CRT_DTS_WQ20
								FROM
									PKUB.WQ20_TSK_QUE
								GROUP BY
									WF_QUE,
									WF_SUB_QUE,
									WN_CTL_TSK,
									PF_REQ_ACT
							)MAX_QUEUE
								ON MAX_QUEUE.WF_QUE = WQ20.WF_QUE
								AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
								AND MAX_QUEUE.WF_SUB_QUE = WQ20.WF_SUB_QUE
								AND MAX_QUEUE.PF_REQ_ACT = WQ20.PF_REQ_ACT
						WHERE
							MAX_QUEUE.WF_QUE IS NULL
							AND WQ20.WF_QUE = 'NP'	
							AND WQ20.WC_STA_WQUE20 IN ('A','H','P','U','W')
					UNION ALL
						
					SELECT DISTINCT
						CURRENT_DATE AS DATE,
						DF_SPE_ACC_ID AS ACCOUNT,
						'N/A' AS ARC,
						WQ20.WN_CTL_TSK,
						'BORROWER HAS PREVIOUSLY PARTICIPATED OR IS CURRENTLY ENROLLED IN AN INCOME DRIVEN REPAYMENT PLAN' AS COMMENT
					FROM
						PKUB.WQ20_TSK_QUE WQ20
						INNER JOIN PKUB.PD10_PRS_NME PD10
								ON PD10.DF_PRS_ID = WQ20.BF_SSN
					INNER JOIN /*GETS ALL IDR SCHEDULES*/
					(
						SELECT DISTINCT
							BF_SSN
						FROM
							PKUB.LN65_LON_RPS
						WHERE
							LC_TYP_SCH_DIS IN ('AG','CA','CP','CQ','C1','C2','C3','IA','IB','IL','IP','I3','I5')/*IDR schedules*/
					)HAD_IDR
						ON HAD_IDR.BF_SSN = WQ20.BF_SSN
					WHERE
						WQ20.WF_QUE = 'NP'	
						AND WQ20.WC_STA_WQUE20 IN ('A','H','P','U','W')
											
					FOR READ ONLY WITH UR
				);
	DISCONNECT FROM DB2;
QUIT;

ENDRSUBMIT;

DATA DIALER_FILES;
SET LEGEND.DIALER_FILES;
RUN;

DATA LETTER_FILES;
SET LEGEND.LETTER_FILES;
RUN;

DATA QUEUE_TASKS_FILE;
SET LEGEND.QUEUE_TASKS_FILE;
RUN;

PROC EXPORT DATA = WORK.QUEUE_TASKS_FILE 
            OUTFILE = REPORT6
            DBMS = EXCEL
			REPLACE;
     SHEET="Sheet1"; 
RUN;

DATA LETTER_FILES (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
	SET LETTER_FILES;
	KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
	MODAY = PUT(DATE(),MMDDYYN4.);
	KEYLINE = "P"||KEYSSN||MODAY||"L";
	CHKDIG = 0;
	LENGTH DIG $2.;
	DO I = 1 TO LENGTH(KEYLINE);
		IF I/2 NE ROUND(I/2,1) 
			THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
		ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
		IF SUBSTR(DIG,1,1) = " " 
			THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
			ELSE DO;
				CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
				CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
				IF CHK1 + CHK2 >= 10
					THEN DO;
						CHK3 = PUT(CHK1 + CHK2,2.);
						CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
						CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
					END;
				CHKDIG = CHKDIG + CHK1 + CHK2;
			END;
	END;
	CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
	IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
	CHECK = PUT(CHKDIGIT,1.);
	ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

DATA D_9_17;
SET LETTER_FILES;
WHERE FILE = 1;
RUN;

DATA D_25_33;
SET LETTER_FILES;
WHERE FILE = 2;
RUN;

%MACRO CREATE_LETTER_FILES(DATA_SET, REPORT);
DATA _NULL_;
	SET &DATA_SET;
	FILE &REPORT DELIMITER=',' DSD DROPOVER LRECL=32767;

	IF _N_ = 1 THEN
		DO;
			PUT
				'DF_SPE_ACC_ID'
				','
				'DM_PRS_1'
				','
				'DM_PRS_LST'
				','
				'DX_STR_ADR_1'
				','
				'DX_STR_ADR_2'
				','
				'DM_CT'
				','
				'DC_DOM_ST'
				','
				'DF_ZIP_CDE'
				','
				'DM_FGN_CNY'
				','
				'DM_FGN_ST'
				','
				'KeyLine'
				','
				'CostCenter';
		END;
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT DM_PRS_1 $ @;
		PUT DM_PRS_LST $ @;
		PUT DX_STR_ADR_1 $ @;
		PUT DX_STR_ADR_2 $ @;
		PUT DM_CT $ @;
		PUT DC_DOM_ST $ @;
		PUT DF_ZIP_CDE $ @;
		PUT DM_FGN_CNY $ @;
		PUT DM_FGN_ST $ @;
		PUT ACSKEY $ @;
		PUT 'MA4481' ;
	;
	END;
RUN;
%MEND;

%CREATE_LETTER_FILES(D_9_17,REPORT4);
%CREATE_LETTER_FILES(D_25_33,REPORT5);
%CREATE_LETTER_FILES(D_9_17,REPORT4A);
%CREATE_LETTER_FILES(D_25_33,REPORT5A);

DATA DIALER_FILES;
	SET DIALER_FILES;
	NAME = CATX(' ', DM_PRS_1,DM_PRS_LST);
	AMOUNT_DUE = TRANWRD(PUT(AMOUNT_DUE, 10.), ".", "");
	TOTAL_BALANCE = TRANWRD(PUT(TOTAL_BALANCE, 10.), ".", "");
RUN;

DATA D_7_14;
SET DIALER_FILES;
WHERE FILE = 1;
RUN;

DATA D_14_21;
SET DIALER_FILES;
WHERE FILE = 2;
RUN;

%MACRO CREATE_DIALER_FILES(DATA_SET, REPORT);
DATA _NULL_;
SET &DATA_SET;
FILE &REPORT DROPOVER LRECL=32767;
DO;
		PUT @1 DF_PRS_ID   			
		@15 NAME
		@70 DAYS_DELQ
		@159 HOME_CONSENT
		@160 HOME_PHONE
		@175 ALT_CONSENT
		@176 ALT_PHONE
		@191 WORK_CONSENT
		@192 WORK_PHONE
		@260 DX_STR_ADR_1
		@290 DX_STR_ADR_2
		@320 DX_STR_ADR_3
		@350 DM_CT
		@370 DC_DOM_ST
		@372 DF_ZIP_CDE
		@414 LF_LON_CUR_OWN
		@422 AMOUNT_DUE
		@429 TOTAL_BALANCE;	
	END;
RUN;
%MEND;

%CREATE_DIALER_FILES(D_7_14,REPORT2);
%CREATE_DIALER_FILES(D_14_21,REPORT3);
%CREATE_DIALER_FILES(D_7_14,REPORT2A);
%CREATE_DIALER_FILES(D_14_21,REPORT3A);
