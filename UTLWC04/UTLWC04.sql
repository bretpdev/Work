USE OLS
GO

CREATE SCHEMA utlwc04 AUTHORIZATION dbo
GO

GRANT EXECUTE ON SCHEMA ::[utlwc04] TO db_executor
GO

CREATE PROCEDURE utlwc04.ClaimsInProcess
AS

DECLARE @Today DATE = GETDATE()
DECLARE @CommentStarts TABLE
(
	[Start] INT 
)

INSERT INTO @CommentStarts([Start]) --Places where a valid CLID might exist in a comment
VALUES
	(76),
	(96),
	(116),
	(151),
	(171),
	(191),
	(226),
	(246),
	(266),
	(301),
	(321),
	(341),
	(376),
	(396),
	(416),
	(451),
	(471),
	(491),
	(526),
	(546),
	(566)

SELECT
	[ACCT],
	DF_PRS_ID,
	[FIRST NAME],
	[LAST NAME],
	[HOLDER],
	[SERVICER],
	[CLAIM TYPE],
	[CLAIM RECEIVED DATE],
	LD_STA_UPD_DC10,
	[UNIQUE ID],
	LC_STA_DC10,
	[CLAIM PRINCIPAL],
	ISNULL(TRY_CAST([CLAIM INTEREST] AS NUMERIC(18,2)),CAST(0.00 AS NUMERIC(18,2))) AS [CLAIM INTEREST]
FROM
( 
	SELECT DISTINCT 
		PD01.DF_SPE_ACC_ID AS [ACCT],
		PD01.DF_PRS_ID,
		PD01.DM_PRS_1 AS [FIRST NAME],
		PD01.DM_PRS_LST AS [LAST NAME],
		DC01.IF_OPS_LDR AS [HOLDER],
		DC01.LF_CUR_LON_SER_AGY AS [SERVICER],
		DC01.LC_PCL_REA AS [CLAIM TYPE],
		AY01.CLAIMREC AS [CLAIM RECEIVED DATE],
		DC01.LD_STA_UPD_DC10,
		CONCAT(DC01.AF_APL_ID,DC01.AF_APL_ID_SFX) AS [UNIQUE ID],--CLID,
		DC01.LC_STA_DC10,
		DC01.LA_CLM_PRI AS [CLAIM PRINCIPAL],
		GA10.AA_OTS_ACR_INT AS [CLAIM INTEREST]
	FROM 
		(
			SELECT
				AY01.DF_PRS_ID,
				AY01.PF_ACT,
				AY01.BX_CMT,
				CAST(AY01.BF_CRT_DTS_AY01 AS DATETIME) AS BF_CRT_DTS_AY01,
				TRY_CONVERT(DATETIME, ISNULL(SUBSTRING(BX_CMT, 63, 2), '') + '/' + ISNULL(SUBSTRING(BX_CMT, 65, 2), '') + '/' + ISNULL(SUBSTRING(BX_CMT, 67, 4), ''), 101) AS CLAIMREC,
				ISNULL(SUBSTRING(BX_CMT, CS.[Start], 19),'') AS CLID
			FROM
				ODW..AY01_BR_ATY AY01
				INNER JOIN @CommentStarts CS --We self join because we want to duplicate on every possible comment
					ON CS.[Start] = CS.[Start]
			WHERE
				PF_ACT IN ('RCLMR','RCLRC')
				AND CAST(BF_CRT_DTS_AY01 AS DATETIME) > DATEADD(DAY, -90, @Today)
				AND ISNULL(SUBSTRING(BX_CMT, CS.[Start], 19),'') != ''
		) AY01
		INNER JOIN ODW..PD01_PDM_INF PD01
			ON PD01.DF_PRS_ID = AY01.DF_PRS_ID
		INNER JOIN ODW..DC01_LON_CLM_INF DC01
			ON CONCAT(DC01.AF_APL_ID,DC01.AF_APL_ID_SFX) = AY01.CLID
		INNER JOIN 
		(
			SELECT 
				DC01.AF_APL_ID,
				DC01.AF_APL_ID_SFX,
				MAX(DC01.LD_STA_UPD_DC10) AS LD_STA_UPD_DC10		 
			FROM	
				ODW..DC01_LON_CLM_INF DC01	 
			GROUP BY 
				AF_APL_ID, AF_APL_ID_SFX
		) DC01_MAX
			ON DC01_MAX.AF_APL_ID = DC01.AF_APL_ID
			AND DC01_MAX.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
			AND DC01_MAX.LD_STA_UPD_DC10 = DC01.LD_STA_UPD_DC10
		LEFT JOIN ODW..GA10_LON_APP GA10
			ON GA10.AF_APL_ID = DC01.AF_APL_ID
			AND GA10.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	WHERE 
		DC01.LC_STA_DC10 IN ('01')
		AND 
		(
			DC01.BD_CLM_PKG_RTN IS NULL
			OR DC01.BD_CLM_PKG_RTN < DC01.BD_CLM_PKG_RCV
			OR DC01.BD_CLM_PKG_RS IS NOT NULL
		)
		AND DC01.LD_STA_UPD_DC10 <= CAST(AY01.BF_CRT_DTS_AY01 AS DATETIME)

	UNION

	/*SELECT THE CLAIMS ADDED THROUGH THE CAM SYSTEM*/
	SELECT DISTINCT 
		PD01.DF_SPE_ACC_ID AS [ACCT],
		PD01.DF_PRS_ID,
		PD01.DM_PRS_1 AS [FIRST NAME],
		PD01.DM_PRS_LST AS [LAST NAME],
		DC01.IF_OPS_LDR AS [HOLDER],
		DC01.LF_CUR_LON_SER_AGY AS [SERVICER],
		DC01.LC_PCL_REA AS [CLAIM TYPE],
		CAST(DC01.BF_CRT_DTS_DC01 AS DATETIME) AS [CLAIM RECEIVED DATE],--CLAIMREC,
		DC01.LD_STA_UPD_DC10,
		CONCAT(DC01.AF_APL_ID,DC01.AF_APL_ID_SFX) AS [UNIQUE ID],--CLID,
		DC01.LC_STA_DC10,
		DC01.LA_CLM_PRI AS [CLAIM PRINCIPAL],
		GA10.AA_OTS_ACR_INT AS [CLAIM INTEREST]
	FROM 
		ODW..PD01_PDM_INF PD01
		INNER JOIN ODW..DC01_LON_CLM_INF DC01
			ON DC01.BF_SSN = PD01.DF_PRS_ID
		INNER JOIN 
		(
			SELECT 
				DC01.AF_APL_ID,
				DC01.AF_APL_ID_SFX,
				DC01.LF_CRT_DTS_DC10,
				MAX(DC01.LD_STA_UPD_DC10) AS LD_STA_UPD_DC10		 
			 FROM	
				ODW..DC01_LON_CLM_INF DC01
			WHERE
				DC01.LC_STA_DC10 IN ('01')
				AND DC01.BF_USR_CRT_DC01 = 'LCXGX'
				AND CAST(DC01.BF_CRT_DTS_DC01 AS DATETIME) > DATEADD(DAY, -90, @Today)
				AND ISNULL(DC01.BC_CLM_PKG_RTN_REA, '') = ''
			 GROUP BY 
				AF_APL_ID, 
				AF_APL_ID_SFX,
				LF_CRT_DTS_DC10
		) DC01_MAX
			ON DC01_MAX.AF_APL_ID = DC01.AF_APL_ID
			AND DC01_MAX.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
			AND DC01_MAX.LF_CRT_DTS_DC10 = DC01.LF_CRT_DTS_DC10
			AND DC01_MAX.LD_STA_UPD_DC10 = DC01.LD_STA_UPD_DC10
		LEFT JOIN ODW..GA10_LON_APP GA10
			ON DC01.AF_APL_ID = GA10.AF_APL_ID
			AND DC01.AF_APL_ID_SFX = GA10.AF_APL_ID_SFX
	WHERE 
		DC01.LC_STA_DC10 IN ('01')
		AND DC01.BF_USR_CRT_DC01 = 'LCXGX'
		AND CAST(DC01.BF_CRT_DTS_DC01 AS DATETIME) > DATEADD(DAY, -90, @Today)
		AND ISNULL(DC01.BC_CLM_PKG_RTN_REA, '') = ''
) POP
ORDER BY 
	[CLAIM RECEIVED DATE],
	[CLAIM TYPE],
	[ACCT]
;