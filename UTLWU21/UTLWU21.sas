/*UTLWU21 - Account changes made by account owner QC*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWU21.LWU21RZ";
FILENAME REPORT2 "&RPTLIB/ULWU21.LWU21R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

DATA _NULL_;
	IF WEEKDAY(DATE()) = 2 THEN DO;
		CALL SYMPUT('YESTERDAY',"'"||PUT(INTNX('DAY',TODAY(),-3,'BEGINNING'), MMDDYYD10.)||"'");
		CALL SYMPUT('LASTBUSDAY',INTNX('DAY',TODAY(),-3,'BEGINNING'));
	END;
	ELSE DO;
		CALL SYMPUT('YESTERDAY',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
		CALL SYMPUT('LASTBUSDAY',INTNX('DAY',TODAY(),-1,'BEGINNING'));
	END;
RUN;

%SYSLPUT YESTERDAY = &YESTERDAY;

RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

DATA ACCOUNT_OWNERS;
	INFILE '/sas/whse/progrevw/Account_Owner_QC.txt' DLM="," DSD MISSOVER;
	FORMAT ACCOUNT $10. OWNER $8.;
	INPUT ACCOUNT $ OWNER $;
	IF LENGTH(OWNER) < 7 THEN DELETE;
RUN;

DATA AGGREGATE;RUN;

%MACRO ONELINK_GA01(TBL_NAME=,IN_TBL=,ATTR_ID=,ATTR_DATE=);
	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE CURRENT AS
	SELECT *
	FROM CONNECTION TO DB2 (
	SELECT DISTINCT
		T.&ATTR_ID		AS USER_ID,
		&IN_TBL			AS IN_TBL,
		PD01.DF_SPE_ACC_ID
	FROM	OLWHRM1.&TBL_NAME T
	INNER JOIN OLWHRM1.GA01_APP GA01
		ON T.AF_APL_ID = GA01.AF_APL_ID
	INNER JOIN OLWHRM1.PD01_PDM_INF PD01
		ON GA01.DF_PRS_ID_BR = PD01.DF_PRS_ID
	INNER JOIN OLWHRM1.GA14_LON_STA GA14
		ON GA01.AF_APL_ID = GA14.AF_APL_ID
	INNER JOIN OLWHRM1.DC01_LON_CLM_INF DC01
		ON GA14.AF_APL_ID = DC01.AF_APL_ID
		AND GA14.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	WHERE	DAYS(T.&ATTR_DATE) = DAYS(&YESTERDAY)
	AND (GA14.AC_LON_STA_TYP IN ('DA', 'FB', 'IA', 'ID', 'IG', 'IM', 'RP', 'UA', 'UB')
		OR (GA14.AC_LON_STA_TYP IN ('CP', 'DN') AND DC01.LC_STA_DC10 = '03' AND (DC01.LC_REA_CLM_ASN_DOE IS NULL OR DC01.LC_REA_CLM_ASN_DOE = ''))
		OR (GA14.AC_LON_STA_TYP = 'CR' AND DC01.LC_STA_DC10 = '01' AND DC01.LC_PCL_REA IN ('DF', 'RS', 'DB', 'DQ')))
	FOR READ ONLY WITH UR
	);
	DISCONNECT FROM DB2;
	QUIT;

	DATA AGGREGATE;
		SET AGGREGATE CURRENT;
	RUN;
%MEND;
%ONELINK_GA01(TBL_NAME=AY01_BR_ATY,IN_TBL='AY01',ATTR_ID=BF_LST_USR_AY01,ATTR_DATE=BF_LST_DTS_AY01);
%ONELINK_GA01(TBL_NAME=DC12_EDS_CMK_ATY,IN_TBL='DC12',ATTR_ID=LF_LST_USR_DC12,ATTR_DATE=LF_LST_DTS_DC12);
%ONELINK_GA01(TBL_NAME=DC14_SUP_INT_CLM,IN_TBL='DC14',ATTR_ID=LF_LST_USR_CRT_SUP,ATTR_DATE=LF_LST_DTS_DC14);
%ONELINK_GA01(TBL_NAME=DC15_LON_ITR,IN_TBL='DC15',ATTR_ID=LF_LST_USR_DC15,ATTR_DATE=LF_LST_DTS_DC15);
/*%ONELINK_GA01(TBL_NAME=DC16_CLA_ATY,IN_TBL='DC16',ATTR_ID=LF_LST_USR_DC16,ATTR_DATE=LF_LST_DTS_DC16);*/
%ONELINK_GA01(TBL_NAME=DC19_DOE_RIN,IN_TBL='DC19',ATTR_ID=LF_LST_USR_DC19,ATTR_DATE=LF_LST_DTS_DC19);
%ONELINK_GA01(TBL_NAME=DC20_FEE_TRF,IN_TBL='DC20',ATTR_ID=LF_LST_USR_DC20,ATTR_DATE=LF_LST_DTS_DC20);
%ONELINK_GA01(TBL_NAME=FD01_QUE_TAB,IN_TBL='FD01',ATTR_ID=LF_LST_USR_FD01,ATTR_DATE=LF_LST_DTS_FD01);
%ONELINK_GA01(TBL_NAME=GA03_APL_LON_AUD,IN_TBL='GA03',ATTR_ID=AF_CRT_USR_GA03,ATTR_DATE=AF_CRT_DTS_GA03 );
%ONELINK_GA01(TBL_NAME=GA11_LON_DSB_ATY,IN_TBL='GA11',ATTR_ID=AF_LST_USR_GA11,ATTR_DATE=AF_LST_DTS_GA11);
%ONELINK_GA01(TBL_NAME=GA14_LON_STA,IN_TBL='GA14',ATTR_ID=AF_LST_USR_GA14,ATTR_DATE=AF_LST_DTS_GA14);
%ONELINK_GA01(TBL_NAME=GA15_NDS_ID,IN_TBL='GA15',ATTR_ID=AF_LST_USR_GA15,ATTR_DATE=AF_LST_DTS_GA15);
%ONELINK_GA01(TBL_NAME=GA16_ENR_RPT_CRF,IN_TBL='GA16',ATTR_ID=AF_LST_USR_GA16,ATTR_DATE=AD_LST_UPD_GA16);

%MACRO COMPASS_AP03(TBL_NAME=,IN_TBL=,ATTR_ID=,ATTR_DATE=);
	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE CURRENT AS
	SELECT *
	FROM CONNECTION TO DB2 (
	SELECT DISTINCT
		T.&ATTR_ID		AS USER_ID,
		&IN_TBL			AS IN_TBL,
		PD10.DF_SPE_ACC_ID
	FROM	OLWHRM1.&TBL_NAME T
	INNER JOIN OLWHRM1.AP03_MASTER_APL AP03
		ON T.AF_APL_ID = AP03.AF_APL_ID
	INNER JOIN OLWHRM1.PD10_PRS_NME PD10
		ON AP03.BF_SSN = PD10.DF_PRS_ID
	INNER JOIN OLWHRM1.LN10_LON LN10
		ON PD10.DF_PRS_ID = LN10.BF_SSN
	WHERE	DAYS(T.&ATTR_DATE) = DAYS(&YESTERDAY)
	AND LN10.LA_CUR_PRI > 0
	AND LN10.LC_STA_LON10 = 'R'
	FOR READ ONLY WITH UR
	);
	DISCONNECT FROM DB2;
	QUIT;

	DATA AGGREGATE;
		SET AGGREGATE CURRENT;
	RUN;
%MEND;
%COMPASS_AP03(TBL_NAME=AP17_LO_APL_ERR,IN_TBL='AP17',ATTR_ID=AF_LST_USR_AP17,ATTR_DATE=AF_LST_DTS_AP17);
%COMPASS_AP03(TBL_NAME=AP33_LO_APL_DTL,IN_TBL='AP33',ATTR_ID=AF_LST_USR_AP33,ATTR_DATE=AF_LST_DTS_AP33);
%COMPASS_AP03(TBL_NAME=AP34_APL_RSD,IN_TBL='AP34',ATTR_ID=AF_LST_USR_AP34,ATTR_DATE=AF_LST_DTS_AP34);
%COMPASS_AP03(TBL_NAME=AP35_APL_CRD,IN_TBL='AP35',ATTR_ID=AF_LST_USR_AP35,ATTR_DATE=AF_LST_DTS_AP35);
%COMPASS_AP03(TBL_NAME=AP36_APL_EMP,IN_TBL='AP36',ATTR_ID=AF_LST_USR_AP36,ATTR_DATE=AF_LST_DTS_AP36);
%COMPASS_AP03(TBL_NAME=AP37_APL_FIN,IN_TBL='AP37',ATTR_ID=AF_LST_USR_AP37,ATTR_DATE=AF_LST_DTS_AP37);
%COMPASS_AP03(TBL_NAME=AP17_LO_APL_ERR,IN_TBL='AP17',ATTR_ID=AF_LST_USR_AP17,ATTR_DATE=AF_LST_DTS_AP17);
%COMPASS_AP03(TBL_NAME=AP33_LO_APL_DTL,IN_TBL='AP33',ATTR_ID=AF_LST_USR_AP33,ATTR_DATE=AF_LST_DTS_AP33);
%COMPASS_AP03(TBL_NAME=AP34_APL_RSD,IN_TBL='AP34',ATTR_ID=AF_LST_USR_AP34,ATTR_DATE=AF_LST_DTS_AP34);
%COMPASS_AP03(TBL_NAME=AP35_APL_CRD,IN_TBL='AP35',ATTR_ID=AF_LST_USR_AP35,ATTR_DATE=AF_LST_DTS_AP35);
%COMPASS_AP03(TBL_NAME=AP36_APL_EMP,IN_TBL='AP36',ATTR_ID=AF_LST_USR_AP36,ATTR_DATE=AF_LST_DTS_AP36);
%COMPASS_AP03(TBL_NAME=AP37_APL_FIN,IN_TBL='AP37',ATTR_ID=AF_LST_USR_AP37,ATTR_DATE=AF_LST_DTS_AP37);

/*Some tables can join directly using SSN, including the LCO tables of interest.*/
%MACRO ONELINK_DIRECT(TBL_NAME=,IN_TBL=,ATTR_ID=,ATTR_DATE=,ATTR_SSN=);
	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE CURRENT AS
	SELECT *
	FROM CONNECTION TO DB2 (
	SELECT DISTINCT
		T.&ATTR_ID		AS USER_ID,
		&IN_TBL			AS IN_TBL,
		PD01.DF_SPE_ACC_ID
	FROM	OLWHRM1.&TBL_NAME T
	INNER JOIN OLWHRM1.PD01_PDM_INF PD01
		ON T.&ATTR_SSN = PD01.DF_PRS_ID
	INNER JOIN OLWHRM1.GA01_APP GA01
		ON PD01.DF_PRS_ID = GA01.DF_PRS_ID_BR
	INNER JOIN OLWHRM1.GA14_LON_STA GA14
		ON GA01.AF_APL_ID = GA14.AF_APL_ID
	INNER JOIN OLWHRM1.DC01_LON_CLM_INF DC01
		ON GA14.AF_APL_ID = DC01.AF_APL_ID
		AND GA14.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	WHERE	DAYS(T.&ATTR_DATE) = DAYS(&YESTERDAY)
	AND (GA14.AC_LON_STA_TYP IN ('DA', 'FB', 'IA', 'ID', 'IG', 'IM', 'RP', 'UA', 'UB')
		OR (GA14.AC_LON_STA_TYP IN ('CP', 'DN') AND DC01.LC_STA_DC10 = '03' AND (DC01.LC_REA_CLM_ASN_DOE IS NULL OR DC01.LC_REA_CLM_ASN_DOE = ''))
		OR (GA14.AC_LON_STA_TYP = 'CR' AND DC01.LC_STA_DC10 = '01' AND DC01.LC_PCL_REA IN ('DF', 'RS', 'DB', 'DQ')))
	FOR READ ONLY WITH UR
	);
	DISCONNECT FROM DB2;
	QUIT;

	DATA AGGREGATE;
		SET AGGREGATE CURRENT;
	RUN;
%MEND;
%ONELINK_DIRECT(TBL_NAME=BR01_BR_CRF,IN_TBL='BR01',ATTR_ID=BF_LST_USR_BR01,ATTR_DATE=BF_LST_DTS_BR01,ATTR_SSN=BF_SSN);
%ONELINK_DIRECT(TBL_NAME=BR02_BR_EMP,IN_TBL='BR02',ATTR_ID=BF_LST_USR_BR02,ATTR_DATE=BF_LST_DTS_BR02,ATTR_SSN=BF_SSN);
%ONELINK_DIRECT(TBL_NAME=BR03_BR_REF,IN_TBL='BR03',ATTR_ID=BF_LST_USR_BR03,ATTR_DATE=BF_LST_DTS_BR03,ATTR_SSN=DF_PRS_ID_BR);
%ONELINK_DIRECT(TBL_NAME=DC01_LON_CLM_INF,IN_TBL='DC01',ATTR_ID=LF_LST_USR_DC10,ATTR_DATE=LF_LST_DTS_DC10,ATTR_SSN=BF_SSN);
%ONELINK_DIRECT(TBL_NAME=LA10_LEG_ACT,IN_TBL='LA10',ATTR_ID=BF_LST_USR_LA10,ATTR_DATE=BF_LST_DTS_LA10,ATTR_SSN=DF_PRS_ID_BR);
%ONELINK_DIRECT(TBL_NAME=LA11_LEG_ACT_ATY,IN_TBL='LA11',ATTR_ID=BF_LST_USR_LA11,ATTR_DATE=BF_LST_DTS_LA11,ATTR_SSN=DF_PRS_ID_BR);
%ONELINK_DIRECT(TBL_NAME=PD01_PDM_INF,IN_TBL='PD01',ATTR_ID=DF_USR_UPD_ADR,ATTR_DATE=DD_LST_UPD_ADR,ATTR_SSN=DF_PRS_ID);
%ONELINK_DIRECT(TBL_NAME=PD01_PDM_INF,IN_TBL='PD01',ATTR_ID=DF_USR_UPD_PHN,ATTR_DATE=DD_LST_UPD_PHN,ATTR_SSN=DF_PRS_ID);
%ONELINK_DIRECT(TBL_NAME=PD03_PRS_ADR_PHN,IN_TBL='PD03',ATTR_ID=DF_USR_UPD_PHN,ATTR_DATE=DD_LST_UPD_PHN,ATTR_SSN=DF_PRS_ID);
%ONELINK_DIRECT(TBL_NAME=PD04_ADR_PHN_HST,IN_TBL='PD04',ATTR_ID=DF_USR_UPD_ADR_HST,ATTR_DATE=DD_LST_UPD_ADR_HST,ATTR_SSN=DF_PRS_ID);
%ONELINK_DIRECT(TBL_NAME=PD05_PRS_NME_HST,IN_TBL='PD05',ATTR_ID=DF_LST_USR_PD05,ATTR_DATE=DD_LST_UPD_PD05,ATTR_SSN=DF_PRS_ID);
%ONELINK_DIRECT(TBL_NAME=PD26_PRS_SKP_PRC,IN_TBL='PD26',ATTR_ID=DF_LST_USR_PD26,ATTR_DATE=DF_LST_DTS_PD26,ATTR_SSN=DF_PRS_ID_BR);
%ONELINK_DIRECT(TBL_NAME=PD40_SKP_DTL,IN_TBL='PD40',ATTR_ID=PF_LST_USR_PD40,ATTR_DATE=PF_LST_DTS_PD40,ATTR_SSN=DF_SKP_TGT_PRS);
%ONELINK_DIRECT(TBL_NAME=SD02_STU_ENR,IN_TBL='SD02',ATTR_ID=LF_LST_USR_SD02,ATTR_DATE=LF_LST_DTS_SD02,ATTR_SSN=DF_PRS_ID_STU);

%MACRO COMPASS_DIRECT(TBL_NAME=,IN_TBL=,ATTR_ID=,ATTR_DATE=,ATTR_SSN=);
	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE CURRENT AS
	SELECT *
	FROM CONNECTION TO DB2 (
	SELECT DISTINCT
		T.&ATTR_ID		AS USER_ID,
		&IN_TBL			AS IN_TBL,
		PD10.DF_SPE_ACC_ID
	FROM	OLWHRM1.&TBL_NAME T
	INNER JOIN OLWHRM1.PD10_PRS_NME PD10
		ON T.&ATTR_SSN = PD10.DF_PRS_ID
	INNER JOIN OLWHRM1.LN10_LON LN10
		ON PD10.DF_PRS_ID = LN10.BF_SSN
	WHERE	DAYS(T.&ATTR_DATE) = DAYS(&YESTERDAY)
	AND LN10.LA_CUR_PRI > 0
	AND LN10.LC_STA_LON10 = 'R'
	FOR READ ONLY WITH UR
	);
	DISCONNECT FROM DB2;
	QUIT;

	DATA AGGREGATE;
		SET AGGREGATE CURRENT;
	RUN;
%MEND;
%COMPASS_DIRECT(TBL_NAME=AY10_BR_LON_ATY,IN_TBL='AY10',ATTR_ID=LF_PRF_BY,ATTR_DATE=LF_LST_DTS_AY10,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN15_DSB,IN_TBL='LN15',ATTR_ID=LF_LST_USR_LN15,ATTR_DATE=LF_DSB_EVT_LN15,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN34_CU_RTT_HST,IN_TBL='LN34',ATTR_ID=LF_LST_USR_LN34,ATTR_DATE=LF_LST_DTS_LN34,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN38_CU_STA_HST,IN_TBL='LN38',ATTR_ID=LF_LST_USR_LN38,ATTR_DATE=LF_LST_DTS_LN38,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN54_LON_BBS,IN_TBL='LN54',ATTR_ID=LF_LST_USR_LN54,ATTR_DATE=LF_LST_DTS_LN54,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN55_LON_BBS_TIR,IN_TBL='LN55',ATTR_ID=LF_LST_USR_LN55,ATTR_DATE=LF_LST_DTS_LN55,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN56_LON_CRB_RPT,IN_TBL='LN56',ATTR_ID=LF_LST_USR_LN56,ATTR_DATE=LF_LST_DTS_LN56,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=LN57_LON_BBT_FAT,IN_TBL='LN57',ATTR_ID=LF_LST_USR_LN57,ATTR_DATE=LF_LST_DTS_LN57,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=PD10_PRS_NME,IN_TBL='PD10',ATTR_ID=DF_LST_USR_PD10,ATTR_DATE=DF_LST_DTS_PD10,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=PD11_PRS_NME_HST,IN_TBL='PD11',ATTR_ID=DF_LST_USR_PD11,ATTR_DATE=DF_LST_DTS_PD11,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=PD31_PRS_INA,IN_TBL='PD31',ATTR_ID=DF_LST_USR_PD31,ATTR_DATE=DF_LST_DTS_PD31,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=PD32_PRS_ADR_EML,IN_TBL='PD32',ATTR_ID=DF_LST_USR_PD32,ATTR_DATE=DF_LST_DTS_PD32,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=PD42_PRS_PHN,IN_TBL='PD42',ATTR_ID=DF_LST_USR_PD42,ATTR_DATE=DF_LST_DTS_PD42,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=PH01_SUPER_ID,IN_TBL='PH01',ATTR_ID=DF_LST_USR_PH01,ATTR_DATE=DF_LST_DTS_PH01,ATTR_SSN=DF_PRS_ID);
%COMPASS_DIRECT(TBL_NAME=RS10_BR_RPD,IN_TBL='RS10',ATTR_ID=LF_USR_RPS_REQ,ATTR_DATE=LF_LST_DTS_RS10,ATTR_SSN=BF_SSN);
%COMPASS_DIRECT(TBL_NAME=SD10_STU_SPR,IN_TBL='SD10',ATTR_ID=LF_LST_USR_SD10,ATTR_DATE=LF_LST_DTS_SD10,ATTR_SSN=LF_STU_SSN);

%MACRO LCO_DIRECT(TBL_NAME=,IN_TBL=,ATTR_ID=,ATTR_DATE=);
	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE CURRENT AS
	SELECT *
	FROM CONNECTION TO DB2 (
	SELECT
		T.&ATTR_ID		AS USER_ID,
		&IN_TBL			AS IN_TBL,
		PD6A.DF_SPE_ACC_ID
	FROM	OLWHRM1.&TBL_NAME T
	INNER JOIN OLWHRM1.PD6A_LCO_PRS_DMO PD6A
		ON T.DF_LCO_PRS_SSN_BR = PD6A.DF_LCO_PRS_SSN
	WHERE	DAYS(T.&ATTR_DATE) = DAYS(&YESTERDAY)
	FOR READ ONLY WITH UR
	);
	DISCONNECT FROM DB2;
	QUIT;

	DATA AGGREGATE;
		SET AGGREGATE CURRENT;
	RUN;
%MEND;
%LCO_DIRECT(TBL_NAME=AP1A_LCO_APL,IN_TBL='AP1A',ATTR_ID=AF_CRT_USR_AP1A,ATTR_DATE=AD_CRT_AP1A);
%LCO_DIRECT(TBL_NAME=LC10_UND_LN_INF,IN_TBL='LC10',ATTR_ID=IF_CRT_USR_LC10,ATTR_DATE=ID_CRT_LC10);
%LCO_DIRECT(TBL_NAME=LC10_UND_LN_INF,IN_TBL='LC10',ATTR_ID=IF_UPD_USR_LC10,ATTR_DATE=ID_UPD_LC10);

PROC SQL;
CREATE TABLE MATCH AS
SELECT DISTINCT
	A.USER_ID,
	A.IN_TBL,
	A.DF_SPE_ACC_ID
FROM AGGREGATE A
INNER JOIN ACCOUNT_OWNERS O
	ON A.DF_SPE_ACC_ID = O.ACCOUNT
WHERE A.USER_ID = O.OWNER
;
QUIT;

/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

ENDRSUBMIT;

DATA MATCH;
SET WORKLOCL.MATCH;
RUN;

PROC SORT DATA = MATCH;
BY USER_ID;
RUN;

DATA _NULL_;
SET MATCH ;
LENGTH DESCRIPTION $600.;
USER = USER_ID;
ACT_DT = &LASTBUSDAY;
DESCRIPTION = CATX(',',
	IN_TBL,
	DF_SPE_ACC_ID);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
