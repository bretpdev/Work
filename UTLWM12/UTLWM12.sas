/*UTLWM12 LOAN MANAGEMENT COMPASS DAILY WORK TRACKING REPORT*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/

%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM12.LWM12R2";
FILENAME REPORTZ "&RPTLIB/ULWM12.LWM12RZ";

DATA _NULL_;
	CALL SYMPUT('CDATE',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;

%SYSLPUT CDATE = &CDATE;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LMDTR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	A.LF_PRF_BY				AS USER_ID,
	A.PF_REQ_ACT			AS ACTION_CODE,
	RTRIM(B.PX_ACT_DSC_REQ)	AS ACD,  
	A.LC_ATY_RCP			AS ACTIVITY_TYPE,
	A.PF_RSP_ACT			AS RESPONSE_CODE, 
	A.BF_SSN				AS SSN,
	A.LD_ATY_RSP			AS DATE,
	D.LX_ATY				AS COMMENTS,
	D.LF_LST_DTS_AY20		AS CDTSMP,
	D.LN_ATY_CMT_SEQ,
	C.LN_ATY_SEQ	
FROM OLWHRM1.AY10_BR_LON_ATY A 
INNER JOIN OLWHRM1.AC10_ACT_REQ B
	ON A.PF_REQ_ACT = B.PF_REQ_ACT
INNER JOIN OLWHRM1.AY15_ATY_CMT C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_ATY_SEQ = C.LN_ATY_SEQ
INNER JOIN OLWHRM1.AY20_ATY_TXT D
 	ON C.BF_SSN = D.BF_SSN
	AND C.LN_ATY_SEQ = D.LN_ATY_SEQ
	AND C.LN_ATY_CMT_SEQ = D.LN_ATY_CMT_SEQ
INNER JOIN OLWHRM1.US10_USR_PFL E
	ON A.LF_PRF_BY = E.PF_USR
WHERE A.LD_ATY_RSP = &CDATE
AND E.PC_SER_DPT = 'LMN'
AND E.PD_USR_IN_DPT_END IS NULL
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWM08.LWM08RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA LMDTR;
SET WORKLOCL.LMDTR;
RUN;

PROC SORT DATA = LMDTR;
BY SSN LN_ATY_SEQ USER_ID ACTION_CODE ACD ACTIVITY_TYPE RESPONSE_CODE DATE;
RUN;

PROC TRANSPOSE DATA=LMDTR OUT=LMDTR (DROP=_NAME_ _LABEL_) PREFIX=CMT;
VAR COMMENTS;
BY SSN LN_ATY_SEQ USER_ID ACTION_CODE ACD ACTIVITY_TYPE RESPONSE_CODE DATE;
RUN;

DATA LMDTR (DROP=CMT1-CMT15);
LENGTH CMT1-CMT15 $ 360.;
SET LMDTR;
COMMENTS = TRIM(CMT1)||' '||TRIM(CMT2)||' '||TRIM(CMT3)||' '||TRIM(CMT4)||' '||TRIM(CMT5)||' '||
		   TRIM(CMT6)||' '||TRIM(CMT7)||' '||TRIM(CMT8)||' '||TRIM(CMT9)||' '||TRIM(CMT10)||' '||
		   TRIM(CMT11)||' '||TRIM(CMT12)||' '||TRIM(CMT13)||' '||TRIM(CMT14)||' '||TRIM(CMT15);		   
COMMENTS = LEFT(TRIM(COMMENTS));
RUN;

PROC SORT DATA = LMDTR;
BY USER_ID SSN LN_ATY_SEQ;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=127 PS=39;
PROC REPORT DATA= LMDTR HEADSKIP SPLIT='*' NOWD;
TITLE "LOAN MANAGEMENT DAILY TRACKING REPORT";
TITLE2 "&RUNDATE - &RUNTIME";
FOOTNOTE 'JOB = UTLWM12     REPORT = ULWM12.LWM12R2';
COLUMN USER_ID ACTION_CODE ACD ACTIVITY_TYPE RESPONSE_CODE SSN DATE 
COMMENTS;
DEFINE USER_ID / DISPLAY "USER ID" WIDTH=7;
DEFINE ACTION_CODE /  "ACTION*CODE" WIDTH=6;
DEFINE ACD /  "ACTION CODE*DESCRIPTION" WIDTH=15;
DEFINE ACTIVITY_TYPE /  "ACTIVITY*TYPE" WIDTH=8;
DEFINE RESPONSE_CODE /  "RESPONSE*CODE" WIDTH=8;
DEFINE SSN /  'SSN';
DEFINE DATE /  'DATE' FORMAT=MMDDYY10.;
DEFINE COMMENTS / DISPLAY WIDTH=47 FLOW;
RUN;
PROC PRINTTO;
RUN; 
