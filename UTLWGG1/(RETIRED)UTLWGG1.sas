/*EMPLOYER NOT RESPONDING - 45 DAY NOTICE*/
/*This report lists employers which have been sent an order of withholding but which are*/
/*not sending payments and which have been sent a 45 Day notice.*/
/*Collections uses the report to contact employers to resolve the noncompliance with the*/
/*order of withholding.*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWGG1.LWGG1R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
DATA _NULL_;
	CALL SYMPUT('DAYS_AGO_7',"'"||PUT(INTNX('DAY',TODAY(),-7,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('DAYS_AGO_30',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT DAYS_AGO_7 = &DAYS_AGO_7;
%SYSLPUT DAYS_AGO_30 = &DAYS_AGO_30;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DAY45 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID AS SSN
	,C.BC_HLD_REA
	,C.BD_HLD
	,A.DF_SPE_ACC_ID
	,AY01.PF_ACT
	,AY01.BD_ATY_PRF
FROM 	OLWHRM1.PD01_PDM_INF A 
	INNER JOIN OLWHRM1.IN01_LGS_IDM_MST B 
		ON A.BF_EMP_ID_1 = B.IF_IST 
	INNER JOIN OLWHRM1.LA10_LEG_ACT C 
		ON A.DF_PRS_ID = C.DF_PRS_ID_BR 
	INNER JOIN OLWHRM1.DC01_LON_CLM_INF D 
		ON A.DF_PRS_ID = D.BF_SSN 
	INNER JOIN OLWHRM1.DC02_BAL_INT E 
		ON D.AF_APL_ID = E.AF_APL_ID 
		AND D.AF_APL_ID_SFX = E.AF_APL_ID_SFX
	LEFT OUTER JOIN (SELECT Z.DF_PRS_ID
						,MAX(Z.BD_ATY_PRF) AS BD_ATY_PRF
						,Z.PF_ACT
					FROM OLWHRM1.AY01_BR_ATY Z
					WHERE Z.PF_ACT IN ('LUBEI','LSBEI')
					GROUP BY Z.DF_PRS_ID, Z.PF_ACT
					) AY01
		ON AY01.DF_PRS_ID = A.DF_PRS_ID
WHERE D.LC_AUX_STA = '' 
		AND D.LC_STA_DC10 = '03' 
		AND C.BC_WDR_REA = '' 
		AND C.BC_INA_REA = '' 
		AND	C.BC_LEG_ACT_REC_TYP = '1' 		/* Legal Action Record Type = '1' (AWG). */
		AND C.BD_NCP_FWP_LTR_1 <= &DAYS_AGO_7 	/* The first follow up letter has been sent AND*/
		AND C.BD_NCP_FWP_LTR_2 is NULL		/*the second follow up letter has not been sent.*/
		AND C.BD_HLD < CURRENT DATE	
	 	AND D.LC_GRN = '02' 
		AND E.LA_CLM_BAL > 25
		AND (DAYS(D.LD_LST_PAY) < DAYS(CURRENT DATE) - 60 
		 OR	DAYS(D.LD_LST_PAY) IS NULL)/* The last payment less than 60 days from current date or is null. */ 
		AND NOT EXISTS 
		(	SELECT *
			FROM OLWHRM1.CT30_CALL_QUE X
			WHERE X.DF_PRS_ID_BR = D.BF_SSN
				AND X.IF_WRK_GRP = 'DEMCMP45'
		)
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA WORK.DAY45; 
	SET WORKLOCL.DAY45; 
RUN;
PROC SORT DATA=DAY45 ;
	BY SSN;
RUN;
DATA DAY45 (KEEP=SSN);
SET DAY45;
BY SSN;
RETAIN A;
IF FIRST.SSN THEN DO;
	A=0;
END;
IF A=0 THEN DO;
	IF BC_HLD_REA = '' THEN DO;
		IF PF_ACT = 'LUBEI' AND BD_ATY_PRF < TODAY() - 30 THEN A=1;
		IF PF_ACT = 'LSBEI' AND BD_ATY_PRF < TODAY() - 90 THEN A=1;
	END;
	ELSE IF PF_ACT = '' THEN A=1;
END;
IF LAST.SSN THEN DO;
	IF A=1 THEN OUTPUT;
END;
RUN;

DATA _NULL_;
SET  WORK.DAY45 (KEEP=SSN);
QUEUE_NAME = 'DEMCMP45';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
COMMENTS = ''; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT SSN $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT SSN $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
