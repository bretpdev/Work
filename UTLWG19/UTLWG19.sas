/*UTLWG19 BORROWERS WITH NO MATCHING SSN ON ONELINK*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG19.LWG19R2";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BWNMSOL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT   A.BF_SSN AS CSSN
		,C.DF_PRS_ID_BR AS OSSN
		,RTRIM(D.DM_PRS_LST)||' '||RTRIM(D.DM_PRS_1)||' '||D.DM_PRS_MID AS NAME
		,A.LF_LON_CUR_OWN
		,RTRIM(CHAR(A.LF_LON_ALT))||'0'||CHAR(A.LN_LON_ALT_SEQ) AS C_UID
		,RTRIM(CHAR(B.AF_APL_ID))||''||CHAR(B.AF_APL_ID_SFX) AS O_UID 
		,A.LN_SEQ
	
FROM	 OLWHRM1.LN10_LON A
		INNER JOIN OLWHRM1.GA10_LON_APP B 
		 ON A.LF_LON_ALT = B.AF_APL_ID
		 AND A.LN_LON_ALT_SEQ = INT(B.AF_APL_ID_SFX)
		INNER JOIN OLWHRM1.GA01_APP C
		 ON B.AF_APL_ID = C.AF_APL_ID
		INNER JOIN OLWHRM1.PD10_PRS_NME D
		 ON A.BF_SSN = D.DF_PRS_ID

WHERE	A.LC_STA_LON10 = 'R'
AND 	A.IF_GTR = '000749'
AND 	A.LA_CUR_PRI > 0
AND 	A.BF_SSN <> C.DF_PRS_ID_BR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA BWNMSOL;
SET WORKLOCL.BWNMSOL;
RUN;

PROC SORT DATA=BWNMSOL;
BY CSSN LN_SEQ;
RUN;
DATA BWNMSOL (DROP=LN_SEQ SEQLIST SEQLIST2);
SET BWNMSOL;
LENGTH SEQLIST SEQLIST2 $200. ;
BY CSSN;
RETAIN SEQLIST SEQLIST2;
IF FIRST.CSSN THEN DO;
	SEQLIST = PUT(LN_SEQ,3.);
	SEQLIST2 = SEQLIST;
END;
ELSE DO;
	SEQLIST2 = TRIM(SEQLIST2);
	SEQLIST = PUT(LN_SEQ,3.);
	SEQLIST2 = TRIM(SEQLIST2)||' '||TRIM(SEQLIST);
END;
IF LAST.CSSN THEN DO;
	SEQ_LIST = LEFT(TRIM(SEQLIST2));
	OUTPUT ;
END;
RUN;
DATA _NULL_;
SET BWNMSOL ;
LENGTH DESCRIPTION $600.;
USER = '';
ACT_DT = TODAY();
DESCRIPTION = CATX(',',
	'COMPASS SSN = '||CSSN,
	'ONELINK SSN = '||OSSN,
	'COMPASS NAME = '||NAME,   
	'COMPASS CURRENT OWNER = '||LF_LON_CUR_OWN,
	'ONELINK UID = '||O_UID,
	'COMPASS UID = '||C_UID,
	'LOAN SEQ = '||SEQ_LIST 
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
