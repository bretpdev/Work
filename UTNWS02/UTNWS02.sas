/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS02.NWS02RZ";
FILENAME REPORT2 "&RPTLIB/UNWS02.NWS02R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=LEGEND SLIBREF=work  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL);
CREATE TABLE DRFED AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,B.LC_STA_LON16
	,A.LA_CUR_PRI
FROM PKUB.LN10_LON A
INNER JOIN PKUB.DW01_DW_CLC_CLU C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN PKUB.LN16_LON_DLQ_HST B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND B.LC_STA_LON16 = '1'
	AND B.LN_DLQ_MAX > 30
WHERE A.LA_CUR_PRI > 0 
	AND A.LC_STA_LON10 = 'R' 
	AND C.WC_DW_LON_STA NOT IN ('01','02','04','05')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
QUIT;
ENDRSUBMIT;
DATA DRFED;
	SET WORKLOCL.DRFED;
RUN;

PROC SORT DATA=DRFED NODUPKEY;
	BY LC_STA_LON16 BF_SSN LN_SEQ;
RUN;

DATA DRFED2(KEEP=RATE DELQ PRINCIPLE);
	SET DRFED END=LAST;
	FORMAT RATE PERCENT10.3 DELQ PRINCIPLE DOLLAR20.2;
	BY LC_STA_LON16 BF_SSN LN_SEQ;
	RETAIN DELQ;
	IF LC_STA_LON16 = '1' THEN DO;
		IF FIRST.LC_STA_LON16 THEN DELQ = LA_CUR_PRI;
		ELSE DELQ = DELQ+LA_CUR_PRI;
	END;
	PRINCIPLE+LA_CUR_PRI;
	RATE = 	DELQ / PRINCIPLE ;
	IF LAST;
RUN;

ods close _all_;
ods listing;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'Delinquency Rate - FED';
FOOTNOTE1  	"Dollars Delinquent = Total dollars equal to or greater than 31 days delinquent";
FOOTNOTE2	'Dollars in Repayment = Total dollars in active repayment';
FOOTNOTE3	'Delinquency Rate = Dollars Delinquent divided by Dollars in Repayment';

PROC PRINT NOOBS SPLIT='/' DATA=DRFED2 WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT RATE PERCENT10.3;
VAR DELQ PRINCIPLE RATE;
LABEL DELQ = 'Dollars Delinquent'
	PRINCIPLE = 'Dollars in Repayment'
	RATE = 'Delinquency Rate';
RUN;

PROC PRINTTO;
RUN;

/*Detail File*/
PROC EXPORT DATA= DEMO
            OUTFILE= "T:\SAS\UTNWS02.Detail.xlsx" 
            DBMS=EXCEL REPLACE;
RUN;
