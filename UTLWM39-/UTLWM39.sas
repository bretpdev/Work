*--------------------------------------------*
|  Default Aversion Special E-mail Campaign  |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM39.LWM39R2";
FILENAME REPORT3 "&RPTLIB/ULWM39.LWM39R3";
FILENAME REPORT4 "&RPTLIB/ULWM39.LWM39R4";
FILENAME REPORT5 "&RPTLIB/ULWM39.LWM39R5";
FILENAME REPORT6 "&RPTLIB/ULWM39.LWM39R6";
FILENAME REPORTZ "&RPTLIB/ULWM39.LWM39RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DALTC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_SPE_ACC_ID
	,A.DM_PRS_1
	,A.DM_PRS_LST
	,A.DX_STR_ADR_1
	,A.DX_STR_ADR_2
	,A.DM_CT
	,A.DC_DOM_ST
	,A.DF_ZIP
	,DAYS(CURRENT DATE) - DAYS(C.LD_DCO) AS DAYS_DELINQ
	,F.PF_ACT
	,D.AD_ENT_RPD
FROM OLWHRM1.PD01_PDM_INF A
INNER JOIN OLWHRM1.DC01_LON_CLM_INF C
	ON A.DF_PRS_ID = C.BF_SSN
INNER JOIN (SELECT MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
				,AF_APL_ID
				,AF_APL_ID_SFX
		FROM OLWHRM1.DC01_LON_CLM_INF
		GROUP BY AF_APL_ID, AF_APL_ID_SFX) E
	ON C.AF_APL_ID = E.AF_APL_ID
	AND C.AF_APL_ID_SFX = E.AF_APL_ID_SFX
	AND C.LF_CRT_DTS_DC10 = E.LF_CRT_DTS_DC10
LEFT OUTER JOIN (SELECT AF_APL_ID
				,AF_APL_ID_SFX
				,AD_ENT_RPD
		FROM OLWHRM1.GA10_LON_APP 
		WHERE AD_ENT_RPD >= '10-01-2008'
)D
	ON C.AF_APL_ID = D.AF_APL_ID
	AND C.AF_APL_ID_SFX = D.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT F.DF_PRS_ID
					,PF_ACT
				FROM OLWHRM1.AY01_BR_ATY F 
				WHERE DAYS(F.BD_ATY_PRF) >= DAYS(CURRENT DATE) - 13
					AND PF_ACT IN ('ALSVE','ALHVY','ALWOR','ALWTH','ALSTL','ALINF','ALTOF','ALTOL','ALST1','ALMNG','AL300')) F
	ON A.DF_PRS_ID = F.DF_PRS_ID
WHERE C.LC_STA_DC10 = '01'
	AND C.LC_PCL_REA = 'DF'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/
ENDRSUBMIT;
DATA DALTC ;
	SET WORKLOCL.DALTC;
RUN;
PROC SORT DATA=DALTC;
	BY DF_SPE_ACC_ID DAYS_DELINQ;
RUN;
/*USE ONLY THE MOST RECENT ROW IN DETERMINING LENGTH OF DELINQUENCY*/
DATA DALTC;
SET DALTC;
BY DF_SPE_ACC_ID DAYS_DELINQ;
IF FIRST.DF_SPE_ACC_ID THEN DO;
	IF DAYS_DELINQ >=240 THEN OUTPUT;
END;
RUN;

%MACRO REPORTS(REP,DAYLO,DAYHI,ACT);
DATA _NULL_;
SET DALTC;
FILE REPORT&REP DELIMITER=',' DSD DROPOVER LRECL=32767;
WHERE &DAYLO <= DAYS_DELINQ <= &DAYHI AND PF_ACT NOT IN (&ACT);
ACS = 'LGP';
CST_CTR_CODE = 'MA2328';
IF _N_ = 1 THEN DO;
   PUT 'ACS PARTICIPANT CODE,BORROWER FIRST NAME,BORROWER LAST NAME,ADDRESS LINE 1,ADDRESS LINE 2,CITY,STATE,ZIP,COST CENTER CODE';
END;
DO;
	PUT ACS @;
	PUT DM_PRS_1 @;
	PUT DM_PRS_LST @;
	PUT DX_STR_ADR_1 @;
	PUT DX_STR_ADR_2 @;
	PUT DM_CT @;
	PUT DC_DOM_ST @;
	PUT DF_ZIP @;
	PUT CST_CTR_CODE $;
END;
RUN;
%MEND;
%REPORTS(2,240,254,%STR('ALSVE','ALHVY','ALWOR','ALWTH','ALSTL','ALINF','ALTOF','ALTOL'));
%REPORTS(3,255,269,%STR('ALSVE','ALHVY','ALWOR','ALWTH','ALSTL','ALINF','ALTOF','ALTOL'));
%REPORTS(4,270,284,'ALST1');
%REPORTS(5,285,299,'ALMNG');
/*THE CRITERIA CALLS FOR GREATER THAN 300.  50000 WAS A DAY WE WOULD NEVER REACH*/
%REPORTS(6,300,50000,'AL300');
