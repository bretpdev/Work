/*	This report prints the total number of PCAs and total dollar amount
	for a month period.
*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULW___.LW___R2";*/

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132 SYMBOLGEN;
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	
		A.bf_ssn						as SSN,
		A.af_apl_id||A.af_apl_id_sfx	as CLUID,
		A.ld_pcl_rcv					as PCLRCV,
		A.la_clm_pri					as PRIDFL,
		A.la_clm_int					as INTDFL,
		A.lc_clm_lon_typ				as LONTYP,
		CASE
		WHEN A.ld_org_dco is not null THEN A.ld_org_dco
		ELSE A.ld_dco
		END								as DCODT,
		A.lf_cur_lon_ser_agy			as CURSER
FROM  OLWHRM1.DC01_LON_CLM_INF A
WHERE A.ld_pcl_rcv BETWEEN &BEGIN AND &END
AND A.LC_PCL_REA IN ('DF','DQ')
ORDER BY A.bf_ssn, A.af_apl_id||A.af_apl_id_sfx
);
DISCONNECT FROM DB2;
endrsubmit  ;

DATA DEMO; 
SET WORKLOCL.DEMO; 
RUN;

PROC SQL;
CREATE TABLE DEMOSTAT AS
SELECT	SUM(PRIDFL)	AS SUMPRI,
		SUM(INTDFL)	AS SUMINT,
		COUNT(*)	AS LONCNT,
		.			AS PCACNT
FROM DEMO
;
QUIT;

DATA DEMO; 
SET DEMO; 
IF LONTYP IN ('SF','SU') THEN
	LONTYP = 'S';
RUN;

PROC SORT DATA=DEMO NODUPKEY;
BY SSN LONTYP PCLRCV DCODT CURSER;
RUN;

PROC SQL;
UPDATE DEMOSTAT
SET PCACNT=(SELECT COUNT(*) FROM DEMO);
QUIT;

DATA DEMOSTAT;
SET DEMOSTAT;
TOTDOL = SUM(SUMINT, SUMPRI);
RUN;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/

OPTIONS NOCENTER NODATE NONUMBER LS=120;
PROC PRINT NOOBS SPLIT='/' DATA=DEMOSTAT WIDTH=UNIFORM WIDTH=MIN;
VAR LONCNT PCACNT TOTDOL;
LABEL LONCNT='Number of Loans' PCACNT='Number of PCAs'
	  TOTDOL = 'Total Dollar Amount';
FORMAT TOTDOL DOLLAR15.2;
TITLE "Monthly DAAR Request Totals - &EFFDATE";
FOOTNOTE  'JOB = UTLWDP9     REPORT = ULWDP9.LWDP9R2';
RUN;
