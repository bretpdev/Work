OPTIONS NOCENTER;

/*libname dlgsutwh db2 database=dlgsutwh owner=olwhrm1 ;*/
/*%let  rptlib = %sysget(reportdir)  ;*/
%let  rptlib = T:\SAS;
filename report2  "&rptlib/ULWG4B.LWG4BR2"  ;
filename report3  "&rptlib/ULWG4B.LWG4BR3"  ;





LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;


PROC SQL ;
CONNECT TO DB2 (database=dlgsutwh);
CREATE TABLE A1 AS
SELECT *
FROM CONNECTION TO DB2  (
SELECT DISTINCT
		A.IF_IST,
		D.DF_SPE_ACC_ID					AS SSNB,
		E.DF_SPE_ACC_ID  			 	AS SSNS,
		DEC(D.DF_SPE_ACC_ID,10)			AS SSN,
		B.AF_CUR_APL_OPS_LDR     	 	AS LNDR,
		C.AF_APL_ID||C.AF_APL_ID_SFX	AS UID,
		C.AC_PRC_STA  					AS STAT,
		C.AD_PRC    				    AS DATE
FROM	olwhrm1.SC01_LGS_SCL_INF A
		inner join olwhrm1.GA01_APP B ON
			A.IF_IST = B.AF_APL_OPS_SCL
        INNER JOIN olwhrm1.GA10_LON_APP C ON
			B.AF_APL_ID = C.AF_APL_ID
		INNER JOIN OLWHRM1.PD01_PDM_INF D ON
			B.DF_PRS_ID_BR = D.DF_PRS_ID
		INNER JOIN OLWHRM1.PD01_PDM_INF E ON
			B.DF_PRS_ID_STU = E.DF_PRS_ID
WHERE
		A.IN_SDL_PTC_YR = ' '
		AND C.AC_PRC_STA NOT IN ('S','H')
		AND C.AC_LON_TYP <> 'CL'
		AND C.AD_PRC > '04/01/2000'
		AND C.AD_PRC <> CURRENT DATE
		AND C.AD_LDR_NTF_PRC_RSL IS NULL
		AND C.AD_SCL_NTF_PRC_RSL IS NULL
		AND C.AD_BR_NTF_PRC_RSL IS NULL
) ;

      DISCONNECT FROM DB2 ;

  %PUT &SQLXRC& &SQLXMSG& ;

DATA TEST ;
 SET A1 ;

PROC SORT DATA=TEST ;
 BY STAT ;

PROC MEANS
  DATA=TEST
  NOPRINT;
  BY STAT;
  VAR SSN;
  OUTPUT OUT=ALLINFO N(SSN)=NUMBER ;
RUN;

PROC SORT DATA=TEST ;
 BY LNDR STAT ;

ENDRSUBMIT;
DATA TEST; SET WORKLOCL.TEST; RUN;
DATA ALLINFO; SET WORKLOCL.ALLINFO; RUN;

DATA NULLRPT  ;  STRING= "***   END OF OBS OR NO OBSERVATIONS FOUND   ***"  ;  RUN ;
PROC FORMAT ;
  VALUE $STATUS   'A' = 'APPROVED'
                  'I' = 'INCOMPLETE'
                  'R' = 'REJECT'
                  'X' = 'INCOMPLETE/REJECT'
                  'P' = 'PENDING APPROVAL'
                  'M' = 'ACCOUNT HOLD'
                      ;

proc printto print=report2 NEW;
run  ;

PROC PRINT NOOBS U N DATA=TEST SPLIT='/' ;
 VAR LNDR SSNS SSNB UID DATE STAT  ;
 FORMAT DATE MMDDYY10. STAT $STATUS. ;
 LABEL SSNS = "STUDENT'S/ACCOUNT/NUMBER"
       SSNB = "BORROWER'S/ACCOUNT/NUMBER"
       UID  = "UNIQUE ID"
       STAT = "PROCESSING/STATUS"
       DATE = "DATE/PROCESSED"
       LNDR = "LENDER"
       ;
  Title1 "ACCOUNTS NOT IN STAFF REVIEW OR ON APPLICATION HOLD, THAT WERE NOT " ;
  Title2 "PROCESSED TODAY AND THE NOTIFIED FIELDS ARE BLANK" ;
  FOOTNOTE 'MEM, JOB=UTLWG4B, REPORT=ULWG4BR2';
RUN ;

PROC PRINT DATA = NULLRPT ; RUN ;

proc printto print=report3 NEW;
run  ;

PROC TABULATE
  DATA=ALLINFO
  FORMAT=COMMA9.
  ORDER=DATA;
  CLASS STAT ;
  VAR NUMBER ;
  TABLE STAT=' ' ALL='TOTAL',
    NUMBER='NUMBER PER STATUS' / RTS=63;
  TITLE 'BREAK DOWN OF NUMBERS' ;
FOOTNOTE 'MEM, JOB=UTLWG4B, REPORT=ULWG4BR3';
RUN;

PROC PRINT DATA = NULLRPT ; RUN ;

PROC PRINTTO;
RUN;
