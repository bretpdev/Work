CREATE PROCEDURE [duedtecng].[AddNewWork]
	@TargetDay TINYINT,
	@DestinationDay TINYINT,
	@WorkAddLimit INT = NULL
AS

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
IF OBJECT_ID('tempdb..#BASE_POP') IS NOT NULL 
	DROP TABLE #BASE_POP

DECLARE @Today DATE = CAST(GETDATE() AS DATE)
DECLARE @DatePlus31 DATE = DATEADD(DAY, 31, @Today)
DECLARE @DateMinus60 DATE = DATEADD(DAY, -60, @Today)

SELECT DISTINCT
	LN10.BF_SSN AS Ssn,
	PD10.DF_SPE_ACC_ID AS AccountNumber,
	@DestinationDay AS DueDate
INTO #BASE_POP
FROM
	CDW..PD10_PRS_NME PD10
	INNER JOIN CDW..LN10_LON LN10
		ON PD10.DF_PRS_ID = LN10.BF_SSN
	INNER JOIN
	(
		SELECT
			B.BF_SSN
		FROM
			CDW..BL10_BR_BIL B
		WHERE
			B.LC_STA_BIL10 = 'A'
			AND B.LC_BIL_TYP = 'P'
		GROUP BY 
			BF_SSN
		HAVING 
			DAY(MAX(B.LD_BIL_DU)) = @TargetDay
			AND
			MAX(B.LD_BIL_DU) <= @DatePlus31
	)BL10
		ON BL10.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT	
			BF_SSN
		FROM
			CDW..DW01_DW_CLC_CLU DW01
		WHERE
			DW01.WC_DW_LON_STA NOT IN ('03', '22')
	) DW01
		ON DW01.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT	
			BF_SSN
		FROM
			CDW..AY10_BR_LON_ATY AY10
		WHERE
			AY10.PF_REQ_ACT IN ('OVRPS')
            AND AY10.LC_STA_ACTY10 = 'A'
            AND CAST(AY10.LD_ATY_REQ_RCV AS DATE) > @DateMinus60
	) AY10
		ON AY10.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT	
			BF_SSN,
			MAX(LN_ATY_SEQ) AS LN_ATY_SEQ
		FROM
			CDW..AY10_BR_LON_ATY AY10
		WHERE
			AY10.PF_REQ_ACT IN ('DUEDT', 'DTPRC')
            AND AY10.LC_STA_ACTY10 = 'A'
		GROUP BY
			BF_SSN
	) AY10_Arcs
		ON AY10_Arcs.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT
			BF_SSN
		FROM
			CDW..LN16_LON_DLQ_HST
		WHERE
			LC_STA_LON16 = '1' 
	)LN16_CUR
		ON LN16_CUR.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT
			BR30.BF_SSN
		FROM
			CDW..BR30_BR_EFT BR30
		WHERE
			BR30.BC_EFT_STA = 'A'
	)BR30
		ON BR30.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT
			BF_SSN
		FROM
			CDW..LN65_LON_RPS
		WHERE
			LC_TYP_SCH_DIS IN ('CQ','C1','C2','C3','IB','IL','IS','I3','CA','CP','IP','PG','PL','I5','IA','FG','FS') 
			AND LC_STA_LON65 = 'A'
	)IDR
		ON IDR.BF_SSN = LN10.BF_SSN
	LEFT JOIN
    (
        SELECT 
            BF_SSN,
            SUM(LA_CUR_PRI) AS LA_CUR_PRI
        FROM
			CDW..LN10_LON LN10
        WHERE
            LN10.LC_STA_LON10 = 'R'
        GROUP BY
            BF_SSN
		HAVING
			SUM(LA_CUR_PRI) < 50
    ) LN10_50
            ON LN10_50.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT
			B.BF_SSN
		FROM
			CDW..BL10_BR_BIL B
		WHERE
			B.LC_STA_BIL10 = 'A'
			AND
			B.LD_BIL_DU >= @Today
	) BL10_EXCL
		ON BL10_EXCL.BF_SSN = LN10.BF_SSN
	LEFT JOIN
	(
		SELECT
			FB10.BF_SSN
		FROM
			CDW..FB10_BR_FOR_REQ FB10
			INNER JOIN CDW..LN60_BR_FOR_APV LN60 
				ON LN60.BF_SSN = FB10.BF_SSN
				AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM
		WHERE
			FB10.LC_FOR_TYP = '21'
			AND
			FB10.LC_FOR_STA = 'A'
			AND
			FB10.LC_STA_FOR10 = 'A'
			AND
			LN60.LC_STA_LON60 = 'A'
			AND
			LN60.LC_FOR_RSP != '003'
	) TLF
		ON TLF.BF_SSN = LN10.BF_SSN	
WHERE
	LN10.LA_CUR_PRI > 0
	AND LN10.LC_STA_LON10 = 'R'
	AND BR30.BF_SSN IS NULL
	AND LN16_CUR.BF_SSN IS NULL
	AND IDR.BF_SSN IS NULL
	AND AY10.BF_SSN IS NULL
	AND AY10_Arcs.BF_SSN IS NULL
	AND DW01.BF_SSN IS NULL
	AND LN10_50.BF_SSN IS NULL
	AND BL10_EXCL.BF_SSN IS NULL
	AND TLF.BF_SSN IS NULL

IF @WorkAddLimit IS NULL SELECT @WorkAddLimit = COUNT(*) FROM #BASE_POP

INSERT INTO duedtecng.DueDateChange(Ssn, AccountNumber, DueDate)
SELECT TOP (@WorkAddLimit)
	BP.*
FROM 
	#BASE_POP BP
	LEFT JOIN /*PREVENTS DUPLICATE RECORDS FROM BEING ADDED*/
	(
		SELECT 
			Ssn
		FROM 
			duedtecng.DueDateChange 
		WHERE 
			ProcessedAt IS NULL
	) UNPROCESSED_POP
		ON BP.Ssn = UNPROCESSED_POP.Ssn
WHERE
	UNPROCESSED_POP.Ssn IS NULL

SELECT @@ROWCOUNT RecordsAdded

IF OBJECT_ID('tempdb..#BASE_POP') IS NOT NULL 
	DROP TABLE #BASE_POP


RETURN 0
