USE ODW
GO

DROP TABLE IF EXISTS #DEMOS;

DECLARE @R2 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN1'),
		@R3 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN2'),
		@R4 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN3'),
		@R5 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN4'),
		@R6 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN5'),
		@R7 INT = (SELECT LetterId FROM ULS.[print].Letters WHERE Letter = 'DFNOPHN6');
DECLARE @ScriptId VARCHAR(10) = 'ICRNOPHN';
DECLARE @ScriptDataIdR2 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R2 AND IsEndorser = 0),
		@ScriptDataIdR3 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R3 AND IsEndorser = 0),
		@ScriptDataIdR4 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R4 AND IsEndorser = 0),
		@ScriptDataIdR5 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R5 AND IsEndorser = 0),
		@ScriptDataIdR6 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R6 AND IsEndorser = 0),
		@ScriptDataIdR7 INT = (SELECT ScriptDataId FROM ULS.[print].ScriptData WHERE ScriptID = @ScriptId AND LetterId = @R7 AND IsEndorser = 0);
DECLARE @TODAY DATE = GETDATE()
--Demo
SELECT
	*,
	'R0' AS Rfile,
	0 AS LetterId,
	0 AS ScriptDataId
INTO
	#Demos
FROM
(
	SELECT DISTINCT 
		DC01.BF_SSN AS SSN,
		PD01.DF_SPE_ACC_ID,
		PD01.DF_PRS_ID,
		PD01.DM_PRS_1,
		PD01.DM_PRS_LST,
		PD01.DX_STR_ADR_1,
		PD01.DX_STR_ADR_2,
		PD01.DM_CT,
		PD01.DC_DOM_ST,
		PD01.DF_ZIP,
		PD01.DM_FGN_CNY,
		SUM(DC01.LA_CLM_PRI + DC01.LA_CLM_INT - DC01.LA_PRI_COL + DC01.LA_INT_ACR + DC02.LA_CLM_INT_ACR - DC01.LA_INT_COL + DC01.LA_LEG_CST_ACR - DC01.LA_LEG_CST_COL + DC01.LA_OTH_CHR_ACR - DC01.LA_OTH_CHR_COL + DC01.LA_COL_CST_ACR - DC01.LA_COL_CST_COL + DC02.LA_CLM_PRJ_COL_CST) OVER(PARTITION BY DC01.BF_SSN) AS TOT_PAYOFF,
		PD01.DC_DOM_ST AS STATE_IND,
		'MA2329' AS COST_CENTER_CODE,
		GETDATE() AS CDAY,
		CASE WHEN AY01.DLIV1 != 0 THEN 'X' ELSE NULL END AS MV1,
		AY01.BD_ATY_PRF_DAYS1 AS DV1,
		CASE WHEN AY01.DLIV2 != 0 THEN 'X' ELSE NULL END AS MV2,
		AY01.BD_ATY_PRF_DAYS2 AS DV2,
		CASE WHEN AY01.DLIV3 != 0 THEN 'X' ELSE NULL END AS MV3,
		AY01.BD_ATY_PRF_DAYS3 AS DV3,
		CASE WHEN AY01.DLIV4 != 0 THEN 'X' ELSE NULL END AS MV4,
		AY01.BD_ATY_PRF_DAYS4 AS DV4,
		CASE WHEN AY01.DLIV5 != 0 THEN 'X' ELSE NULL END AS MV5,
		AY01.BD_ATY_PRF_DAYS5 AS DV5,
		CASE WHEN AY01.DLIV6 != 0 THEN 'X' ELSE NULL END AS MV6,
		AY01.BD_ATY_PRF_DAYS6 AS DV6,
		PD01.DC_ADR
	FROM
		ODW..DC01_LON_CLM_INF DC01
		INNER JOIN ODW..DC02_BAL_INT DC02
			ON DC01.AF_APL_ID = DC02.AF_APL_ID
			AND DC01.AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
			AND DC01.LF_CRT_DTS_DC10 = DC02.LF_CRT_DTS_DC10
		INNER JOIN ODW..PD01_PDM_INF PD01
			ON PD01.DF_PRS_ID = DC01.BF_SSN
			AND PD01.DI_PHN_VLD = 'N'
			AND PD01.DI_ALT_PHN_VLD = 'N'
			AND PD01.DI_VLD_ADR = 'Y'
			AND PD01.DC_ADR = 'L'
		LEFT JOIN 
		(
			SELECT DISTINCT
				AY01.DF_PRS_ID,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV1' THEN 1 ELSE 0 END) AS DLIV1,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV2' THEN 1 ELSE 0 END) AS DLIV2,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV3' THEN 1 ELSE 0 END) AS DLIV3,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV4' THEN 1 ELSE 0 END) AS DLIV4,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV5' THEN 1 ELSE 0 END) AS DLIV5,
				SUM(CASE WHEN AY01.PF_ACT = 'DLIV6' THEN 1 ELSE 0 END) AS DLIV6,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV1' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS1,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV2' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS2,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV3' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS3,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV4' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS4,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV5' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS5,
				MAX(CASE WHEN AY01.PF_ACT = 'DLIV6' THEN AY01.BD_ATY_PRF ELSE NULL END) AS BD_ATY_PRF_DAYS6
			FROM 
				ODW..AY01_BR_ATY AY01 
			WHERE 
				AY01.PF_ACT IN('DLIV1','DLIV2','DLIV3','DLIV4','DLIV5','DLIV6')
			GROUP BY
				AY01.DF_PRS_ID
		) AY01
			ON AY01.DF_PRS_ID = DC01.BF_SSN
	WHERE 
		DC01.LC_STA_DC10 = '03'
		AND DC01.LC_AUX_STA = ''
		AND DC01.LD_CLM_ASN_DOE IS NULL
		AND DC01.LC_PCL_REA IN ('DF', 'DQ', 'DB')
) Demos
WHERE
	Demos.TOT_PAYOFF >= 25

UPDATE
	D
SET
	D.Rfile = 
	(
		CASE WHEN D.DV1 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV1 THEN 'R2' --Make sure it is first time dropping arc, or it has been at least a year
			 WHEN (D.DV2 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV2) AND DATEADD(MONTH,-1,GETDATE()) > D.DV1 THEN 'R3' --Make sure DV1 happened at least a month ago
			 WHEN (D.DV3 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV3) AND DATEADD(MONTH,-1,GETDATE()) > D.DV2 AND DATEADD(MONTH,-1,GETDATE()) > D.DV1 THEN 'R4'
			 WHEN (D.DV4 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV4) AND DATEADD(MONTH,-1,GETDATE()) > D.DV3 AND DATEADD(MONTH,-1,GETDATE()) > D.DV2 AND DATEADD(MONTH,-1,GETDATE()) > D.DV1 THEN 'R5'
			 WHEN (D.DV5 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV5) AND DATEADD(MONTH,-1,GETDATE()) > D.DV4 AND DATEADD(MONTH,-1,GETDATE()) > D.DV3 AND DATEADD(MONTH,-1,GETDATE()) > D.DV2 AND DATEADD(MONTH,-1,GETDATE()) > D.DV1 THEN 'R6'
			 WHEN (D.DV6 IS NULL OR DATEADD(YEAR, -1, GETDATE()) > D.DV6) AND DATEADD(MONTH,-1,GETDATE()) > D.DV5 AND DATEADD(MONTH,-1,GETDATE()) > D.DV4 AND DATEADD(MONTH,-1,GETDATE()) > D.DV3 AND DATEADD(MONTH,-1,GETDATE()) > D.DV2 AND DATEADD(MONTH,-1,GETDATE()) > D.DV1 THEN 'R7'
			 ELSE '' 
		END
	)
FROM
	#Demos D

UPDATE
	D
SET
	D.LetterId = 
		(
			CASE WHEN D.Rfile = 'R2' THEN @R2
				 WHEN D.Rfile = 'R3' THEN @R3
				 WHEN D.Rfile = 'R4' THEN @R4
				 WHEN D.Rfile = 'R5' THEN @R5
				 WHEN D.Rfile = 'R6' THEN @R6
				 WHEN D.Rfile = 'R7' THEN @R7
				 ELSE ''
			END
		),
	D.ScriptDataId =
		(
			CASE WHEN D.Rfile = 'R2' THEN @ScriptDataIdR2
				 WHEN D.Rfile = 'R3' THEN @ScriptDataIdR3
				 WHEN D.Rfile = 'R4' THEN @ScriptDataIdR4
				 WHEN D.Rfile = 'R5' THEN @ScriptDataIdR5
				 WHEN D.Rfile = 'R6' THEN @ScriptDataIdR6
				 WHEN D.Rfile = 'R7' THEN @ScriptDataIdR7
				 ELSE ''
			END
		)
FROM
	#Demos D;

--INSERT INTO ULS.[print].PrintProcessing
--(
--	AccountNumber,
--	EmailAddress, 
--	ScriptDataId, 
--	SourceFile, 
--	LetterData, 
--	CostCenter, 
--	InValidAddress, 
--	DoNotProcessEcorr, 
--	OnEcorr, 
--	ArcNeeded, 
--	ImagingNeeded, 
--	AddedAt, 
--	AddedBy
--)
SELECT
	D.DF_SPE_ACC_ID AS AccountNumber,
	'Ecorr@UHEAA.org' AS EmailAddress, --Default Services does not do Ecorr.
	D.ScriptDataId AS ScriptDataId,
	NULL AS SourceFile,
	D.SSN + ','
		+ D.DF_SPE_ACC_ID + ',"'
		+ RTRIM(D.DM_PRS_1) + '","'
		+ RTRIM(D.DM_PRS_LST) + '","'
		+ RTRIM(D.DX_STR_ADR_1) + '","'
		+ RTRIM(D.DX_STR_ADR_2) + '","'
		+ RTRIM(D.DM_CT) + '",'
		+ RTRIM(CASE WHEN D.DC_DOM_ST = 'FC' THEN ' ' ELSE D.DC_DOM_ST END) + ','
		+ RTRIM(D.DF_ZIP) + ',"'
		+ RTRIM(D.DM_FGN_CNY) + '",'
		+ CAST(D.TOT_PAYOFF AS VARCHAR(20)) + ','
		+ CentralData.dbo.CreateACSKeyline(D.SSN,'B','L') + ','
		+ RTRIM(CASE WHEN D.STATE_IND = 'FC' THEN ' ' ELSE D.STATE_IND END) + ','
		+ D.COST_CENTER_CODE 
	AS LetterData,
	D.COST_CENTER_CODE AS CostCenter,
	IIF(PD03.DI_VLD_ADR = 'Y', 0, 1) AS InValidAddress,
	SD.DoNotProcessEcorr,
	0 AS OnEcorr,
	IIF(ASDM.ArcId IS NOT NULL, 1, 0) AS ArcNeeded,
	IIF(SD.DocIdId IS NOT NULL, 1, 0) AS ImagingNeeded,
	GETDATE() AS AddedAt, 
	'UTLWM36' AS AddedBy
FROM
	#Demos D
	INNER JOIN ULS.[print].ScriptData SD
		ON SD.ScriptDataId = D.ScriptDataId 
		AND SD.LetterId = D.LetterId
		AND SD.Active = 1 --active flag
	INNER JOIN ULS.[print].ArcScriptDataMapping ASDM
		ON ASDM.ScriptDataId = D.ScriptDataId
	INNER JOIN ODW..PD03_PRS_ADR_PHN PD03
		ON PD03.DC_DOM_ST = D.DC_DOM_ST
		AND PD03.DF_PRS_ID = D.DF_PRS_ID
		AND PD03.DC_ADR = 'L' --legal
	LEFT JOIN ULS.[print].PrintProcessing EXISTING_DATA
		ON EXISTING_DATA.AccountNumber = D.DF_SPE_ACC_ID
		AND EXISTING_DATA.EmailAddress = 'Ecorr@Uheaa.org'
		AND EXISTING_DATA.ScriptDataId = D.ScriptDataId
		AND CONVERT(DATE,EXISTING_DATA.AddedAt) = @TODAY
		AND 
		(
			EXISTING_DATA.EcorrDocumentCreatedAt IS NULL
			OR CAST(EXISTING_DATA.EcorrDocumentCreatedAt AS DATE) = @TODAY
		)
		AND
		(
			EXISTING_DATA.PrintedAt IS NULL
			OR CAST(EXISTING_DATA.PrintedAt AS DATE) = @TODAY
		)
		AND EXISTING_DATA.DeletedAt IS NULL
WHERE
	Rfile != ''
	AND EXISTING_DATA.AccountNumber IS NULL;

--'SSN','DF_SPE_ACC_ID','DM_PRS_1','DM_PRS_LST','DX_STR_ADR_1','DX_STR_ADR_2','DM_CT','DC_DOM_ST','DF_ZIP','DM_FGN_CNY','TOT_PAYOFF','ACSKEY','STATE_IND','COST_CENTER_CODE'