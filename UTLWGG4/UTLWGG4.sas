/*
LEGAL AWG EMPLOYERS NOT RESPONDING

This report lists employers which have been served an AWG order for a borrower with judgment 
loans and that are not complying with the order.
Collections uses the report to contact employers to resolve the noncompliance.

*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWGG4.LWGG4R2";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LENR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID											AS SSN,
	RTRIM(A.DM_PRS_1)									AS FIRST,
	RTRIM(A.DM_PRS_LST)									AS LAST,
	A.BF_EMP_ID_1										AS EMPLOYER,
	B.IM_IST_FUL										AS NAME,
	RTRIM(B.IX_GEN_STR_ADR_1)							AS ADD1,
	RTRIM(B.IX_GEN_STR_ADR_2)							AS ADD2,
	RTRIM(B.IX_GEN_STR_ADR_3)							AS ADD3,
	RTRIM(B.IM_GEN_CT)									AS CITY,
	B.IC_GEN_ST											AS ST,
	RTRIM(B.IF_GEN_ZIP)									AS ZIP,
	B.IN_GEN_PHN										AS PHONE,
	F.LD_LST_PAY										AS LASTPAY,
	P.LC_TRX_TYP										AS TYPE,
	D.BD_LEG_ACT_ATY_FIL								AS FILED,
	F.la_clm_pri + F.la_clm_int	- F.la_pri_col +
		F.la_int_acr + G.la_clm_int_acr -
		F.la_int_col									AS PI,
	F.la_leg_cst_acr - F.la_leg_cst_col +
		F.la_oth_chr_acr - F.la_oth_chr_col +
		F.la_col_cst_acr - F.la_col_cst_col + 
		G.la_clm_prj_col_cst							AS OTHER	
FROM 	OLWHRM1.PD01_PDM_INF A INNER JOIN
		OLWHRM1.IN01_LGS_IDM_MST B ON
			A.BF_EMP_ID_1 = B.IF_IST INNER JOIN
		OLWHRM1.LA10_LEG_ACT C ON
			A.DF_PRS_ID = C.DF_PRS_ID_BR AND
			C.BC_WDR_REA = '' AND
			C.BC_INA_REA = '' AND
			C.BC_LEG_ACT_REC_TYP = '2' AND
			(C.BC_HLD_REA = '' OR C.BC_HLD_REA <> '' AND DAYS(C.BD_HLD) < DAYS(CURRENT DATE)) INNER JOIN
		OLWHRM1.LA11_LEG_ACT_ATY D ON
			A.DF_PRS_ID = D.DF_PRS_ID_BR AND
			C.BF_CRT_DTS_LA10 = D.BF_CRT_DTS_LA10 AND
			D.BC_EXE_TYP = '05' AND 
			DAYS(D.BD_LEG_ACT_ATY_FIL) < DAYS(CURRENT DATE) - 45 INNER JOIN
		OLWHRM1.LA12_LEG_ACT_LON E ON
			C.BF_CRT_DTS_LA10 = E.BF_CRT_DTS_LA10 INNER JOIN
		OLWHRM1.DC01_LON_CLM_INF F ON
			E.AF_APL_ID = F.AF_APL_ID AND
			E.AF_APL_ID_SFX = F.AF_APL_ID_SFX AND
			F.LC_GRN = '07' AND 
			(DAYS(F.LD_LST_PAY) < DAYS(CURRENT DATE) - 45 OR F.LD_LST_PAY IS NULL) INNER JOIN
		OLWHRM1.DC02_BAL_INT G ON
			G.AF_APL_ID = E.AF_APL_ID AND
			G.AF_APL_ID_SFX = E.AF_APL_ID_SFX left outer JOIN
		OLWHRM1.DC11_LON_FAT P ON
			A.DF_PRS_ID = P.BF_SSN AND
			F.LD_LST_PAY = P.LD_TRX_EFF

);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA LENR;									
	SET WORKLOCL.LENR;
	ADDRESS = TRIM(ADD1)||' '||TRIM(ADD2)||' '||TRIM(ADD3);
RUN;
PROC SQL;
CREATE TABLE LENRA AS
SELECT SSN,FIRST,LAST,EMPLOYER,NAME,CITY,ST,ZIP,PHONE,LASTPAY,TYPE,FILED,ADDRESS, sum(PI) as PIT, sum(OTHER) as OTHERT
FROM 	LENR
GROUP BY SSN,FIRST,LAST,EMPLOYER,NAME,CITY,ST,ZIP,PHONE,LASTPAY,TYPE,FILED,ADDRESS;

PROC SQL;
CREATE TABLE LENRB AS
SELECT *
FROM 	LENRA
WHERE	PIT > 25 OR
		OTHERT > 100
ORDER BY FILED, SSN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS PAGENO=1 LS=132;
PROC PRINT SPLIT='/' DATA=LENRB WIDTH=MIN;
VAR SSN
	FIRST
	LAST
	EMPLOYER
	NAME
	ADDRESS
	CITY
	ST
	ZIP
	PHONE
	LASTPAY
	TYPE
	FILED;
FORMAT	LASTPAY MMDDYY10.
		FILED MMDDYY10.;
TITLE1 'LEGAL AWG EMPLOYERS NOT RESPONDING';
TITLE2 'NO PAYMENT FOR 45 DAYS';
FOOTNOTE 'JOB = UTLWGG4     REPORT = ULWGG4.LWGG4R2';
RUN;

PROC PRINTTO;
RUN;
