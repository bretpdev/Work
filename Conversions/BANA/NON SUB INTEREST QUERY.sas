%LET BANA = %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\EA27_BANA.dsn; update_lock_typ=nolock; bl_keepnulls=no");
LIBNAME BANA ODBC &BANA ;

PROC SQL;
	CREATE TABLE BWRS AS
		SELECT
			P.BORROWERSSN AS BF_SSN,
			MAP.LN_SEQ,
			sum(BorrowerAccruedInterest) as NON_SUB_INTEREST_EA27

		FROM
			BANA._07_08DisbClaimEnrollRecord p
			INNER JOIN BANA.COMPASSLOANMAPPING MAP
				ON MAP.BORROWERSSN = P.BORROWERSSN
				AND MAP.LOAN_NUMBER = P.LOAN_NUMBER
		WHERE
			MAP.LN_SEQ IS NOT NULL
		GROUP BY
			P.BORROWERSSN,
			MAP.LN_SEQ
			
;
QUIT;

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
DATA DUSTER.BWRS;
SET BWRS;
RUN;

/*TEST*/
/*LIBNAME  DUSTER  REMOTE  SERVER=QADBD004 SLIBREF=WORK  ;*/

/*LIVE*/
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

RSUBMIT;  

/*%let DB = DLGSWQUT;  *This is test;*/
%let DB = DLGSUTWH;  *This is live;

LIBNAME OLWHRM1 DB2 DATABASE=&DB OWNER=OLWHRM1;
PROC SQL;
	CREATE TABLE BWRS_REMOTE AS
		SELECT DISTINCT
			P.*,
			COALESCE(LN10.LA_NSI_OTS,0) AS LA_NSI_OTS,
			LN10.PF_MAJ_BCH,
			LN10.PF_MNR_BCH
		FROM
			BWRS P
			INNER JOIN OLWHRM1.LN10_LON LN10	
				ON P.BF_SSN = LN10.BF_SSN
				AND P.LN_SEQ = LN10.LN_SEQ
		WHERE
			LN10.PF_MAJ_BCH IN ('2016034001','2016034002')
		
;
QUIT;
ENDRSUBMIT;	

DATA BWRS_REMOTE;
SET DUSTER.BWRS_REMOTE;
RUN;

PROC SQL;
	CREATE TABLE INT_FINAL AS
		SELECT
			*
		FROM
			BWRS_REMOTE
		WHERE
			PUT(LA_NSI_OTS,BEST12.) ^= PUT(NON_SUB_INTEREST_EA27,BEST12.)
;
QUIT;

PROC EXPORT DATA = WORK.INT_FINAL 
            OUTFILE = "T:\NON SUB INTEREST QUERY BOTH BATCHES.xlsx" 
            DBMS = EXCEL
			REPLACE;
     SHEET="Sheet1"; 
RUN;
