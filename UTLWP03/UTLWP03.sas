*------------------------------------*
|UTLWP03 GRAD PLUS MONITORING REPORT |
*------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWP03.LWP03R2";
FILENAME REPORT3 "&RPTLIB/ULWP03.LWP03R3";
FILENAME REPORTZ "&RPTLIB/ULWP03.LWP03RZ";
DATA _NULL_;
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYS10.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;

LIBNAME PROGREVW V8 '/sas/whse/progrevw';
DATA GRADPLUS;
SET PROGREVW.GRADPLUS;
RUN;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE GPMR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_SPE_ACC_ID
	,C.DM_PRS_1
	,B.AA_GTE_LON_AMT
	,B.AD_PRC
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
WHERE B.AC_LON_TYP = 'GB'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWP03.LWP03RZ);*/
/*QUIT;*/

/*GB LOANS THAT HAVEN'T SHOWN UP ON REPORT*/
PROC SQL;
CREATE TABLE REPDS AS 
SELECT A.DF_SPE_ACC_ID
	,A.DM_PRS_1
	,A.AA_GTE_LON_AMT
	,A.AD_PRC
	,A.CLUID
FROM GPMR A
WHERE NOT EXISTS (
	SELECT *
	FROM GRADPLUS X
	WHERE X.CLUID = A.CLUID
	)
;
QUIT;
ENDRSUBMIT;
DATA REPDS;SET WORKLOCL.REPDS;RUN;
DATA GRADPLUS;SET WORKLOCL.GRADPLUS;RUN;

%MACRO CRT_REP(DS,TWRD,REPNO);
PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 NODATE;
TITLE "GRAD PLUS MONITORING REPORT - &TWRD";
TITLE2	"RUNDATE: &RUNDT";
FOOTNOTE "JOB = UTLWP03  	 REPORT = ULWP03.LWP03&REPNO";
PROC CONTENTS DATA=&DS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 127*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWP03  	 REPORT = ULWP03.LWP03&REPNO";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=&DS WIDTH=UNIFORM WIDTH=MIN;
FORMAT AD_PRC MMDDYY10. AA_GTE_LON_AMT DOLLAR10.2;
VAR DF_SPE_ACC_ID DM_PRS_1 AA_GTE_LON_AMT AD_PRC CLUID;
LABEL DF_SPE_ACC_ID = 'BORROWER ACCOUNT NUMBER'
	DM_PRS_1 = 'BORROWER FIRST NAME'
	AA_GTE_LON_AMT = 'LOAN AMOUNT'
	AD_PRC = 'APPROVAL DATE'
	CLUID = 'COMMON LINE ID'
;
RUN;
PROC PRINTTO;
RUN;

%MEND CRT_REP;
/*CREATE REPORTS*/
%CRT_REP(GRADPLUS,HISTORICAL,2);
%CRT_REP(REPDS,NEW LOANS,3);

/*----------------------------------------------------------*
| APPEND DATA TO HISORICAL DATA SET - ONLY RUN THIS LOCALLY |
| IF YOU WANT TO OVERWRITE THE PRODUCTION DATA SET          |
*----------------------------------------------------------*/
/*RSUBMIT;*/
/*PROC APPEND DATA=REPDS BASE=PROGREVW.GRADPLUS;RUN;*/
/*PROC SORT DATA=PROGREVW.GRADPLUS NODUPKEY;*/
/*BY DF_SPE_ACC_ID CLUID;*/
/*RUN;*/
/*ENDRSUBMIT;*/
