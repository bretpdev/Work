*-----------------*
| PUT Sale Letter |
*-----------------*;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
%LET RPTLIB = X:\PADD\FTP;
/*%LET RPTLIB = X:\PADD\FTP\Test;*/
%LET FILLIB = X:\PADD\FTP\Put Reports;
FILENAME REPORT4 "&RPTLIB/PUTSALE.R4.txt";
FILENAME REPORT5 "&RPTLIB/PUTSALE.R5.txt";
FILENAME R2 "&FILLIB\20090923 Loan Purchase Detail Schedule Sheet - complete list 09172009.csv"; /*NOTE THE A DATE IS NEEDED*/
/*INPUT SSN AND LOAN SEQ FROM INPUT DATA*/
DATA R2_INPUT;
	INFILE R2 DSD DLM=',' MISSOVER LRECL=26700 FIRSTOBS=2;
	INFORMAT BF_SSN $11. CLUID $19. ;
	INPUT BF_SSN $ CLUID $ ;
	BF_SSN = COMPRESS(BF_SSN,'-');
RUN;
DATA WORKLOCL.R2_INPUT;
	SET R2_INPUT;
RUN;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PUTDAT  AS 
SELECT DISTINCT B.*
	,COALESCE(C.IN_R2,0) AS IN_R2
FROM R2_INPUT A
INNER JOIN CONNECTION TO DB2 (
	SELECT A.BF_SSN 
		,A.LN_SEQ 
		,C.DF_SPE_ACC_ID
		,C.DM_PRS_1
		,C.DM_PRS_LST
		,D.DX_STR_ADR_1
		,D.DX_STR_ADR_2
		,D.DM_CT
		,D.DC_DOM_ST
		,D.DM_FGN_ST
		,D.DF_ZIP_CDE
		,D.DM_FGN_CNY
		,A.LD_LON_1_DSB
		,A.IC_LON_PGM
		,A.LA_LON_AMT_GTR
		,A.LA_CUR_PRI
		,A.LA_LTE_FEE_OTS
		,A.LF_LON_CUR_OWN
		,B.WA_TOT_BRI_OTS
		,D.DI_VLD_ADR	
		,D.DC_ADR	
		,A.LF_LON_ALT||'0'||CHAR(A.LN_LON_ALT_SEQ) AS CLUID
	FROM OLWHRM1.LN10_LON A
	LEFT OUTER JOIN OLWHRM1.DW01_DW_CLC_CLU B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	INNER JOIN OLWHRM1.PD10_PRS_NME C
		ON A.BF_SSN = C.DF_PRS_ID
	LEFT OUTER JOIN OLWHRM1.PD30_PRS_ADR D
		ON C.DF_PRS_ID = D.DF_PRS_ID 
		AND D.DC_ADR = 'L'
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN
LEFT OUTER JOIN (
	SELECT BF_SSN
		,CLUID
		,1 AS IN_R2
	FROM R2_INPUT 
	) C
	ON B.BF_SSN = C.BF_SSN
	AND B.CLUID = C.CLUID
;
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA PUTDAT;
	SET WORKLOCL.PUTDAT;
RUN;
DATA SPLIT(KEEP=BF_SSN IS_SPLIT);
	SET PUTDAT;
	WHERE NOT IN_R2 AND SUM(COALESCE(LA_CUR_PRI,0),COALESCE(LA_LTE_FEE_OTS,0),COALESCE(WA_TOT_BRI_OTS,0)) > 0;
	IS_SPLIT = 1;
RUN;
PROC SORT DATA=SPLIT NODUPKEY;BY BF_SSN;RUN;
PROC SORT DATA=PUTDAT; BY BF_SSN LN_SEQ;RUN;
DATA PUTDAT (DROP=DM_FGN_ST DC_DOM_ST);
	LENGTH STATE $15. CCC_STATE $2.;
	MERGE PUTDAT SPLIT;
	BY BF_SSN;
	IS_SPLIT = COALESCE(IS_SPLIT,0);
	CUR_BAL = SUM(COALESCE(LA_CUR_PRI,0),COALESCE(LA_LTE_FEE_OTS,0),COALESCE(WA_TOT_BRI_OTS,0));
	COST_CENTER_CODE = 'MA2324';
	IF DM_FGN_CNY ^= '' THEN DO;
		SVAR = 1;
		STATE = DM_FGN_ST;
		CCC_STATE = '';
	END;
	ELSE DO; 
		SVAR = 2;
		STATE = DC_DOM_ST;
		CCC_STATE = DC_DOM_ST;
	END;
RUN;
/*TRANSPOSE UHEAA LOANS AND PUT LOANS*/
DATA UHEAA_LN PUT_LOAN;
	SET PUTDAT;
	IF IN_R2 = 0 AND CUR_BAL <= 0 THEN
		DELETE;
	ELSE IF IN_R2 THEN 
		OUTPUT PUT_LOAN;
	ELSE 
		OUTPUT UHEAA_LN;
RUN;
%MACRO BREAK2COLS(DS,NEWDS,NEWDSNUM,NEWCOL,VARCOL);
	PROC TRANSPOSE DATA=&DS OUT=&NEWDS&NEWDSNUM (DROP=_NAME_ ) PREFIX=&NEWCOL;
		VAR &VARCOL;
		BY BF_SSN;
	RUN;
%MEND BREAK2COLS;
%MACRO COMBINE_COLS(NEWDS,RTDS);
	DATA &NEWDS ;
		MERGE &NEWDS.1 &NEWDS.2 &NEWDS.3 &NEWDS.4 &NEWDS.5;
		BY BF_SSN ;
	RUN;
	PROC DATASETS;
		DELETE &NEWDS.1-&NEWDS.5;
		DELETE &RTDS;
	QUIT;
%MEND COMBINE_COLS;
%BREAK2COLS(UHEAA_LN,UHEAA,1,UHEAA_LD_LON_1_DSB,LD_LON_1_DSB);
%BREAK2COLS(UHEAA_LN,UHEAA,2,UHEAA_IC_LON_PGM,IC_LON_PGM);
%BREAK2COLS(UHEAA_LN,UHEAA,3,UHEAA_LA_LON_AMT_GTR,LA_LON_AMT_GTR);
%BREAK2COLS(UHEAA_LN,UHEAA,4,UHEAA_CUR_BAL,CUR_BAL);
%BREAK2COLS(UHEAA_LN,UHEAA,5,UHEAA_LF_LON_CUR_OWN,LF_LON_CUR_OWN);
%COMBINE_COLS(UHEAA,UHEAA_LN);
%BREAK2COLS(PUT_LOAN,PUT,1,PUT_LD_LON_1_DSB,LD_LON_1_DSB);
%BREAK2COLS(PUT_LOAN,PUT,2,PUT_IC_LON_PGM,IC_LON_PGM);
%BREAK2COLS(PUT_LOAN,PUT,3,PUT_LA_LON_AMT_GTR,LA_LON_AMT_GTR);
%BREAK2COLS(PUT_LOAN,PUT,4,PUT_CUR_BAL,CUR_BAL);
%BREAK2COLS(PUT_LOAN,PUT,5,PUT_LF_LON_CUR_OWN,LF_LON_CUR_OWN);
%COMBINE_COLS(PUT,PUT_LOAN);
/*COMBINE ALL THE DATA INTO ONE DATASET*/
PROC SORT DATA=PUT NODUPKEY;BY BF_SSN;RUN;
PROC SORT DATA=UHEAA NODUPKEY;BY BF_SSN;RUN;
PROC SORT DATA=PUTDAT OUT=ALLDAT(DROP=LN_SEQ LD_LON_1_DSB IC_LON_PGM LA_LON_AMT_GTR LF_LON_CUR_OWN LA_CUR_PRI 
	LA_LTE_FEE_OTS WA_TOT_BRI_OTS CUR_BAL IN_R2) NODUPKEY;
BY BF_SSN;
RUN;
DATA ALLDAT (DROP=_LABEL_);
	MERGE ALLDAT UHEAA PUT;
	BY BF_SSN;
RUN;
%MACRO KEY_CLC(TBL);*CALCULATE KEYLINE;
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%KEY_CLC(ALLDAT);
/*CREATE THE REPORTS*/
%LET MAX_CT = 60;
%MACRO PUT_SALE_LETTER_FILES_VALID(RNO,SPLIT_IND,SVI);

	%LET RECS_PER_FILE = 1000; /*SET LIMIT FOR RECORDS PER FILE*/

/*	CREATE THE DATA SET FOR FILE CREATION*/
	PROC SORT DATA=ALLDAT OUT=PUT_REPDS(WHERE=(DI_VLD_ADR = 'Y' AND IS_SPLIT = &SPLIT_IND AND SVAR = &SVI));
		BY SVAR DM_FGN_CNY STATE DF_SPE_ACC_ID;
	RUN;

/*	DETERMINE HOW MANY TIMES THE LOOP NEEDS TO ITERATE*/
	PROC SQL NOPRINT;
		SELECT CEIL(COUNT(*) / &RECS_PER_FILE) 
			INTO: UBOUND
		FROM PUT_REPDS
		;
	QUIT;

	%PUT &UBOUND=;

/*	ITERATE AND CREATE FILES*/
	%LET FIRST = 1;
	%LET LAST = 1000;
	%DO FILE_NUM=1 %TO &UBOUND;
		%IF &SVI EQ 1 %THEN %DO;
			%LET REP_PRINT_NUM = &FILE_NUM;
		%END;
		%ELSE %DO;
			%LET REP_PRINT_NUM = %EVAL(&FILE_NUM+1);
		%END;
		DATA _NULL_;
			SET  PUT_REPDS;
			IF &FIRST <= _N_ <= &LAST;
			FILE "&RPTLIB/PUTSALE.R&RNO..&REP_PRINT_NUM..txt" DELIMITER=',' DSD DROPOVER LRECL=32767;
				FORMAT DF_SPE_ACC_ID $10. ;
				FORMAT DM_PRS_1 $13. ;
				FORMAT DM_PRS_LST $23. ;
				FORMAT DX_STR_ADR_1 $30. ;
				FORMAT DX_STR_ADR_2 $30. ;
				FORMAT DM_CT $20. ;
				FORMAT DF_ZIP_CDE $17. ;
				FORMAT DM_FGN_CNY $25. ;
				FORMAT UHEAA_LD_LON_1_DSB1-UHEAA_LD_LON_1_DSB&MAX_CT MMDDYY10. ;
				FORMAT UHEAA_IC_LON_PGM1-UHEAA_IC_LON_PGM&MAX_CT $6. ;
				FORMAT UHEAA_LA_LON_AMT_GTR1-UHEAA_LA_LON_AMT_GTR&MAX_CT DOLLAR10.2 ;
				FORMAT UHEAA_CUR_BAL1-UHEAA_CUR_BAL&MAX_CT DOLLAR10.2 ;
				FORMAT UHEAA_LF_LON_CUR_OWN1-UHEAA_LF_LON_CUR_OWN&MAX_CT $8. ;
				FORMAT PUT_LD_LON_1_DSB1-PUT_LD_LON_1_DSB&MAX_CT MMDDYY10. ;
				FORMAT PUT_IC_LON_PGM1-PUT_IC_LON_PGM&MAX_CT $6. ;
				FORMAT PUT_LA_LON_AMT_GTR1-PUT_LA_LON_AMT_GTR&MAX_CT DOLLAR10.2 ;
				FORMAT PUT_CUR_BAL1-PUT_CUR_BAL&MAX_CT DOLLAR10.2 ;
				FORMAT PUT_LF_LON_CUR_OWN1-PUT_LF_LON_CUR_OWN&MAX_CT $8. ;
				FORMAT BF_SSN $9. ;
				FORMAT ACSKEY $18. ;
				FORMAT STATE $15. ; 
				FORMAT CCC_STATE $2.;
				FORMAT COST_CENTER_CODE $6. ;
			IF _N_ = &FIRST THEN DO;
				PUT 
					"BF_SSN"
					','
					"DF_SPE_ACC_ID"
					','
					"DM_PRS_1"
					','
					"DM_PRS_LST"
					','
					"DX_STR_ADR_1"
					','
					"DX_STR_ADR_2"
					','
					"DM_CT"
					','
					"STATE"
					','
					"DF_ZIP_CDE"
					','
					"DM_FGN_CNY"
					','	@;
					DO I=1 TO &MAX_CT;
						PUT 'UHEAA_LD_LON_1_DSB' I @;
						PUT 'UHEAA_IC_LON_PGM' I @;
						PUT 'UHEAA_LA_LON_AMT_GTR' I @;
						PUT 'UHEAA_CUR_BAL' I @;
						PUT 'UHEAA_LF_LON_CUR_OWN' I @;
						PUT 'PUT_LD_LON_1_DSB' I @;
						PUT 'PUT_IC_LON_PGM' I @;
						PUT 'PUT_LA_LON_AMT_GTR' I @;
						PUT 'PUT_CUR_BAL' I @;
						PUT 'PUT_LF_LON_CUR_OWN' I @;
					END;
				PUT
					"ACSKEY"
					','
					"CCC_STATE"
					','
					"COST_CENTER_CODE";
				END;
			DO;
			   PUT BF_SSN $ @;	
			   PUT DF_SPE_ACC_ID $ @;
			   PUT DM_PRS_1 $ @;
			   PUT DM_PRS_LST $ @;
			   PUT DX_STR_ADR_1 $ @;
			   PUT DX_STR_ADR_2 $ @;
			   PUT DM_CT $ @;
			   PUT STATE $ @;
			   PUT DF_ZIP_CDE $ @;
			   PUT DM_FGN_CNY $ @;
			   %DO I=1 %TO &MAX_CT;
					PUT UHEAA_LD_LON_1_DSB&I @;
					PUT UHEAA_IC_LON_PGM&I $ @;
					PUT UHEAA_LA_LON_AMT_GTR&I @;
					PUT UHEAA_CUR_BAL&I @;
					PUT UHEAA_LF_LON_CUR_OWN&I $ @;
					PUT PUT_LD_LON_1_DSB&I @;
					PUT PUT_IC_LON_PGM&I $ @;
					PUT PUT_LA_LON_AMT_GTR&I @;
					PUT PUT_CUR_BAL&I @;
					PUT PUT_LF_LON_CUR_OWN&I $ @;
			   %END;
			   PUT ACSKEY $ @;
			   PUT CCC_STATE $ @;
			   PUT COST_CENTER_CODE $ ;
			END;
			RUN;
			%LET FIRST = %EVAL(&LAST+1);
			%LET LAST = %EVAL(&LAST+1000);
		%END;
%MEND PUT_SALE_LETTER_FILES_VALID;
%MACRO PUT_SALE_LETTER_FILES_INVALID(RNO,SPLIT_IND);
	PROC SORT DATA=ALLDAT OUT=PUT_REPDS(WHERE=(DI_VLD_ADR = 'N' AND IS_SPLIT = &SPLIT_IND));
		BY BF_SSN;
	RUN;
	DATA _NULL_;
		SET  PUT_REPDS;
		FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
			FORMAT DF_SPE_ACC_ID $10. ;
		IF _N_ = 1 THEN DO;
			PUT 
				"DF_SPE_ACC_ID";
			END;
		DO;
		   PUT DF_SPE_ACC_ID $ ;
		END;
	RUN;
%MEND PUT_SALE_LETTER_FILES_INVALID;
%PUT_SALE_LETTER_FILES_VALID(2,0,1);
%PUT_SALE_LETTER_FILES_VALID(2,0,2);
%PUT_SALE_LETTER_FILES_VALID(3,1,1);
%PUT_SALE_LETTER_FILES_VALID(3,1,2);
%PUT_SALE_LETTER_FILES_INVALID(4,0);
%PUT_SALE_LETTER_FILES_INVALID(5,1);
