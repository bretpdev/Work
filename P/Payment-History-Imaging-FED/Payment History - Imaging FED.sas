/*LIBNAME CLS ODBC %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\CLS.dsn; update_lock_typ=nolock; bl_keepnulls=no");*/
LIBNAME CLS ODBC %STR(REQUIRED="FILEDSN=X:\PADR\ODBC\CLS_TEST.dsn; update_lock_typ=nolock; bl_keepnulls=no");

/*get descriptions from CLS*/
DATA FULL_TYP_DESC;
	SET CLS.PaymentHistoryPaymentTypes;
RUN;

DATA PC_FAT_TYP_DESC;
	SET CLS.PaymentHistoryTransactionTypes;
RUN;


%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/Payment History - Imaging FED.R2";
FILENAME REPORT3 "&RPTLIB/Payment History - Imaging FED.R3";
LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=WORK  ;

/*GET SSNS AND PERSON TYPE FROM INPUT FILE*/
DATA INLST;
	INFILE "&RPTLIB\Image History Accounts.txt" DSD DLM=',' MISSOVER LRECL=67000;
	INFORMAT SSN $100. PRSN_TYP $1.;
	INPUT SSN $ PRSN_TYP $;
RUN;

PROC SQL NOPRINT;
	SELECT "'"||TRIM(SSN)||"'" 
		INTO :SSN_LIST SEPARATED BY "," 
	FROM INLST;
QUIT;

DATA LEGEND.INLST;
	SET INLST;
RUN;

%SYSLPUT SSN_LIST = &SSN_LIST;
RSUBMIT;

PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL);
CREATE TABLE FIN_TRAN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN
	,LN10.LN_SEQ
	,LN_FAT_SEQ
	,LD_FAT_EFF
	,LD_FAT_PST
	,LD_FAT_APL
	,PC_FAT_TYP
	,PC_FAT_TYP || PC_FAT_SUB_TYP AS TYPE
	,COALESCE(LA_FAT_CUR_PRI,0) AS LA_FAT_CUR_PRI
	,COALESCE(LA_FAT_NSI,0) AS LA_FAT_NSI
	,COALESCE(LA_FAT_LTE_FEE,0) + coalesce(la_fat_dl_reb,0) AS LA_FAT_LTE_FEE
	,RTRIM(DM_PRS_1)||' '||RTRIM(DM_PRS_MID)||' '||RTRIM(DM_PRS_LST)||' ' ||DM_PRS_LST_SFX AS BOR_NAME
	,LN10.IC_LON_PGM
	,LN10.LD_LON_1_DSB
FROM pkub.LN10_LON LN10
INNER JOIN PKUB.LN90_FIN_ATY A
	ON A.BF_SSN = LN10.BF_SSN
	AND A.LN_SEQ = LN10.LN_SEQ
INNER JOIN PKUB.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
WHERE LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	AND A.BF_SSN IN (&SSN_LIST)
ORDER BY BF_SSN
	,LN_SEQ
	,LN_FAT_SEQ
);
DISCONNECT FROM DB2;
QUIT;

ENDRSUBMIT;

DATA FIN_TRAN; SET LEGEND.FIN_TRAN; RUN;

PROC SQL;
	CREATE TABLE FIN_TRAN2 AS 
	SELECT DISTINCT A.*
		,B.*
	FROM INLST A
	LEFT OUTER JOIN FIN_TRAN B
		ON A.SSN = B.BF_SSN
	ORDER BY A.SSN;
QUIT;

PROC SORT DATA=FIN_TRAN2 OUT=FIN_TRAN NODUPKEY;
	WHERE BF_SSN ^= '';
	BY BF_SSN LN_SEQ LN_FAT_SEQ;
RUN;

PROC SORT DATA=FIN_TRAN2 OUT=R3(KEEP=SSN) NODUPKEY;
	WHERE BF_SSN = '';
	BY SSN;
RUN;

PROC SQL;
	CREATE TABLE FIN_TRAN_TEMP AS
	SELECT A.*
		,COALESCE(B.Description,C.Description) AS ENG_DESC
	FROM FIN_TRAN A
	LEFT OUTER JOIN FULL_TYP_DESC B
		ON A.TYPE = B.TYPE
	LEFT OUTER JOIN PC_FAT_TYP_DESC C
		ON A.PC_FAT_TYP = C.Type
	;
QUIT;

PROC DATASETS NOPRINT ;
	DELETE FIN_TRAN FIN_TRAN2;
	CHANGE FIN_TRAN_TEMP=FIN_TRAN;
QUIT;

PROC SORT DATA=FIN_TRAN; BY BF_SSN LN_SEQ LD_FAT_EFF; RUN;

DATA FIN_TRAN;
	SET FIN_TRAN;
	BY BF_SSN LN_SEQ;
	RETAIN PRINCIPAL_BALANCE 0;
	IF FIRST.LN_SEQ THEN PRINCIPAL_BALANCE = 0;
	PRINCIPAL_BALANCE + LA_FAT_CUR_PRI;
	TranAmt = LA_FAT_CUR_PRI + LA_FAT_NSI + LA_FAT_LTE_FEE ;
	IF -.01 < PRINCIPAL_BALANCE < 0 THEN PRINCIPAL_BALANCE = 0;
RUN;

DATA _NULL_;
	SET FIN_TRAN;
	WHERE PRSN_TYP ^= 'E';
	BY BF_SSN;
	FORMAT LD_FAT_EFF LD_FAT_PST LD_FAT_APL LD_LON_1_DSB MMDDYY10. TRANAMT LA_FAT_CUR_PRI LA_FAT_NSI 
		LA_FAT_LTE_FEE PRINCIPAL_BALANCE DOLLAR20.2;
	ARRAY DOL{5} TRANAMT LA_FAT_CUR_PRI LA_FAT_NSI LA_FAT_LTE_FEE PRINCIPAL_BALANCE;
	DO I = 1 TO 5;
		DOL(I) = COALESCE(DOL(I),0);
	END;
	RETAIN COUNTER 0;
	IF FIRST.BF_SSN THEN COUNTER+1;
	FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	IF _N_ = 1 THEN DO;
		PUT "Counter,SSN,Name,LoanProgram,Disb Date,TranEffDt,PostEffDt,AppDt,TypeCode,TranAmt,AppPrin,AppInterest,AppFees,RemainPrincipal";
	END;
	   PUT COUNTER BF_SSN BOR_NAME IC_LON_PGM LD_LON_1_DSB LD_FAT_EFF LD_FAT_PST LD_FAT_APL ENG_DESC 
			TRANAMT LA_FAT_CUR_PRI LA_FAT_NSI LA_FAT_LTE_FEE PRINCIPAL_BALANCE;
RUN;

DATA _NULL_;
	SET R3 ;
	FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
	IF _N_ = 1 THEN DO;
		PUT "SSN";
	END;
		PUT SSN;
RUN;
