/*UTLWQ08 - LOAN SERVICING CENTER DELINQUENCY REPORT*/

/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWQ08.LWQ08R2";
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=work  ;
RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1; 
LIBNAME PROGREVW V8 '/sas/whse/progrevw'; 
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;

CREATE TABLE DET AS
SELECT 
	LN10.BF_SSN,
	LN10.LF_DOE_SCL_ORG,
	SC10.IM_SCL_FUL,
	SUM(LN10.LA_CUR_PRI + DW01.WA_TOT_BRI_OTS) AS BALANCE,
	MAX(LN16.LD_DLQ_OCC) AS LD_DLQ_OCC
FROM 	
	OLWHRM1.LN10_LON LN10 
	INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01 
		ON LN10.BF_SSN = DW01.BF_SSN
		AND LN10.LN_SEQ = DW01.LN_SEQ
		AND LN10.LC_STA_LON10 = 'R'
		AND DW01.WC_DW_LON_STA NOT IN ('01','02','04','05','12','17','19','22','23','88','98')
	INNER JOIN OLWHRM1.SC10_SCH_DMO SC10
		ON LN10.LF_DOE_SCL_ORG = SC10.IF_DOE_SCL
	INNER JOIN 
	(
		SELECT DISTINCT 
			LN65SUB.BF_SSN
		FROM
			OLWHRM1.LN65_LON_RPS LN65SUB
		WHERE
			LN65SUB.LC_STA_LON65 = 'A'
	) LN65 
		ON LN10.BF_SSN = LN65.BF_SSN
	LEFT OUTER JOIN 
	(
		SELECT DISTINCT 
			LN16SUB.BF_SSN,
			LN16SUB.LN_SEQ,
			MAX(LN16SUB.LD_DLQ_OCC) AS LD_DLQ_OCC
		FROM 
			OLWHRM1.LN16_LON_DLQ_HST LN16SUB
		WHERE
			TODAY() - LN16SUB.LD_DLQ_OCC >= 30
			AND LN16SUB.LC_STA_LON16 = '1'
		GROUP BY 
			LN16SUB.BF_SSN,
			LN16SUB.LN_SEQ
	) LN16
		ON LN65.BF_SSN = LN16.BF_SSN
		AND LN16.LN_SEQ = LN10.LN_SEQ
WHERE
	LN10.LA_CUR_PRI + DW01.WA_TOT_BRI_OTS > 0
	AND TODAY() - DW01.WD_LON_RPD_SR >= 75
	AND LN10.IC_LON_PGM <> 'TILP'
	AND LN10.LI_CON_PAY_STP_PUR <> 'Y'
GROUP BY
	LN10.BF_SSN,
	LN10.LF_DOE_SCL_ORG,
	SC10.IM_SCL_FUL
ORDER BY 
	LF_DOE_SCL_ORG
;
QUIT;
ENDRSUBMIT;

DATA DET;
	SET DUSTER.DET;
RUN;

/*DELQ2 IS FOR TESTING ONLY*/
/*PROC SQL;*/
/*CREATE TABLE DELQ2 AS*/
/*SELECT 	DISTINCT BF_SSN*/
/*		,LF_DOE_SCL_ORG*/
/*		,IM_SCL_FUL*/
/*		,SUM(BALANCE) AS BALANCE*/
/*		,COUNT(DISTINCT BF_SSN) AS DLQ_BWR_CT*/
/*		,LD_DLQ_OCC*/
/*FROM 	DET*/
/*WHERE	LD_DLQ_OCC IS NOT NULL*/
/*GROUP BY LF_DOE_SCL_ORG, IM_SCL_FUL*/
/*;*/
/*QUIT;*/
/*PROC SORT DATA=DELQ2;*/
/*BY BF_SSN;*/
/*RUN;*/

PROC SQL;
CREATE TABLE DELQ AS
	SELECT 
		LF_DOE_SCL_ORG,
		IM_SCL_FUL,
		SUM(BALANCE) AS BALANCE,
		COUNT(DISTINCT BF_SSN) AS DLQ_BWR_CT
	FROM
		DET
	WHERE
		LD_DLQ_OCC IS NOT NULL
	GROUP BY
		LF_DOE_SCL_ORG,
		IM_SCL_FUL
;

CREATE TABLE REPA AS
	SELECT 
		LF_DOE_SCL_ORG,
		IM_SCL_FUL,
		COUNT(DISTINCT BF_SSN) AS TOT_BWR_CT
	FROM
		DET
	GROUP BY
		LF_DOE_SCL_ORG,
		IM_SCL_FUL
;
QUIT;

DATA DELQ;
	MERGE DELQ (IN=A) REPA (IN=B);
	BY LF_DOE_SCL_ORG;
	IF A;
RUN;

DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=127 PS=39;
PROC REPORT DATA=DELQ NOWD SPACING=1 HEADSKIP SPLIT='/';
TITLE "IN-HOUSE SERVICING";
TITLE2 "SCHOOL DELINQUENCY REPORT";
TITLE3 "&RUNDATE";
FOOTNOTE  'JOB = UTLWQ08     REPORT = ULWQ08.LWQ08R2';
COLUMN LF_DOE_SCL_ORG IM_SCL_FUL DLQ_BWR_CT TOT_BWR_CT PCT_BWR BALANCE AVG_BAL ;
DEFINE LF_DOE_SCL_ORG / GROUP DISPLAY "SCHOOL/ID";
DEFINE IM_SCL_FUL / DISPLAY "SCHOOL NAME";
DEFINE DLQ_BWR_CT / ANALYSIS SUM "BORROWERS/DELINQUENT/30+ DAYS" WIDTH = 15 FORMAT=COMMA5.;
DEFINE TOT_BWR_CT / ANALYSIS SUM "BORROWERS/IN REPAYMENT/30+ DAYS BEYOND/FIRST DUE DATE" WIDTH = 16 FORMAT=COMMA5.;
DEFINE PCT_BWR / COMPUTED "DELINQUENCY/PERCENTAGE" FORMAT=PERCENT8.1 WIDTH=15;
DEFINE BALANCE / ANALYSIS NOPRINT;

DEFINE AVG_BAL / COMPUTED "AVERAGE/DELINQUENT/ACCOUNT/BALANCE" FORMAT=DOLLAR15.2;

COMPUTE AVG_BAL;
	AVG_BAL = BALANCE.SUM / DLQ_BWR_CT.SUM;
	ENDCOMP;

COMPUTE PCT_BWR;
	PCT_BWR = DLQ_BWR_CT.SUM / TOT_BWR_CT.SUM;
	ENDCOMP;
RBREAK AFTER / SUMMARIZE SKIP OL;
RUN;

PROC PRINTTO;
RUN;
/*PROC EXPORT DATA=WORK.DELQ2*/
/*            OUTFILE= "T:\SAS\DELQ2.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
