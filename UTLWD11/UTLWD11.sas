/*UTLWD11 - COMPASS I1 AND I2 SCHOOL LETTERS*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET TBLLIB = /sas/whse/progrevw;*/
%LET RPTLIB = T:\SAS;
%LET TBLLIB = Q:\Process Automation\TabSAS;

FILENAME REPORT2 "&RPTLIB/ULWD11.LWD11R2";
FILENAME REPORT3 "&RPTLIB/ULWD11.LWD11R3";
FILENAME REPORT4 "&RPTLIB/ULWD11.LWD11R4";

/*INPUT LOAN TYPES FOR PRIVATE AND FFEL LOANS*/
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "&TBLLIB/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
/*CREATE MACRO VARIALBE LISTS OF LOAN PROGRAMS(FFEL AND PRIVATE LOANS)*/
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY "," /*FFEL LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';

	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :PRIVATE_LIST SEPARATED BY "," /*PRIVATE LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM ^= 'FFEL';
QUIT;
%SYSLPUT FFELP_LIST = &FFELP_LIST;
%SYSLPUT PRIVATE_LIST = &PRIVATE_LIST;

%LET QUEUES = 'I1','I2';
%SYSLPUT QUEUES = &QUEUES;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK  ;
RSUBMIT;

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*GET PDEM INFORMATION FOR ALL BORROWERS IN QUEUE*/
CREATE TABLE I1I2 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.BF_SSN AS SSN
	,A.WF_QUE
	,B.DM_PRS_1
	,B.DM_PRS_MID
	,B.DM_PRS_LST
	,C.DX_STR_ADR_1
	,C.DX_STR_ADR_2
	,C.DM_CT
	,C.DC_DOM_ST
	,C.DF_ZIP_CDE
	,D.DN_DOM_PHN_ARA||D.DN_DOM_PHN_XCH||D.DN_DOM_PHN_LCL AS PHN_EVE
	,C.DI_VLD_ADR
	,D.DI_PHN_VLD
	,CASE
		WHEN D2.DN_DOM_PHN_ARA <> ''
		THEN D2.DN_DOM_PHN_ARA || D2.DN_DOM_PHN_XCH || D2.DN_DOM_PHN_LCL 
		ELSE ''
		END AS DN_ALT_PHN
	,F.DX_ADR_EML
	,E.DD_SKP_BEG
FROM OLWHRM1.WQ20_TSK_QUE A 
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
	AND D.DC_PHN = 'H'
LEFT OUTER JOIN OLWHRM1.PD42_PRS_PHN D2
	ON A.BF_SSN = D2.DF_PRS_ID
	AND D2.DC_PHN = 'A'
INNER JOIN OLWHRM1.PD27_PRS_SKP_PRC E
	ON A.BF_SSN = E.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD32_PRS_ADR_EML F
	ON A.BF_SSN = F.DF_PRS_ID
	AND F.DC_STA_PD32 = 'A' 
	AND F.DI_VLD_ADR_EML = 'Y'

WHERE C.DC_ADR = 'L'
AND E.DC_STA_SKP = '2' /*ACTIVE SKIP*/
AND A.WF_QUE IN (&QUEUES)
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.AY10_BR_LON_ATY X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.LD_ATY_REQ_RCV > E.DD_SKP_BEG
	AND (X.PF_REQ_ACT = 'KLSLT'
		OR X.PF_REQ_ACT = 'SCHL1')
	)
);

/*GET ALL UNIQUE SCHOOLS ON LG29 FOR STUDENTS ASSOCIATED WITH BWRS IN QUEUES*/
CREATE TABLE LG29S AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	C.BF_SSN AS SSN
	,A.IF_OPS_SCL_RPT AS SCL_ID
	,B.IC_SSR_PTC
	,B.IC_DOE_SCL_STA
FROM OLWHRM1.SD02_STU_ENR A
INNER JOIN OLWHRM1.SC01_LGS_SCL_INF B
	ON A.IF_OPS_SCL_RPT = B.IF_IST
INNER JOIN OLWHRM1.LN10_LON C
	ON C.LF_STU_SSN = A.DF_PRS_ID_STU
INNER JOIN OLWHRM1.WQ20_TSK_QUE D
	ON D.BF_SSN = C.BF_SSN
WHERE D.WF_QUE IN (&QUEUES)
AND C.LC_STA_LON10 = 'R'
);

/*GET MOST RECENT GTY DATE & SCHOOL FOR BORROWERS IN THESE QUEUES*/
CREATE TABLE RECLN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID_BR AS SSN
	,CASE
		WHEN GR10.LF_DOE_SCL_ENR_CUR IS NOT NULL
			THEN GR10.LF_DOE_SCL_ENR_CUR
		ELSE
			A.AF_APL_OPS_SCL
		END AS SCL_ID
FROM OLWHRM1.GA01_APP A 
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.WQ20_TSK_QUE C
	ON C.BF_SSN = A.DF_PRS_ID_BR
INNER JOIN
	(SELECT X.DF_PRS_ID_BR
	,MAX(Y.AD_PRC) AS AD_PRC
	FROM OLWHRM1.GA01_APP X
	INNER JOIN OLWHRM1.GA10_LON_APP Y
		ON X.AF_APL_ID = Y.AF_APL_ID
	INNER JOIN OLWHRM1.WQ20_TSK_QUE Z
		ON Z.BF_SSN = X.DF_PRS_ID_BR
	WHERE Y.AC_PRC_STA = 'A'
	AND Y.AA_GTE_LON_AMT - Y.AA_TOT_RFD - COALESCE(Y.AA_TOT_CAN,0) > 0
	AND Z.WF_QUE IN (&QUEUES)
	GROUP BY X.DF_PRS_ID_BR
	)D
	ON B.AD_PRC = D.AD_PRC
	AND A.DF_PRS_ID_BR = D.DF_PRS_ID_BR
INNER JOIN OLWHRM1.LN10_LON LN10
	ON B.AF_APL_ID = LN10.LF_LON_ALT
	AND B.AF_APL_ID_SFX = '0' || CHAR(LN10.LN_LON_ALT_SEQ)
LEFT OUTER JOIN (
	SELECT SUBQ.BF_SSN, SUBQ.LF_DOE_SCL_ENR_CUR, MAX(SUBQ.WN_SEQ_GR10), SC10.II_SCL_CHS_PTC
	FROM OLWHRM1.GR10_RPT_LON_APL SUBQ
	INNER JOIN OLWHRM1.SC10_SCH_DMO SC10
		ON SUBQ.LF_DOE_SCL_ENR_CUR = SC10.IF_DOE_SCL
	GROUP BY SUBQ.BF_SSN, SUBQ.LF_DOE_SCL_ENR_CUR, SC10.II_SCL_CHS_PTC
) GR10
	ON GR10.BF_SSN = A.DF_PRS_ID_BR
	AND LN10.IC_LON_PGM IN (&PRIVATE_LIST)
	AND GR10.II_SCL_CHS_PTC <> 'Y'
WHERE B.AC_PRC_STA = 'A'
AND B.AA_GTE_LON_AMT - B.AA_TOT_RFD - COALESCE(B.AA_TOT_CAN,0) > 0
AND C.WF_QUE IN (&QUEUES)
AND LN10.LC_STA_LON10 = 'R'
);

DISCONNECT FROM DB2;
ENDRSUBMIT  ;

DATA I1I2; SET WORKLOCL.I1I2; RUN;
DATA LG29S; SET WORKLOCL.LG29S; RUN;
DATA RECLN; SET WORKLOCL.RECLN; RUN;

/*ALNOCH I= Y IF ALL SCHOOLS FOR BORROWER DO NOT PARTICIPATE IN CLEARING HOUSE*/
PROC SQL;
CREATE TABLE LG29SX AS
SELECT A.*
	,COALESCE(B.ALNOCH,'N') AS ALNOCH
FROM LG29S A
LEFT OUTER JOIN 
	(SELECT DISTINCT
	X.SSN
	,'Y' AS ALNOCH
	FROM LG29S X
	WHERE NOT EXISTS
		(SELECT *
		FROM LG29S Y
		WHERE X.SSN = Y.SSN
		AND Y.IC_SSR_PTC = 'C'
		)
	)B
	ON B.SSN = A.SSN
;
QUIT;

PROC SORT DATA=I1I2; BY SSN; RUN;
PROC SORT DATA=LG29SX; BY SSN; RUN;
PROC SORT DATA=RECLN; BY SSN; RUN;

/*MERGE DEMO AND MOST RECENT LOAN SCHOOL*/
DATA GA;
MERGE I1I2 (IN=A) RECLN (IN=B);
BY SSN;
IF A;
RUN;

DATA TEMP (KEEP=SSN TEMP_SCL ALNOCH IC_DOE_SCL_STA);
SET LG29SX;
TEMP_SCL = SCL_ID;
RUN;
DATA GA;
MERGE GA TEMP;
BY SSN;
RUN;
DATA GA;
SET GA;
IF (SCL_ID = '88888800' OR SCL_ID = . OR SCL_ID = '') THEN SCL_ID = TEMP_SCL;
RUN;

/*MERGE DEMO AND LG29 SCHOOLS*/
DATA GB;
MERGE I1I2 (IN=A) LG29SX (IN=B);
BY SSN;
IF A;
RUN;

/*GROUP 1*/
DATA G1;
MERGE GA ;
BY SSN;
WHERE DI_PHN_VLD = 'N'
AND DI_VLD_ADR = 'Y';
NIXQ = 'Y';
RUN;

/*GROUP 2*/
DATA G2A;
SET GA;
WHERE DI_PHN_VLD = 'N'
AND DI_VLD_ADR = 'N';
RUN;


DATA G2B;
SET GB;
WHERE DI_PHN_VLD = 'N'
AND DI_VLD_ADR = 'N'
AND IC_SSR_PTC NE 'C';
RUN;

DATA G2;
SET G2B G2A;
RUN;

PROC SQL;
CREATE TABLE G2B2 AS
SELECT A.*, COALESCE(B.NIXQ,'N') AS NIXQ
FROM G2 A
LEFT OUTER JOIN
	(SELECT DISTINCT SSN
	,'Y' AS NIXQ
	FROM G2 
	WHERE ALNOCH = 'Y')B
	ON A.SSN = B.SSN
;
QUIT;

/*GROUP 3*/
DATA G3;
SET GB;
WHERE DI_PHN_VLD = 'Y'
AND DI_VLD_ADR = 'N'
AND IC_SSR_PTC NE 'C';
IF ALNOCH = 'Y' THEN NIXQ = 'Y';
RUN;

/*CREATE MASTER DATASET GX*/
DATA GX;
SET G2B2 G3 G1 ;
RUN;

/*==========REMOVE CONSOLIDATION ONLY BORROWERS==========*/
DATA GKEEP RZ (KEEP=SSN) ;
SET GX;
IF SCL_ID = '88888800' OR SCL_ID = '' OR SCL_ID = . THEN OUTPUT RZ;
ELSE OUTPUT GKEEP;
RUN;

PROC SORT DATA=RZ NODUPKEY; BY SSN; RUN;
PROC SORT DATA=GKEEP; BY SSN; RUN;

DATA GKEEP (KEEP=SSN);
MERGE GKEEP (IN=A) RZ (IN=B);
BY SSN;
IF B AND NOT A THEN DELETE;
ELSE OUTPUT;
RUN;

PROC SORT DATA=GX;BY SSN;RUN;

DATA GX;
MERGE GX (IN=A) GKEEP (IN=B);
BY SSN;
IF A = B;
RUN;

/*=======FINAL SORT=======*/
PROC SORT DATA=GX NODUPKEY; BY SCL_ID SSN; RUN;

/*LETTER FILE*/
DATA _NULL_;
SET  WORK.GX;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT SSN $9. ;
	FORMAT WF_QUE $2. ;
	FORMAT DM_PRS_1 $13. ;
	FORMAT DM_PRS_MID $13. ;
	FORMAT DM_PRS_LST $23. ;
	FORMAT DX_STR_ADR_1 $30. ;
	FORMAT DX_STR_ADR_2 $30. ;
	FORMAT DM_CT $20. ;
	FORMAT DC_DOM_ST $2. ;
	FORMAT DF_ZIP_CDE $17. ;
	FORMAT PHN_EVE $10. ;
	FORMAT SCL_ID $8. ;
	FORMAT IC_DOE_SCL_STA $1.;
	FORMAT DN_ALT_PHN $12.;
	FORMAT DX_ADR_EML ;
DO;
	EFIOUT + 1;
	PUT SSN $ @;
	PUT WF_QUE $ @;
	PUT DM_PRS_1 $ @;
	PUT DM_PRS_MID $ @;
	PUT DM_PRS_LST $ @;
	PUT DX_STR_ADR_1 $ @;
	PUT DX_STR_ADR_2 $ @;
	PUT DM_CT $ @;
	PUT DC_DOM_ST $ @;
	PUT DF_ZIP_CDE $ @;
	PUT PHN_EVE $ @;
	PUT SCL_ID $ @;
	PUT IC_DOE_SCL_STA $ @;
	PUT DN_ALT_PHN $ @;
	PUT DX_ADR_EML $;
	;
 	END;
RUN;

/*QUEUE DELETION FILE*/
DATA R3 (KEEP=SSN WF_QUE);
SET GX;
WHERE NIXQ = 'Y';
RUN;

PROC SORT DATA=R3 NODUPKEY;
BY SSN;
RUN;

DATA _NULL_;
SET  WORK.R3;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT SSN $9. ;
	PUT SSN $ @;
	PUT WF_QUE $;
RUN;

/*COMMENT CREATION FILE*/
DATA R4 (KEEP=SSN IC_DOE_SCL_STA);
SET GX;
RUN;

PROC SORT DATA=R4 NODUPKEY;
BY SSN;
RUN;

DATA _NULL_;
SET  WORK.R4;
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT SSN $9. ;
	FORMAT IC_DOE_SCL_STA $1.;
	PUT SSN $ @;
	PUT IC_DOE_SCL_STA $ ;
RUN;
