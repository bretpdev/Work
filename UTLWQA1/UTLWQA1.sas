/*
ACTIVE AWG ACCOUNTS WITH OPEN BANKRUPTCY ACTION

This job creates a report which lists loans in accounts with an active AWG record and
an open bankruptcy action.  Used by DBQ for QA.

*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWQA1.LWQA1R2";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AWGBANK AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
		C.DF_SPE_ACC_ID						AS SSN,
		A.af_apl_id||A.af_apl_id_sfx		AS CLID,
		A.lf_clm_id							AS CLAIMID

FROM 	OLWHRM1.DC01_LON_CLM_INF A INNER JOIN
		OLWHRM1.LA10_LEG_ACT B ON
			A.bf_ssn = B.DF_PRS_ID_BR AND
			A.lc_sta_dc10 = '03' AND
			A.ld_clm_asn_doe IS NULL AND
			A.lc_aux_sta = '07' AND
			B.bd_wdr IS NULL AND
			B.bd_ina IS NULL AND
			(B.bd_hld < current date OR
			 B.bd_hld IS NULL) AND
			B.BC_LEG_ACT_REC_TYP = '1' INNER JOIN
		OLWHRM1.PD01_PDM_INF C ON
			A.bf_ssn = C.DF_PRS_ID
ORDER BY C.DF_SPE_ACC_ID
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS PAGENO=1 LS=80;
PROC PRINT NOOBS SPLIT='/' DATA=WORKLOCL.AWGBANK;
VAR SSN
	CLID
	CLAIMID;
LABEL	SSN = 'ACCOUNT NUMBER'
		CLID = 'UNIQUE ID'
		CLAIMID = 'CLAIM ID';
TITLE	'ACTIVE AWG ACCOUNTS WITH OPEN BANKRUPTCY ACTION';
FOOTNOTE  'JOB = UTLWQA1     REPORT = ULWQA1.LWQA1R2';
RUN;
