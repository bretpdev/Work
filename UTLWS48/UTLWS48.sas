/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
/*COMMENT FOR PRODUCTION*/
ODS CLOSE _ALL;
ODS LISTING;

FILENAME REPORT2 "&RPTLIB/ULWS48.LWS48R2";
FILENAME REPORT3 "&RPTLIB/ULWS48.LWS48R3";
FILENAME REPORTZ "&RPTLIB/ULWS48.LWS48RZ";
DATA NULL;
	CALL SYMPUT('BEGIN',"'" || PUT(INTNX('MONTH',TODAY(),-1,'B'),MMDDYY10.) || "'");
	CALL SYMPUT('ENDIN',"'" || PUT(INTNX('MONTH',TODAY(),-1,'E'),MMDDYY10.) || "'");
	CALL SYMPUT('rBEGIN',PUT(INTNX('MONTH',TODAY(),-1,'B'),MMDDYY10.));
	CALL SYMPUT('rENDIN',PUT(INTNX('MONTH',TODAY(),-1,'E'),MMDDYY10.));
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT ENDIN = &ENDIN;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TESTQUERY AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.LD_RMT_BCH_INI
	,A.LF_RMT_BCH_USR_INI
	,A.LA_RMT_BCH_TOT
	,A.LN_RMT_BCH_ITM_TOT
	,CASE
		WHEN SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) IN ('UT') THEN A.LC_BCH_REV_REA
	END AS LC_BCH_REV_REA 
FROM OLWHRM1.RM10_RMT_BCH A
WHERE DAYS(A.LD_RMT_BCH_INI) BETWEEN DAYS(&BEGIN) AND DAYS(&ENDIN)
	AND A.LF_RMT_BCH_USR_INI NOT IN ('CANQUE','LOREAL','U1','WEBRFD')
	AND NOT (SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) IN ('UT') AND A.LC_BCH_REV_REA ^= '')
FOR READ ONLY WITH UR
);

CREATE TABLE CMPSBAU AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.LD_RMT_BCH_INI
	,A.LC_RMT_BCH_SRC_IPT
	,A.LN_RMT_BCH_SEQ
	,B.LN_RMT_SEQ
	,B.LN_RMT_ITM
	,B.LN_RMT_ITM_SEQ
	,B.LC_RMT_STA
	,B.LA_BR_RMT
	,A.LF_RMT_BCH_USR_INI
	,SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) AS LF_RMT_BCH_USR_INI_PRE
	,A.LA_RMT_BCH_TOT
	,A.LN_RMT_BCH_ITM_TOT
	,B.PC_FAT_TYP
	,B.PC_FAT_TYP||B.PC_FAT_SUB_TYP AS TRX_TYP
FROM OLWHRM1.RM10_RMT_BCH A
INNER JOIN OLWHRM1.RM30_BR_RMT B
	ON A.LD_RMT_BCH_INI = B.LD_RMT_BCH_INI
	AND A.LC_RMT_BCH_SRC_IPT = B.LC_RMT_BCH_SRC_IPT
	AND A.LN_RMT_BCH_SEQ = B.LN_RMT_BCH_SEQ
WHERE DAYS(A.LD_RMT_BCH_INI) BETWEEN DAYS(&BEGIN) AND DAYS(&ENDIN)
	AND A.LF_RMT_BCH_USR_INI NOT IN ('CANQUE','LOREAL','U1','WEBRFD')
	AND NOT (SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) IN ('UT') AND A.LC_BCH_REV_REA ^= '')

	AND A.LC_RMT_BCH_SRC_IPT = 'M' 
	AND SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) = 'UT'
	AND B.PC_FAT_TYP = '10'

FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/**/
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/
QUIT;

ENDRSUBMIT;
DATA TESTQUERY;
	SET WORKLOCL.TESTQUERY ;
RUN;
DATA CMPSBAU;
	SET WORKLOCL.CMPSBAU;
RUN;

PROC SQL;
CREATE TABLE ELEC_PAY AS
SELECT SUM(A.LA_RMT_BCH_TOT) AS DOL
	,SUM(A.LN_RMT_BCH_ITM_TOT) AS TRANS
	,COUNT(A.LC_BCH_REV_REA) AS REVERS
	,CASE
		WHEN SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) IN ('UT') AND A.LC_BCH_REV_REA = '' THEN 'UT'
		ELSE A.LF_RMT_BCH_USR_INI
	END AS USERS FORMAT = $TRANSLA.
	,CASE
		WHEN SUBSTR(A.LF_RMT_BCH_USR_INI,1,2) IN ('UT') THEN 'M'
		ELSE 'E'
	END AS SUBTOTAL
FROM TESTQUERY A
GROUP BY USERS, SUBTOTAL;
QUIT;

PROC SORT DATA=ELEC_PAY;
BY SUBTOTAL USERS;
RUN;

DATA CMPSBAU;
	SET CMPSBAU;
	N = 1;
	PRC = 'PAPER CHECKS';
	IF TRX_TYP IN ('1010','1011','1012','1041') THEN PTYP = '1';
	ELSE IF TRX_TYP IN ('1070') THEN PTYP = '2';
	ELSE PTYP = '3';
RUN;

PROC FORMAT;
	VALUE $TRANSLA
		'BILLSTUB' = 'Lockbox'
		'EFT' = 'ACH'
		'MIDATA','ONLNRE' = 'Billpay'
		'TELPAY' = 'Telephone'
		'UT' = 'Paper Checks'
		'WEBPAY' = 'OPS';

	VALUE $PAYTYP
		'1' = 'BORROWER PAYMENTS'
		'2' = 'CONSOLIDATION PAYMENTS'
		'3' = 'OTHER'
	;
RUN;

		
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
/*FOR PORTRAIT REPORTS;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=57 LS=96 DATE;
TITLE 'Electronic Payments';

PROC PRINT NOOBS SPLIT='/' DATA=ELEC_PAY WIDTH=UNIFORM WIDTH=MIN LABEL;
BY SUBTOTAL;
VAR USERS SUBTOTAL TRANS DOL ;
FORMAT DOL DOLLAR14.2;
SUM DOL;
LABEL USERS = 'PROCESS'
	SUBTOTAL = 'PROCESS TYPE'
	TRANS = 'TOTAL TRANSACTIONS'
	DOL = 'DOLLAR AMOUNT';

RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=33 LS=127 NODATE PAGENO=1;
TITLE 'MANUAL PAYMENTS';
TITLE2 "&rBEGIN - &rENDIN"; 
FOOTNOTE 'JOB=UTLWS48               REPORT=ULWS48.LWS48R3';
PROC REPORT DATA=CMPSBAU NOWD HEADSKIP SPLIT='~';
	COLUMN PRC PTYP N LA_BR_RMT;
	DEFINE PRC / GROUP 'PROCESS';
	DEFINE PTYP / GROUP 'PAYMENT TYPE' FORMAT=$PAYTYP.;
	DEFINE N  / N 'TOTAL TRANSACTIONS' WIDTH=15 FORMAT=COMMA9.;
	DEFINE LA_BR_RMT / ANALYSIS SUM 'DOLLAR AMOUNT' FORMAT=DOLLAR20.2 WIDTH=20;
	RBREAK AFTER/ SUMMARIZE SUPPRESS SKIP DOL;
RUN;
PROC PRINTTO;
RUN;

