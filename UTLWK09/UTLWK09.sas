/*======================================*/
/*UTLWK09 - MONTHLY REAL EDA SKIP VOLUME*/
/*======================================*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB =  T:\SAS ;
FILENAME REPORTZ "&RPTLIB/ULWK09.LWK09RZ";
FILENAME REPORT2 "&RPTLIB/ULWK09.LWK09R2";
FILENAME REPORT3 "&RPTLIB/ULWK09.LWK09R3";



DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
	 CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'END'), MMDDYYD10.)||"'");
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYS10.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	FILENAME REPORTZ "&RPTLIB/&SQLRPT";
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*GET BORROWER INFO*/
CREATE TABLE BRWRS AS  
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT AY01.DF_PRS_ID 
	,AY01.PF_ACT
	,AY01.BD_ATY_PRF
	,COMPASS.DD_SKP_END
	,COMPASS.DC_STA_SKP
	,COMPASS.DD_SKP_EFF_OCC
	,DC01.LC_STA_DC10
	,DC01.LD_CLM_ASN_DOE
	,DC01.LD_STA_UPD_DC10
FROM OLWHRM1.AY01_BR_ATY AY01
LEFT OUTER JOIN 
	(SELECT A.BF_SSN
		,B.DC_STA_SKP
		,B.DD_SKP_END 
		,B.DD_SKP_EFF_OCC
	 FROM OLWHRM1.LN10_LON A
	 INNER JOIN OLWHRM1.PD27_PRS_SKP_PRC B
	 	ON A.BF_SSN = B.DF_PRS_ID
	 WHERE A.LA_CUR_PRI > 0
	 AND A.LC_STA_LON10 = 'R'
	 AND (B.DC_STA_SKP IN ('2','6','7')
	 	  OR 
		  B.DD_SKP_END BETWEEN &BEGIN AND &END
		  OR 
		  B.DD_SKP_EFF_OCC BETWEEN &BEGIN AND &END )
	 ) COMPASS
	ON AY01.DF_PRS_ID = COMPASS.BF_SSN
LEFT OUTER JOIN 
	(SELECT X.BF_SSN
		,X.LC_STA_DC10
		,X.LD_CLM_ASN_DOE
		,X.LD_STA_UPD_DC10
	 FROM OLWHRM1.DC01_LON_CLM_INF X
  	 WHERE X.LF_CRT_DTS_DC10 = 
				(SELECT MAX(Y.LF_CRT_DTS_DC10)
				 FROM OLWHRM1.DC01_LON_CLM_INF Y
				 WHERE Y.BF_SSN = X.BF_SSN)
	 ) DC01
	ON AY01.DF_PRS_ID = DC01.BF_SSN
WHERE AY01.PF_ACT = 'KFAMR'
AND AY01.BD_ATY_PRF BETWEEN &BEGIN AND &END
AND SUBSTR(AY01.DF_PRS_ID,1,2) <> 'RF'
);

/*CREATE REFERENCE TABLE WITH ASSOCIATED BORROWER INFO*/
CREATE TABLE REFS AS  
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT AY01.DF_PRS_ID 
	,AY01.PF_ACT
	,AY01.BD_ATY_PRF
	,COMPASS.DD_SKP_END
	,COMPASS.DC_STA_SKP
	,COMPASS.DD_SKP_EFF_OCC
	,DC01.LC_STA_DC10
	,DC01.LD_CLM_ASN_DOE
	,DC01.LD_STA_UPD_DC10
	,BR03.DF_PRS_ID_RFR
	,BR03.DF_PRS_ID_BR
FROM OLWHRM1.BR03_BR_REF BR03
INNER JOIN OLWHRM1.AY01_BR_ATY AY01
	ON AY01.DF_PRS_ID = BR03.DF_PRS_ID_RFR
LEFT OUTER JOIN 
	(SELECT A.BF_SSN
		,B.DC_STA_SKP
		,B.DD_SKP_END 
		,B.DD_SKP_EFF_OCC
	 FROM OLWHRM1.LN10_LON A
	 INNER JOIN OLWHRM1.PD27_PRS_SKP_PRC B
	 	ON A.BF_SSN = B.DF_PRS_ID
	 WHERE A.LA_CUR_PRI > 0
	 AND A.LC_STA_LON10 = 'R'
	 AND (B.DC_STA_SKP IN ('2','6','7')
	 	  OR 
		  B.DD_SKP_END BETWEEN &BEGIN AND &END
          OR
		  B.DD_SKP_EFF_OCC BETWEEN &BEGIN AND &END )
	 ) COMPASS
	ON BR03.DF_PRS_ID_BR = COMPASS.BF_SSN
LEFT OUTER JOIN 
	(SELECT X.BF_SSN
		,X.LC_STA_DC10
		,X.LD_CLM_ASN_DOE
		,X.LD_STA_UPD_DC10
	 FROM OLWHRM1.DC01_LON_CLM_INF X
  	 WHERE X.LF_CRT_DTS_DC10 = 
				(SELECT MAX(Y.LF_CRT_DTS_DC10)
				 FROM OLWHRM1.DC01_LON_CLM_INF Y
				 WHERE Y.BF_SSN = X.BF_SSN)
	 ) DC01
	ON BR03.DF_PRS_ID_BR = DC01.BF_SSN
WHERE AY01.PF_ACT = 'KFAMR'
AND AY01.BD_ATY_PRF BETWEEN &BEGIN AND &END
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWK09.LWK09RZ);*/
/*QUIT;*/

ENDRSUBMIT;

DATA BRWRS ;
SET WORKLOCL.BRWRS ;
RUN;

DATA REFS ;
SET WORKLOCL.REFS ;
RUN;

DATA ADELAER (DROP=DF_PRS_ID_RFR DF_PRS_ID_BR);
SET BRWRS REFS;
RUN;

PROC SORT DATA=ADELAER;
BY DF_PRS_ID BD_ATY_PRF;
RUN;

PROC SQL;
CREATE TABLE ADELAERA AS 
SELECT DISTINCT A.DF_PRS_ID
	,A.BD_ATY_PRF
	,A.PF_ACT
	,B.CMPS
	,C.PST_CLM
	,D.PRE_CLM

FROM ADELAER A
LEFT OUTER JOIN 
	(SELECT DF_PRS_ID
		,BD_ATY_PRF
		,'X' AS CMPS
	 FROM ADELAER
	 WHERE DC_STA_SKP IN ('2','6','7')
	 OR DD_SKP_END GE BD_ATY_PRF
	 OR DD_SKP_EFF_OCC GE BD_ATY_PRF
	 ) B
	ON A.DF_PRS_ID = B.DF_PRS_ID
	AND A.BD_ATY_PRF = B.BD_ATY_PRF
LEFT OUTER JOIN 
	(SELECT DF_PRS_ID
		,BD_ATY_PRF
		,'X' AS PST_CLM
	 FROM ADELAER
	 WHERE (LC_STA_DC10 = '03' 
	 		AND LD_CLM_ASN_DOE = .)
	 OR LD_CLM_ASN_DOE GE BD_ATY_PRF
	 OR (LC_STA_DC10 = '04'
	 	 AND LD_STA_UPD_DC10 GT BD_ATY_PRF) 
	 ) C
	ON A.DF_PRS_ID = C.DF_PRS_ID
	AND A.BD_ATY_PRF = C.BD_ATY_PRF
LEFT OUTER JOIN 
	(SELECT P.DF_PRS_ID
		,P.BD_ATY_PRF
		,'X' AS PRE_CLM
	 FROM ADELAER P
	 WHERE P.DF_PRS_ID NOT IN 
			 	(SELECT DISTINCT X.DF_PRS_ID
	 			 FROM ADELAER X
				 WHERE X.LC_STA_DC10 = '03')
	 AND (P.LC_STA_DC10 = '01'
	 	  OR 
		 (P.LC_STA_DC10 = '02'
	 	  AND P.LD_STA_UPD_DC10 GE P.BD_ATY_PRF))
	 ) D
	ON A.DF_PRS_ID = D.DF_PRS_ID
	AND A.BD_ATY_PRF = D.BD_ATY_PRF
;
QUIT;
RUN;

PROC SORT DATA=ADELAERA NODUPKEY;
BY DF_PRS_ID BD_ATY_PRF;
RUN;

DATA ADELAERA (DROP=CMPS PST_CLM PRE_CLM);
SET ADELAERA;
IF CMPS = 'X' THEN COMPASS = 'Y';
ELSE IF CMPS = ' ' AND PST_CLM = 'X' THEN POST_CLAIM = 'Y';
ELSE IF CMPS = ' ' AND PST_CLM = ' ' AND PRE_CLM = 'X' THEN PRE_CLAIM = 'Y';
ELSE OTHER = 'Y';
RUN;

PROC SORT DATA=ADELAERA NODUPKEY;
BY DF_PRS_ID BD_ATY_PRF;
RUN;

PROC SQL;
CREATE TABLE COUNTS AS 
SELECT COMPASS
	,POST_CLAIM
	,PRE_CLAIM
	,OTHER
	,COUNT(*) AS COUNT
	,CASE
		WHEN COMPASS = 'Y' THEN 'COMPASS'
		WHEN POST_CLAIM = 'Y' THEN 'POST_CLAIM'
		WHEN PRE_CLAIM = 'Y' THEN 'PRE_CLAIM'
		WHEN OTHER = 'Y' THEN 'OTHER'
	 END AS TYP 
FROM ADELAERA 
GROUP BY COMPASS
	,POST_CLAIM
	,PRE_CLAIM
	,OTHER
ORDER BY TYP;
QUIT;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE 'MONTHLY REAL EDA SKIP VOLUME';
TITLE2	"RUNDATE &RUNDT";
FOOTNOTE 'JOB = UTLWK09  	 REPORT = ULWK09.LWK09R2';

PROC PRINT NOOBS SPLIT='/' DATA=ADELAERA WIDTH=UNIFORM WIDTH=MIN;
FORMAT BD_ATY_PRF MMDDYY10.;
VAR DF_PRS_ID PF_ACT BD_ATY_PRF COMPASS POST_CLAIM PRE_CLAIM OTHER;
LABEL DF_PRS_ID = 'SSN'
	PF_ACT = 'ACTION CODE' 
	BD_ATY_PRF = 'ACTIVITY DATE' 
	POST_CLAIM = 'ONELINK SKIP DEFAULT'
	PRE_CLAIM = 'ONELINK SKIP PRECLAIM';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE 'MONTHLY REAL EDA SKIP VOLUME TOTALS';
TITLE2	"RUNDATE &RUNDT";
FOOTNOTE 'JOB = UTLWK09  	 REPORT = ULWK09.LWK09R3';

PROC PRINT NOOBS SPLIT='/' DATA=COUNTS WIDTH=UNIFORM WIDTH=MIN;
VAR TYP COUNT;
LABEL TYP = 'SKIP TYPE'
	COUNT = 'SKIP COUNT';
SUM COUNT;
RUN;

PROC PRINTTO;
RUN;
