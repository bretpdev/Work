USE [CentralData]
GO

/****** Object:  StoredProcedure [telpayfed].[TelephonePaymentsFED]    Script Date: 9/24/2020 7:47:09 AM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO







CREATE PROCEDURE [telpayfed].[TelephonePaymentsFED]
	@BEGIN DATE,
	@END DATE
AS

SELECT
	ISNULL(CBP_DATA.AGENT_CBP_CNT,0) AS AGENT_CBP_CNT,
	ISNULL(CBP_DATA.AGENT_CBP_AMT,0.00) AS AGENT_CBP_AMT,
	ISNULL(CBP_DATA.AGENT_IVR_CNT,0) AS AGENT_IVR_CNT,
	ISNULL(CBP_DATA.AGENT_IVR_AMT,0.00) AS AGENT_IVR_AMT,
	ISNULL(RM03_DATA.AGENT_OPS_CNT,0) AS AGENT_OPS_CNT,
	ISNULL(RM03_DATA.AGENT_OPS_AMT,0.00) AS AGENT_OPS_AMT,
	ISNULL(CBP_DATA.BRW_IVR_CNT,0) AS BRW_IVR_CNT,
	ISNULL(CBP_DATA.BRW_IVR_AMT,0.00) AS BRW_IVR_AMT,
	ISNULL(RM03_DATA.BRW_OPS_CNT,0) AS BRW_OPS_CNT,
	ISNULL(RM03_DATA.BRW_OPS_AMT,0.00) AS BRW_OPS_AMT,
	ISNULL(RM03_DATA.BRW_MOBILE_APP_CNT, 0) AS BRW_MOBILE_APP_CNT,
	ISNULL(RM03_DATA.BRW_MOBILE_APP_AMT, 0) AS BRW_MOBILE_APP_AMT,
	ISNULL(RM30_DATA.ACH_AUTO_PAY_CNT,0) AS ACH_AUTO_PAY_CNT,
	ISNULL(RM30_DATA.ACH_AUTO_PAY_AMT,0.00) AS ACH_AUTO_PAY_AMT,
	ISNULL(RM30_DATA.MAIL_CNT,0) AS MAIL_CNT,
	ISNULL(RM30_DATA.MAIL_AMT,0.00) AS MAIL_AMT,
	ISNULL(CBP_DATA.AGENT_CBP_CNT,0) + ISNULL(CBP_DATA.AGENT_IVR_CNT,0) + ISNULL(RM03_DATA.AGENT_OPS_CNT,0) AS TTL_AGENT_CNT,
	ISNULL(CBP_DATA.AGENT_CBP_AMT,0.00) + ISNULL(CBP_DATA.AGENT_IVR_AMT,0.00) + ISNULL(RM03_DATA.AGENT_OPS_AMT,0.00) AS TTL_AGENT_AMT,
	ISNULL(CBP_DATA.BRW_IVR_CNT,0) + ISNULL(RM03_DATA.BRW_OPS_CNT,0) + ISNULL(RM30_DATA.MAIL_CNT,0) + ISNULL(RM30_DATA.ACH_AUTO_PAY_CNT,0) + ISNULL(RM03_DATA.BRW_MOBILE_APP_CNT, 0) AS TTL_BRW_CNT,
	ISNULL(CBP_DATA.BRW_IVR_AMT,0.00) + ISNULL(RM03_DATA.BRW_OPS_AMT,0.00) + ISNULL(RM30_DATA.MAIL_AMT,0.00) + ISNULL(RM30_DATA.ACH_AUTO_PAY_AMT,0) + ISNULL(RM03_DATA.BRW_MOBILE_APP_AMT, 0) AS TTL_BRW_AMT
FROM
	( --counts from CLS CheckByPhone
		SELECT
			SUM(CASE WHEN CBP.DataSource IN ('OPSCHKPHN','OPSCBPFED') THEN 1 ELSE 0 END) AS AGENT_CBP_CNT,
			SUM(CASE WHEN CBP.DataSource IN ('OPSCHKPHN','OPSCBPFED') THEN PaymentAmount ELSE 0.00 END) AS AGENT_CBP_AMT,
			SUM(CASE WHEN CBP.DataSource IN ('AgentIVR') THEN 1 ELSE 0 END) AS AGENT_IVR_CNT,
			SUM(CASE WHEN CBP.DataSource IN ('AgentIVR') THEN PaymentAmount ELSE 0.00 END) AS AGENT_IVR_AMT,
			SUM(CASE WHEN CBP.DataSource IN ('IVR') THEN 1 ELSE 0 END) AS BRW_IVR_CNT,
			SUM(CASE WHEN CBP.DataSource IN ('IVR') THEN PaymentAmount ELSE 0.00 END) AS BRW_IVR_AMT
		FROM
			CLS..CheckByPhone CBP
		WHERE
			CBP.DataSource IN ('OPSCHKPHN','OPSCBPFED','AgentIVR','IVR')
			AND CONVERT(DATE, CBP.CreatedAt) BETWEEN @BEGIN AND @END
			AND CBP.DeletedAt IS NULL
	) CBP_DATA
	INNER JOIN
	( --counts from CDW RM03
		SELECT
			SUM(CASE WHEN RM03.NC_PAY_PRC = '02' AND RM03.NF_IPH NOT LIKE '%MBLAPP%' THEN 1 ELSE 0 END) AS AGENT_OPS_CNT,
			SUM(CASE WHEN RM03.NC_PAY_PRC = '02' AND RM03.NF_IPH NOT LIKE '%MBLAPP%' THEN RM03.BA_PAY ELSE 0.00 END) AS AGENT_OPS_AMT,
			SUM(CASE WHEN RM03.NC_PAY_PRC = '01' AND RM03.NF_IPH NOT LIKE '%MBLAPP%' THEN 1 ELSE 0 END) AS BRW_OPS_CNT,
			SUM(CASE WHEN RM03.NC_PAY_PRC = '01' AND RM03.NF_IPH NOT LIKE '%MBLAPP%' THEN RM03.BA_PAY ELSE 0.00 END) AS BRW_OPS_AMT,
			SUM(CASE WHEN RM03.NF_IPH LIKE '%MBLAPP%' THEN 1 ELSE 0 END) AS BRW_MOBILE_APP_CNT,
			SUM(CASE WHEN RM03.NF_IPH LIKE '%MBLAPP%' THEN BA_PAY ELSE 0.00 END) AS BRW_MOBILE_APP_AMT
		FROM
			CDW..RM03_ONL_PAY RM03
		WHERE
			CONVERT(DATE, RM03.NF_ONL_PAY_DTS) BETWEEN  @BEGIN AND @END
	) RM03_DATA
		ON 1= 1 --each derived table only has one row, combine data from each table
	INNER JOIN
	(--counts from CDW RM30
		SELECT
			SUM(CASE WHEN RM30.LC_RMT_PAY_SRC  = '01' THEN 1 ELSE 0 END) AS ACH_AUTO_PAY_CNT, 
			SUM(CASE WHEN RM30.LC_RMT_PAY_SRC  = '01' THEN LA_BR_RMT ELSE 0.00 END) AS ACH_AUTO_PAY_AMT,
			SUM(CASE WHEN RM30.LC_RMT_PAY_SRC IN ('03', '08') THEN 1 ELSE 0 END) AS MAIL_CNT,
			SUM(CASE WHEN RM30.LC_RMT_PAY_SRC IN ('03', '08') THEN LA_BR_RMT ELSE 0.00 END) AS MAIL_AMT
		FROM
			CDW..RM30_BR_RMT RM30
		WHERE
			 RM30.LD_RMT_PAY_EFF BETWEEN  @BEGIN AND @END
			 AND RM30.LC_RMT_STA  = 'A'
	) RM30_DATA
		ON 1 = 1 --each derived table only has one row, combine data from each table





GO


