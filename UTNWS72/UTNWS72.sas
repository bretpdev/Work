%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORTZ "&RPTLIB/Weekly BalancesRZ";
FILENAME REPORT2 "&RPTLIB/UNWS72.NWS72R2";
DATA _NULL_;
	RUN_DAY = TODAY();
	CALL SYMPUT('MON_AGO_1_BEG',"'"||PUT(INTNX('MONTH',RUN_DAY,-1,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('MON_AGO_1_END',"'"||PUT(INTNX('MONTH',RUN_DAY,-1,'E'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUN_DATE',RUN_DAY);
	CALL SYMPUTX('TITLE_DATE',PUT(INTNX('DAY',RUN_DAY,-1,'E'), WORDDATE20.));
RUN;
/*%SYSLPUT MON_AGO_1_BEG = &MON_AGO_1_BEG;*/
/*%SYSLPUT MON_AGO_1_END = &MON_AGO_1_END;*/
/*LIBNAME LEGEND REMOTE SERVER=LEGEND SLIBREF=WORK;*/
/*RSUBMIT LEGEND;*/
LIBNAME LGND '/sas/whse/progrevw';
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL);
CREATE TABLE MSBTFF AS 
	SELECT 
		*
	FROM 
		CONNECTION TO DB2 
		(
			SELECT 
				 PD10.DF_SPE_ACC_ID
				,LN10.BF_SSN
				,LN10.LN_SEQ
				,LN10.IC_LON_PGM
				,LN10.LC_FED_PGM_YR
				,LN10.LF_FED_CLC_RSK
				,LN10.LA_CUR_PRI 			AS LA_FAT_CUR_PRI
				,FR15.LA_FMS_MTH_INT_ADJ
				,FR15.LA_FMS_MTH_INT_ACR
				,DW01.WA_TOT_BRI_OTS 		AS INT_FINAL
				,LN10.LA_LTE_FEE_OTS 		AS LA_FAT_LTE_FEE
				,COALESCE(DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC),0) AS DAYS_DELQ
				,LN10.LA_CUR_PRI 			AS LA_OTS_PRI_ELG
				,DW01.WC_DW_LON_STA
			FROM 
				PKUB.LN10_LON LN10
				LEFT JOIN 
				(
					SELECT 
						 FR15.BF_SSN
						,FR15.LN_SEQ
						,SUM(FR15.LA_FMS_MTH_INT_ADJ) AS LA_FMS_MTH_INT_ADJ
						,SUM(FR15.LA_FMS_MTH_INT_ACR) AS LA_FMS_MTH_INT_ACR
					FROM 
						PKUR.FR15_MTH_INT_RPT FR15
					GROUP BY 
						 FR15.BF_SSN
						,FR15.LN_SEQ
				) FR15
					ON LN10.BF_SSN = FR15.BF_SSN
					AND LN10.LN_SEQ = FR15.LN_SEQ
				LEFT JOIN PKUB.PD10_PRS_NME PD10
					ON LN10.BF_SSN = PD10.DF_PRS_ID
				LEFT JOIN PKUB.LN16_LON_DLQ_HST LN16
					ON LN10.BF_SSN = LN16.BF_SSN
					AND LN10.LN_SEQ = LN16.LN_SEQ
					AND LN16.LC_STA_LON16 = '1'
					AND LN16.LC_DLQ_TYP = 'P'
				LEFT JOIN PKUB.DW01_DW_CLC_CLU DW01
					ON LN10.BF_SSN = DW01.BF_SSN
					AND LN10.LN_SEQ = DW01.LN_SEQ
			WHERE 
				(
					LN10.LA_CUR_PRI > 0 
					AND LN10.LC_STA_LON10 IN ('R','L')
				)
				OR DAYS(LN10.LD_PIF_RPT) > DAYS(CURRENT DATE) - 8
		)  
	;
DISCONNECT FROM DB2;
QUIT;
/*ENDRSUBMIT;*/
/*DATA MSBTFF;*/
/*	SET LEGEND.MSBTFF;*/
/*RUN;*/

DATA MSBTFF;
	SET MSBTFF;
	IF WC_DW_LON_STA IN ('03','13','14','15') AND DAYS_DELQ <= 5 
	THEN LS = '3A';
	ELSE IF WC_DW_LON_STA IN ('03','13','14','15') AND DAYS_DELQ > 5 
	THEN LS = '3B';
	ELSE IF WC_DW_LON_STA IN ('06','07','08','09','10','11','12','16','17','18','19','20','21','23','88')
	THEN LS = 'IS';
	ELSE IF WC_DW_LON_STA = '22' AND LA_OTS_PRI_ELG < 0 
	THEN LS = 'CB';
	ELSE LS = WC_DW_LON_STA;
	IF LA_FAT_LTE_FEE = . 
	THEN LA_FAT_LTE_FEE = 0;
RUN;

PROC SORT DATA=MSBTFF;
	BY BF_SSN LS;
RUN;

DATA MSBTFF;
	SET MSBTFF;
	BY BF_SSN LS;
	IF FIRST.BF_SSN 
	THEN BSC = 1;
	ELSE BSC = 0;
	LOAN_IND = 1;
	HAS_AMT = (LA_FAT_CUR_PRI>0|INT_FINAL>0);
RUN;

PROC FORMAT;
	VALUE $LNSTA
	'3A' = 'In Repayment(0-6 days delinquent)'
	'3B' = 'In Repayment(>6 days delinquent)'
	'01' = 'Grace'
	'02' = 'In School'
	'04' = 'In Deferment'
	'05' = 'In Forbearance'
	'22' = 'PIF'
	'IS' = 'In Suspense/Specialty'
	'CB' = 'PIF w/ Credit Balance'
	'98' = 'Other';
RUN;

ODS _ALL_ CLOSE;
ODS LISTING;
PROC PRINTTO PRINT=REPORT2 NEW; 
RUN;
OPTIONS ORIENTATION = LANDSCAPE NODATE PAGENO=1 ;
OPTIONS PS=39 LS=127;
TITLE 'Weekly Portfolio Balance Report';
TITLE2 "&TITLE_DATE";
FOOTNOTE 'JOB = Weekly Balancing 	 REPORT = WeeklyBalancingR2';
PROC REPORT DATA=MSBTFF NOWD HEADSKIP SPLIT='~' SPACING=1;
	COLUMN LS BSC LOAN_IND LA_FAT_CUR_PRI INT_FINAL LA_FAT_LTE_FEE;
	DEFINE LS / GROUP FORMAT=$LNSTA. 'LOAN STATUS'; 
	DEFINE BSC / SUM FORMAT=COMMA9. 'TOTAL BORROWER COUNT';
	DEFINE LOAN_IND / SUM  FORMAT=COMMA9. 'TOTAL LOAN COUNT';
	DEFINE LA_FAT_CUR_PRI / SUM FORMAT=COMMA18.2 'PRINCIPAL~BALANCE';
	DEFINE INT_FINAL / SUM FORMAT=COMMA18.2 'INTEREST~BALANCE';
	DEFINE LA_FAT_LTE_FEE / SUM FORMAT=COMMA10.2 'LATE FEE~BALANCE';
	RBREAK AFTER / SUMMARIZE ;
RUN;
PROC PRINTTO;
RUN;
