/*	Notice to Lenders of a Bankruptcy Filing.  */

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWB01.LWB01R2";*/

DATA _NULL_;
	*Previous Monday (beginning of last week);
	CALL SYMPUT('START',put(INTNX('WEEK',DATE(),-1)+1,mmddyy10.));
	CALL SYMPUT('BEG',"'"||put(INTNX('WEEK',DATE(),-1)+1,mmddyy10.)||"'");
	*Previous Sunday (end of last week);
	CALL SYMPUT('FINISH',put(INTNX('WEEK',DATE(),0),mmddyy10.));
	CALL SYMPUT('END',"'"||put(INTNX('WEEK',DATE(),0),mmddyy10.)||"'");
RUN;

%SYSLPUT BEG = &BEG;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132 SYMBOLGEN;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE NOTICE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	integer(A.DF_PRS_ID_BR)			as SSN,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
										AS NAME,
		C.af_apl_id||C.af_apl_id_sfx	as CLUID,
		C.AC_LON_TYP					AS LONTYP,
		C.AD_PRC						AS GTYDT,
		C.AA_GTE_LON_AMT				AS GTYAMT,
		C.AF_CUR_LON_OPS_LDR			AS CURLDR,
		C.AF_CUR_LON_SER_AGY			AS CURSVC,
		B.LF_BKR_DKT					AS BKRDKT,
		B.LD_BKR_FIL					AS FILDT  
FROM  OLWHRM1.GA01_APP A INNER JOIN
		(SELECT DISTINCT X.BF_SSN, Y.LF_BKR_DKT, Y.LD_BKR_FIL
		FROM OLWHRM1.DC01_LON_CLM_INF X INNER JOIN OLWHRM1.DC18_BKR Y
			ON X.AF_APL_ID = Y.AF_APL_ID
			AND X.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
		WHERE ((X.LC_STA_DC10 = '03'
				AND X.LC_PCL_REA IN ('BC','BH','BO')
				AND X.LD_STA_UPD_DC10 BETWEEN &BEG AND &END)
				OR
				(X.LC_STA_DC10 = '03'
				AND X.LC_AUX_STA = '07'
				AND X.LD_AUX_STA_UPD BETWEEN &BEG AND &END
				))
		AND Y.LD_BKR_FIL =
			(SELECT MAX(Z.LD_BKR_FIL)
			FROM OLWHRM1.DC18_BKR Z INNER JOIN OLWHRM1.DC01_LON_CLM_INF ZZ
			ON ZZ.AF_APL_ID = Z.AF_APL_ID
			AND ZZ.AF_APL_ID_SFX = Z.AF_APL_ID_SFX
			WHERE X.BF_SSN = ZZ.BF_SSN)
		) B
		ON A.DF_PRS_ID_BR = B.BF_SSN
	INNER JOIN OLWHRM1.GA10_LON_APP C
		on a.af_apl_id = C.af_apl_id
	INNER JOIN OLWHRM1.GA14_LON_STA F
		ON C.AF_APL_ID = F.AF_APL_ID
		AND C.AF_APL_ID_SFX = F.AF_APL_ID_SFX
		AND F.AC_LON_STA_TYP IN 
			('CR','DA','FB','IA','ID','IG','IM','RP','UA','UB','UC','UD','UI')
		AND F.AC_STA_GA14 = 'A' 	/*ONLY ACTIVE GA14 ROWS*/
	INNER JOIN OLWHRM1.PD01_PDM_INF D
		on A.DF_PRS_ID_BR = D.DF_PRS_ID
ORDER BY C.AF_CUR_LON_OPS_LDR, C.AF_CUR_LON_SER_AGY, A.DF_PRS_ID_BR, 
C.af_apl_id||C.af_apl_id_sfx
);
DISCONNECT FROM DB2;
endrsubmit  ;

DATA NOTICE; 
SET WORKLOCL.NOTICE; 
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS CENTER NODATE NONUMBER LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=NOTICE WIDTH=UNIFORM WIDTH=MIN;
VAR SSN NAME CLUID LONTYP GTYDT GTYAMT /*CURLDR CURSVC*/ BKRDKT FILDT;
BY CURLDR CURSVC;
PAGEBY CURLDR;
LABEL	NAME = 'Name' CLUID = 'Commonline Unique ID' LONTYP='Loan Type'
		GTYDT='Guaranty Date' GTYAMT='Guaranty Amount'
		CURLDR='Current Holder' CURSVC='Servicer Code'
		BKRDKT='Bankruptcy Docket Number' FILDT='Bankruptcy Filing Date';
FORMAT SSN SSN11. GTYDT MMDDYY10. GTYAMT DOLLAR12.2 FILDT MMDDYY10.	;
TITLE 'NOTICE TO LENDERS OF A BANKRUPTCY FILING';
TITLE2 "&START TO &FINISH";
FOOTNOTE  'JOB = UTLWB01     REPORT = ULWB01.LWB01R2';
RUN;
