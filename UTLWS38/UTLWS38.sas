*--------------------------------------------*
|  Default Aversion Special E-mail Campaign  |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS38.LWS38R2";
FILENAME REPORT3 "&RPTLIB/ULWS38.LWS38R3";
FILENAME REPORT4 "&RPTLIB/ULWS38.LWS38R4";
FILENAME REPORT5 "&RPTLIB/ULWS38.LWS38R5";
FILENAME REPORT6 "&RPTLIB/ULWS38.LWS38R6";
FILENAME REPORT7 "&RPTLIB/ULWS38.LWS38R7";
FILENAME REPORTZ "&RPTLIB/ULWS38.LWS38RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DALTC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_SPE_ACC_ID
	,TRIM(A.DM_PRS_1)|| ' ' || TRIM(A.DM_PRS_LST) AS BOR_NAME
	,A.DX_EML_ADR
	,CASE
		WHEN C.LD_ADJ_DCO IS NOT NULL THEN DAYS(CURRENT DATE) - DAYS(C.LD_ADJ_DCO)
		ELSE DAYS(CURRENT DATE) - DAYS(C.LD_DCO) 
	END AS DAYS_DELINQ
	,F.PF_ACT
FROM OLWHRM1.PD01_PDM_INF A
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN B
	ON A.DF_PRS_ID = B.DF_PRS_ID
INNER JOIN OLWHRM1.DC01_LON_CLM_INF C
	ON A.DF_PRS_ID = C.BF_SSN
INNER JOIN (SELECT MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
				,AF_APL_ID
				,AF_APL_ID_SFX
		FROM OLWHRM1.DC01_LON_CLM_INF
		GROUP BY AF_APL_ID, AF_APL_ID_SFX) E
	ON C.AF_APL_ID = E.AF_APL_ID
	AND C.AF_APL_ID_SFX = E.AF_APL_ID_SFX
	AND C.LF_CRT_DTS_DC10 = E.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.GA14_LON_STA	D
	ON C.AF_APL_ID = D.AF_APL_ID
	AND C.AF_APL_ID_SFX = D.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT F.DF_PRS_ID
					,F.PF_ACT
				FROM OLWHRM1.AY01_BR_ATY F 
				WHERE DAYS(F.BD_ATY_PRF) >= DAYS(CURRENT DATE) - 13
					AND PF_ACT IN ('AEM01','AEM02','AEM03','AEM04','AEM05','AEM06')) F
	ON A.DF_PRS_ID = F.DF_PRS_ID
WHERE B.DI_EML_ADR_VAL = 'Y'
	AND C.LC_STA_DC10 = '01'
	AND D.AC_LON_STA_REA = 'DF'
	AND D.AC_LON_STA_TYP = 'CR'
	AND D.AC_STA_GA14 = 'A'

FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA DALTC;
	SET WORKLOCL.DALTC;
RUN;
PROC SORT DATA=DALTC;
	BY DF_SPE_ACC_ID DAYS_DELINQ;
RUN;
DATA DALTC;
SET DALTC;
BY DF_SPE_ACC_ID DAYS_DELINQ;
IF FIRST.DF_SPE_ACC_ID THEN DO;
	IF DAYS_DELINQ >=240 THEN OUTPUT;
END;
RUN;

%MACRO REPORTS(REP,DAYLO,DAYHI,ACT);
DATA _NULL_;
SET DALTC;
FILE REPORT&REP DELIMITER=',' DSD DROPOVER LRECL=32767;
WHERE &DAYLO <= DAYS_DELINQ <= &DAYHI AND PF_ACT ^= &ACT;
IF _N_ = 1 THEN DO;
   PUT 'ACCOUNT NUMBER,NAME,RECIPIENT';
END;
DO;
   PUT DF_SPE_ACC_ID $ @;
   PUT BOR_NAME $ @;
   PUT DX_EML_ADR $;
END;
RUN;
%MEND;
%REPORTS(2,240,254,'AEM01');
%REPORTS(3,255,269,'AEM02');
%REPORTS(4,270,284,'AEM03');
%REPORTS(5,285,299,'AEM04');
%REPORTS(6,300,309,'AEM05');
%REPORTS(7,310,50000,'AEM06');
