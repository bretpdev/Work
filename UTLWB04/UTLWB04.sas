/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWB04.LWB04RZ";
FILENAME REPORT2 "&RPTLIB/ULWB04.LWB04R2";
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1; 

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE HARD_EASY AS
SELECT 
	*
FROM CONNECTION TO DB2
(
	SELECT DISTINCT 
		LN10.BF_SSN,
		LN10.LN_SEQ,
		PD10.DF_SPE_ACC_ID AS ACCT,
		LN40.LD_SBM_CLM_PCL AS DATVA,
		LN40.LN_SEQ_CLM_PCL AS LN40_SEQ,
		LN40.LC_TYP_REC_CLP_LON as CLM_TYP,
		LN40.LD_CAN_CLM_PCL,
		WQ20.WF_QUE || WQ20.WF_SUB_QUE AS QUEUE
	FROM 
		OLWHRM1.LN10_LON LN10
		INNER JOIN OLWHRM1.LN40_LON_CLM_PCL LN40
			ON LN10.BF_SSN = LN40.BF_SSN
			AND LN10.LN_SEQ = LN40.LN_SEQ	
		INNER JOIN OLWHRM1.WQ20_TSK_QUE WQ20
			ON LN10.BF_SSN = WQ20.BF_SSN
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		LEFT JOIN /*Exclude loans that already have an active pending queue*/
		(
			SELECT DISTINCT
				BF_SSN
			FROM
				OLWHRM1.AY10_BR_LON_ATY
			WHERE
				PF_REQ_ACT = 'RWCLM'
				AND PF_RSP_ACT IS NULL
		) AY10
			ON AY10.BF_SSN = LN10.BF_SSN 
	WHERE 
		LN10.LA_CUR_PRI > 0
		AND LN10.LC_STA_LON10 = 'R'
		AND LN40.LD_CAN_CLM_PCL IS NULL
		AND LN40.LC_TYP_REC_CLP_LON <> '3'
		AND AY10.BF_SSN IS NULL

	FOR READ ONLY WITH UR
)
;
DISCONNECT FROM DB2;
QUIT;

PROC SORT DATA=HARD_EASY;
	BY BF_SSN LN_SEQ LN40_SEQ;
RUN;

DATA HARD_EASY;
	SET HARD_EASY;
	BY BF_SSN LN_SEQ LN40_SEQ;
	IF LAST.LN_SEQ THEN 
		OUTPUT;
RUN;
PROC SQL;
CREATE TABLE HARD1 AS
SELECT DISTINCT 
	BF_SSN, 
	ACCT, 
	LN_SEQ, 
	DATVA 
FROM 
	HARD_EASY
WHERE 
	DATVA >= today() - 10
	AND CLM_TYP NE '3';
	
CREATE TABLE HARD2 AS
SELECT DISTINCT 
	BF_SSN, 
	ACCT, 
	LN_SEQ, 
	DATVA 
FROM 
	HARD_EASY
WHERE 
	DATVA = .;
	
CREATE TABLE HARD3 AS
SELECT DISTINCT 
	BF_SSN
FROM 
	HARD_EASY
WHERE 
	QUEUE = 'GFCF'
ORDER BY 
	BF_SSN;
QUIT;

PROC SQL;
CREATE TABLE FILTERED AS
SELECT DISTINCT 
	A.BF_SSN
FROM 
	HARD1 A
	INNER JOIN HARD2 B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ NE B.LN_SEQ
	LEFT JOIN HARD3 C
		ON A.BF_SSN = C.BF_SSN
WHERE 
	C.BF_SSN IS NULL
ORDER BY 
	A.BF_SSN
;
QUIT;
ENDRSUBMIT;
DATA FILTERED;
	SET DUSTER.FILTERED;
RUN;

PROC SORT DATA=FILTERED(KEEP=BF_SSN) NODUPKEY; BY BF_SSN; RUN;

DATA _NULL_;
SET FILTERED ;
BY BF_SSN;
TARGET_ID = BF_SSN ;
ARC = 'RWCLM' ;
FROM_DATE = ' ' ;
TO_DATE = ' ' ;
NEED_DATE = ' ' ;
RECIPIENT = ' ' ;
REGARDS_CODE = ' ' ;
REGARDS_ID = ' ' ;
LN_SEQ = 'ALL' ;
COMMENTS = 'REVIEW LOAN NOT INCLUDED IN CLAIM';
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT TARGET_ID $10. ;
DO;
	PUT TARGET_ID $ @;
	PUT ARC @;
	PUT FROM_DATE @;
	PUT TO_DATE @;
	PUT NEED_DATE @;
	PUT RECIPIENT @;
	PUT REGARDS_CODE @;
	PUT REGARDS_ID @;
	PUT LN_SEQ @ ;
	PUT COMMENTS $ ;
END;
RUN;
