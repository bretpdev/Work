%LET REPORT_DATE = TODAY();
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS37.NWS37RZ";
FILENAME REPORT2 "&RPTLIB/UNWS37.NWS37R2";

/*LIBNAME SAS_TAB V8 '/sas/whse/progrevw';*/
LIBNAME SAS_TAB V8 'Y:\Development\SAS Test Files\progrevw';

DATA _NULL_;
	SET SAS_TAB.CAMPAIGNS;
	CALL SYMPUT('BEG_DATE',BEGIN_DATE);
	CALL SYMPUT('END_DATE',END_DATE);
	WHERE &REPORT_DATE BETWEEN BEGIN_DATE AND END_DATE;
	WHERE CAMPAIGN_NAME LIKE '%Late Stage Resolution';
RUN;

%SYSLPUT REPORT_DATE = &REPORT_DATE;
%SYSLPUT BEG_DATE = &BEG_DATE;
%SYSLPUT END_DATE = &END_DATE;

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=work;

PROC SQL;
      CREATE TABLE CURRENT_CAMPAIGN_BORROWERS AS
            SELECT
                  BORR.DF_SPE_ACC_ID
            FROM
                  SAS_TAB.CAMPAIGNS CAMP
                  JOIN SAS_TAB.CAMPAIGN_BORROWERS BORR
                        ON CAMP.CAMPAIGN_ID = BORR.CAMPAIGN_ID
            WHERE
                  &REPORT_DATE BETWEEN BEGIN_DATE AND END_DATE
				  AND CAMP.CAMPAIGN_NAME LIKE '%Late Stage Resolution'
      ;
QUIT;

DATA LEGEND.CURRENT_CAMPAIGN_BORROWERS; SET CURRENT_CAMPAIGN_BORROWERS; RUN;

RSUBMIT LEGEND;
/*%let DB = DNFPRQUT;  *This is test;*/
/*%let DB = DNFPRUUT;  *This is VUK3 test;*/
%LET DB = DNFPUTDL;  *This is live;

LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;

%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL ;
	CREATE TABLE S37 AS
		SELECT DISTINCT
			PD10.DF_SPE_ACC_ID,
			TRIM(PD10.DM_PRS_1)||' '||TRIM(PD10.DM_PRS_LST) AS BRW_NAME,
			PD10.DD_BRT,
			CATX(' ',PD30.DX_STR_ADR_1,PD30.DX_STR_ADR_2,PD30.DM_CT,PD30.DC_DOM_ST,PD30.DF_ZIP_CDE) AS CURRENT_ADDRESS,
/*			TRIM(PD30.DX_STR_ADR_1)||' '||TRIM(||PD30.DX_STR_ADR_2||' '||TRIM(PD30.DM_CT)||' '||TRIM(PD30.DC_DOM_ST)||' '||TRIM(PD30.DF_ZIP_CDE) AS CURRENT_ADDRESS,*/
			PD30.DM_FGN_CNY,
			PD30.DI_VLD_ADR,
			CASE
				WHEN HPHN.DN_DOM_PHN_ARA <> '' THEN TRIM(HPHN.DN_DOM_PHN_ARA)||TRIM(HPHN.DN_DOM_PHN_XCH)||TRIM(HPHN.DN_DOM_PHN_LCL)||TRIM(HPHN.DN_PHN_XTN)
				ELSE TRIM(HPHN.DN_FGN_PHN_CNY)||TRIM(HPHN.DN_FGN_PHN_CT)||TRIM(HPHN.DN_FGN_PHN_LCL)||TRIM(HPHN.DN_PHN_XTN)
			END AS HOME_PHONE,
			HPHN.DI_PHN_VLD AS HOME_PHN_VLD,
			CASE
				WHEN APHN.DN_DOM_PHN_ARA <> '' THEN TRIM(APHN.DN_DOM_PHN_ARA)||TRIM(APHN.DN_DOM_PHN_XCH)||TRIM(APHN.DN_DOM_PHN_LCL)||TRIM(APHN.DN_PHN_XTN)
				ELSE TRIM(APHN.DN_FGN_PHN_CNY)||TRIM(APHN.DN_FGN_PHN_CT)||TRIM(APHN.DN_FGN_PHN_LCL)||TRIM(APHN.DN_PHN_XTN)
			END AS ALT_PHONE,
			APHN.DI_PHN_VLD AS ALT_PHN_VLD,
			CASE
				WHEN WPHN.DN_DOM_PHN_ARA <> '' THEN TRIM(WPHN.DN_DOM_PHN_ARA)||TRIM(WPHN.DN_DOM_PHN_XCH)||TRIM(WPHN.DN_DOM_PHN_LCL)||TRIM(WPHN.DN_PHN_XTN)
				ELSE TRIM(WPHN.DN_FGN_PHN_CNY)||TRIM(WPHN.DN_FGN_PHN_CT)||TRIM(WPHN.DN_FGN_PHN_LCL)||TRIM(WPHN.DN_PHN_XTN)
			END AS WORK_PHONE,
			WPHN.DI_PHN_VLD AS WORK_PHN_VLD,
			HEML.DX_ADR_EML AS HOME_EMAIL,
			HEML.DI_VLD_ADR_EML AS HOME_EML_VLD,
			AEML.DX_ADR_EML AS ALT_EMAIL,
			AEML.DI_VLD_ADR_EML AS ALT_EML_VLD,
			WEML.DX_ADR_EML AS WORK_EMAIL,
			WEML.DI_VLD_ADR_EML AS WORK_EML_VLD,
			LN10.LF_DOE_SCL_ORG || ' ' || SC10.IM_SCL_FUL AS SCHOOL
		FROM
			CURRENT_CAMPAIGN_BORROWERS CCB
			JOIN PKUB.PD10_PRS_NME PD10
				ON CCB.DF_SPE_ACC_ID = PD10.DF_SPE_ACC_ID
			JOIN PKUB.LN10_LON LN10
				ON PD10.DF_PRS_ID = LN10.BF_SSN
				AND LN10.LC_STA_LON10 = 'R'
				AND LN10.LA_CUR_PRI > 0
				AND LN10.LC_SST_LON10 <> '5'
			JOIN PKUB.PD26_PRS_SKP_PRC PD26
				ON PD10.DF_PRS_ID = PD26.DF_PRS_ID
				AND LN10.BF_SSN = PD26.BF_SSN
				AND LN10.LN_SEQ = PD26.LN_SEQ
				AND PD26.DC_STA_SKP = '2'
			JOIN PKUB.DW01_DW_CLC_CLU DW01
				ON LN10.BF_SSN = DW01.BF_SSN
				AND LN10.LN_SEQ = DW01.LN_SEQ
				AND DW01.WC_DW_LON_STA NOT IN ('07', '13', '16', '17', '18', '19', '20', '21')
			LEFT JOIN PKUB.PD30_PRS_ADR PD30
				ON PD10.DF_PRS_ID = PD30.DF_PRS_ID
				AND PD30.DC_ADR = 'L'
			LEFT JOIN PKUB.PD40_PRS_PHN HPHN
				ON PD10.DF_PRS_ID = HPHN.DF_PRS_ID
				AND HPHN.DC_PHN = 'H'
			LEFT JOIN PKUB.PD40_PRS_PHN APHN
				ON PD10.DF_PRS_ID = APHN.DF_PRS_ID
				AND APHN.DC_PHN = 'A'
			LEFT JOIN PKUB.PD40_PRS_PHN WPHN
				ON PD10.DF_PRS_ID = WPHN.DF_PRS_ID
				AND WPHN.DC_PHN = 'W'
			LEFT JOIN PKUB.PD32_PRS_ADR_EML HEML
				ON PD10.DF_PRS_ID = HEML.DF_PRS_ID
				AND HEML.DC_ADR_EML = 'H'
				AND HEML.DC_STA_PD32 = 'A'
			LEFT JOIN PKUB.PD32_PRS_ADR_EML AEML
				ON PD10.DF_PRS_ID = AEML.DF_PRS_ID
				AND AEML.DC_ADR_EML = 'A'
				AND AEML.DC_STA_PD32 = 'A'
			LEFT JOIN PKUB.PD32_PRS_ADR_EML WEML
				ON PD10.DF_PRS_ID = WEML.DF_PRS_ID
				AND WEML.DC_ADR_EML = 'W'
				AND WEML.DC_STA_PD32 = 'A'
			JOIN PKUB.SC10_SCH_DMO SC10
				ON LN10.LF_DOE_SCL_ORG = SC10.IF_DOE_SCL
		ORDER BY
			DF_SPE_ACC_ID
	;
QUIT;

ENDRSUBMIT;

DATA S37; SET LEGEND.S37; RUN;

DATA _NULL_ ;
	SET	S37;
	FILE REPORT2 DELIMITER=',' DSD DROPOVER lrecl=32767;
	FORMAT DD_BRT MMDDYY10.;
	BY DF_SPE_ACC_ID;
	LENGTH SCHOOLS $500;
	RETAIN SCHOOLS;

	IF _N_ = 1 THEN PUT 
		'Borrower Account Number,'
		'Borrower Name,'
		'Date of Birth,'
		'Current Address,'
		'Current Foreign Country,'
		'Address Validity,'
		'Current Home Phone,'
		'Home Phone Validity,'
		'Current Alt Phone,'
		'Alt Phone Validity,'
		'Current Work Phone,'
		'Work Phone Validity,'
		'Current Home Email,'
		'Home Email Validity,'
		'Current Alt Email,'
		'Alt Email Validity,'
		'Current Work Email,'
		'Work Email Validity,'
		'School'
	;
	IF FIRST.DF_SPE_ACC_ID THEN
		DO;
			PUT DF_SPE_ACC_ID $ @;
			PUT BRW_NAME $ @;
			PUT DD_BRT $ @;
			PUT CURRENT_ADDRESS $ @;
			PUT DM_FGN_CNY $ @;
			PUT DI_VLD_ADR $ @;
			PUT HOME_PHONE $ @;
			PUT HOME_PHN_VLD $ @;
			PUT ALT_PHONE $ @;
			PUT ALT_PHN_VLD $ @;
			PUT WORK_PHONE $ @;
			PUT WORK_PHN_VLD $ @;
			PUT HOME_EMAIL $ @;
			PUT HOME_EML_VLD $ @;
			PUT ALT_EMAIL $ @;
			PUT ALT_EML_VLD $ @;
			PUT WORK_EMAIL $ @;
			PUT WORK_EML_VLD $ @;
			SCHOOLS = SCHOOL;
		END;
	ELSE SCHOOLS = CATX('',SCHOOLS,SCHOOL);

	IF LAST.DF_SPE_ACC_ID THEN PUT SCHOOLS $;
RUN;
