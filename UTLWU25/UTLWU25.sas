*---------------------------------------------*
| UTLWU25 Key Loan Identifier Audit Procedure |
*---------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWU25.LWU25RZ";
FILENAME REPORT2 "&RPTLIB/ULWU25.LWU25R2";
FILENAME REPORT3 "&RPTLIB/ULWU25.LWU25R3";
FILENAME REPORT4 "&RPTLIB/ULWU25.LWU25R4";
DATA _NULL_;
	CALL SYMPUT('CDATE',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;
%SYSLPUT CDATE = &CDATE;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK ;
  %IF &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN;
  %END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE COMPSN AS
SELECT *
FROM CONNECTION TO DB2 ( 
SELECT A.DF_PRS_ID_NEW  AS NSSN /*COMPASS QUERY*/
	,A.DF_PRS_ID_OLD  AS OSSN	
	,A.DF_USR_ID_CHG_REQ AS UID	
	,DATE(A.DF_LST_DTS_PI10) AS DATE
	,CASE 
	 	WHEN A.DF_USR_ID_CHG_REQ IS NOT NULL
	 	THEN 'COMPASS'
	 END AS SYSTEM
FROM OLWHRM1.PI10_PID_CHG_LOG A
WHERE DATE(A.DF_LST_DTS_PI10) = &CDATE
);
CREATE TABLE OLSN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.DF_CRR_PRS AS NSSN /*ONELINK QUERY*/
	,B.DF_IRR_PRS AS OSSN
	,B.DF_USR_VER AS UID
	,DATE(B.DF_ACT_DTS_PI01) AS DATE
	,CASE 
		 WHEN B.DF_USR_VER IS NOT NULL
		 THEN 'ONELINK'
	 END AS SYSTEM
FROM OLWHRM1.PI01_PID_CHG_INF B
WHERE DATE(B.DF_ACT_DTS_PI01) = &CDATE
);

CREATE TABLE NEWBD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT	C.DF_SPE_ACC_ID AS ACCT 
	,B.DD_BRT_HST AS OLD_BD
	,C.DD_BRT AS NEW_BD
	,DATE(B.DF_LST_DTS_PD05) AS DATE
	,B.DF_LST_USR_PD05 AS USERID
FROM OLWHRM1.PD05_PRS_NME_HST B
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON B.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.GA01_APP D
	ON B.DF_PRS_ID = D.DF_PRS_ID_BR
INNER JOIN OLWHRM1.GA10_LON_APP E
	ON E.AF_APL_ID = D.AF_APL_ID
	AND E.AC_PRC_STA = 'A'
WHERE DATE(B.DF_LST_DTS_PD05) = &CDATE
AND (B.DD_BRT_HST <> C.DD_BRT OR (B.DD_BRT_HST IS NULL AND C.DD_BRT IS NOT NULL))
);

CREATE TABLE NEWNM AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_SPE_ACC_ID AS ACCT 
	,RTRIM(B.DM_PRS_1_HST) || ' ' || RTRIM(B.DM_PRS_MID_HST) || ' ' || RTRIM(B.DM_PRS_LST_HST) AS OLD_NAME
	,RTRIM(C.DM_PRS_1) || ' ' || RTRIM(C.DM_PRS_MID) || ' ' || RTRIM(C.DM_PRS_LST) AS NEW_NAME
	,DATE(B.DF_LST_DTS_PD05) AS DATE
	,B.DF_LST_USR_PD05 AS USERID
FROM OLWHRM1.PD05_PRS_NME_HST B
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON B.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.GA01_APP D
	ON B.DF_PRS_ID = D.DF_PRS_ID_BR
INNER JOIN OLWHRM1.GA10_LON_APP E
	ON E.AF_APL_ID = D.AF_APL_ID
	AND E.AC_PRC_STA = 'A'
WHERE DATE(B.DF_LST_DTS_PD05) = &CDATE
AND B.DM_PRS_1_HST || ' ' || B.DM_PRS_MID_HST || ' ' || B.DM_PRS_LST_HST <> C.DM_PRS_1 || ' ' || C.DM_PRS_MID || ' ' || C.DM_PRS_LST
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK;*/
/*QUIT;*/
DATA SSNCHNG;
SET COMPSN OLSN;
RUN;
ENDRSUBMIT;
DATA SSNCHNG;SET WORKLOCL.SSNCHNG;RUN;
DATA NEWBD;SET WORKLOCL.NEWBD;RUN;
DATA NEWNM;SET WORKLOCL.NEWNM;RUN;

DATA SSNCHNG;
SET SSNCHNG;
FORMAT DATE MMDDYY10.;
IF UID = 'LPX2M' THEN DELETE;
RUN;
PROC SORT DATA=SSNCHNG;BY DATE SYSTEM NSSN;RUN;
PROC SORT DATA=NEWBD;BY DATE USERID ACCT;RUN;
PROC SORT DATA=NEWNM;BY DATE USERID ACCT;RUN;
DATA _NULL_;
SET SSNCHNG ;
LENGTH DESCRIPTION $600.;
USER = UID ;
ACT_DT = DATE ;
DESCRIPTION = CATX(',',
		NSSN
		,OSSN
		,UID
		,SYSTEM
		,PUT(DATE,MMDDYY10.)
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;

DATA _NULL_;
SET NEWNM ;
LENGTH DESCRIPTION $600.;
USER = USERID ;
ACT_DT = DATE ;
DESCRIPTION = CATX(',',
		ACCT
		,OLD_NAME
		,NEW_NAME
);
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;

DATA _NULL_;
SET NEWBD ;
LENGTH DESCRIPTION $600.;
USER = USERID ;
ACT_DT = DATE ;
DESCRIPTION = CATX(',',
		ACCT
		,PUT(OLD_BD,MMDDYY10.)
		,PUT(NEW_BD,MMDDYY10.)
);
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
