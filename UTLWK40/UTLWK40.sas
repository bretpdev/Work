/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWK40.LWK40RZ";
FILENAME REPORT2 "&RPTLIB/ULWK40.LWK40R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;

/*IF A BORROWER HAS HAD A TYPE 7 BANKRUPTCY*/
/*THEY SHOULD BE EXCLUDED FROM THE REPORT*/
/*UNLESS THEY HAVE HAD EITHER A DEATH OR*/
/*HANDICAP CLAIM MORE RECENTLY*/

PROC SQL;
CREATE TABLE EXC AS
SELECT DF_PRS_ID
	,DD_BKR_NTF AS NOTIFIED
	,DC_BKR_TYP AS TYPE
FROM DLGSUTWH.PD24_PRS_BKR
UNION
SELECT DF_PRS_ID
	,DD_DTH_NTF AS NOTIFIED
	,'00' AS TYPE
FROM DLGSUTWH.PD20_PRS_DTH
UNION 
SELECT DF_PRS_ID
	,DD_DSA_RPT AS NOTIFIED
	,'00' AS TYPE
FROM DLGSUTWH.PD23_GTR_DSA
;
QUIT;
PROC SORT DATA=EXC; BY DF_PRS_ID NOTIFIED; RUN;
DATA EXC(KEEP=DF_PRS_ID);
SET EXC;
BY DF_PRS_ID;
IF LAST.DF_PRS_ID AND TYPE = '07' THEN OUTPUT;
RUN;

PROC SQL;
CREATE TABLE DEMO AS
SELECT DISTINCT D.DF_PRS_ID
	,A.LN_SEQ
FROM DLGSUTWH.PD10_PRS_NME D
INNER JOIN DLGSUTWH.LN10_LON A
	ON D.DF_PRS_ID = A.BF_SSN
INNER JOIN DLGSUTWH.DW01_DW_CLC_CLU B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN DLGSUTWH.LN40_LON_CLM_PCL C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN EXC E
	ON D.DF_PRS_ID = E.DF_PRS_ID
WHERE A.LA_CUR_PRI > 0
AND A.LC_STA_LON10 = 'R'
	AND A.LF_LON_CUR_OWN = '834529'
	AND (B.WC_DW_LON_STA IN ('17','19','21')
		OR (B.WC_DW_LON_STA IN ('07','08') 
		AND LC_REA_CLP_LON IN ('00','04','05')))
	AND E.DF_PRS_ID IS NULL;

QUIT;

ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;
PROC SORT DATA=DEMO;
	BY DF_PRS_ID;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
/*FOR PORTRAIT REPORTS;*/
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=96;
TITLE 'Conduit Redemptions for Specialty Claim Reasons';
TITLE2	"RUNDATE &SYSDATE9.";
FOOTNOTE1   'JOB = UTLWK40  	 REPORT = ULWK40.LWK40R2';

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
VAR DF_PRS_ID LN_SEQ;
LABEL DF_PRS_ID = 'SSN'
	LN_SEQ = 'LN SEQ';
RUN;

PROC PRINTTO;
RUN;
