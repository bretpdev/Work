/*===============================================*/
/*UTLWO25 CONSOLIDATION LOAN INTEREST RATE REVIEW*/
/*===============================================*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWO25.LWO25R2";*/

DATA _NULL_;
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYS10.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	FILENAME REPORTZ "&RPTLIB/&SQLRPT";
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT PD10.DF_SPE_ACC_ID
	,LN10.BF_SSN
	,LN10.LN_SEQ  
	,LN10.IC_LON_PGM
	,LN10.LA_CUR_PRI
	,LN10.LD_LON_1_DSB
	,LN72.LD_ITR_EFF_BEG
	,LN72.LD_ITR_EFF_END
	,LN72.LR_ITR	
	,LN72.LR_INT_RDC_PGM_ORG
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.LN72_INT_RTE_HST LN72
	ON LN10.BF_SSN = LN72.BF_SSN
	AND LN10.LN_SEQ = LN72.LN_SEQ
WHERE LN72.LC_STA_LON72 = 'A' 
AND LN10.BF_SSN IN 
		(SELECT DISTINCT X.BF_SSN 
		 FROM OLWHRM1.LN10_LON X
		 WHERE X.IC_LON_PGM IN 
			('SUBCNS','UNCNS','SUBSPC','UNSPC')
		 AND X.LA_CUR_PRI > 0
		 AND X.LC_STA_LON10 = 'R'
		 AND (
				NOT EXISTS
					(SELECT * 
					 FROM OLWHRM1.AY10_BR_LON_ATY Z
					 /*INNER JOIN OLWHRM1.LN85_LON_ATY Y*/
/*					 	ON Y.BF_SSN = Z.BF_SSN*/
/*						AND Y.LN_ATY_SEQ = Z.LN_ATY_SEQ*/
					 WHERE Z.BF_SSN = X.BF_SSN
					 AND Z.PF_REQ_ACT = 'OC319'
					 AND Z.LC_STA_ACTY10 = 'A')
			  OR
			  	NOT EXISTS
					(SELECT *
					 FROM OLWHRM1.LN10_LON Q
/*					 INNER JOIN OLWHRM1.LN85_LON_ATY Y*/
/*					 	ON Q.BF_SSN = Y.BF_SSN*/
/*						AND Q.LN_SEQ = Y.LN_SEQ*/
					 WHERE Q.BF_SSN = X.BF_SSN
					 AND Q.LD_LON_1_DSB <
					 		(SELECT MAX(Z.LD_REQ_RSP_ATY_PRF)
							 FROM OLWHRM1.AY10_BR_LON_ATY Z
							 WHERE Q.BF_SSN = Z.BF_SSN
/*							 AND Y.LN_ATY_SEQ = Z.LN_ATY_SEQ*/
							 AND Z.PF_REQ_ACT = 'OC319'
					 		 AND Z.LC_STA_ACTY10 = 'A')
					  )
				)
		   )
);

CREATE TABLE LCO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT PD10.DF_SPE_ACC_ID
	,LC10.DF_LCO_PRS_SSN_BR AS BF_SSN
	,SUBSTR(LF_UND_LN_ACC_NUM,8,2) AS LNSEQ 
	,LC10.LR_UND_LN_INT
	,AP1A.AD_LCO_APL_RCV
	,LC10.LC_UND_LN_PGM
	,LC10.LA_UND_LN_BAL
	,LC10.LA_UND_LN_ACL_PAY
	,AP1A.AD_LCO_APL_DSB
	,AP1A.AN_LCO_APL_SEQ
FROM OLWHRM1.LC10_UND_LN_INF LC10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LC10.DF_LCO_PRS_SSN_BR = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.AP1A_LCO_APL AP1A
	ON AP1A.DF_LCO_PRS_SSN_BR = LC10.DF_LCO_PRS_SSN_BR	
	AND AP1A.AN_LCO_APL_SEQ = LC10.AN_LCO_APL_SEQ
WHERE LC10.LI_UND_LN_CON = 'Y'
AND LC10.LC_UND_LN_PGM IN 
		('STFFRD','UNSTFD','PLUS')
AND SUBSTR(LF_UND_LN_ACC_NUM,1,4) = 'L0UT'
);
DISCONNECT FROM DB2;

/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO25.LWO25RZ);*/
/*QUIT;*/

PROC SORT DATA=LS;
BY BF_SSN LN_SEQ;
RUN;

DATA LCO (DROP=LNSEQ);
SET LCO;
FORMAT LN_SEQ 6.;
LN_SEQ = INPUT(LNSEQ,2.);
RUN;

PROC SQL;
CREATE TABLE APDP AS 
SELECT DISTINCT A.BF_SSN
	,A.AN_LCO_APL_SEQ,A.DF_SPE_ACC_ID
FROM LCO A
INNER JOIN LS B
	ON A.BF_SSN = B.BF_SSN
	AND A.AD_LCO_APL_DSB = B.LD_LON_1_DSB
WHERE B.LA_CUR_PRI > 0;
QUIT;
RUN;

PROC SORT DATA=APDP;BY BF_SSN AN_LCO_APL_SEQ;RUN;
PROC SORT DATA=LCO;BY BF_SSN AN_LCO_APL_SEQ;RUN;

DATA LCO;
MERGE LCO (IN=A) APDP (IN=B);
BY BF_SSN AN_LCO_APL_SEQ DF_SPE_ACC_ID;
IF B;
RUN; 

PROC SORT DATA=LS;BY BF_SSN LN_SEQ;RUN;
PROC SORT DATA=LCO;BY BF_SSN LN_SEQ;RUN;

PROC SQL;
CREATE TABLE MSTR AS 
SELECT DISTINCT A.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.LN_SEQ
	,A.LD_ITR_EFF_BEG
	,A.LD_ITR_EFF_END
	,A.LA_CUR_PRI
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,B.AD_LCO_APL_DSB
	,B.LR_UND_LN_INT
	,B.AD_LCO_APL_RCV	
	,B.LC_UND_LN_PGM
	,B.LA_UND_LN_BAL
	,B.LA_UND_LN_ACL_PAY
	,A.LR_INT_RDC_PGM_ORG
FROM LS A
INNER JOIN LCO B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
/*WHERE B.AD_LCO_APL_RCV BETWEEN A.LD_ITR_EFF_BEG AND A.LD_ITR_EFF_END;*/
;
QUIT;
RUN;

DATA MSTR2;
SET MSTR;
IF LR_INT_RDC_PGM_ORG EQ LR_UND_LN_INT THEN OUTPUT;
ELSE DELETE;
RUN;

PROC SORT DATA=MSTR;BY BF_SSN LN_SEQ;RUN;
PROC SORT DATA=MSTR2;BY BF_SSN LN_SEQ;RUN;

DATA MSTR;
MERGE MSTR (IN=A) MSTR2 (IN=B);
BY BF_SSN LN_SEQ;
IF A AND NOT B;
RUN;

DATA MSTR;
SET MSTR;
WHERE AD_LCO_APL_RCV >= LD_ITR_EFF_BEG AND AD_LCO_APL_RCV <= LD_ITR_EFF_END;
RUN;

PROC SORT DATA=MSTR;BY DF_SPE_ACC_ID LN_SEQ;RUN;

ENDRSUBMIT;

DATA MSTR;
SET WORKLOCL.MSTR;
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 CENTER PAGENO=1;
TITLE 'CONSOLIDATION LOAN INTEREST RATE QC REPORT';
TITLE2	"RUNDATE &RUNDT";
FOOTNOTE 'JOB = UTLWO25  	 REPORT = ULWO25.LWO25R2';
PROC CONTENTS DATA=MSTR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 127*'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT /////////////
		@46 "JOB = UTLWO25  	 REPORT = ULWO25.LWO25R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=MSTR;
VAR DF_SPE_ACC_ID LC_UND_LN_PGM LR_UND_LN_INT LR_INT_RDC_PGM_ORG LA_UND_LN_BAL LN_SEQ LA_UND_LN_ACL_PAY;
LABEL DF_SPE_ACC_ID = "ACCOUNT/NUMBER" 
	LC_UND_LN_PGM = "LCO/UNDERLYING/LOAN/PROGRAM" 
	LR_UND_LN_INT = "LCO/INTEREST/RATE"
	LR_INT_RDC_PGM_ORG = "COMPASS/INTEREST/RATE" 
	LA_UND_LN_BAL = "LCO/CURRENT/BALANCE" 
	LN_SEQ = "LOAN SEQUENCE" 
	LA_UND_LN_ACL_PAY = "LCO/ACTUAL/DISBURSEMENT/AMOUNT";
RUN;
