/*UTLWA06 - TOTALS FOR CONSOLIDATION BORROWERS, LOANS, AND AMOUNTS*/
/*FOR LOANS SERVICED BY UHEAA*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*FILENAME REPORT2 "&RPTLIB/ULWA06.LWA06R2";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

OPTIONS SYMBOLGEN;

DATA _NULL_;
     BEGDATE = PUT(INTNX('MONTH',TODAY(),-1), YYMMDD10.);	/*RESOLVES TO 1ST OF PRIOR MONTH*/
     ENDDATE = PUT(INTNX('MONTH',TODAY(),0)-1 , YYMMDD10.);	/*RESOLVES TO END OF PRIOR MONTH*/
     CALL SYMPUT('BEGIN',"'"||BEGDATE||"'");	            /*CREATES MACRO VARIABLE WITH IN FORMAT  'YYYY/MM/DD'*/
     CALL SYMPUT('END',"'"||ENDDATE||"'");              	/* WILL BE USED AS REPLACEMENTS IN CODE*/
RUN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CLTOT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	COUNT(DISTINCT A.BF_SSN)	AS CNT_SSN
		,COUNT(A.LN_SEQ) 			AS CNT_LN
		,SUM(COALESCE(A.LA_FAT_NSI,0) + COALESCE(A.LA_FAT_LTE_FEE,0) + COALESCE(A.LA_FAT_CUR_PRI,0)) * -1	AS PMTS
/*======FOR TESTING=======*/
/*SELECT	A.BF_SSN*/
/*		,A.LN_SEQ*/
/*		,COALESCE(A.LA_FAT_NSI,0) + COALESCE(A.LA_FAT_LTE_FEE,0) + COALESCE(A.LA_FAT_CUR_PRI,0) AS LA_CON_PMTS*/
/*========================*/
		,A.PC_FAT_SUB_TYP
FROM	OLWHRM1.LN90_FIN_ATY A
		INNER JOIN OLWHRM1.LN10_LON B ON
			A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
			AND B.LF_LON_CUR_OWN = '828476'
			AND A.LC_STA_LON90 = 'A'
			AND A.LC_FAT_REV_REA = ''
			AND A.PC_FAT_TYP = '10'
			AND (A.PC_FAT_SUB_TYP = '70' OR A.PC_FAT_SUB_TYP = '80')
			AND A.LD_FAT_PST BETWEEN &BEGIN AND &END
	GROUP BY A.PC_FAT_SUB_TYP
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA CLTOT;
SET WORKLOCL.CLTOT;
RUN;

DATA CLTOT;
SET CLTOT;
IF PC_FAT_SUB_TYP = '70' THEN PMT_SRC = 'EXTERNAL';
ELSE PMT_SRC = 'INTERNAL';
RUN;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',TRIM(LEFT(UPCASE(EFFMO||' '||EFFYR))));
RUN;

/*PROC PRINTTO PRINT=REPORT2;*/
/*RUN;*/
OPTIONS CENTER PAGENO=1 ORIENTATION=PORTRAIT ;
OPTIONS LS=96 PS=52;

PROC PRINT NOOBS SPLIT='/' DATA=CLTOT WIDTH=UNIFORM WIDTH=MIN;

VAR 	PMT_SRC
		PC_FAT_SUB_TYP
		CNT_SSN
		CNT_LN
		PMTS;
FORMAT	CNT_SSN COMMA.;
FORMAT	LN_SSN COMMA.;
FORMAT	PMTS DOLLAR15.2;
LABEL	PMT_SRC = 'PAYMENT SOURCE'
		PC_FAT_SUB_TYP = 'TRANSACTION SUB TYPE'
		CNT_SSN = 'BORROWERS'
		CNT_LN = 'LOANS'
		PMTS = 'TOTAL AMOUNT';
TITLE	'CONSOLIDATION PAYMENTS RECEIVED';
TITLE2	"FOR THE MONTH OF &EFFDATE";
FOOTNOTE  'JOB = UTLWA06     REPORT = ULWA06.LWA06R2';
RUN;
