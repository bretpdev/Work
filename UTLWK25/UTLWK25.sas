*---------------------------------*
| UTLWK25 Super Reference Letters |
*---------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET TABLIB = /sas/whse/progrevw;*/
%LET RPTLIB = T:\SAS;
%LET TABLIB = Q:\Process Automation\TabSAS;
FILENAME REPORT2 "&RPTLIB/ULWK25.LWK25R2";
FILENAME REPORT3 "&RPTLIB/ULWK25.LWK25R3";
FILENAME REPORT4 "&RPTLIB/ULWK25.LWK25R4";
FILENAME REPORTZ "&RPTLIB/ULWK25.LWK25RZ";
/*************************************************************************************
* NOTE: THE .txt FILE THAT THIS DATA STEP READS NEEDS TO FOLLOW THIS CONVENTION
* A1 VALUE, A2 VALUE, A3 VALUE, B1 VALUE, B2 VALUE, B3 VALUE, C1 VALUE, C2 VALUE, C3 VALUE
* WHERE A1,A2,A3 ARE VALUES FOR R2 - B1,B2,B3 ARE VALUES FOR R3 AND C1,C2,C3 ARE VALUES FOR R4
* A BREAKDOWN OF WHAT THESE VALUES REPRESENTS IS LISTED BELOW
* A1, B1, C1 - LAST REFERENCE CONTACT DATE FOR RESPECTIVE FILE
* A2, B2, C2 - LAST REFERENCE ATTEMPT DATE FOR RESPECTIVE FILE
* A3, B3, C3 - ACTION CODE DATE FOR EXCLUSION FOR RESPECTIVE FILE 
**************************************************************************************/
DATA DATE_VALS;
INFILE "&TABLIB/SUPER_REF_LTRS_DVAL.txt" DLM=',' MISSOVER ;
INPUT A1 A2 A3 B1 B2 B3 C1 C2 C3;
RUN;
/********************************************
* CREATE MACRO VARIALBES FOR FILE PROCESSING
********************************************/
DATA _NULL_;
SET DATE_VALS;
/*SET VALUES FOR REPORT2*/
    CALL SYMPUT('R2_DT1',INTNX('DAY',TODAY(),-A1,'BEGINNING'));
	CALL SYMPUT('R2_DT2',INTNX('DAY',TODAY(),-A2,'BEGINNING'));
	CALL SYMPUT('R2_DT3',INTNX('DAY',TODAY(),-A3,'BEGINNING'));
/*SET VALUES FOR REPORT3*/
	CALL SYMPUT('R3_DT1',INTNX('DAY',TODAY(),-B1,'BEGINNING'));
	CALL SYMPUT('R3_DT2',INTNX('DAY',TODAY(),-B2,'BEGINNING'));
	CALL SYMPUT('R3_DT3',INTNX('DAY',TODAY(),-B3,'BEGINNING'));
/*SET VALUES FOR REPORT4*/
	CALL SYMPUT('R4_DT1',INTNX('DAY',TODAY(),-C1,'BEGINNING'));
	CALL SYMPUT('R4_DT2',INTNX('DAY',TODAY(),-C2,'BEGINNING'));
	CALL SYMPUT('R4_DT3',INTNX('DAY',TODAY(),-C3,'BEGINNING'));
RUN;

%SYSLPUT  R2_DT1= &R2_DT1;
%SYSLPUT  R2_DT2= &R2_DT2;
%SYSLPUT  R2_DT3= &R2_DT3;
%SYSLPUT  R3_DT1= &R3_DT1;
%SYSLPUT  R3_DT2= &R3_DT2;
%SYSLPUT  R3_DT3= &R3_DT3;
%SYSLPUT  R4_DT1= &R4_DT1;
%SYSLPUT  R4_DT2= &R4_DT2;
%SYSLPUT  R4_DT3= &R4_DT3;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FL1_A AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT LN10.BF_SSN
	,CASE 
		WHEN CO_SKP.DF_PRS_ID IS NULL THEN 'N'
		ELSE 'Y'
	END AS COMP_SKP
FROM OLWHRM1.LN10_LON LN10
LEFT OUTER JOIN (
		SELECT DF_PRS_ID 
		FROM OLWHRM1.PD30_PRS_ADR 
		WHERE DI_VLD_ADR = 'N'
		AND DC_ADR = 'L'
	UNION
		SELECT DF_PRS_ID
		FROM OLWHRM1.PD42_PRS_PHN
		WHERE DI_PHN_VLD = 'N'
		AND DC_PHN = 'H'
	) CO_SKP
	ON LN10.BF_SSN = CO_SKP.DF_PRS_ID
WHERE LN10.LA_CUR_PRI > 0
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.LN10_LON X
	WHERE X.BF_SSN = LN10.BF_SSN
		AND X.LI_CON_PAY_STP_PUR = 'Y'
		AND X.LC_STA_LON10 = 'R'
	)
	AND LN10.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);

CREATE TABLE FL2_A AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT GA01.DF_PRS_ID_BR
,DC01_A.STA_03
,DC01_B.STA_03_ONLY
,GA10.OL_BAL
FROM OLWHRM1.GA01_APP GA01 
INNER JOIN (
	SELECT DF_PRS_ID 
	FROM OLWHRM1.PD03_PRS_ADR_PHN 
	WHERE DC_ADR = 'L'
	AND (DI_VLD_ADR = 'N' OR 
		 DI_PHN_VLD = 'N')
	) OL_SKP
	ON GA01.DF_PRS_ID_BR = OL_SKP.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,'Y' AS STA_03
	FROM OLWHRM1.DC01_LON_CLM_INF
	WHERE LC_STA_DC10 = '03'
	AND LD_CLM_ASN_DOE IS NULL
	AND LC_AUX_STA = ''
	) DC01_A
	ON GA01.DF_PRS_ID_BR = DC01_A.BF_SSN
LEFT OUTER JOIN (
	SELECT DISTINCT GX.DF_PRS_ID_BR
		,'Y' AS OL_BAL 
	FROM OLWHRM1.GA01_APP GX 
	INNER JOIN OLWHRM1.GA10_LON_APP GY
		ON GX.AF_APL_ID = GY.AF_APL_ID
	WHERE GY.AA_CUR_PRI > 0
	) GA10
	ON GA01.DF_PRS_ID_BR = GA10.DF_PRS_ID_BR
LEFT OUTER JOIN (
	SELECT BF_SSN
		,'Y' AS STA_03_ONLY
	FROM OLWHRM1.DC01_LON_CLM_INF
	WHERE LC_STA_DC10 = '03'
	) DC01_B
	ON GA01.DF_PRS_ID_BR = DC01_B.BF_SSN
FOR READ ONLY WITH UR
);

CREATE TABLE BR03 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT BR03.DF_PRS_ID_BR
	,BR03.DF_PRS_ID_RFR
	,BR03.BD_RFR_LST_CNC_HME 
	,BR03.BD_RFR_LST_ATT_HME 
FROM OLWHRM1.BR03_BR_REF BR03
INNER JOIN (
	SELECT DISTINCT A.DF_PRS_ID_BR
	FROM OLWHRM1.GA01_APP A
	INNER JOIN OLWHRM1.GA10_LON_APP B
		ON A.AF_APL_ID = B.AF_APL_ID
	INNER JOIN OLWHRM1.GA14_LON_STA C
		ON B.AF_APL_ID = C.AF_APL_ID
		AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
	WHERE C.AC_LON_STA_TYP IN (
		'CR','DA','FB','IA','ID','IG','IM',
		'RP','CP','UA','UB','UC','UD','UI'		
		)
	AND C.AC_STA_GA14 = 'A'
	) GA14
	ON GA14.DF_PRS_ID_BR = BR03.DF_PRS_ID_BR
WHERE BR03.BC_STA_BR03 = 'A'
AND BR03.BC_HME_CNC_RSL != '04'
AND BR03.BI_VLD_ADR = 'Y'
AND BX_RFR_STR_ADR_1 IS NOT NULL
FOR READ ONLY WITH UR
);

CREATE TABLE EXC AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT DISTINCT DF_PRS_ID_TGT 
		,LD_LST_CNC AS BD_ATY_PRF /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
		,'CQ' AS EX_TYP
	FROM OLWHRM1.CT30_CALL_QUE
	WHERE IF_WRK_GRP = 'KREFRLTR'
UNION
	SELECT DISTINCT DF_PRS_ID AS DF_PRS_ID_TGT
		,BD_ATY_PRF
		,'AC' AS EX_TYP
	FROM OLWHRM1.AY01_BR_ATY
	WHERE PF_ACT IN (
		'KLREF','KRREF','KABL2'
		)
UNION
	SELECT DISTINCT DF_PRS_ID AS DF_PRS_ID_TGT
		,DD_SKP_TRC_EFF AS BD_ATY_PRF /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
		,'ST' AS EX_TYP
	FROM OLWHRM1.PD01_PDM_INF
	WHERE DC_SKP_TRC_STA = 'S'
	AND DAYS(DD_SKP_TRC_EFF) > DAYS(CURRENT DATE) - 29
UNION
	SELECT DISTINCT BF_SSN AS DF_PRS_ID_TGT
		,LD_ATY_REQ_RCV AS BD_ATY_PRF /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
		,'CA' AS EX_TYP
	FROM OLWHRM1.AY10_BR_LON_ATY
	WHERE PF_REQ_ACT = 'SKPME'
	AND LC_STA_ACTY10 = 'A'
	AND DAYS(LD_ATY_REQ_RCV) > DAYS(CURRENT DATE) - 7
UNION
	SELECT X.DF_PRS_ID AS DF_PRS_ID_TGT
		,X.DD_VER_ADR AS BD_ATY_PRF /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
		,'PD' AS EX_TYP
	FROM OLWHRM1.PD30_PRS_ADR X
	INNER JOIN OLWHRM1.PD42_PRS_PHN Y
		ON X.DF_PRS_ID = Y.DF_PRS_ID
	WHERE X.DI_VLD_ADR = 'Y'
	AND X.DC_ADR = 'L'
	AND Y.DI_PHN_VLD = 'N'
	AND Y.DC_PHN = 'H'
	AND NOT EXISTS (
		SELECT *
		FROM OLWHRM1.LN16_LON_DLQ_HST Z
		WHERE Z.BF_SSN = X.DF_PRS_ID
		AND Z.LC_STA_LON16 = '1'
		AND DAYS(CURRENT DATE) - DAYS(Z.LD_DLQ_OCC) > 1	
		) 
UNION
	SELECT BF_SSN AS DF_PRS_ID_TGT
		,DATE(CURRENT DATE) AS BD_ATY_PRF /*THIS DATE IS SUPERFLUOUS - IT IS ONLY USED TO SUPPORT THE UNION*/
		,'CS' AS EX_TYP
	FROM OLWHRM1.DW01_DW_CLC_CLU
	WHERE WC_DW_LON_STA IN ('17','21')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWK25.LWK25RZ);*/
/*QUIT;*/

/*CREATE DATA FOR REPORT2*/
PROC SQL;
CREATE TABLE DUCE AS
SELECT DISTINCT A.DF_PRS_ID_BR
	,A.DF_PRS_ID_RFR
	,'2' AS FILE_IND
FROM BR03 A 
INNER JOIN FL1_A B
	ON A.DF_PRS_ID_BR = B.BF_SSN
WHERE B.COMP_SKP = 'Y'
AND A.BD_RFR_LST_CNC_HME < &R2_DT1
AND A.BD_RFR_LST_ATT_HME < &R2_DT2
AND A.DF_PRS_ID_RFR NOT IN (
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'CQ'
	UNION
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'AC'
		AND BD_ATY_PRF >= &R2_DT3
		)
AND A.DF_PRS_ID_BR NOT IN (
	SELECT DISTINCT DF_PRS_ID_TGT 
	FROM EXC
	WHERE EX_TYP IN ('ST','CA','PD','CS')
	)
;
QUIT;

/*CREATE DATA FOR REPORT3*/
PROC SQL;
CREATE TABLE TRIPLE AS 
SELECT DISTINCT A.DF_PRS_ID_BR
	,A.DF_PRS_ID_RFR
	,'3' AS FILE_IND
FROM BR03 A
INNER JOIN FL2_A B
	ON A.DF_PRS_ID_BR = B.DF_PRS_ID_BR
WHERE B.STA_03 = 'Y'
AND A.BD_RFR_LST_CNC_HME < &R3_DT1
AND A.BD_RFR_LST_ATT_HME < &R3_DT2
AND B.DF_PRS_ID_BR NOT IN (
		SELECT BF_SSN AS DF_PRS_ID_TGT 
		FROM FL1_A
	UNION
		SELECT DISTINCT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP IN ('ST','CS')
	)
AND A.DF_PRS_ID_RFR NOT IN (
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'CQ'
	UNION
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'AC'
		AND BD_ATY_PRF >= &R3_DT3
	)
;
QUIT;

/*CREATE DATA FOR REPORT4*/
PROC SQL;
CREATE TABLE QUAD AS 
SELECT DISTINCT A.DF_PRS_ID_BR
	,A.DF_PRS_ID_RFR
	,'4' AS FILE_IND
FROM BR03 A
INNER JOIN FL2_A B
	ON A.DF_PRS_ID_BR = B.DF_PRS_ID_BR
WHERE B.STA_03_ONLY = ''
AND B.OL_BAL = 'Y'
AND A.BD_RFR_LST_CNC_HME < &R4_DT1
AND A.BD_RFR_LST_ATT_HME < &R4_DT2
AND B.DF_PRS_ID_BR NOT IN (
		SELECT BF_SSN AS DF_PRS_ID_TGT
		FROM FL1_A
	UNION
		SELECT DISTINCT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP IN ('ST','CS')
	)
AND A.DF_PRS_ID_RFR NOT IN (
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'CQ'
	UNION
		SELECT DF_PRS_ID_TGT 
		FROM EXC
		WHERE EX_TYP = 'AC'
		AND BD_ATY_PRF >= &R4_DT3
	)
;
QUIT;

DATA SUPERC;
SET DUCE TRIPLE QUAD;
RUN;
ENDRSUBMIT;

DATA SUPERC;
SET WORKLOCL.SUPERC;
RUN;
/*************************
* VARIABLE INITIALIZATION
**************************/
DATA SUPERC (KEEP=TARGET_ID QUEUE_NAME INSTITUTION_ID INSTITUTION_TYPE DATE_DUE TIME_DUE COMMENTS FILE_IND);
SET SUPERC ;
LENGTH COMMENTS $ 600.;
TARGET_ID = DF_PRS_ID_RFR;
QUEUE_NAME = 'KREFRLTR';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
COMMENTS = DF_PRS_ID_RFR;
RUN;
/***************
* FILE CREATION
****************/
%MACRO CREATE_FILES(REPNO);
DATA _NULL_;
SET SUPERC; 
WHERE FILE_IND = "&REPNO";
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
%MEND CREATE_FILES;
%CREATE_FILES(2);
%CREATE_FILES(3);
%CREATE_FILES(4);
