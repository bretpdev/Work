USE UDW
GO
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
SELECT DISTINCT
	PD10.DF_SPE_ACC_ID,
	RTRIM(PD10.DM_PRS_1) + ' ' + RTRIM(PD10.DM_PRS_LST) AS [Name],
	SUM(COALESCE(LN10.LA_CUR_PRI,0.00) + COALESCE(LN10.LA_NSI_OTS,0.00)) OVER(PARTITION BY PD10.DF_SPE_ACC_ID) AS [Balance],
	COALESCE(PH05.DX_CNC_EML_ADR, PD32.DX_ADR_EML) AS [Email],
	RTRIM(PD30.DX_STR_ADR_1) AS DX_STR_ADR_1,
	RTRIM(PD30.DX_STR_ADR_2) AS DX_STR_ADR_2,
	RTRIM(PD30.DX_STR_ADR_3) AS DX_STR_ADR_3,
	RTRIM(PD30.DM_CT) AS DM_CT,
	RTRIM(PD30.DC_DOM_ST) AS DC_DOM_ST,
	PD30.DF_ZIP_CDE,
	PD42.DN_DOM_PHN_ARA + PD42.DN_DOM_PHN_XCH + PD42.DN_DOM_PHN_LCL AS [Phone],
	DW01.WC_DW_LON_STA AS LoanStatus, --TODO: Translation?
	DATEDIFF(DAY,COALESCE(LN16.LD_DLQ_OCC,GETDATE()),GETDATE()) AS Delinquency,
	CASE WHEN BR30.BF_SSN IS NOT NULL THEN BR30.BC_EFT_STA ELSE 'I' END AS AutopayStatus,
	LN65.ExpectedPayoffDate, 
	LN65.LC_TYP_SCH_DIS AS ActiveRepaymentSchedule --TODO: Translation?
FROM
	UDW..PD10_PRS_NME PD10
	INNER JOIN UDW..LN10_LON LN10
		ON LN10.BF_SSN = PD10.DF_PRS_ID
		AND LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.IC_LON_PGM IN('SUBSPC','UNSPC')
	INNER JOIN UDW..DW01_DW_CLC_CLU DW01
		ON DW01.BF_SSN = LN10.BF_SSN
		AND DW01.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN UDW..PD32_PRS_ADR_EML PD32
		ON PD32.DF_PRS_ID = PD10.DF_PRS_ID
		AND PD32.DC_STA_PD32 = 'A'
		AND PD32.DI_VLD_ADR_EML = 'Y'
	LEFT JOIN UDW..PH05_CNC_EML PH05
		ON PH05.DF_SPE_ID = PD10.DF_SPE_ACC_ID
		AND PH05.DI_CNC_ELT_OPI = 'Y'
		AND PH05.DI_VLD_CNC_EML_ADR = 'Y'
	LEFT JOIN UDW..PD30_PRS_ADR PD30
		ON PD30.DF_PRS_ID = PD10.DF_PRS_ID
		AND PD30.DI_VLD_ADR = 'Y'
		AND PD30.DC_ADR = 'L'
	LEFT JOIN UDW..PD42_PRS_PHN PD42
		ON PD42.DF_PRS_ID = PD10.DF_PRS_ID
		AND PD42.DI_PHN_VLD = 'Y'
	LEFT JOIN UDW..LN16_LON_DLQ_HST LN16
		ON LN16.BF_SSN = LN10.BF_SSN
		AND LN16.LN_SEQ = LN10.LN_SEQ
		AND LN16.LC_STA_LON16 = '1'
	LEFT JOIN 
	(
		SELECT
			LN65.BF_SSN,
			LN65.LN_SEQ,
			LN65.LC_TYP_SCH_DIS,
			DATEADD(MONTH,SUM(LN66.LN_RPS_TRM) OVER(PARTITION BY LN65.BF_SSN, LN65.LN_SEQ) ,RS10.LD_RPS_1_PAY_DU) AS ExpectedPayoffDate
		FROM
			UDW..RS10_BR_RPD RS10
			INNER JOIN UDW..LN65_LON_RPS LN65
				ON LN65.BF_SSN = RS10.BF_SSN
				AND LN65.LN_RPS_SEQ = RS10.LN_RPS_SEQ
				AND RS10.LC_STA_RPST10 = 'A'
			INNER JOIN UDW..LN66_LON_RPS_SPF LN66
				ON LN66.BF_SSN = LN65.BF_SSN
				AND LN66.LN_RPS_SEQ = LN65.LN_RPS_SEQ
		WHERE
			LN65.LC_STA_LON65 = 'A'
	) LN65
		ON LN65.BF_SSN = LN10.BF_SSN
		AND LN65.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN UDW..LN83_EFT_TO_LON LN83
		ON LN83.BF_SSN = LN10.BF_SSN
		AND LN83.LN_SEQ = LN10.LN_SEQ
		AND LN83.LC_STA_LN83 ='A'
	LEFT JOIN UDW..BR30_BR_EFT BR30
		ON BR30.BF_SSN = LN83.BF_SSN
		AND BR30.BN_EFT_SEQ = LN83.BN_EFT_SEQ
		AND BR30.BC_EFT_STA = 'A'