/*DSASQ05*/

/*AMF RECONCILIATION
This job identifies loans for which UHEAA likely received AMF monies
incorrectly.  These are essentially closed loans where the loan was updated
to a closed status after the end of the specified federal fiscal year 
with a closed status effective date prior to the end of that fiscal year.

Specify the fiscal year for reporting in the first line of code below.*/

%LET FY = 2016;

OPTIONS SYMBOLGEN;
DATA _NULL_;
	CALL SYMPUT('FYBEG',"'"||PUT(INTNX('YEAR.10',"01JAN&FY."D,0,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('FYEND',"'"||PUT(INTNX('YEAR.10',"01JAN&FY."D,0,'E'), MMDDYYD10.)||"'");
	CALL SYMPUT('FY4YR',"'"||
		PUT(INTNX('MONTH',INTNX('YEAR.10',"01JAN&FY."D,-4,'E'),-2), DATE9.)||"'D");
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT FYBEG = &FYBEG;
%SYSLPUT FYEND = &FYEND;

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
/*libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;*/
RSUBMIT;
OPTIONS SYMBOLGEN;
PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH);
	CREATE TABLE AMFUL AS
		SELECT *
		FROM CONNECTION TO DB2 
		(
			SELECT 
				A.DF_PRS_ID_BR
				,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
				,B.AA_GTE_LON_AMT - (COALESCE(B.AA_TOT_CAN,0)) AS NET_GTY
				,D.AC_LON_STA_TYP
				,D.AD_LON_STA
				,D.AF_CRT_DTS_GA14
			FROM 
				OLWHRM1.GA01_APP A 
				INNER JOIN OLWHRM1.GA10_LON_APP B
					ON A.AF_APL_ID = B.AF_APL_ID
				INNER JOIN OLWHRM1.GA14_LON_STA C
					ON B.AF_APL_ID = C.AF_APL_ID
					AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
					AND C.AC_STA_GA14 = 'A'

			/*ONLY INCLUDE LOANS OPEN AT CONVERSION*/
			INNER JOIN
			(
				SELECT	
					AF_APL_ID
					,AF_APL_ID_SFX
				FROM 
					OLWHRM1.GA14_LON_STA
				WHERE 
					AF_LON_STA_RPT_SEQ = '00001'
					AND AC_LON_STA_TYP IN ('DA','FB','RP','CR','RF','IA','ID','IG','IM')
			)OAC
				ON OAC.AF_APL_ID = B.AF_APL_ID
				AND OAC.AF_APL_ID_SFX = B.AF_APL_ID_SFX

			/*FIRST CLOSED STATUS RECORD AFTER THE MOST RECENT OPEN STATUS RECORD*/
			INNER JOIN
			(
				SELECT 
					A.AF_APL_ID
					,A.AF_APL_ID_SFX
					,A.AC_LON_STA_TYP
					,A.AD_LON_STA
					,A.AF_CRT_DTS_GA14
				FROM
					OLWHRM1.GA14_LON_STA A
					INNER JOIN
					(
						SELECT
							X.AF_APL_ID
							,X.AF_APL_ID_SFX
							,MIN(X.AF_CRT_DTS_GA14) AS AF_CRT_DTS_GA14
						FROM 
							OLWHRM1.GA14_LON_STA X
						WHERE 
							X.AC_LON_STA_TYP IN ('PF','PC','PN','CA')
							AND X.AF_CRT_DTS_GA14 >
							(
								SELECT 
									MAX(Y.AF_CRT_DTS_GA14)
								FROM 
									OLWHRM1.GA14_LON_STA Y
								WHERE 
									X.AF_APL_ID = Y.AF_APL_ID
									AND X.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
									AND Y.AC_LON_STA_TYP IN ('DA','FB','RP','CR','RF','IA','ID','IG','IM')
							)
					/*and X.AF_APL_ID IN ('00074900000000879','00074900000000269','00074900000000740'
					,'00074900000000773','00074900000000740')*/
						GROUP BY 
							X.AF_APL_ID
							,X.AF_APL_ID_SFX
					)B
						ON A.AF_APL_ID = B.AF_APL_ID
						AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
						AND A.AF_CRT_DTS_GA14 = B.AF_CRT_DTS_GA14
			)D
				ON B.AF_APL_ID = D.AF_APL_ID
				AND B.AF_APL_ID_SFX = D.AF_APL_ID_SFX

			/*LOAN IS CURRENTLY CLOSED AS OF A DATE AFTER CONVERSION*/
			WHERE 
				C.AC_LON_STA_TYP IN ('PF','PC','PN','CA')
				AND DATE(C.AF_CRT_DTS_GA14) > '06/01/2001'

				/*LOAN CLAIM CRITERIA*/
				AND NOT EXISTS
					(
						SELECT 
							*
						FROM
							OLWHRM1.DC01_LON_CLM_INF X
						WHERE
							X.AF_APL_ID = B.AF_APL_ID
							AND X.AF_APL_ID_SFX = B.AF_APL_ID_SFX
							AND (
									X.LC_STA_DC10 = '03'
									OR 
									(
										X.LC_STA_DC10 = '04'
										AND X.LC_AUX_STA NOT IN ('11','12')
									)
								)
					)

				AND DATE(D.AF_CRT_DTS_GA14) > &FYEND
				AND D.AD_LON_STA <= &FYEND

				/*EXCLUDE UPDATES RIGHT AFTER FYE*/
				AND NOT 
					(
						D.AD_LON_STA BETWEEN &FYBEG AND &FYEND
						AND YEAR(D.AF_CRT_DTS_GA14) = YEAR(&FYEND)
						AND MONTH(D.AF_CRT_DTS_GA14) = 10
						AND DAY(D.AF_CRT_DTS_GA14) < 21
					)
				/*AND NOT */
				/*	(D.AD_LON_STA BETWEEN &FYBEG AND &FYEND*/
				/*	AND YEAR(C.AF_CRT_DTS_GA14) = YEAR(&FYEND)*/
				/*	AND MONTH(C.AF_CRT_DTS_GA14) = 10*/
				/*	AND DAY(C.AF_CRT_DTS_GA14) < 21)*/

				AND NOT EXISTS
					(
						SELECT 
							*
						 FROM
							OLWHRM1.AY01_BR_ATY Y
						 WHERE 
							Y.AF_APL_ID = B.AF_APL_ID
						 	AND Y.AF_APL_ID_SFX = B.AF_APL_ID_SFX
						 	AND Y.PF_ACT = 'RTAMF'
					)

			ORDER BY A.DF_PRS_ID_BR
			);
	DISCONNECT FROM DB2;
QUIT;
ENDRSUBMIT  ;

DATA AMFUL ; 
	SET DUSTER.AMFUL; 
RUN;

DATA AMFUL RP99;
	SET AMFUL;
	IF AD_LON_STA >= &FY4YR THEN OUTPUT AMFUL;
	ELSE IF &FY = 1999 THEN OUTPUT RP99;
RUN;

PROC SQL;
	CREATE TABLE AMFTOT AS
	SELECT 
		COUNT(*) AS COUNT
		,SUM(NET_GTY) AS NET_GTY
	FROM 
		AMFUL;
QUIT;

DATA AMFTOT;
	SET AMFTOT;
	IF &FY IN(1999,2000) 
	THEN OWED = NET_GTY * 0.0012;
	ELSE IF &FY >= 2001 AND &FY <= 2007 
	THEN OWED = NET_GTY * 0.0010;
	ELSE IF &FY >= 2008 
	THEN OWED = NET_GTY * 0.0006;
RUN;

FILENAME REPORT2 "T:\SAS\DSASQ05.R2.&FY..&SYSDATE";
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=126 PS=39;
PROC PRINTTO 
	PRINT=REPORT2 NEW;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=AMFTOT WIDTH=UNIFORM WIDTH=MIN;
	TITLE "AMF RECONCILIATION FOR FISCAL YEAR &FY";
	TITLE2 "RUN DATE:  &RUNDATE.";
	FOOTNOTE  'JOB = ON DEMAND     REPORT = AMF RECONCILIATION';
	VAR COUNT NET_GTY OWED;
	LABEL COUNT="LOANS INCLUDED IN CALCULATION"
	NET_GTY="TOTAL NET GUARANTY AMOUNT" OWED="CALCULATED AMF MONIES OWED";
	FORMAT COUNT COMMA7. NET_GTY OWED DOLLAR18.2;
RUN;
PROC PRINTTO;
RUN;

%MACRO RP99PRINT;
	%IF &FY = 1999 
	%THEN 
		%DO;
			OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=126;
			PROC PRINT NOOBS SPLIT='/' DATA=RP99 WIDTH=UNIFORM WIDTH=MIN;
			TITLE "AMF RECONCILIATION FOR FISCAL YEAR &FY";
			TITLE2 "LOANS NOT SELECTED DUE TO 4 1/2 YEAR RULE";
			TITLE3 "RUN DATE:  &RUNDATE.";
			FOOTNOTE  'JOB = ON DEMAND     REPORT = AMF RECONCILIATION';
			VAR DF_PRS_ID_BR CLUID NET_GTY AC_LON_STA_TYP AD_LON_STA AF_CRT_DTS_GA14;
			FORMAT NET_GTY DOLLAR10.2 AD_LON_STA MMDDYY10.;
			RUN;
		%END;
%MEND RP99PRINT;
%RP99PRINT;

PROC EXPORT DATA= AMFUL
            OUTFILE= "T:\SAS\AMF REC FY&FY..xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;

DATA _NULL_;
	SET WORK.AMFUL;
	FILE "T:\SAS\DSASQ05.R4.&FY..&SYSDATE" DELIMITER=',' DSD DROPOVER LRECL=32767;
	   FORMAT DF_PRS_ID_BR $9. ;
	   FORMAT CLUID $19. ;
	   FORMAT NET_GTY 16.2 ;
	   FORMAT AC_LON_STA_TYP $2. ;
	   FORMAT AD_LON_STA MMDDYY10. ;
	   FORMAT AF_CRT_DTS_GA14 DATETIME25.6 ;
	DO;
	   PUT DF_PRS_ID_BR $ @;
	   PUT CLUID $ @;
	   PUT NET_GTY @;
	   PUT AC_LON_STA_TYP $ @;
	   PUT AD_LON_STA @;
	   PUT AF_CRT_DTS_GA14 ;
	END;
RUN;
