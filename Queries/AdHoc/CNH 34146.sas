/*CREATE LIBNAME FOR REMOTE SERVER'S WORK FOLDER*/
LIBNAME WORKLOCL REMOTE SERVER=LEGEND SLIBREF=WORK;

/*CREATE FILENAME FOR OUTPUT*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTX "&RPTLIB/CNH_XXXXX.XLSX";

/*CREATE LIBRARY OF EXCEL DATA*/
LIBNAME XLIB XLSX 'T:\CNH XXXXX\NSLDS Reporting DCR.xlsx';

/*CREATE DATA SET FROM A SPECIFIC SHEET IN THE EXCEL FILE*/
DATA XLDATA; SET XLIB.SHEETX; RUN;

/*CAST CHAR SEQ ATTRIBUTE TO NUMERIC ATTRIBUTE WITH THE SAME NAME AND COPY NEW DATA SET TO REMOTE SERVER*/
DATA XDATA (DROP = CHAR_SEQ);
	RETAIN BF_SSN WN_SEQ_GRXX;
	SET XLDATA (RENAME = (WN_SEQ_GRXX = CHAR_SEQ));
	WN_SEQ_GRXX = INPUT(CHAR_SEQ, X.);
RUN;
DATA WORKLOCL.XDATA; SET XDATA; RUN;


/*SUBMIT CODE TO REMOTE SERVER*/
RSUBMIT LEGEND;
LIBNAME PKUB DBX DATABASE=DNFPUTDL OWNER=PKUB;
LIBNAME AES DBX DATABASE=DNFPUTDL OWNER=AES;
%let DB = DNFPUTDL;
%let OWNR = PKUB;

PROC SQL STIMER;
	CONNECT TO DBX (DATABASE=&DB); 

/*	GET DATA FOR ALL LOANS*/
	CREATE TABLE JOINDATA AS
		SELECT 
			XDAT.*
			,DBXD.WC_NDS_LN_DSB_RPT
			,DBXD.WC_NDS_LN_STA_RPT
			,DBXD.LI_RCL_LON_STA_HST
			,DBXD.LD_LON_STA_SR_RPT
			,DBXD.LD_LON_EFF_ADD

		FROM 
			CONNECTION TO DBX 
			(
				SELECT DISTINCT 
					LNXX.BF_SSN 
					,GRXX.WN_SEQ_GRXX
					,GRXX.WC_NDS_LN_DSB_RPT
					,GRXX.WC_NDS_LN_STA_RPT
					,GRXX.LI_RCL_LON_STA_HST
					,GRXX.LD_LON_STA_SR_RPT
					,LNXX.LD_LON_EFF_ADD
				FROM 
					&OWNR..LNXX_LON LNXX
					INNER JOIN &OWNR..GRXX_RPT_LON_APL GRXX
						ON LNXX.BF_SSN = GRXX.BF_SSN
						AND LNXX.LN_SEQ = GRXX.LN_SEQ
		
				FOR READ ONLY WITH UR
			) DBXD
			INNER JOIN XDATA XDAT
				ON DBXD.BF_SSN = XDAT.BF_SSN
				AND DBXD.WN_SEQ_GRXX = XDAT.WN_SEQ_GRXX
	;
	DISCONNECT FROM DBX;


QUIT;

ENDRSUBMIT;

/*COPY JOINED DATA TO WORK*/
DATA JOINDATA; SET WORKLOCL.JOINDATA; RUN;

/*EXPORT JOINED DATA TO A REPORT*/
PROC EXPORT DATA= WORK.JOINDATA
	OUTFILE= REPORTX
	REPLACE
    DBMS=XLSX;
RUN;
