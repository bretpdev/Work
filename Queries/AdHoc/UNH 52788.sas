/*CREATE LIBNAME FOR REMOTE SERVER'S WORK FOLDER*/
LIBNAME WORKLOCL REMOTE SERVER=DUSTER SLIBREF=WORK;


/*CREATE FILENAME FOR OUTPUT*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT1 "&RPTLIB/UNH_52788.XLSX"; 

/*CREATE LIBRARY OF EXCEL DATA*/
LIBNAME XLIB XLSX 'T:\SAS\Approved BB Corrections.xlsx'; 

/*CREATE DATA SET FROM A SPECIFIC SHEET IN THE EXCEL FILE*/
DATA XLDATA; SET XLIB.SHEET1; RUN; 

/*CAST CHAR SEQ ATTRIBUTE TO NUMERIC ATTRIBUTE WITH THE SAME NAME AND COPY NEW DATA SET TO REMOTE SERVER*/
DATA XDATA (DROP = CHAR_SEQ);
	RETAIN SSN LoanSeq; /*only the attribute that was cast and any attributes to the left of it are needed*/
	SET XLDATA (RENAME = (LoanSeq = CHAR_SEQ));
	LoanSeq = INPUT(CHAR_SEQ, 6.);
RUN;
DATA WORKLOCL.XDATA; SET XDATA; RUN;

/*SUBMIT CODE TO REMOTE SERVER*/
RSUBMIT DUSTER;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%let DB = DLGSUTWH;
%let OWNR = OLWHRM1;

PROC SQL STIMER;
	CONNECT TO DB2 (DATABASE=&DB); 

/*	GET DATA FOR ALL LOANS*/
	CREATE TABLE JOINDATA AS
		SELECT
			XDAT.SSN,
			XDAT.LoanSeq,
			XDAT.BorrowerBenefitType,
			XDAT.Tier,
			XDAT.PCVPayments,
			DB2D.DisqualDate,
			XDAT.DisqualReason,
			XDAT.Comment
/*			,DB2D.LN_SEQ*/

		FROM 
			CONNECTION TO DB2 
			(
				SELECT DISTINCT 
					LN10.BF_SSN,
					LN10.LN_SEQ,
					LN10.LD_LON_ACL_ADD AS DisqualDate
				FROM 
					&OWNR..LN10_LON LN10
		
				FOR READ ONLY WITH UR
			) DB2D
			INNER JOIN XDATA XDAT
				ON DB2D.BF_SSN = XDAT.SSN
				AND DB2D.LN_SEQ = XDAT.LoanSeq
	;
	DISCONNECT FROM DB2;


QUIT;

ENDRSUBMIT;

/*COPY JOINED DATA TO WORK*/
DATA JOINDATA; SET WORKLOCL.JOINDATA; RUN;

/*EXPORT JOINED DATA TO A REPORT*/
PROC EXPORT DATA= WORK.JOINDATA
	OUTFILE= REPORT1 
	REPLACE
	DBMS=XLSX;
RUN;
