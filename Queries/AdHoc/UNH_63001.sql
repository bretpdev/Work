USE UDW;
GO

--Due date on the 14th or 15th
--in repayment or forbearance

SELECT DISTINCT
	PD10.DF_SPE_ACC_ID,
	IIF(DAY(LN80.LD_BIL_DU_LON) IN (14,15),'Y','N') AS DUE_1415,
	DW01.WC_DW_LON_STA,
	IIF(LN60.LC_FOR_RSP != '003','<>003','003') AS LC_FOR_RSP,
	LN60.LC_STA_LON60,
	FB10.LC_STA_FOR10,
	FB10.LC_FOR_STA,
	LN10.LC_STA_LON10,
	IIF(LN10.LA_CUR_PRI > 0.00,'>0','0') AS BALANCE,
	LN10.LA_CUR_PRI - ISNULL(LN90Sum.CUR_PRI, 0) AS BALANCE_AS_OF_8_21,
	ISNULL(LN10.LA_LTE_FEE_OTS, 0) - ISNULL(LN90Sum.LTE_FEE, 0) AS LATE_FEES_AS_OF_8_21,
	ISNULL(LN16ADJ.DAYS_DELQ_AS_OF_8_21, 0) AS DAYS_DELQ_AS_OF_8_21
FROM
	LN10_LON LN10
	INNER JOIN LN80_LON_BIL_CRF LN80
		ON LN10.BF_SSN = LN80.BF_SSN
		AND LN10.LN_SEQ = LN80.LN_SEQ	
	INNER JOIN PD10_PRS_NME PD10
		ON LN10.BF_SSN = PD10.DF_PRS_ID
	INNER JOIN DW01_DW_CLC_CLU DW01
		ON LN10.BF_SSN = DW01.BF_SSN
		AND LN10.LN_SEQ = DW01.LN_SEQ
	INNER JOIN LN60_BR_FOR_APV LN60
		ON LN10.BF_SSN = LN60.BF_SSN
		AND LN10.LN_SEQ = LN60.LN_SEQ
	INNER JOIN FB10_BR_FOR_REQ FB10
		ON LN60.BF_SSN = FB10.BF_SSN
		AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
	LEFT JOIN
	(
		SELECT
			LN90.BF_SSN,
			LN90.LN_SEQ,
			SUM(LN90.LA_FAT_CUR_PRI) AS CUR_PRI,
			SUM(LN90.LA_FAT_LTE_FEE) AS LTE_FEE
		FROM
			LN90_FIN_ATY LN90
		WHERE
			CAST(LN90.LD_FAT_EFF AS DATE) > '2019-08-20'
			AND LN90.LC_STA_LON90 = 'A'
		GROUP BY
			BF_SSN,
			LN_SEQ
	) LN90Sum
		ON LN10.BF_SSN = LN90Sum.BF_SSN
		AND LN10.LN_SEQ = LN90Sum.LN_SEQ
	LEFT JOIN
	(
		SELECT
			LN16.BF_SSN,
			LN16.LN_SEQ,
			DATEDIFF(DAY, CAST(LD_DLQ_OCC AS DATE), '2019-08-21') + 1 AS DAYS_DELQ_AS_OF_8_21
		FROM
			LN16_LON_DLQ_HST LN16
		WHERE
			LC_STA_LON16 = '1'
			AND '2019-08-21' BETWEEN CAST(LD_DLQ_OCC AS DATE) AND CAST(LD_DLQ_MAX AS DATE)
	) LN16ADJ
		ON LN10.BF_SSN = LN16ADJ.BF_SSN
		AND LN10.LN_SEQ = LN16ADJ.LN_SEQ
WHERE
	DAY(LN80.LD_BIL_DU_LON) IN (14,15) --due date on 14th or 15th
	AND LN10.LC_STA_LON10 = 'R'
	AND LN10.LA_CUR_PRI > 0.00
	AND DW01.WC_DW_LON_STA = '03' --in repayment
	AND LN60.LC_FOR_RSP != '003' --denials
	AND LN60.LC_STA_LON60 = 'A' --forbearance
	AND FB10.LC_STA_FOR10 = 'A' --forbearance
	AND FB10.LC_FOR_STA = 'A'   --forbearance
ORDER BY
	PD10.DF_SPE_ACC_ID
;