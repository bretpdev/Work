*import data sets from text files;
PROC IMPORT DATAFILE='Q:\Support Services\JR\CRI_Audit_Files_-_Lender_ID_826717.txt'
            OUT=SOURCE1
            DBMS=DLM			
            REPLACE;
			DELIMITER=',';
QUIT;
PROC IMPORT DATAFILE='Q:\Support Services\JR\CRI_Audit_Files_-_Lender_ID_828476.txt'
            OUT=_SOURCE2
            DBMS=DLM			
            REPLACE;
			DELIMITER=',';
QUIT;
PROC IMPORT DATAFILE='Q:\Support Services\JR\CRI_Audit_Files_-_Lender_ID_830248.txt'
            OUT=SOURCE3
            DBMS=DLM			
            REPLACE;
			DELIMITER=',';
QUIT;
PROC IMPORT DATAFILE='Q:\Support Services\JR\ERRORS_LENDER_828476.txt'
            OUT=SOURCE4
            DBMS=DLM			
            REPLACE;
			DELIMITER=',';
QUIT;

*reformat troublesome data;
DATA SOURCE2 
	(RENAME=
	(
		_PREVIOUS_LENDER_CODE = PREVIOUS_LENDER_CODE
		_ORIGINAL_HOLDER = ORIGINAL_HOLDER
	));
	SET _SOURCE2
	;
		_PREVIOUS_LENDER_CODE = PUT(PREVIOUS_LENDER_CODE,6.);
		_ORIGINAL_HOLDER = PUT(ORIGINAL_HOLDER,6.);
	DROP 
		PREVIOUS_LENDER_CODE
		ORIGINAL_HOLDER;
RUN;

DATA SOURCE4 
	(RENAME =
	(
		_LOAN_TYPE = LOAN_TYPE
	));
	SET SOURCE4		
		(KEEP=
			BF_SSN 
			CURRENT_LENDER_CODE 
			FIRST_DISBURSEMENT_DATE 
			CURRENT_GUARANTOR_EFFECTIVE_DATE 
			LOAN_TYPE
		);
	_LOAN_TYPE = PUT(LOAN_TYPE,2.);
	DROP LOAN_TYPE;
RUN;


*combine all data sets;
DATA SOURCE_ALL;
	LENGTH 
		PREVIOUS_LENDER_CODE $ 6.
		ORIGINAL_HOLDER $ 6.
		E_SIGNATURE_SOURCE_TYPE_CODE $ 7.
	;
	SET
		SOURCE3
		SOURCE1
		SOURCE2
		SOURCE4
	;
RUN;

DATA SOURCE_UNSORTED;
	SET SOURCE_ALL
		(KEEP=
			BF_SSN 
			CURRENT_LENDER_CODE 
			FIRST_DISBURSEMENT_DATE 
			CURRENT_GUARANTOR_EFFECTIVE_DATE 
			LOAN_TYPE
		);
RUN;

PROC SORT DATA=SOURCE_UNSORTED OUT=SOURCE_SORTED NODUPKEY;
	BY
		BF_SSN 
		CURRENT_LENDER_CODE 
		FIRST_DISBURSEMENT_DATE 
		CURRENT_GUARANTOR_EFFECTIVE_DATE 
		LOAN_TYPE
	;
QUIT;

DATA SOURCE 
	(RENAME=
	(
		_BF_SSN = BF_SSN 
		_CURRENT_LENDER_CODE = CURRENT_LENDER_CODE 
		_FIRST_DISBURSEMENT_DATE = FIRST_DISBURSEMENT_DATE 
		_CURRENT_GUARANTOR_EFFECTIVE_DT = CURRENT_GUARANTOR_EFFECTIVE_DATE 
	));
	SET SOURCE_SORTED
	;
		_BF_SSN = PUT(BF_SSN, Z9.);
		_CURRENT_LENDER_CODE = PUT(CURRENT_LENDER_CODE , Z6.);
		_FIRST_DISBURSEMENT_DATE = INPUT(PUT(FIRST_DISBURSEMENT_DATE, 8.), YYMMDD10.);
		_CURRENT_GUARANTOR_EFFECTIVE_DT = INPUT(PUT(CURRENT_GUARANTOR_EFFECTIVE_DATE, 8.), YYMMDD10.);
	FORMAT
		_FIRST_DISBURSEMENT_DATE
		_CURRENT_GUARANTOR_EFFECTIVE_DT
		YYMMDD10.
	;
	DROP
		BF_SSN 
		CURRENT_LENDER_CODE 
		FIRST_DISBURSEMENT_DATE 
		CURRENT_GUARANTOR_EFFECTIVE_DATE 
	;
RUN;

%LET RPTLIB = T:\SAS;
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK; /*live*/
/*LIBNAME  DUSTER  REMOTE  SERVER=QADBD004 SLIBREF=WORK ;*/ /*test*/

DATA DUSTER.SOURCE; *Send data to Duster;
	SET SOURCE;
RUN;

RSUBMIT;

%LET DB = DLGSUTWH; /*live*/
/*%LET DB = DLGSWQUT;*/ /*test*/
LIBNAME OLWHRM1 DB2 DATABASE=&DB OWNER=OLWHRM1; /*needed for SQL queries, but not for DB2 queries*/

PROC SQL;
	CREATE TABLE LoanDetail AS
		SELECT DISTINCT
			PD10.DF_SPE_ACC_ID
			,LN10.LN_SEQ
			,LN16.LC_DLQ_TYP 	AS Delinquency
			,LN16.LC_STA_LON16
			,LN16.LN_DLQ_MAX
			,DW01.WC_DW_LON_STA AS Status
/*			,AY10.PF_REQ_ACT 	AS ARC*/
		FROM
			SOURCE S
			INNER JOIN 
			(
				SELECT
					BF_SSN
					,LN_SEQ
					,SUBSTR(LF_LON_CUR_OWN,1,6) AS LF_LON_CUR_OWN
					,LD_LON_1_DSB
					,LD_LON_GTR
					,CASE
						WHEN IC_LON_PGM = 'STFFRD' THEN 'SF'
						WHEN IC_LON_PGM = 'UNSTFD' THEN 'SU'
						WHEN IC_LON_PGM = 'PLUS' THEN 'PL'
						WHEN IC_LON_PGM = 'PLUSGB' THEN 'GB'
						WHEN IC_LON_PGM = 'CONSOL' THEN 'CL'
						WHEN IC_LON_PGM = 'SUBCNS' THEN 'CL'
						WHEN IC_LON_PGM = 'UNCNS' THEN 'CL'
						WHEN IC_LON_PGM = 'SUBSPC' THEN 'CL'
						WHEN IC_LON_PGM = 'UNSPC' THEN 'CL'
						WHEN IC_LON_PGM = 'SLS' THEN 'SL'
					ELSE IC_LON_PGM
					END AS IC_LON_PGM					
				FROM
					OLWHRM1.LN10_LON
			)LN10
				ON LN10.BF_SSN = S.BF_SSN
				AND LN10.LF_LON_CUR_OWN = S.CURRENT_LENDER_CODE
				AND LN10.LD_LON_1_DSB = S.FIRST_DISBURSEMENT_DATE
				AND LN10.LD_LON_GTR = S.CURRENT_GUARANTOR_EFFECTIVE_DATE
				AND LN10.IC_LON_PGM = S.LOAN_TYPE
			INNER JOIN OLWHRM1.PD10_PRS_NME PD10
				ON LN10.BF_SSN = PD10.DF_PRS_ID
			INNER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
				ON LN10.BF_SSN = LN16.BF_SSN
				AND LN10.LN_SEQ = LN16.LN_SEQ
			INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
				ON LN10.BF_SSN = DW01.BF_SSN
				AND LN10.LN_SEQ = DW01.LN_SEQ
/*			INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10*/
/*				ON LN10.BF_SSN = AY10.BF_SSN*/
			LEFT JOIN 
			(
				SELECT 
					AY10.BF_SSN
					,AY10.PF_REQ_ACT
				FROM
					OLWHRM1.AY10_BR_LON_ATY AY10
				WHERE 
					AY10.PF_REQ_ACT = 'RI142'
			) ARC
				ON LN10.BF_SSN = ARC.BF_SSN
		WHERE
			ARC.BF_SSN IS NULL
			AND LN16.LC_DLQ_TYP = 'P'
			AND LN16.LC_STA_LON16 = '1'
			AND LN16.LN_DLQ_MAX >= 360
			AND DW01.WC_DW_LON_STA ^= '08'			
	;
QUIT;
ENDRSUBMIT;

DATA LoanDetail;
	SET DUSTER.LoanDetail;
RUN;

PROC EXPORT
		DATA=LoanDetail
		OUTFILE="&RPTLIB\UNH 51742.xlsx"
		DBMS = EXCEL
		REPLACE;
		SHEET="CRI";
RUN;
