/*set up library to SQL Server and include common code*/
/* TEST*/
	LIBNAME ULS ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS_TEST.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= SCRA;
	LIBNAME UDW ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\UDW_TEST.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= dbo;
	LIBNAME ULSPrint ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS_TEST.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= print;
	%INCLUDE "Y:\Codebase\SAS\ArcAdd Common.SAS";
	%LET RPTLIB = T:\;
	%LET ScriptDataId = 97;

/*LIVE*/
/*	LIBNAME ULS ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS.DSN; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= SCRA;
	LIBNAME UDW ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\UDW.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= dbo;
	LIBNAME ULSPrint ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS.DSN; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= print;
	%INCLUDE "Z:\Codebase\SAS\ArcAdd Common.SAS";
	%LET RPTLIB = X:\PADD\FTP;
	%LET ScriptDataId = 106;*/

/*begin job specific code*/
%LET NUMDATE = %SYSFUNC(TODAY(), YYMMDDN8.);
%LET INPUTFILE = SCRA_3_0_ULWOO3*;

FILENAME REPORT5 "&RPTLIB/SCRA_UHEAA_R5.&NUMDATE";

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

%MACRO ExecuteSas;

	%IF 
		%SYSFUNC(FILEEXIST(&RPTLIB\&INPUTFILE))^=0 %THEN %DO;  
		DATA DODZ;                                    
			INFILE "&RPTLIB\&INPUTFILE";
			INPUT 	SSN $ 1-9 
					NOTIFICATION_DATE $ 92-99
					LC_ACT_DUT $ 101
					LC_LFT_ACT_DUT $ 102
					LC_NTF_ACT_DUT $ 103
					ACT_DUT_END $ 104-111
					ACT_DUT_BEG $ 122-129
					EID_BEGIN_DATE $ 130-137 ; 

			FORMAT 	LD_ACT_DUT_BEG 
					LD_ACT_DUT_END
					LD_EID_BEGIN_DATE
					LD_NOTIFICATION_DATE MMDDYY10.;

			IF ACT_DUT_BEG ^= '00000000' THEN LD_ACT_DUT_BEG = INPUT(ACT_DUT_BEG, YYMMDD8.);
			IF ACT_DUT_END ^= '00000000' THEN LD_ACT_DUT_END = INPUT(ACT_DUT_END, YYMMDD8.);
			IF EID_BEGIN_DATE ^= '00000000' THEN LD_EID_BEGIN_DATE = INPUT(EID_BEGIN_DATE, YYMMDD8.);
			IF NOTIFICATION_DATE ^= '00000000' THEN LD_NOTIFICATION_DATE = INPUT(NOTIFICATION_DATE, YYMMDD8.);                              
		RUN;                                         
	%END;                                          
	%ELSE %DO;
		%PUT The file &INPUTFILE does not exist. ;
	%END;

	DATA DOD;
		SET DODZ;
		WHERE 
			LC_ACT_DUT IN('X','Y')
			OR LC_LFT_ACT_DUT = 'Y'
			OR LC_NTF_ACT_DUT = 'Y'
		;
	RUN;

	DATA DUSTER.DOD; SET DOD; RUN;

	RSUBMIT;

	LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
	LIBNAME SESSION DB2 DATABASE=DLGSUTWH SCHEMA=SESSION CONNECTION=GLOBAL;

	PROC SQL;
	CONNECT TO DB2 (DATABASE=DLGSUTWH CONNECTION=GLOBAL);

	EXECUTE (DECLARE GLOBAL TEMPORARY TABLE SESSION.DOD 
		(
			SSN CHAR(9), 
			NOTIFICATION_DATE CHAR(8), 
			LC_ACT_DUT CHAR(1), 
			LC_LFT_ACT_DUT CHAR(1), 
			LC_NTF_ACT_DUT CHAR(1), 
			ACT_DUT_END CHAR(8),
			ACT_DUT_BEG CHAR(8), 
			EID_BEGIN_DATE CHAR(8),
			LD_ACT_DUT_BEG DATE,
			LD_ACT_DUT_END DATE,
			LD_EID_BEGIN_DATE DATE,
			LD_NOTIFICATION_DATE DATE
		) 
		ON COMMIT PRESERVE ROWS
	) BY DB2;

	INSERT INTO SESSION.DOD 
		SELECT 
			SSN,
			NOTIFICATION_DATE,
			LC_ACT_DUT,
			LC_LFT_ACT_DUT,
			LC_NTF_ACT_DUT,
			ACT_DUT_END,
			ACT_DUT_BEG,
			EID_BEGIN_DATE,
			LD_ACT_DUT_BEG,
			LD_ACT_DUT_END,
			LD_EID_BEGIN_DATE,
			LD_NOTIFICATION_DATE
		FROM DOD;

	/*borrowers*/
	CREATE TABLE BORROWERS AS
	SELECT * FROM CONNECTION TO DB2(
		SELECT DISTINCT
			PD10.DF_SPE_ACC_ID AS ACCOUNTNUMBER,
			PD10.DM_PRS_1 AS FIRSTNAME,
			PD10.DM_PRS_LST AS LASTNAME
		FROM
			SESSION.DOD DOD
			INNER JOIN OLWHRM1.PD10_PRS_NME PD10
				ON DOD.SSN = PD10.DF_PRS_ID
		ORDER BY 
			PD10.DF_SPE_ACC_ID
	)
	;

	/*endorsers/co-makers*/
	CREATE TABLE ENDORSERS AS
	SELECT * FROM CONNECTION TO DB2(
		SELECT DISTINCT
			PD10.DF_SPE_ACC_ID AS BORROWERACCOUNTNUMBER,
			PD10E.DF_SPE_ACC_ID AS ENDORSERACCOUNTNUMBER,
			PD10E.DM_PRS_1 AS ENDORSERFIRSTNAME,
			PD10E.DM_PRS_LST AS ENDORSERLASTNAME
		FROM
			SESSION.DOD DOD
			INNER JOIN OLWHRM1.LN20_EDS LN20
				ON DOD.SSN = LN20.LF_EDS
			INNER JOIN OLWHRM1.LN10_LON LN10
				ON LN20.BF_SSN = LN10.BF_SSN
				AND LN20.LN_SEQ = LN10.LN_SEQ
			INNER JOIN OLWHRM1.PD10_PRS_NME PD10
				ON LN20.BF_SSN = PD10.DF_PRS_ID
			INNER JOIN OLWHRM1.PD10_PRS_NME PD10E
				ON LN20.LF_EDS = PD10E.DF_PRS_ID
		ORDER BY 
			PD10.DF_SPE_ACC_ID
	)
	;
	DISCONNECT FROM DB2;

	QUIT;
	ENDRSUBMIT;

	DATA BORROWERS; SET DUSTER.BORROWERS; RUN;
	DATA ENDORSERS; SET DUSTER.ENDORSERS; RUN;
%MEND ExecuteSas;

%ExecuteSas;

PROC EXPORT DATA = WORK.BORROWERS 
            OUTFILE = "T:\SAS\Borrowers.xlsx" 
            DBMS = EXCEL
			REPLACE;
     SHEET="A"; 
RUN;


PROC EXPORT DATA = WORK.ENDORSERS 
            OUTFILE = "T:\SAS\Endorsers.xlsx" 
            DBMS = EXCEL
			REPLACE;
     SHEET="A"; 
RUN;
