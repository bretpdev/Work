LIBNAME DUSTER REMOTE SERVER=DUSTER SLIBREF=WORK;

DATA DOD;
	INFILE 'T:\SCRA_3_0_ULWOO3.LWOO3R2 2-5-15.txt';
	INPUT SSN $ 1-9 LT_DOD $ 104-111;
RUN;

DATA DOD1 (DROP=LT_DOD);
	SET DOD;
	FORMAT LD_DOD DATE9.;
	IF LT_DOD = '00000000' THEN LD_DOD = '31DEC9999'D;
	ELSE LD_DOD = INPUT(LT_DOD,YYMMDD8.);
RUN;

DATA DOD2;
	SET DOD1;
	WHERE LD_DOD ^= '31DEC9999'D;
RUN;

DATA DUSTER.DOD; SET DOD2; RUN;

RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
PROC SQL;
	CREATE TABLE MILT_INT AS
		SELECT DISTINCT
			DOD.SSN,
			DOD.LD_DOD,
			PD10.DF_SPE_ACC_ID,
			LN72.LN_SEQ,
			LN72.LD_ITR_EFF_BEG,
			LN72.LD_ITR_EFF_END,
			LN72.LR_ITR,
			LN72.LC_INT_RDC_PGM
		FROM
			DOD
			JOIN OLWHRM1.PD10_PRS_NME PD10
				ON DOD.SSN = PD10.DF_PRS_ID
			JOIN OLWHRM1.LN72_INT_RTE_HST LN72
				ON PD10.DF_PRS_ID = LN72.BF_SSN
		WHERE
			TODAY() BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
			AND LN72.LR_ITR  < 6
			AND LN72.LC_INT_RDC_PGM = 'M'
			AND LN72.LD_ITR_EFF_END > DOD.LD_DOD
			AND LN72.LC_STA_LON72 = 'A'
	;
QUIT;
ENDRSUBMIT;
DATA MILT_INT; SET DUSTER.MILT_INT; RUN;

RSUBMIT;
PROC SQL;
	CREATE TABLE MILT_DEF AS
		SELECT DISTINCT
			DOD.SSN,
			DOD.LD_DOD,
			PD10.DF_SPE_ACC_ID,
			DEFR.LN_SEQ,
			DEFR.LC_DFR_TYP,
			DEFR.LD_DFR_END
		FROM
			DOD
			JOIN OLWHRM1.PD10_PRS_NME PD10
				ON DOD.SSN = PD10.DF_PRS_ID
			LEFT JOIN
				(
					SELECT 
						LN50.BF_SSN,
						LN50.LN_SEQ,
						DF10.LC_DFR_TYP,
						LN50.LD_DFR_END
					FROM
						OLWHRM1.DF10_BR_DFR_REQ DF10
						JOIN OLWHRM1.LN50_BR_DFR_APV LN50
							ON DF10.BF_SSN = LN50.BF_SSN
							AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM
							AND LN50.LC_STA_LON50 = 'A'
							AND DF10.LC_DFR_STA = 'A'
							AND DF10.LC_STA_DFR10 = 'A'
							AND DF10.LC_DFR_TYP = '38'	
				) DEFR
				ON DOD.SSN = DEFR.BF_SSN
				AND DEFR.LD_DFR_END > DOD.LD_DOD
		WHERE
			DEFR.BF_SSN IS NOT NULL
	;
QUIT;
ENDRSUBMIT;
DATA MILT_DEF; SET DUSTER.MILT_DEF; RUN;

DATA ARC_FILE;
	SET MILT_INT (KEEP=DF_SPE_ACC_ID) MILT_DEF (KEEP=DF_SPE_ACC_ID);
RUN;

PROC SORT DATA=ARC_FILE NODUPKEY;
	BY DF_SPE_ACC_ID;
RUN;

DATA _NULL_;
	SET ARC_FILE ;
	FILE 'T:\QBLDR FILE - Active Duty Ended Query.TXT' DELIMITER=',' DSD DROPOVER LRECL=32767;
	PUT DF_SPE_ACC_ID 'NNNNN,,,,,,,ALL,' ;
RUN;

PROC EXPORT
		DATA=MILT_INT
		OUTFILE='T:\DETAIL FILE - Active Duty Ended Query.XLSX'
		REPLACE;
RUN;

PROC EXPORT
		DATA=MILT_DEF
		OUTFILE='T:\DETAIL FILE - Active Duty Ended Query.XLSX'
		REPLACE;
RUN;

