/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWDW1.LWDW1R2.&sysdate";
FILENAME REPORTZ "&RPTLIB/ULWDW1.LWDW1RZ";
FILENAME REPORT3 "&RPTLIB/ULWDW1.LWDW1R3.&sysdate";
FILENAME REPORT4 "&RPTLIB/ULWDW1.LWDW1R4.&sysdate";
FILENAME REPORT5 "&RPTLIB/ULWDW1.LWDW1R5.&sysdate";
FILENAME REPORT6 "&RPTLIB/ULWDW1.LWDW1R6.&sysdate";
FILENAME REPORT7 "&RPTLIB/ULWDW1.LWDW1R7.&sysdate";
FILENAME REPORT8 "&RPTLIB/ULWDW1.LWDW1R8.&sysdate";
FILENAME REPORT9 "&RPTLIB/ULWDW1.LWDW1R9.&sysdate";
FILENAME REPORT10 "&RPTLIB/ULWDW1.LWDW1R10.&sysdate";
FILENAME REPORT11 "&RPTLIB/ULWDW1.LWDW1R11.&sysdate";
FILENAME REPORT12 "&RPTLIB/ULWDW1.LWDW1R12.&sysdate";
FILENAME REPORT13 "&RPTLIB/ULWDW1.LWDW1R13.&sysdate";
FILENAME REPORT14 "&RPTLIB/ULWDW1.LWDW1R14.&sysdate";
FILENAME REPORT15 "&RPTLIB/ULWDW1.LWDW1R15.&sysdate";
FILENAME REPORT16 "&RPTLIB/ULWDW1.LWDW1R16.&sysdate";
FILENAME REPORT17 "&RPTLIB/ULWDW1.LWDW1R17.&sysdate";
FILENAME REPORT18 "&RPTLIB/ULWDW1.LWDW1R18.&sysdate";
FILENAME REPORT19 "&RPTLIB/ULWDW1.LWDW1R19.&sysdate";
FILENAME REPORT20 "&RPTLIB/ULWDW1.LWDW1R20.&sysdate";
FILENAME REPORT21 "&RPTLIB/ULWDW1.LWDW1R21.&sysdate";
FILENAME REPORT22 "&RPTLIB/ULWDW1.LWDW1R22.&sysdate";
FILENAME REPORT23 "&RPTLIB/ULWDW1.LWDW1R23.&sysdate";
FILENAME REPORT24 "&RPTLIB/ULWDW1.LWDW1R24.&sysdate";
FILENAME REPORT25 "&RPTLIB/ULWDW1.LWDW1R25.&sysdate";
FILENAME REPORT26 "&RPTLIB/ULWDW1.LWDW1R26.&sysdate";
FILENAME REPORT27 "&RPTLIB/ULWDW1.LWDW1R27.&sysdate";
FILENAME REPORT28 "&RPTLIB/ULWDW1.LWDW1R28.&sysdate";
FILENAME REPORT29 "&RPTLIB/ULWDW1.LWDW1R29.&sysdate";
FILENAME REPORT30 "&RPTLIB/ULWDW1.LWDW1R30.&sysdate";
FILENAME REPORT31 "&RPTLIB/ULWDW1.LWDW1R31.&sysdate";
FILENAME REPORT32 "&RPTLIB/ULWDW1.LWDW1R32.&sysdate";
FILENAME REPORT33 "&RPTLIB/ULWDW1.LWDW1R33.&sysdate";
FILENAME REPORT34 "&RPTLIB/ULWDW1.LWDW1R34.&sysdate";

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
data _null_;
call symput('rsub',put(time(),time9.));
run;
RSUBMIT;  
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1; 
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
DATA _NULL_; 
SET SAS_TAB.LASTRUN_JOBS;
/*If the job must be run manually set this macro to the last day it successfully ran(last business day).*/
LAST_RUN = TODAY() - 2000;	*COMMENT FOR PROD, IT WILL READ THE DATE FROM A TABLE;
IF JOB = 'UTLWDW1' THEN DO;
	CALL SYMPUT('LAST_RUN',"'"||TRIM(LEFT(PUT(LAST_RUN,DATE10.)))|| "'D");
	CALL SYMPUT('LAST_RUNPASS',"'"|| PUT(LAST_RUN,MMDDYY10.) || "'");
END;
RUN;

%PUT &LAST_RUN &LAST_RUNPASS;

%LET SSN = '';

/*THE TABLES ARE GOING TO HAVE THE ACCOUNT NUMBER RATHER THAN SSN AS A PRIMARY KEY*/
/*THE ACCOUNT NUMBER IS BEING TAKEN FROM THE PD10 TABLE WHILE SSN IS DROPPED*/
%MACRO SSN2ACC(TBL,ADKEY);
PROC SORT DATA=&TBL; BY BF_SSN &ADKEY; RUN;
DATA &TBL(DROP=BF_SSN);
MERGE SSN2ACC(IN=B) &TBL(IN=A);
BY BF_SSN;
IF A AND B;
RUN;
PROC SORT DATA=&TBL; BY DF_SPE_ACC_ID &ADKEY; RUN;
%MEND;

/*******************************************
* BORROWER DATA
********************************************/
PROC SQL;
	CREATE TABLE SSN2ACC AS
	SELECT DISTINCT A.DF_SPE_ACC_ID
		,B.BF_SSN
	FROM OLWHRM1.PD10_PRS_NME A
	INNER JOIN OLWHRM1.LN10_LON B
		ON A.DF_PRS_ID = B.BF_SSN
	WHERE B.BF_SSN = &SSN
;
QUIT;
PROC SORT DATA=SSN2ACC; BY BF_SSN; RUN;

/*******************************************
* ENDORSER DATA
********************************************/

DATA LN20(DROP=LF_LST_DTS_LN20);
	SET OLWHRM1.LN20_EDS(KEEP=BF_SSN LC_STA_LON20 LC_EDS_TYP LF_LST_DTS_LN20 LN_SEQ);
	IF DATEPART(LF_LST_DTS_LN20) >= &LAST_RUN ;
	WHERE BF_SSN = &SSN;
RUN; 
%SSN2ACC(LN20,LN_SEQ);

/*******************************************
* REFERENCE DATA
********************************************/
DATA BR03(DROP=BF_LST_DTS_BR03 BD_RFR_LST_ATT_HME BD_RFR_LST_ATT_ALT BD_RFR_LST_CNC BD_LTR_SNT_RFR);
	SET OLWHRM1.BR03_BR_REF(RENAME=(DF_PRS_ID_BR=BF_SSN  ) 
		KEEP=DF_PRS_ID_BR DF_PRS_ID_RFR BC_STA_BR03 BI_ATH_3_PTY BC_RFR_REL_BR 
			BM_RFR_1 BM_RFR_LST BF_LST_DTS_BR03 BD_RFR_LST_CNC BD_RFR_LST_ATT_HME 
			BD_RFR_LST_ATT_ALT BD_LTR_SNT_RFR);
	format LST_ATT  LST_CNC mmddyy10.;
	IF DATEPART(BF_LST_DTS_BR03) >= &LAST_RUN;
	LST_CNC = MAX(BD_RFR_LST_CNC,BD_LTR_SNT_RFR);
	LST_ATT = MAX(BD_RFR_LST_ATT_HME,BD_RFR_LST_ATT_ALT);
	WHERE BF_SSN = &SSN;
RUN;
%SSN2ACC(BR03,DF_PRS_ID_RFR);

/*******************************************
* TRANSACTION DATA
********************************************/

DATA LN90(DROP=LF_LST_DTS_LN90 ) ;
	SET OLWHRM1.LN90_FIN_ATY(KEEP=BF_SSN LN_SEQ LN_FAT_SEQ LD_FAT_PST
		LD_FAT_EFF LC_STA_LON90 LA_FAT_CUR_PRI LA_FAT_NSI LA_FAT_LTE_FEE 
		LF_LST_DTS_LN90 PC_FAT_TYP PC_FAT_SUB_TYP LC_FAT_REV_REA );
	WHERE LC_FAT_REV_REA ^= '0'
		AND DATEPART(LF_LST_DTS_LN90) >= &LAST_RUN
		AND BF_SSN = &SSN;
	LA_FAT_CUR_PRI = COALESCE(LA_FAT_CUR_PRI,0);
	LA_FAT_NSI = COALESCE(LA_FAT_NSI,0);
	LA_FAT_LTE_FEE = COALESCE(LA_FAT_LTE_FEE,0);
IF  PC_FAT_TYP IN ('05','10','14','15','17','26','50','51','55','60');
RUN;
%SSN2ACC(LN90,LN_SEQ LN_FAT_SEQ);

/*******************************************
* BILLING DATA
********************************************/
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LN80 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.BF_SSN
	,B.LN_SEQ
	,B.LD_BIL_CRT
	,B.LN_SEQ_BIL_WI_DTE
	,B.LN_BIL_OCC_SEQ
	,B.LD_BIL_DU_LON
	,B.LC_STA_LON80
	,COALESCE(b.la_bil_cur_du,0) AS LA_BIL_CUR_DU
	,COALESCE(b.la_bil_pas_du,0) AS la_bil_pas_du
	,COALESCE(b.la_tot_bil_sts,0) AS la_tot_bil_sts
	,LC_BIL_MTD
	,LC_IND_BIL_SNT
	,LC_STA_BIL10
	,E.LN_FAT_SEQ
	,E.LD_FAT_EFF
FROM OLWHRM1.BL10_BR_BIL C
INNER JOIN OLWHRM1.LN80_LON_BIL_CRF B
	ON B.BF_SSN = C.BF_SSN
	AND B.LD_BIL_CRT = C.LD_BIL_CRT
	AND B.LN_SEQ_BIL_WI_DTE = C.LN_SEQ_BIL_WI_DTE
LEFT OUTER JOIN OLWHRM1.LN75_BIL_LON_FAT D
	ON D.BF_SSN = B.BF_SSN
	AND D.LN_SEQ = B.LN_SEQ
	AND D.LD_BIL_CRT = B.LD_BIL_CRT
	AND D.LN_SEQ_BIL_WI_DTE = B.LN_SEQ_BIL_WI_DTE
	AND D.LN_BIL_OCC_SEQ = B.LN_BIL_OCC_SEQ
	AND B.LA_BIL_CUR_DU = B.LA_TOT_BIL_STS
LEFT OUTER JOIN OLWHRM1.LN90_FIN_ATY E
	ON D.BF_SSN = E.BF_SSN
	AND D.LN_SEQ = E.LN_SEQ
	AND D.LN_FAT_SEQ = E.LN_FAT_SEQ
WHERE B.LC_BIL_TYP_LON = 'P' 
	AND B.LD_LST_DTS_LN80 >= &LAST_RUNPASS
	AND B.BF_SSN = &SSN
);
QUIT; 
proc sort data=ln80; by bf_ssn ln_seq LD_BIL_CRT LN_SEQ_BIL_WI_DTE LN_BIL_OCC_SEQ LN_FAT_SEQ; run;
data ln80(drop=LN_BIL_OCC_SEQ LN_FAT_SEQ); 
set ln80;
by bf_ssn ln_seq LD_BIL_CRT LN_SEQ_BIL_WI_DTE ;
if last.ln_seq_bil_wi_dte;
run;
%SSN2ACC(LN80,LN_SEQ LD_BIL_CRT LN_SEQ_BIL_WI_DTE);

/*******************************************
* DEFERMENT DATA
********************************************/
PROC SQL;
CREATE TABLE DF10 AS
	SELECT B.BF_SSN
		,B.LN_SEQ
		,B.LF_DFR_CTL_NUM
		,B.LN_DFR_OCC_SEQ
		,C.LC_DFR_TYP
		,C.LD_DFR_INF_CER
		,B.LD_DFR_BEG
		,B.LD_DFR_END
		,B.LC_LON_LEV_DFR_CAP
		,B.LC_STA_LON50
		,C.LC_DFR_STA
		,C.LC_STA_DFR10
	FROM OLWHRM1.LN50_BR_DFR_APV B
	INNER JOIN OLWHRM1.DF10_BR_DFR_REQ C
		ON B.BF_SSN = C.BF_SSN
		AND B.LF_DFR_CTL_NUM = C.LF_DFR_CTL_NUM
	WHERE (DATEPART(B.LF_LST_DTS_LN50) >= &LAST_RUN 
			OR DATEPART(C.LF_LST_DTS_DF10) >= &LAST_RUN)
		AND B.BF_SSN = &SSN
;

/*******************************************
* FORBEARANCE DATA
********************************************/
CREATE TABLE FB10 AS
	SELECT B.BF_SSN
		,B.LN_SEQ
		,B.LF_FOR_CTL_NUM
		,B.LN_FOR_OCC_SEQ
		,C.LC_FOR_TYP
		,C.LD_FOR_INF_CER
		,B.LD_FOR_BEG
		,B.LD_FOR_END
		,B.LC_LON_LEV_FOR_CAP 
		,B.LC_STA_LON60
		,C.LC_FOR_STA
		,C.LC_STA_FOR10
		,C.LA_REQ_RDC_PAY
	FROM OLWHRM1.LN60_BR_FOR_APV B
	INNER JOIN OLWHRM1.FB10_BR_FOR_REQ C
		ON B.BF_SSN = C.BF_SSN
		AND B.LF_FOR_CTL_NUM = C.LF_FOR_CTL_NUM
	WHERE (DATEPART(B.LF_LST_DTS_LN60) >= &LAST_RUN
			OR DATEPART(C.LF_LST_DTS_FB10) >= &LAST_RUN)
		AND B.BF_SSN = &SSN
;
QUIT;
%SSN2ACC(DF10,LN_SEQ LF_DFR_CTL_NUM LN_DFR_OCC_SEQ);
%SSN2ACC(FB10,LN_SEQ LF_FOR_CTL_NUM LN_FOR_OCC_SEQ);

/*******************************************
* LOAN DATA
********************************************/
DATA LN10(DROP=	LF_LST_DTS_LN10 ) ;
	SET OLWHRM1.LN10_LON (KEEP= BF_SSN LN_SEQ LC_STA_LON10 LA_CUR_PRI LA_LON_AMT_GTR LD_END_GRC_PRD
		IC_LON_PGM LD_LON_1_DSB LF_LST_DTS_LN10 LD_PIF_RPT);
	WHERE DATEPART(LF_LST_DTS_LN10) >= &LAST_RUN
		AND BF_SSN = &SSN;
RUN;
%SSN2ACC(LN10,LN_SEQ);

/*******************************************
* ACH DATA
********************************************/
DATA LN83(DROP=LF_LST_DTS_LN83 );
	SET OLWHRM1.LN83_EFT_TO_LON(KEEP=BF_SSN LN_SEQ BN_EFT_SEQ LF_LST_DTS_LN83 
		LD_EFT_EFF_END LC_STA_LN83 );
	IF DATEPART(LF_LST_DTS_LN83) >= &LAST_RUN;
	WHERE BF_SSN = &SSN;
RUN;
PROC SORT DATA=LN83; BY BF_SSN LN_SEQ DESCENDING BN_EFT_SEQ; RUN;
DATA LN83;
SET LN83;
BY BF_SSN LN_SEQ;
IF FIRST.LN_SEQ;
RUN;
%SSN2ACC(LN83,LN_SEQ);

/*******************************************
* REHAB DATA 
********************************************/
DATA LN09(DROP= LF_LST_DTS_LN09);
	SET OLWHRM1.LN09_RPD_PIO_CVN (KEEP= BF_SSN LN_SEQ LF_LST_DTS_LN09 LD_LON_RHB_PCV);
	WHERE LD_LON_RHB_PCV ^= .
		and DATEPART(LF_LST_DTS_LN09) >= &LAST_RUN
		AND BF_SSN = &SSN;
RUN;
%SSN2ACC(LN09,LN_SEQ);

/*********************************************
* DELINQUENCY DATA
**********************************************/
DATA LN16(DROP=LF_LST_DTS_LN16 LC_STA_LON16);
	SET OLWHRM1.LN16_LON_DLQ_HST(KEEP=BF_SSN LN_SEQ LN_DLQ_SEQ LD_DLQ_OCC
		LF_LST_DTS_LN16 LC_STA_LON16 LN_DLQ_MAX LD_DLQ_MAX);
	IF DATEPART(LF_LST_DTS_LN16) >= &LAST_RUN;
	IF LC_STA_LON16 ^= '1' THEN DO;
		LN_DLQ_MAX = 0;
		LD_DLQ_OCC = .;
	END;
	WHERE BF_SSN = &SSN;
RUN;
PROC SORT DATA=LN16; BY BF_SSN LN_SEQ DESCENDING LN_DLQ_SEQ; RUN;
DATA LN16 TILPCHECK;
SET LN16(DROP=LN_DLQ_SEQ);
BY BF_SSN LN_SEQ;
IF FIRST.LN_SEQ;
RUN;
%SSN2ACC(LN16,LN_SEQ);

/*******************************************
* CALL FORWARDING DATA - ONELINK
********************************************/
PROC SQL;
CREATE TABLE TILP AS
	SELECT H.DF_SPE_ACC_ID
		,a.bf_ssn
		,A.LN_SEQ
		,CASE 
			WHEN LN_DLQ_MAX >= 90 THEN '06'
			ELSE '04'
		END AS FORWARDING
	FROM OLWHRM1.PD10_PRS_NME H 
	INNER JOIN OLWHRM1.LN10_LON A
		ON A.BF_SSN = H.DF_PRS_ID
	LEFT OUTER JOIN TILPCHECK B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	WHERE A.IC_LON_PGM = 'TILP'
		AND A.BF_SSN = &SSN;

CREATE TABLE GA10 AS
	SELECT DISTINCT	B.DF_SPE_ACC_ID
		,coalesce(a.bf_ssn,B.DF_PRS_ID) as BF_SSN
		,C.AF_APL_ID || C.AF_APL_ID_SFX AS CLUID	
		,A.LN_SEQ
		,CASE 
			WHEN D.AC_LON_STA_TYP = 'AE' THEN '05' /*A.5*/
			WHEN D.AC_LON_STA_TYP IN ('CP','CR','DN')  /*A.1*/
				AND D.AC_LON_STA_REA IN ('BC','BO','BH') 
				AND A.LA_CUR_PRI > 0
				THEN '04'
			WHEN D.AC_LON_STA_TYP IN ('CP','DN') THEN '01' /*A.2*/
			WHEN D.AC_LON_STA_TYP IN ('CA','PC','PF','PM','PN','RF') /*A.4*/ 
				AND F.AC_LON_STA_TYP IN ('CP','DN')
				THEN '01'
			WHEN D.AC_LON_STA_TYP IN ('CA','PC','PF','PM','PN','RF') 
				AND C.AF_CUR_LON_SER_AGY IN ('700079','700004','700789','700191','700190') 
				THEN '07'
			WHEN D.AC_LON_STA_TYP IN ('CA','PC','PF','PM','PN','RF')
				AND C.AF_CUR_LON_SER_AGY = '700121' 
				THEN '08' 
			WHEN D.AC_LON_STA_TYP IN ('CA','PC','PF','PM','PN','RF')
				AND C.AF_CUR_LON_SER_AGY in ('700126','700075') 
				THEN '04'
			WHEN C.AF_CUR_LON_SER_AGY IN ('700079','700004','700789','700191','700190')  /*A.3*/
				THEN '02'
			WHEN C.AF_CUR_LON_SER_AGY  = '700121' THEN '03'
			WHEN C.AF_CUR_LON_SER_AGY  = '700126' THEN '04'
			WHEN A.BF_SSN IS NULL THEN ''
			ELSE '04'
		END AS FORWARDING
	FROM OLWHRM1.PD01_PDM_INF B
	INNER JOIN OLWHRM1.GA01_APP E
		ON E.DF_PRS_ID_BR = B.DF_PRS_ID
	INNER JOIN OLWHRM1.GA10_LON_APP C
		ON E.AF_APL_ID = C.AF_APL_ID 
	INNER JOIN OLWHRM1.GA14_LON_STA D
		ON C.AF_APL_ID = D.AF_APL_ID
		AND C.AF_APL_ID_SFX = D.AF_APL_ID_SFX
		AND D.AC_STA_GA14 = 'A'
	LEFT OUTER JOIN OLWHRM1.GA14_LON_STA F
		ON D.AF_APL_ID = F.AF_APL_ID
		AND D.AF_APL_ID_SFX = F.AF_APL_ID_SFX
		AND D.AC_LON_STA_TYP IN ('CA','PC','PF','PM','PN','RF')
		AND INPUT(D.AF_LON_STA_RPT_SEQ,5.0) = INPUT(F.AF_LON_STA_RPT_SEQ,5.0)-1
	LEFT OUTER JOIN OLWHRM1.LN10_LON A
		ON A.LF_LON_ALT = C.AF_APL_ID
		AND A.LN_LON_ALT_SEQ = INPUT(C.AF_APL_ID_SFX,2.0)
	WHERE (DATEPART(E.AF_LST_DTS_GA01) >= &LAST_RUN
			OR DATEPART(C.AF_LST_DTS_GA10) >= &LAST_RUN
			OR DATEPART(D.AF_LST_DTS_GA14) >= &LAST_RUN)
		AND coalesce(a.bf_ssn,B.DF_PRS_ID) = &SSN
;
QUIT;
PROC APPEND BASE=GA10 DATA=TILP;
RUN;
PROC SORT DATA=GA10; BY DF_SPE_ACC_ID CLUID LN_SEQ; RUN;

/*******************************************
* BORROWER BENEFIT ELIGIBILITY
********************************************/
DATA LN54(DROP=LF_LST_DTS_LN54);
	SET OLWHRM1.LN54_LON_BBS(KEEP=BF_SSN LN_SEQ LC_STA_LN54 LD_BBS_DSQ LC_BBS_ELG LF_LST_DTS_LN54);
	IF DATEPART(LF_LST_DTS_LN54) >= &LAST_RUN ;
	WHERE BF_SSN = &SSN;
RUN;
PROC SORT DATA=LN54; BY BF_SSN LN_SEQ LC_STA_LN54; RUN;
DATA LN54(drop=LC_STA_LN54);
SET LN54;
BY BF_SSN LN_SEQ LC_STA_LN54;
IF FIRST.LN_SEQ;
RUN;
%SSN2ACC(LN54,LN_SEQ);

/*******************************************
* BORROWER BENEFITS
********************************************/
DATA LN55(DROP=LF_LST_DTS_LN55);
	SET OLWHRM1.LN55_LON_BBS_TIR
		(KEEP=BF_SSN LN_SEQ PM_BBS_PGM PN_BBS_PGM_SEQ LC_STA_LN55 LN_LON_BBT_PAY LF_LST_DTS_LN55);
	IF DATEPART(LF_LST_DTS_LN55) >= &LAST_RUN ;
	LN_LON_BBT_PAY = COALESCE(LN_LON_BBT_PAY,0);
	WHERE BF_SSN = &SSN;
RUN;
PROC SORT DATA=LN55; BY BF_SSN LN_SEQ LC_STA_LN55; RUN;
DATA LN55;
	SET LN55;
		BY BF_SSN LN_SEQ LC_STA_LN55;
	IF FIRST.LN_SEQ;
RUN;
DATA RP02;
	SET OLWHRM1.RP02_BBS_PGM_TIR(KEEP=PM_BBS_PGM PN_BBS_PGM_SEQ  
		PN_BBT_PAY_ICV );
RUN;
PROC SORT DATA=LN55; BY PM_BBS_PGM PN_BBS_PGM_SEQ;RUN;
PROC SORT DATA=RP02; BY PM_BBS_PGM PN_BBS_PGM_SEQ;RUN;
DATA LN55(DROP=PM_BBS_PGM PN_BBS_PGM_SEQ);
	MERGE LN55(IN=A) RP02;
		BY PM_BBS_PGM PN_BBS_PGM_SEQ;
	IF A;
RUN;
%SSN2ACC(LN55,LN_SEQ);

/*******************************************
* REPAYMENT SCHEDULE DATA
********************************************/
PROC SQL;
CREATE TABLE LN65 AS
	SELECT C.BF_SSN
		,C.LN_SEQ
		,D.LN_GRD_RPS_SEQ
		,D.LN_RPS_TRM
		,B.LD_RPS_1_PAY_DU
		,B.LD_SNT_RPD_DIS
		,C.LD_CRT_LON65
		,C.LC_TYP_SCH_DIS
		,D.LA_RPS_ISL
	FROM OLWHRM1.RS10_BR_RPD B
	INNER JOIN OLWHRM1.LN65_LON_RPS C
		ON B.BF_SSN = C.BF_SSN
		AND B.LN_RPS_SEQ = C.LN_RPS_SEQ
	INNER JOIN OLWHRM1.LN66_LON_RPS_SPF D
		ON C.BF_SSN = D.BF_SSN
		AND C.LN_SEQ = D.LN_SEQ
		AND C.LN_RPS_SEQ = D.LN_RPS_SEQ
	WHERE C.LC_STA_LON65 = 'A'
		AND C.BF_SSN = &SSN;
QUIT;
PROC SORT DATA=LN65; BY BF_SSN LN_SEQ LN_GRD_RPS_SEQ; RUN;
DATA LN65(DROP= A B C NEXT_SEQ LD_RPS_1_PAY_DU LN_GRD_RPS_SEQ);
SET LN65;
FORMAT NEXT_SEQ DATE9.;
BY BF_SSN LN_SEQ LN_GRD_RPS_SEQ;
RETAIN NEXT_SEQ A C B;
IF FIRST.LN_SEQ THEN DO;
	A = .;
	NEXT_SEQ = INTNX('MONTH',LD_RPS_1_PAY_DU,LN_RPS_TRM,'S');
	IF NEXT_SEQ > TODAY() THEN DO;
		A = LN_GRD_RPS_SEQ ;
		C = LA_RPS_ISL ;
	END;
END;
ELSE IF A= . THEN DO;
	NEXT_SEQ = INTNX('MONTH',NEXT_SEQ,LN_RPS_TRM,'S');
	IF NEXT_SEQ > TODAY() THEN DO;
		A = LN_GRD_RPS_SEQ ;
		C = LA_RPS_ISL ;
	END;
END;
IF FIRST.LN_SEQ THEN B = LN_RPS_TRM;
ELSE B = B + LN_RPS_TRM;
IF LAST.LN_SEQ THEN DO;
	LN_RPS_TRM = B;
	LA_RPS_ISL = C;
	LN_GRD_RPS_SEQ = A;
	DAY_DUE = DAY(LD_RPS_1_PAY_DU);
	OUTPUT;
END;
RUN;
%SSN2ACC(LN65,LN_SEQ);

/*******************************************
* INTEREST RATE DATA
********************************************/
DATA LN72(DROP=LC_STA_LON72 LD_ITR_EFF_BEG LD_ITR_EFF_END LF_LST_DTS_LN72);
	SET OLWHRM1.LN72_INT_RTE_HST(KEEP=BF_SSN LN_SEQ LC_STA_LON72 
		LD_ITR_EFF_BEG LD_ITR_EFF_END LF_LST_DTS_LN72 LR_ITR LR_INT_RDC_PGM_ORG);
	WHERE LC_STA_LON72 = 'A'
		AND LD_ITR_EFF_BEG <= TODAY() 
		AND LD_ITR_EFF_END >= TODAY() 
		AND DATEPART(LF_LST_DTS_LN72) >= &LAST_RUN
		AND BF_SSN = &SSN;
	LR_ITR = COALESCE(LR_ITR,0);
	LR_INT_RDC_PGM_ORG = COALESCE(LR_INT_RDC_PGM_ORG,LR_ITR);
RUN;
%SSN2ACC(LN72,LN_SEQ);

/*********************************************
* ENROLLMENT - LOAN LEVEL
**********************************************/
PROC SQL;
CREATE TABLE SD10 AS
	SELECT A.BF_SSN
		,A.LN_SEQ
		,B.LN_STU_SPR_SEQ
		,B.LD_SCL_SPR
		,D.IM_SCL_FUL
	FROM OLWHRM1.SD10_STU_SPR B
	INNER JOIN OLWHRM1.SC10_SCH_DMO D
		ON B.LF_DOE_SCL_ENR_CUR = D.IF_DOE_SCL
	INNER JOIN OLWHRM1.LN13_LON_STU_OSD C
		ON B.LF_STU_SSN = C.LF_STU_SSN
		AND B.LN_STU_SPR_SEQ = C.LN_STU_SPR_SEQ
	INNER JOIN OLWHRM1.LN10_LON A
		ON C.BF_SSN = A.BF_SSN
		AND C.LN_SEQ = A.LN_SEQ
	WHERE B.LC_STA_STU10 = 'A'
		AND C.LC_STA_LON13 = 'A'
		AND (DATEPART(B.LF_LST_DTS_SD10) >= &LAST_RUN
			OR DATEPART(C.LF_CRT_DTS_LN13) >= &LAST_RUN)
		AND A.BF_SSN = &SSN;
QUIT;
PROC SORT DATA=SD10; BY BF_SSN LN_SEQ LN_STU_SPR_SEQ;RUN;
DATA SD10(DROP=LN_STU_SPR_SEQ);
	SET SD10;
		BY BF_SSN LN_SEQ LN_STU_SPR_SEQ;
	IF LAST.LN_SEQ;
RUN;
%SSN2ACC(SD10,LN_SEQ);

/*********************************************
* EMPLOYER DATA
**********************************************/
PROC SQL;
CREATE TABLE BR02 AS
	SELECT A.BF_SSN
		,B.IM_IST_FUL
		,B.IX_GEN_STR_ADR_1
		,B.IX_GEN_STR_ADR_2
		,B.IM_GEN_CT
		,B.IC_GEN_ST
		,B.IF_GEN_ZIP
		,B.IN_GEN_PHN
	FROM OLWHRM1.BR02_BR_EMP A
	INNER JOIN OLWHRM1.IN01_LGS_IDM_MST B
		ON A.BF_EMP_ID_1 = B.IF_IST
	WHERE DATEPART(A.BF_LST_DTS_BR02) >= &LAST_RUN
		AND A.BF_SSN = &SSN;
QUIT;
%SSN2ACC(BR02,);

/*********************************************
* AUTOPAY DATA
**********************************************/
DATA BR30(DROP=BF_LST_DTS_BR30 );
	SET OLWHRM1.BR30_BR_EFT(KEEP=BF_SSN BN_EFT_SEQ BF_EFT_ABA BF_EFT_ACC BC_EFT_STA
		BD_EFT_STA BA_EFT_ADD_WDR BN_EFT_NSF_CTR BN_EFT_DAY_DUE BC_EFT_DNL_REA BF_LST_DTS_BR30);
	IF DATEPART(BF_LST_DTS_BR30) >= &LAST_RUN ;
	WHERE BF_SSN = &SSN;
RUN;
PROC SORT DATA=BR30; BY BF_SSN BC_EFT_STA DESCENDING BN_EFT_SEQ; RUN;
DATA BR30; 
SET BR30; 
BY BF_SSN BC_EFT_STA DESCENDING BN_EFT_SEQ;
IF FIRST.BF_SSN OR BC_EFT_STA = 'A';
RUN;
PROC SORT DATA=BR30; BY BF_SSN BN_EFT_SEQ; RUN;
DATA BR30(DROP=A B BN_EFT_DAY_DUE);
FORMAT A BN_EFT_DAY_DUE $8.;
SET BR30;
BY BF_SSN;
RETAIN A B;
IF FIRST.BF_SSN AND LAST.BF_SSN THEN OUTPUT;
ELSE IF FIRST.BF_SSN AND LAST.BF_SSN = 0 AND BC_EFT_STA = 'A' THEN DO;
	A = BN_EFT_DAY_DUE;
	B = BA_EFT_ADD_WDR;
END;
ELSE IF FIRST.BF_SSN = 0 AND BC_EFT_STA = 'A' THEN DO;
	IF A ^= BN_EFT_DAY_DUE THEN A = TRIM(A) || ',' || BN_EFT_DAY_DUE;
	B = B + BA_EFT_ADD_WDR;
END;
IF FIRST.BF_SSN = 0 AND LAST.BF_SSN THEN DO;
	BN_EFT_DAY_DUE = A;
	BA_EFT_ADD_WDR = B;
	OUTPUT; 
END;
RUN;
%SSN2ACC(BR30,);

/*********************************************
* DEMOGRAPHICS
**********************************************/
PROC SQL;
CREATE TABLE PD10 AS
	SELECT distinct A.DF_SPE_ACC_ID
		,a.df_prs_id as bf_ssn
		,A.DM_PRS_1
		,A.DM_PRS_LST
		,A.DM_PRS_MID
		,A.DD_BRT
	FROM OLWHRM1.PD10_PRS_NME A
	WHERE substr(df_prs_id,1,1) ^= 'P'
		and DATEPART(A.DF_LST_DTS_PD10) >= &LAST_RUN
		AND A.df_prs_id = &SSN;

CREATE TABLE PD30 AS
	SELECT distinct c.df_prs_id as bf_ssn
		,C.DX_STR_ADR_1
		,C.DX_STR_ADR_2
		,C.DM_CT
		,C.DC_DOM_ST
		,C.DF_ZIP_CDE
		,C.DM_FGN_ST
		,C.DM_FGN_CNY
		,C.DD_VER_ADR
		,C.DI_VLD_ADR
	FROM OLWHRM1.PD30_PRS_ADR C
	WHERE C.DC_ADR = 'L'
		AND DATEPART(C.DF_LST_DTS_PD30) >= &LAST_RUN
		AND C.df_prs_id = &SSN;

CREATE TABLE PD42 AS
	SELECT DISTINCT d.df_prs_id as bf_ssn
		,D.DC_PHN
		,D.DC_ALW_ADL_PHN
		,D.DD_PHN_VER
		,D.DI_PHN_VLD
		,D.DN_DOM_PHN_ARA
		,D.DN_DOM_PHN_XCH
		,D.DN_DOM_PHN_LCL
		,D.DN_PHN_XTN
		,D.DN_FGN_PHN_CNY
		,D.DN_FGN_PHN_CT
		,D.DN_FGN_PHN_LCL
	FROM  OLWHRM1.PD42_PRS_PHN D
	WHERE DATEPART(D.DF_LST_DTS_PD42) >= &LAST_RUN
		AND D.df_prs_id = &SSN;

CREATE TABLE PD32 AS
	SELECT DISTINCT g.df_prs_id as bf_ssn
		,G.DC_ADR_EML
		,G.DX_ADR_EML 
		,G.DD_VER_ADR_EML
		,G.DI_VLD_ADR_EML
	FROM OLWHRM1.PD32_PRS_ADR_EML G
	WHERE DATEPART(G.DF_LST_DTS_PD32) >= &LAST_RUN
		AND G.df_prs_id = &SSN;
QUIT;
proc sort data=pd32; by bf_ssn dc_adr_eml descending dd_ver_adr_eml; run;
data pd32;
set pd32;
by bf_ssn dc_adr_eml;
if first.dc_adr_eml;
run;
%SSN2ACC(PD30,);
%SSN2ACC(PD42,DC_PHN);
%SSN2ACC(PD32,dc_adr_eml);

/*********************************************
* SUSPENSE PAYMENTS
**********************************************/
PROC SQL;
CREATE TABLE RM31 AS
	SELECT DISTINCT A.BF_SSN
		,SUM(A.LA_BR_RMT_PST) AS LA_BR_RMT_PST
	FROM OLWHRM1.RM31_BR_RMT_PST A
	WHERE LC_RMT_STA_PST = 'S'
		AND BF_SSN = &SSN
	GROUP BY BF_SSN;
QUIT;
%SSN2ACC(RM31,);

/*********************************************
* AMOUNTS DUE INFORMATION
**********************************************/
PROC SQL ;
CREATE TABLE CUR_DUE AS
SELECT A.BF_SSN
	,B.LD_BIL_CRT
	,B.LN_SEQ_BIL_WI_DTE
	,B.LD_BIL_DU_LON
	,SUM(COALESCE(B.LA_BIL_CUR_DU,0) - COALESCE(B.LA_TOT_BIL_STS,0)) AS Cur_Due
	,A.LA_LTE_FEE_OTS
from (SELECT BF_SSN
			,LN_SEQ
			,LA_CUR_PRI
			,LC_STA_LON10
			,SUM(COALESCE(LA_LTE_FEE_OTS,0)) AS LA_LTE_FEE_OTS
		FROM OLWHRM1.LN10_LON 
		GROUP BY BF_SSN) A
LEFT OUTER JOIN OLWHRM1.LN80_LON_BIL_CRF b
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND b.lc_sta_lon80 = 'A'
	AND B.LC_LON_STA_BIL = '1'
	AND A.LC_STA_LON10 = 'R'
	AND A.LA_CUR_PRI > 0
WHERE
	A.BF_SSN = &SSN
GROUP BY A.BF_SSN
	,B.LD_BIL_CRT
	,B.LN_SEQ_BIL_WI_DTE
	,B.LD_BIL_DU_LON
;
QUIT;
PROC SORT DATA=CUR_DUE; BY BF_SSN LD_BIL_CRT LN_SEQ_BIL_WI_DTE; RUN;
DATA CUR_DUE(keep=bf_ssn cur_due past_due tot_due tot_due_fee); 
SET CUR_DUE;
BY BF_SSN ;
RETAIN PAST_DUE a 0 ;
IF FIRST.BF_SSN THEN do;
	PAST_DUE = 0;
	a = 0;
end;

IF LD_BIL_DU_LON < TODAY() THEN PAST_DUE = PAST_DUE + CUR_DUE;
else do;
	if ld_bil_du_lon > intnx('month',today(),+1,'s') then cur_due = 0;
	a = a + cur_due;
end;

IF LAST.BF_SSN THEN DO;
	cur_due = a;
	if intnx('month',today(),-1,'s') >= ld_bil_du_lon then do;
		cur_due = 0;
		past_due = 0;
		LA_LTE_FEE_OTS = 0;
	end;
	if intnx('month',ld_bil_du_lon,-1,'s') > ld_bil_crt then do;
		past_due = 0;
		LA_LTE_FEE_OTS = 0;
	end;
	TOT_DUE = PAST_DUE + CUR_DUE;
	TOT_DUE_FEE = TOT_DUE + LA_LTE_FEE_OTS;

	OUTPUT;
END;
RUN;
%SSN2ACC(CUR_DUE,);

/*********************************************
* ADDITIONAL LOAN DATA
**********************************************/
PROC SQL;
CREATE TABLE DW01 AS
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.WC_DW_LON_STA 
	,A.WA_TOT_BRI_OTS 
	,A.WD_LON_RPD_SR 
	,A.WX_OVR_DW_LON_STA
FROM OLWHRM1.DW01_DW_CLC_CLU A
INNER JOIN OLWHRM1.LN10_LON B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE (DATEPART(LF_LST_DTS_LN10) >= &LAST_RUN
	OR B.LA_CUR_PRI > 0)
	AND A.BF_SSN = &SSN;
QUIT;
%SSN2ACC(DW01,LN_SEQ);

/*********************************************
* CREDIT REPORTING INFORMATION
**********************************************/
PROC SQL;
CREATE TABLE LN56 AS
SELECT A.BF_SSN 
	,A.LN_SEQ
	,A.LC_RPT_STA_CRB
	,A.LD_RPT_CRB
	,B.LD_STA_LN56 AS DT_ADJ
FROM OLWHRM1.LN56_LON_CRB_RPT A
LEFT OUTER JOIN OLWHRM1.LN56_LON_CRB_RPT B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND A.LD_RPT_CRB = B.LD_RPT_CRB
	AND SUBSTR(A.LF_LST_USR_LN56,1,2) = 'UT'
	AND B.LC_STA_LN56 = 'I'
WHERE A.LD_RPT_CRB > INTNX('YEAR',TODAY(),-1,'S')
	AND A.LC_STA_LN56 = 'A'
	AND DATEPART(A.LF_LST_DTS_LN56) >= &LAST_RUN
	AND A.BF_SSN = &SSN;
QUIT;
%SSN2ACC(LN56,LN_SEQ);

/*********************************************
* ACTIVITY HISTORY
**********************************************/
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ARCS AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT AY10.BF_SSN
		,AY10.LN_ATY_SEQ
		,AY10.PF_REQ_ACT
		,AY10.LC_STA_ACTY10
		,LX_ATY
	FROM OLWHRM1.AY10_BR_LON_ATY AY10
	LEFT OUTER JOIN OLWHRM1.AY15_ATY_CMT AY15
		ON AY10.BF_SSN = AY15.BF_SSN 
		AND AY10.LN_ATY_SEQ = AY15.LN_ATY_SEQ
		AND AY15.LN_ATY_CMT_SEQ = 1
 	LEFT OUTER JOIN OLWHRM1.AY20_ATY_TXT AY20
		ON AY15.BF_SSN = AY20.BF_SSN 
		AND AY15.LN_ATY_SEQ = AY20.LN_ATY_SEQ
		AND AY15.LN_ATY_CMT_SEQ = AY20.LN_ATY_CMT_SEQ
	WHERE AY10.PF_REQ_ACT IN ('DRLFA','K0ADD','K0PHN','M1411')
		AND (AY10.LF_LST_DTS_AY10 >= &LAST_RUNPASS or AY10.PF_REQ_ACT = 'DRLFA')
		AND AY10.BF_SSN = &SSN
FOR READ ONLY WITH UR
);

CREATE TABLE ARC_IND AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT AY10.BF_SSN
		,AY10.LN_ATY_SEQ
		,AY10.PF_REQ_ACT
		,AY10.LC_STA_ACTY10
	FROM OLWHRM1.AY10_BR_LON_ATY AY10
	WHERE AY10.PF_REQ_ACT IN ('SPHAE','VIPSS')
		AND AY10.LF_LST_DTS_AY10 >= &LAST_RUNPASS
		AND AY10.BF_SSN = &SSN
FOR READ ONLY WITH UR
);

CREATE TABLE DL200 AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT AY10.BF_SSN
		,LN_ATY_SEQ
		,LD_ATY_REQ_RCV
		,LC_STA_ACTY10
	FROM OLWHRM1.AY10_BR_LON_ATY AY10
	WHERE PF_REQ_ACT = 'DL200'
		AND AY10.LF_LST_DTS_AY10 >= &LAST_RUNPASS
		AND AY10.BF_SSN = &SSN
);

CREATE TABLE ARCHIST AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT A.BF_SSN
		,A.LN_ATY_SEQ
		,A.LC_STA_ACTY10
		,A.PF_REQ_ACT
		,A.PF_RSP_ACT
		,A.LD_ATY_REQ_RCV
		,A.LD_ATY_RSP
		,A.LF_USR_REQ_ATY
		,A.LT_ATY_RSP
		,B.LX_ATY
		,c.px_act_dsc_req
	FROM OLWHRM1.AY10_BR_LON_ATY A
 	LEFT OUTER JOIN OLWHRM1.AY20_ATY_TXT B
		ON A.BF_SSN = B.BF_SSN 
		AND A.LN_ATY_SEQ = B.LN_ATY_SEQ
		AND B.LN_ATY_CMT_SEQ = 1
		AND B.LN_ATY_TXT_SEQ = 1
	LEFT OUTER JOIN OLWHRM1.AC10_ACT_REQ c
		on a.pf_req_act = c.pf_req_act
	WHERE LC_ATY_RCP  = 'B'
		and a.LF_LST_DTS_AY10 >= &LAST_RUNPASS
		AND A.BF_SSN = &SSN
);
DISCONNECT FROM DB2;
QUIT;

DATA DRLFA ARCS(DROP=DOL) ;
SET ARCS;
IF PF_REQ_ACT = 'DRLFA' THEN DO;
	IF SUBSTR(LX_ATY,1,1) = '{' and lc_sta_acty10 = 'A' THEN DO;
		DOL = INPUT(SCAN(LX_ATY,1,'{}'),DOLLAR8.2);
		OUTPUT DRLFA;
	END;
END;
ELSE OUTPUT ARCS;
RUN;

PROC SORT DATA=DRLFA; BY BF_SSN;RUN;
DATA DRLFA (KEEP=BF_SSN FEE_WAV_DOL FEE_WAV_CT);
	SET DRLFA;
	RETAIN FEE_WAV_DOL FEE_WAV_CT;
	BY BF_SSN;
	IF FIRST.BF_SSN THEN DO;
		FEE_WAV_DOL = DOL;
		FEE_WAV_CT = 1;
	END;
	ELSE DO;
		FEE_WAV_DOL + DOL;
		FEE_WAV_CT + 1;
	END;
	IF LAST.BF_SSN THEN OUTPUT;	
RUN;

DATA K0ADD(KEEP=BF_SSN LN_ATY_SEQ LC_STA_ACTY10 DX_STR_ADR_1 DX_STR_ADR_2 DM_CT DC_DOM_ST DF_ZIP_CDE DM_FGN_CNY COMMENTS)
	K0PHN(KEEP=BF_SSN LN_ATY_SEQ LC_STA_ACTY10 PHN1 PHN2 PHN3 COMMENTS)
	ARC_M1411(KEEP=BF_SSN LN_ATY_SEQ LC_STA_ACTY10 LX_ATY);
SET ARCS;
LENGTH DX_STR_ADR_1 $30. DX_STR_ADR_2 $30. DM_CT $20. DC_DOM_ST $2. DF_ZIP_CDE $17. DM_FGN_CNY $25. COMMENTS $300.;
LENGTH  PHN1 PHN2 PHN3 $25.;
ARRAY ADR{7} $ DX_STR_ADR_1 DX_STR_ADR_2 DM_CT DC_DOM_ST DF_ZIP_CDE DM_FGN_CNY COMMENTS;
ARRAY PHN{4} $ PHN1 PHN2 PHN3 COMMENTS;
IF PF_REQ_ACT = 'K0ADD' THEN DO;
	DO I = 1 TO 7;
		ADR(I) = SCAN(LX_ATY,I,',');
	END;
	OUTPUT K0ADD;
END;
ELSE if pf_req_act = 'K0PHN' THEN DO;
	DO I = 1 TO 4;
		PHN(I) = SCAN(LX_ATY,I,',');
	END;
	OUTPUT K0PHN;
END;
ELSE IF PF_REQ_ACT = 'M1411' THEN OUTPUT ARC_M1411;
RUN;

%SSN2ACC(K0ADD,LN_ATY_SEQ);
%SSN2ACC(K0PHN,LN_ATY_SEQ);
%SSN2ACC(DL200,LN_ATY_SEQ);
%SSN2ACC(DRLFA,);
%SSN2ACC(ARCHIST,LN_ATY_SEQ); 
%SSN2ACC(ARC_M1411,LN_ATY_SEQ);
%SSN2ACC(ARC_IND,LN_ATY_SEQ); 

/*DATA SAS_TAB.LASTRUN_JOBS;*/
/*SET SAS_TAB.LASTRUN_JOBS;*/
/*IF JOB = 'UTLWDW1' THEN LAST_RUN = TODAY();*/
/*RUN;*/
ENDRSUBMIT;
/*data _null_;*/
/*call symput('endrsub',put(time(),time9.));*/
/*run;*/

DATA LN20; SET DUSTER.LN20; RUN; *2;
DATA BR03; SET DUSTER.BR03; RUN; *3;
DATA LN80; SET DUSTER.LN80; RUN; *4;
DATA LN90; SET DUSTER.LN90; RUN; *5;
DATA DF10; SET DUSTER.DF10; RUN; *6;
DATA FB10; SET DUSTER.FB10; RUN; *7;
DATA PD10; SET DUSTER.PD10; RUN; *8;
DATA K0PHN; SET DUSTER.K0PHN; RUN; *9;  
DATA DRLFA; SET DUSTER.DRLFA; RUN; *10;
DATA LN10; SET DUSTER.LN10; RUN; *11;
DATA LN83; SET DUSTER.LN83; RUN; *12;
DATA LN09; SET DUSTER.LN09; RUN; *13;
DATA GA10; SET DUSTER.GA10; RUN; *14;
DATA LN54; SET DUSTER.LN54; RUN; *15;
DATA LN55; SET DUSTER.LN55; RUN; *16;
DATA LN65; SET DUSTER.LN65; RUN; *17;
DATA LN72; SET DUSTER.LN72; RUN; *18;
DATA SD10; SET DUSTER.SD10; RUN; *19;
DATA LN16; SET DUSTER.LN16; RUN; *20;
DATA BR02; SET DUSTER.BR02; RUN; *21;
DATA BR30; SET DUSTER.BR30; RUN; *22;
DATA PD32; SET DUSTER.PD32; RUN; *23;
DATA RM31; SET DUSTER.RM31; RUN; *24;
DATA PD30; SET DUSTER.PD30; RUN; *25;
DATA PD42; SET DUSTER.PD42; RUN; *26;
DATA K0ADD; SET DUSTER.K0ADD; RUN; *27;
DATA ARCHIST; SET DUSTER.ARCHIST; RUN; *28;
DATA DW01; SET DUSTER.DW01; RUN; *29;
DATA LN56; SET DUSTER.LN56; RUN; *30;
DATA CUR_DUE; SET DUSTER.CUR_DUE; RUN; *31;
DATA DL200; SET DUSTER.DL200; RUN; *32; 
DATA ARC_M1411; SET DUSTER.ARC_M1411; RUN; *33;
DATA ARC_IND; SET DUSTER.ARC_IND; RUN; *34;  
data _null_;
call symput('FILETRAN',put(time(),time9.));
run;
DATA _NULL_;
SET LN20 end = eof;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LC_STA_LON20 $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET BR03 end = eof;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LST_CNC LST_ATT MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT DF_PRS_ID_RFR  @;
	PUT BC_STA_BR03 @;
	PUT BI_ATH_3_PTY @;
	PUT BC_RFR_REL_BR @;
	PUT BM_RFR_1 @;
	PUT BM_RFR_LST @;
	PUT LST_CNC @;
	PUT LST_ATT $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN80 end = eof;
FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_BIL_CRT LD_FAT_EFF LD_BIL_DU_LON MMDDYY10. ;
FORMAT 	LA_BIL_CUR_DU LA_BIL_PAS_DU	LA_TOT_BIL_STS 9.2;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LD_BIL_CRT @;
	PUT LN_SEQ_BIL_WI_DTE @;
	PUT LD_FAT_EFF @;
	PUT LD_BIL_DU_LON @;
	PUT LC_STA_LON80 @;
	PUT LA_BIL_CUR_DU @;
	PUT LA_BIL_PAS_DU @;
	PUT LC_BIL_MTD @;
	PUT LC_IND_BIL_SNT @;
	PUT LC_STA_BIL10 @;
	PUT LA_TOT_BIL_STS $;
END;
if eof then put "-End-";
RUN; 

DATA _NULL_;
SET LN90 end = eof;
FILE REPORT5 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_FAT_PST LD_FAT_EFF MMDDYY10. ;
FORMAT LA_FAT_CUR_PRI LA_FAT_LTE_FEE 10.2 ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LN_FAT_SEQ @;
	PUT LD_FAT_PST @;
	PUT LD_FAT_EFF  @;
	PUT LC_STA_LON90 @;
	PUT LA_FAT_CUR_PRI @;
	PUT LA_FAT_LTE_FEE @;
	PUT PC_FAT_TYP @;
	PUT PC_FAT_SUB_TYP @;
	PUT LA_FAT_NSI @;
	PUT LC_FAT_REV_REA $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET DF10 end = eof;
FILE REPORT6 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_DFR_INF_CER LD_DFR_BEG LD_DFR_END MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LF_DFR_CTL_NUM @;
	PUT LN_DFR_OCC_SEQ @;
	PUT LC_DFR_TYP @;
	PUT LD_DFR_INF_CER @;
	PUT LD_DFR_BEG @;
	PUT LD_DFR_END @;
	PUT LC_LON_LEV_DFR_CAP @;
	PUT LC_STA_LON50 @;
	PUT LC_DFR_STA @;
	PUT LC_STA_DFR10 $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET FB10 end = eof;
FILE REPORT7 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_FOR_INF_CER LD_FOR_BEG LD_FOR_END MMDDYY10. LA_REQ_RDC_PAY 9.2;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LF_FOR_CTL_NUM @;
	PUT LN_FOR_OCC_SEQ @;
	PUT LC_FOR_TYP @;
	PUT LD_FOR_INF_CER @;
	PUT LD_FOR_BEG @;
	PUT LD_FOR_END @;
	PUT LC_LON_LEV_FOR_CAP @;
	PUT LC_STA_LON60 @;
	PUT LC_FOR_STA @;
	PUT LC_STA_FOR10 @;
	PUT LA_REQ_RDC_PAY $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET PD10 end = eof;
FILE REPORT8 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DD_BRT MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT BF_SSN @;
	PUT DM_PRS_1 @;
	PUT DM_PRS_LST @;
	PUT DM_PRS_MID @;
	PUT DD_BRT $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET K0PHN end = eof;
FILE REPORT9 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;	
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ @;
	PUT LC_STA_ACTY10 @;
	PUT PHN1 @;
	PUT PHN2 @;
	PUT PHN3 @;
	PUT COMMENTS $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET DRLFA end = eof;
FILE REPORT10 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT FEE_WAV_DOL @;
	PUT FEE_WAV_CT $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN10 end = eof;
FILE REPORT11 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_END_GRC_PRD LD_LON_1_DSB  LD_PIF_RPT MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LC_STA_LON10 @;
	PUT LA_CUR_PRI  @;
	PUT LA_LON_AMT_GTR @;
	PUT LD_END_GRC_PRD @;
	PUT IC_LON_PGM @;
	PUT LD_LON_1_DSB @;
	PUT LD_PIF_RPT $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN83 end = eof;
FILE REPORT12 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT BN_EFT_SEQ @;
	PUT LC_STA_LN83 $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN09 end = eof;
FILE REPORT13 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_LON_RHB_PCV MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LD_LON_RHB_PCV $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET GA10 end = eof;
FILE REPORT14 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT BF_SSN @;
	PUT CLUID @;
	PUT LN_SEQ @;
	PUT FORWARDING $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN54 end = eof;
FILE REPORT15 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_BBS_DSQ MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LD_BBS_DSQ  @;
	PUT LC_BBS_ELG $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN55 end = eof;
FILE REPORT16 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LN_LON_BBT_PAY @;
	PUT PN_BBT_PAY_ICV $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN65 end = eof;
FILE REPORT17 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_CRT_LON65 LD_SNT_RPD_DIS MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LD_CRT_LON65 @;	
	PUT LC_TYP_SCH_DIS @; 
	PUT LD_SNT_RPD_DIS @;
	PUT LA_RPS_ISL @;
	PUT DAY_DUE @;
	PUT LN_RPS_TRM $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN72 end = eof;
FILE REPORT18 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LR_ITR @;
	PUT LR_INT_RDC_PGM_ORG $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET SD10 end = eof;
FILE REPORT19 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_SCL_SPR MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LD_SCL_SPR @;
	PUT IM_SCL_FUL $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN16 end = eof;
FILE REPORT20 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_DLQ_OCC LD_DLQ_MAX MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ @;
	PUT LD_DLQ_OCC @;
	PUT LN_DLQ_MAX @;
	PUT LD_DLQ_MAX $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET BR02 end = eof;
FILE REPORT21 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT IM_IST_FUL @;
	PUT IX_GEN_STR_ADR_1 @;
	PUT IX_GEN_STR_ADR_2 @;
	PUT IM_GEN_CT @;
	PUT IC_GEN_ST @;
	PUT IF_GEN_ZIP @;
	PUT IN_GEN_PHN $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET BR30 end = eof;
FILE REPORT22 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT BD_EFT_STA MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT BN_EFT_SEQ @;
	PUT BF_EFT_ABA @;
	PUT BF_EFT_ACC @;
	PUT BC_EFT_STA @;
	PUT BD_EFT_STA @;
	PUT BA_EFT_ADD_WDR @;
	PUT BN_EFT_NSF_CTR @;
	PUT BC_EFT_DNL_REA $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET PD32 end = eof;
FILE REPORT23 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DD_VER_ADR_EML MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT DC_ADR_EML @;
	PUT DX_ADR_EML @;
	PUT DD_VER_ADR_EML @;
	PUT DI_VLD_ADR_EML $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET RM31 end = eof;
FILE REPORT24 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LA_BR_RMT_PST 10.2 ;

IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LA_BR_RMT_PST $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET PD30 end = eof;
FILE REPORT25 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DD_VER_ADR MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT DX_STR_ADR_1 @;
	PUT DX_STR_ADR_2 @;
	PUT DM_CT @;
	PUT DC_DOM_ST @;
	PUT DF_ZIP_CDE @;
	PUT DM_FGN_ST @;
	PUT DM_FGN_CNY @;
	PUT DD_VER_ADR @;
	PUT DI_VLD_ADR $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET PD42 end = eof;
FILE REPORT26 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DD_PHN_VER MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT DC_PHN @;
	PUT DC_ALW_ADL_PHN @;
	PUT DD_PHN_VER @;
	PUT DI_PHN_VLD @;
	PUT DN_DOM_PHN_ARA @;
	PUT DN_DOM_PHN_XCH @;
	PUT DN_DOM_PHN_LCL @;
	PUT DN_PHN_XTN @;
	PUT DN_FGN_PHN_CNY @;
	PUT DN_FGN_PHN_CT @;
	PUT DN_FGN_PHN_LCL $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET K0ADD end = eof;
FILE REPORT27 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ  @;
	PUT LC_STA_ACTY10 @;
	PUT DX_STR_ADR_1 @;
	PUT DX_STR_ADR_2 @;
	PUT DM_CT @;
	PUT DC_DOM_ST @;
	PUT DF_ZIP_CDE @;
	PUT DM_FGN_CNY @;
	PUT COMMENTS $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET ARCHIST end = eof;
FILE REPORT28 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_ATY_REQ_RCV LD_ATY_RSP MMDDYY10.;
FORMAT LT_ATY_RSP TIME8.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ @;
	PUT LC_STA_ACTY10 @;
	PUT PF_REQ_ACT @;
	PUT PF_RSP_ACT @;
	PUT PX_ACT_DSC_REQ @;
	PUT LD_ATY_REQ_RCV @;
	PUT LD_ATY_RSP @;
	PUT LF_USR_REQ_ATY @;
	PUT LT_ATY_RSP @;
	PUT LX_ATY $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET DW01 end = eof;
FILE REPORT29 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT WA_TOT_BRI_OTS 9.2 WD_LON_RPD_SR mmddyy10.;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT WC_DW_LON_STA  @;
	PUT WA_TOT_BRI_OTS @;
	PUT WD_LON_RPD_SR @;
	PUT WX_OVR_DW_LON_STA $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET LN56 end = eof;
FILE REPORT30 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DT_ADJ LD_RPT_CRB MMDDYY10. ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT LN_SEQ  @;
	PUT LC_RPT_STA_CRB  @;
	PUT LD_RPT_CRB @;
	PUT DT_ADJ $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET CUR_DUE end = eof;
FILE REPORT31 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT cur_due past_due tot_due tot_due_fee 9.2 ;
IF _N_ = 1 THEN put "-Begin-";
DO;
	PUT DF_SPE_ACC_ID @;
	PUT cur_due @;
	PUT past_due @;
	PUT tot_due @;
	PUT tot_due_fee $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET DL200 end = eof;
FILE REPORT32 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT LD_ATY_REQ_RCV MMDDYY10.;
IF _N_ = 1 THEN put "-Begin-";
DO;	
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ @;
	PUT LC_STA_ACTY10 @;
	PUT LD_ATY_REQ_RCV $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET ARC_M1411 end = eof;
FILE REPORT33 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;	
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ @;
	PUT LC_STA_ACTY10 @;
	PUT LX_ATY $;
end;
if eof then put "-End-";
RUN;

DATA _NULL_;
SET ARC_IND end = eof;
FILE REPORT34 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN put "-Begin-";
DO;	
	PUT DF_SPE_ACC_ID @;
	PUT LN_ATY_SEQ @;
	PUT PF_REQ_ACT @;
	PUT LC_STA_ACTY10 $;
end;
if eof then put "-End-";
RUN;
/*%PUT &rsub &ENDRSUB &FILETRAN ;*/
