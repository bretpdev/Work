USE [ULS]
GO
/****** Object:  StoredProcedure [achrirdf].[AddNewWorkToQueue]    Script Date: 9/5/2017 8:08:36 AM ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE .[achrirdf].[AddNewWorkToQueue]
AS
BEGIN

IF OBJECT_ID('tempdb..##AchData') IS NOT NULL
    DROP TABLE #AchData

CREATE TABLE #AchData
(
	Report VARCHAR(3),
	Ssn CHAR(9),
	AccountNumber CHAR(10),
	LoanSequence VARCHAR(3),
	OwnerCode VARCHAR(8),
	DefermentOrForbearanceOriginalBeginDate DATE,
	DefermentOrForbearanceOriginalEndDate DATE,
	DefermentOrForbearanceBeginDate DATE,
	DefermentOrForbearanceEndDate DATE,
	UpdatedAt DATETIME	
)

DECLARE @LASTRUN VARCHAR(30) = 
(
	SELECT 
		ISNULL(CONVERT(DATE,MAX(UpdatedAt),101), '2017-02-04')
	FROM 
		ULS.[achrirdf].[ProcessQueue]
)

INSERT INTO #AchData
SELECT 
	*
FROM
(
	SELECT DISTINCT
		'R2D' AS Report,
		LN10.BF_SSN AS Ssn,
		PD10.DF_SPE_ACC_ID AS AccountNumber,
		LN10.LN_SEQ AS LoanSequence,
		LN10.LF_LON_CUR_OWN AS OwnerCode,
		NULL AS DefermentOrForbearanceOriginalBeginDate,
		NULL AS DefermentOrForbearanceOriginalEndDate,
		CASE WHEN LN50.LD_DFR_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN50.LD_DFR_BEG 
		END AS DefermentOrForbearanceBeginDate,
		CASE WHEN LN50.LD_DFR_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN50.LD_DFR_END 
		END AS DefermentOrForbearanceEndDate,
		LN50.LF_LST_DTS_LN50 AS UpdatedAt
	FROM
		UDW..LN10_LON LN10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN UDW..LN83_EFT_TO_LON LN83
			ON LN10.BF_SSN = LN83.BF_SSN
			AND LN10.LN_SEQ = LN83.LN_SEQ
			AND LN83.LC_STA_LN83 = 'A'
		INNER JOIN UDW..LN50_BR_DFR_APV LN50
			ON LN10.BF_SSN = LN50.BF_SSN
			AND LN10.LN_SEQ = LN50.LN_SEQ
			AND LN50.LF_LST_DTS_LN50 > @LASTRUN 
			AND LN50.LC_STA_LON50 = 'A'
			AND LN50.LN_DFR_OCC_SEQ = '1'
			AND LN50.LC_DFR_RSP != '003'
		INNER JOIN UDW..DF10_BR_DFR_REQ DF10
			ON LN50.BF_SSN = DF10.BF_SSN
			AND LN50.LF_DFR_CTL_NUM = DF10.LF_DFR_CTL_NUM
			AND DF10.LC_DFR_STA = 'A'
			AND DF10.LC_STA_DFR10 = 'A'
			AND DF10.LF_DFR_PRV IS NULL 
	WHERE 
		LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.LF_LON_CUR_OWN LIKE '8297690%'

	UNION ALL

	SELECT DISTINCT
		'R2F' AS Report,
		LN10.BF_SSN AS Ssn,
		PD10.DF_SPE_ACC_ID AS AccountNumber,
		LN10.LN_SEQ AS LoanSequence,
		LN10.LF_LON_CUR_OWN AS OwnerCode,
		NULL AS DefermentOrForbearanceOriginalBeginDate,
		NULL AS DefermentOrForbearanceOriginalEndDate,
		CASE WHEN LN60.LD_FOR_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN60.LD_FOR_BEG 
		END AS DefermentOrForbearanceBeginDate,
		CASE WHEN LN60.LD_FOR_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN60.LD_FOR_END 
		END AS DefermentOrForbearanceEndDate,
		LN60.LF_LST_DTS_LN60 AS UpdatedAt
	FROM
		UDW..LN10_LON LN10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN UDW..LN83_EFT_TO_LON LN83
			ON LN10.BF_SSN = LN83.BF_SSN
			AND LN10.LN_SEQ = LN83.LN_SEQ
			AND LN83.LC_STA_LN83 = 'A'
		INNER JOIN UDW..LN60_BR_FOR_APV LN60
			ON LN10.BF_SSN = LN60.BF_SSN
			AND LN10.LN_SEQ = LN60.LN_SEQ
			AND LN60.LF_LST_DTS_LN60 > @LASTRUN
			AND LN60.LC_STA_LON60 = 'A'
			AND LN60.LN_FOR_OCC_SEQ = '1'
			AND LN60.LC_FOR_RSP != '003'
		INNER JOIN UDW..FB10_BR_FOR_REQ FB10
			ON LN60.BF_SSN = FB10.BF_SSN
			AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
			AND FB10.LC_FOR_STA = 'A'
			AND FB10.LC_STA_FOR10 = 'A'
			AND FB10.LF_FOR_PRV IS NULL
	WHERE 
		LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.LF_LON_CUR_OWN LIKE '8297690%'

	UNION ALL
	/*R3*/
	SELECT DISTINCT
		'R3D' AS Report,
		LN10.BF_SSN AS Ssn,
		PD10.DF_SPE_ACC_ID AS AccountNumber,
		LN10.LN_SEQ AS LoanSequence,
		LN10.LF_LON_CUR_OWN AS OwnerCode,
		CASE WHEN DF10A.LD_DFR_REQ_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE DF10A.LD_DFR_REQ_BEG
		END AS DefermentOrForbearanceOriginalBeginDate,
		CASE WHEN DF10A.LD_DFR_REQ_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE DF10A.LD_DFR_REQ_END
		END AS DefermentOrForbearanceOriginalEndDate,
		CASE WHEN DF10B.LD_DFR_REQ_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE DF10B.LD_DFR_REQ_BEG
		END AS DefermentOrForbearanceBeginDate,
		CASE WHEN DF10B.LD_DFR_REQ_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE DF10B.LD_DFR_REQ_END
		END AS DefermentOrForbearanceEndDate,
		DF10B.LF_LST_DTS_DF10 AS UpdatedAt
	FROM 
		UDW..LN10_LON LN10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN UDW..LN83_EFT_TO_LON LN83
			ON LN10.BF_SSN = LN83.BF_SSN
			AND LN10.LN_SEQ = LN83.LN_SEQ
			AND LN83.LC_STA_LN83 = 'A'
		INNER JOIN UDW..LN50_BR_DFR_APV LN50
			ON LN50.BF_SSN = LN10.BF_SSN
			AND LN50.LN_SEQ = LN10.LN_SEQ
			AND LN50.LC_DFR_RSP != '003'
			AND LN50.LC_STA_LON50 = 'A'
		INNER JOIN UDW..DF10_BR_DFR_REQ DF10A
			ON LN50.BF_SSN = DF10A.BF_SSN
		INNER JOIN UDW..DF10_BR_DFR_REQ DF10B
			ON DF10A.BF_SSN = DF10B.BF_SSN
			AND DF10A.LC_DFR_TYP = DF10B.LC_DFR_TYP
			AND DF10A.LF_DFR_CTL_NUM = DF10B.LF_DFR_PRV
			AND DF10B.LC_DFR_STA = 'A'
			AND DF10B.LC_STA_DFR10 = 'A'
			AND DF10B.LF_DFR_PRV IS NOT NULL
			AND DF10B.LF_LST_DTS_DF10 > @LASTRUN
	WHERE 
		LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.LF_LON_CUR_OWN LIKE '8297690%'
	GROUP BY
		LN10.BF_SSN,
		PD10.DF_SPE_ACC_ID,
		LN10.LN_SEQ,
		LN10.LF_LON_CUR_OWN,
		DF10A.LD_DFR_REQ_BEG,
		DF10A.LD_DFR_REQ_END,
		DF10B.LD_DFR_REQ_BEG,
		DF10B.LD_DFR_REQ_END,
		DF10B.LF_LST_DTS_DF10,
		LN10.LD_LON_EFF_ADD
	HAVING
		DATEDIFF(DAY,DF10B.LD_DFR_REQ_BEG,DF10A.LD_DFR_REQ_END) != 1

	UNION ALL

	SELECT DISTINCT
		'R3F' AS Report,
		LN10.BF_SSN AS Ssn,
		PD10.DF_SPE_ACC_ID AS AccountNumber,
		LN10.LN_SEQ AS LoanSequence,
		LN10.LF_LON_CUR_OWN AS OwnerCode,
		CASE WHEN FB10A.LD_FOR_REQ_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE FB10A.LD_FOR_REQ_BEG
		END AS DefermentOrForbearanceOriginalBeginDate,
		CASE WHEN FB10A.LD_FOR_REQ_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE FB10A.LD_FOR_REQ_END
		END AS DefermentOrForbearanceOriginalEndDate,
		CASE WHEN FB10B.LD_FOR_REQ_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE FB10B.LD_FOR_REQ_BEG
		END AS DefermentOrForbearanceBeginDate,
		CASE WHEN FB10B.LD_FOR_REQ_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE FB10B.LD_FOR_REQ_END
		END AS DefermentOrForbearanceEndDate,
		FB10B.LF_LST_DTS_FB10 AS UpdatedAt
	FROM 
		UDW..LN10_LON LN10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN UDW..LN83_EFT_TO_LON LN83
			ON LN10.BF_SSN = LN83.BF_SSN
			AND LN10.LN_SEQ = LN83.LN_SEQ
			AND LN83.LC_STA_LN83 = 'A'
		INNER JOIN UDW..LN60_BR_FOR_APV LN60
			ON LN60.BF_SSN = LN10.BF_SSN
			AND LN60.LN_SEQ = LN10.LN_SEQ
			AND LN60.LC_FOR_RSP != '003'
			AND LN60.LC_STA_LON60 = 'A'
		INNER JOIN UDW..FB10_BR_FOR_REQ FB10A
			ON LN60.BF_SSN = FB10A.BF_SSN
		INNER JOIN UDW..FB10_BR_FOR_REQ FB10B
			ON FB10A.BF_SSN = FB10B.BF_SSN
			AND FB10A.LC_FOR_TYP = FB10B.LC_FOR_TYP
			AND FB10A.LF_FOR_CTL_NUM = FB10B.LF_FOR_PRV
			AND FB10B.LC_FOR_STA = 'A'
			AND FB10B.LC_STA_FOR10 = 'A'
			AND FB10B.LF_FOR_PRV IS NOT NULL
			AND FB10B.LF_LST_DTS_FB10 > @LASTRUN
	WHERE 
		LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.LF_LON_CUR_OWN LIKE '8297690%'

	GROUP BY
		LN10.BF_SSN,
		PD10.DF_SPE_ACC_ID,
		LN10.LN_SEQ,
		LN10.LF_LON_CUR_OWN,
		FB10A.LD_FOR_REQ_BEG,
		FB10A.LD_FOR_REQ_END,
		FB10B.LD_FOR_REQ_BEG,
		FB10B.LD_FOR_REQ_END,
		FB10B.LF_LST_DTS_FB10,
		LN10.LD_LON_EFF_ADD
	HAVING 
		DATEDIFF(DAY,FB10B.LD_FOR_REQ_BEG,FB10A.LD_FOR_REQ_END) != 1

	UNION ALL

	SELECT DISTINCT
		'R4' AS Report,
		LN10.BF_SSN AS Ssn,
		PD10.DF_SPE_ACC_ID AS AccountNumber,
		LN10.LN_SEQ AS LoanSequence,
		LN10.LF_LON_CUR_OWN AS OwnerCode,
		CASE WHEN LN72.LD_ITR_EFF_BEG < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN72.LD_ITR_EFF_BEG
		END AS DefermentOrForbearanceOriginalBeginDate,
		CASE WHEN LN72.LD_ITR_EFF_END < LN10.LD_LON_EFF_ADD THEN LN10.LD_LON_EFF_ADD
		     ELSE LN72.LD_ITR_EFF_END
		END AS DefermentOrForbearanceOriginalEndDate,
		NULL AS DefermentOrForbearanceBeginDate,
		NULL AS DefermentOrForbearanceEndDate,
		LN72.LF_LST_DTS_LN72 AS UpdatedAt
	FROM
		UDW..LN10_LON LN10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN UDW..LN83_EFT_TO_LON LN83
			ON LN10.BF_SSN = LN83.BF_SSN
			AND LN10.LN_SEQ = LN83.LN_SEQ
			AND LN83.LC_STA_LN83 = 'A'
		INNER JOIN UDW..LN72_INT_RTE_HST LN72
			ON LN72.BF_SSN = LN10.BF_SSN
			AND LN72.LN_SEQ = LN10.LN_SEQ
			AND LN72.LC_STA_LON72 = 'A'
			AND LN72.LC_INT_RDC_PGM = 'S'
			AND LN72.LF_LST_DTS_LN72 > @LASTRUN 
		LEFT JOIN UDW..LN50_BR_DFR_APV LN50
			ON LN50.BF_SSN = LN72.BF_SSN
			AND LN50.LN_SEQ = LN72.LN_SEQ
			AND LN50.LC_STA_LON50 = 'A'
			AND LN72.LD_ITR_EFF_BEG BETWEEN LN50.LD_DFR_BEG AND LN50.LD_DFR_END 
			AND LN72.LD_ITR_EFF_END BETWEEN LN50.LD_DFR_BEG AND LN50.LD_DFR_END
		LEFT JOIN UDW..DF10_BR_DFR_REQ DF10
			ON DF10.BF_SSN = LN50.BF_SSN 
			AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM
			AND DF10.LC_DFR_STA = 'A'
			AND DF10.LC_STA_DFR10 = 'A'
		LEFT JOIN UDW..LN60_BR_FOR_APV LN60
			ON LN60.BF_SSN = LN72.BF_SSN
			AND LN60.LN_SEQ = LN72.LN_SEQ
			AND LN60.LC_STA_LON60 = 'A'
			AND LN72.LD_ITR_EFF_BEG BETWEEN LN60.LD_FOR_BEG AND LN60.LD_FOR_END 
			AND LN72.LD_ITR_EFF_END BETWEEN LN60.LD_FOR_BEG AND LN60.LD_FOR_END
		LEFT JOIN UDW..FB10_BR_FOR_REQ FB10
			ON FB10.BF_SSN = LN60.BF_SSN 
			AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM
			AND FB10.LC_FOR_STA = 'A'
			AND FB10.LC_STA_FOR10 = 'A'	
	WHERE
		LN10.LC_STA_LON10 = 'R'
		AND LN10.LA_CUR_PRI > 0.00
		AND LN10.LF_LON_CUR_OWN LIKE '8297690%'
		AND LN50.BF_SSN IS NULL 
		AND LN60.BF_SSN IS NULL
) Data

MERGE 
	ULS.achrirdf.ProcessQueue PQ
USING
	(
		SELECT 
			* 
		FROM 
			#AchData

	) D 
		ON D.Ssn = PQ.Ssn
		AND D.Report = PQ.Report
		AND D.AccountNumber = PQ.AccountNumber
		AND D.LoanSequence = PQ.LoanSequence
		AND D.OwnerCode = PQ.OwnerCode
		AND D.UpdatedAt = PQ.UpdatedAt
WHEN NOT MATCHED THEN
	INSERT 
	(
		Report,
		Ssn,
		AccountNumber,
		LoanSequence,
		OwnerCode,
		DefermentOrForbearanceOriginalBeginDate,
		DefermentOrForbearanceOriginalEndDate,
		DefermentOrForbearanceBeginDate,
		DefermentOrForbearanceEndDate,
		UpdatedAt,
		CreatedAt,
		CreatedBy
	)
	VALUES
	(
		D.Report,
		D.Ssn,
		D.AccountNumber,
		D.LoanSequence,
		D.OwnerCode,
		D.DefermentOrForbearanceOriginalBeginDate,
		D.DefermentOrForbearanceOriginalEndDate,
		D.DefermentOrForbearanceBeginDate,
		D.DefermentOrForbearanceEndDate,
		D.UpdatedAt,
		GETDATE(),
		SUSER_SNAME()
	)