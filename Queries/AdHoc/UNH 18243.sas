LIBNAME DUSTER REMOTE SERVER=DUSTER SLIBREF=WORK;
RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;

PROC SQL;
	CREATE TABLE TILP AS
		SELECT DISTINCT
			CATX(' ',SC10.IM_SCL_FUL,SC10.IF_DOE_SCL) AS SCHOOL,
			LN10.LD_TRM_BEG,
			LN10.LD_TRM_END,
			LN10.BF_SSN,
			CATX('',LN10.BF_SSN,LN10.LN_SEQ) AS LOAN,
			LN10.LA_LON_AMT_GTR
		FROM
			OLWHRM1.LN10_LON LN10
			JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
				ON LN10.BF_SSN = DW01.BF_SSN
				AND LN10.LN_SEQ = DW01.LN_SEQ
			JOIN OLWHRM1.LN50_BR_DFR_APV LN50
				ON LN10.BF_SSN = LN50.BF_SSN
				AND LN10.LN_SEQ = LN50.LN_SEQ
				AND LN50.LC_STA_LON50 = 'A'
			JOIN OLWHRM1.DF10_BR_DFR_REQ DF10
				ON LN50.BF_SSN = DF10.BF_SSN
				AND LN50.LF_DFR_CTL_NUM = DF10.LF_DFR_CTL_NUM
				AND DF10.LC_STA_DFR10 = 'A'
			JOIN OLWHRM1.SC10_SCH_DMO SC10
				ON LN10.LF_DOE_SCL_ORG = SC10.IF_DOE_SCL
		WHERE
			LN10.LA_CUR_PRI > 0
			AND LN10.IC_LON_PGM = 'TILP'
			AND DW01.WC_DW_LON_STA IN ('03','04')
			AND DF10.LC_DFR_TYP NOT IN ('33','06')
		ORDER BY 
			SC10.IM_SCL_FUL,
			LN10.LD_TRM_BEG,
			LN10.BF_SSN,
			LN10.LN_SEQ
	;
QUIT;

ENDRSUBMIT;

DATA TILP; SET DUSTER.TILP; RUN;

PROC SQL;
	CREATE TABLE BYSCH AS
		SELECT DISTINCT
			SCHOOL,
			COUNT(DISTINCT BF_SSN) AS BRWS,
			COUNT(LOAN) AS LOANS,
			SUM(LA_LON_AMT_GTR) AS AMOUNT
		FROM
			TILP
		GROUP BY
			SCHOOL
	;
			

 	CREATE TABLE BYSCH_DET AS
		SELECT DISTINCT
			SCHOOL,
			LD_TRM_BEG,
			LD_TRM_END,
			COUNT(DISTINCT BF_SSN) AS BRWS,
			COUNT(LOAN) AS LOANS,
			SUM(LA_LON_AMT_GTR) AS AMOUNT
		FROM
			TILP
		GROUP BY
			SCHOOL,
			LD_TRM_BEG,
			LD_TRM_END
	;
QUIT;

PROC EXPORT
		DATA=TILP
		OUTFILE='T:\SAS\TILP Loans by School.xlsx'
		REPLACE;
RUN;

ODS RTF FILE='T:\SAS\TILP Loans by School - Summary.rtf';

TITLE 'TILP Loans by School - Summary';

PROC PRINT DATA = BYSCH NOOBS LABEL SUMLABEL;
	VAR
		SCHOOL
		BRWS
		LOANS
		AMOUNT
	;

	FORMAT BRWS LOANS COMMA9.;
	FORMAT AMOUNT DOLLAR15.2;

	LABEL 
		SCHOOL = 'School '
		BRWS = 'Borrowers'
		LOANS = 'Loans'
		AMOUNT = 'Amount'
	;

RUN;

ODS RTF CLOSE;

ODS RTF FILE='T:\SAS\TILP Loans by School - Detail.rtf';

TITLE 'TILP Loans by School - Detail';

PROC PRINT DATA = BYSCH_DET NOOBS LABEL SUMLABEL;
	BY SCHOOL;
	SUM BRWS LOANS AMOUNT;

	VAR
		LD_TRM_BEG
		LD_TRM_END
		BRWS
		LOANS
		AMOUNT
	;

	FORMAT LD_TRM_BEG LD_TRM_END MMDDYY10.;
	FORMAT BRWS LOANS COMMA9.;
	FORMAT AMOUNT DOLLAR15.2;

	LABEL 
		SCHOOL = 'School '
		LD_TRM_BEG = 'Term Begin Date'
		LD_TRM_END = 'Term End Date'
		BRWS = 'Borrowers'
		LOANS = 'Loans'
		AMOUNT = 'Amount'
	;

RUN;

ODS RTF CLOSE;

