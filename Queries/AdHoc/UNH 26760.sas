LIBNAME XL "T:\Paid Ahead at ACS.xlsx";
DATA WORK.SOURCE (keep=BORROWERSSN NextPaymentDueDate);
	SET XL.'ALL WAVES$'N;
RUN;

%LET RPTLIB = T:\SAS;
LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

DATA DUSTER.SOURCE; 
	SET SOURCE;
RUN;

RSUBMIT;
%LET DB = DLGSUTWH;
LIBNAME OLWHRM1 DB2 DATABASE=&DB OWNER=OLWHRM1; 

PROC SQL;
	CREATE TABLE BORROWERS AS
		SELECT DISTINCT
			S.BORROWERSSN 				AS SSN
			,S.NextPaymentDueDate 		AS NPD
			,SUM(LN10.LA_CUR_PRI)		AS ACCT_PRINC
			,SUM(DW01.WA_TOT_BRI_OTS) 	AS ACCT_INT
			,SUM(LN80.LA_BIL_CUR_DU) 	AS CURR_BILL_INST
			,CASE
				WHEN LN83.LC_STA_LN83 = 'A' THEN 'Y'
				ELSE 'N'
			END AS ON_ACH
			,LN90.PMTS_AT_UH
		FROM
			WORK.SOURCE S
			INNER JOIN OLWHRM1.LN10_LON LN10
				ON LN10.BF_SSN = S.BORROWERSSN
			INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
				ON LN10.BF_SSN = DW01.BF_SSN
				AND LN10.LN_SEQ = DW01.LN_SEQ
			LEFT OUTER JOIN 
				(
					SELECT DISTINCT
						BF_SSN
						,LN_SEQ
						,LC_STA_LN83
					FROM
						OLWHRM1.LN83_EFT_TO_LON
					WHERE
						LC_STA_LN83 = 'A'
				)LN83
					ON LN83.BF_SSN = LN10.BF_SSN
					AND LN83.LN_SEQ = LN10.LN_SEQ
			LEFT OUTER JOIN 
				(
					SELECT DISTINCT
						BF_SSN
						,LN_SEQ
						,LA_BIL_CUR_DU
						,MAX(DAY(LD_BIL_DU_LON)) AS LD_BIL_DU_LON
					FROM
						OLWHRM1.LN80_LON_BIL_CRF LN80
					WHERE
						LC_STA_LON80 = 'A'
						AND LC_BIL_TYP_LON = 'P'
						AND LD_BIL_CRT >= MDY(02,11,2016)
					GROUP BY
						BF_SSN
						,LN_SEQ
						,LA_BIL_CUR_DU
				)LN80
					ON LN80.BF_SSN = LN10.BF_SSN
					AND LN80.LN_SEQ = LN10.LN_SEQ
			LEFT OUTER JOIN
				(
					SELECT
						BF_SSN
						,LN_SEQ
						,COUNT(DISTINCT LD_FAT_EFF) AS PMTS_AT_UH
					FROM
						OLWHRM1.LN90_FIN_ATY
					WHERE
						LC_STA_LON90 = 'A'
						AND LC_FAT_REV_REA = ' '
						AND PC_FAT_TYP = '10'
						AND PC_FAT_SUB_TYP = '10'
					GROUP BY
						BF_SSN
						,LN_SEQ
				)LN90
					ON LN90.BF_SSN = LN10.BF_SSN
					AND LN90.LN_SEQ = LN10.LN_SEQ

		GROUP BY
			S.BORROWERSSN
			,S.NextPaymentDueDate
			,ON_ACH
			,LN90.PMTS_AT_UH

	;
QUIT;

ENDRSUBMIT;

DATA BORROWERS;
	SET DUSTER.BORROWERS;
	ACCT_TOT_BAL = ACCT_PRINC + ACCT_INT;
	EST_PMT_LEFT = CEIL(ACCT_TOT_BAL / CURR_BILL_INST);
RUN;

PROC EXPORT
		DATA=BORROWERS
		OUTFILE="&RPTLIB\UNH 26760.xlsx"
		DBMS = EXCEL
		REPLACE;
		SHEET="BORROWERS";
RUN;
