%LET RPTLIB = T:\SAS;

PROC IMPORT DATAFILE="Q:\UHEAA Projects\BANA\Wave 2 Supplemental Files\J00BX_829769_INCENTIVE_SC370R5_2016021604181866_UU.csv"
     OUT=ACH
     DBMS=CSV
     REPLACE;
     GETNAMES=YES;
RUN;

PROC SQL;
	CREATE TABLE Borrowers AS
		SELECT DISTINCT 
			Borrower_SSN 
		FROM 
			ACH;
QUIT;

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;/*live*/

DATA DUSTER.Borrowers; *Send data to Duster;
SET Borrowers;
RUN;

RSUBMIT;

%LET DB = DLGSUTWH;/*live*/

LIBNAME OLWHRM1 DB2 DATABASE=&DB OWNER=OLWHRM1; /*needed for SQL queries, but not for DB2 queries*/

PROC SQL;
CREATE TABLE ACHfile AS
	SELECT DISTINCT
		BL10.BF_SSN,
		BL10.LD_BIL_CRT,
		BL10.LN_SEQ_BIL_WI_DTE,
		BL10.LC_IND_BIL_SNT AS LC_IND_BIL_SNT_old_value,
		'A' AS LC_IND_BIL_SNT_new_value
	FROM
		WORK.Borrowers BORR
		INNER JOIN OLWHRM1.BL10_BR_BIL BL10
			ON BORR.Borrower_SSN = BL10.BF_SSN
		INNER JOIN OLWHRM1.LN80_LON_BIL_CRF LN80
			ON LN80.BF_SSN = BL10.BF_SSN
			AND LN80.LD_BIL_CRT = BL10.LD_BIL_CRT
			AND LN80.LN_SEQ_BIL_WI_DTE = BL10.LN_SEQ_BIL_WI_DTE	
			AND LN80.LC_STA_LON80 = 'A'
		INNER JOIN OLWHRM1.LN10_LON LN10 /*join on loan level*/
			ON LN10.BF_SSN = LN80.BF_SSN
			AND LN10.LN_SEQ = LN80.LN_SEQ
			AND LN10.LF_LON_CUR_OWN LIKE '829769%' /*lender ID's*/
			AND LN10.LA_CUR_PRI > 0 /*open loans*/
			AND LN10.LC_STA_LON10 = 'R' /*released loans*/
		INNER JOIN 
			(
				SELECT 
					BF_SSN,
					LN_SEQ,
					LC_STA_LN83,
					MAX(LF_LST_DTS_LN83) AS changedate
				FROM
					OLWHRM1.LN83_EFT_TO_LON
				WHERE
					LC_STA_LN83 = 'A'
				GROUP BY
					BF_SSN,
					LN_SEQ,
					LC_STA_LN83
			) LN83 ON LN83.BF_SSN = LN80.BF_SSN
			AND LN83.LN_SEQ = LN80.LN_SEQ
	WHERE
		BL10.LD_BIL_CRT > MDY(02,25,2016) /*loans after this date - Wave 2*/
;

QUIT;

ENDRSUBMIT;

DATA ACHfile;
	SET DUSTER.ACHfile;
RUN;

PROC EXPORT
		DATA=ACHfile
		OUTFILE="&RPTLIB\UNH 25729.xlsx"
		DBMS = EXCEL
		REPLACE;
		SHEET="BL10-Change";
RUN;
