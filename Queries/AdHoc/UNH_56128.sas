%LET RPTLIB = T:\SAS;
FILENAME REPORT7 "&RPTLIB/UNH_56128_753.XLSX";
FILENAME REPORT8 "&&RPTLIB/UNH_56128_800.XLSX";

%LET SRVR = DUSTER;
%LET DB = DLGSUTWH;
	%SYSLPUT SRVR = &SRVR;
	%SYSLPUT DB = &DB;

LIBNAME  WORKLOCL  REMOTE  SERVER=&SRVR SLIBREF=WORK;

LIBNAME NOCFILES XLSX 'T:\700126 NOC Files_PWP.xlsx';

DATA XL753; SET NOCFILES.TAB753; RUN;
DATA XL800; SET NOCFILES.TAB800; RUN;

/*CHANGE CHAR SEQ TO NUMERIC TO MATCH LN10.LN_LON_ALT_SEQ*/
DATA XNOC753;
	SET XL753;
	CL_SEQ = INPUT(SEQ, 2.);
RUN;

/*CHANGE CHAR SEQ TO NUMERIC TO MATCH LN10.LN_LON_ALT_SEQ*/
DATA XNOC800;
	SET XL800;
	CL_SEQ = INPUT(SEQ, 2.);
RUN;

DATA WORKLOCL.XNOC753; SET XNOC753; RUN;
DATA WORKLOCL.XNOC800; SET XNOC800; RUN;

RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=&DB OWNER=OLWHRM1;

PROC SQL STIMER;
	CONNECT TO DB2 (DATABASE=&DB); 

/*	GET DATA FOR ALL LOANS*/
	CREATE TABLE NOCDATA AS
		SELECT 
			*
		FROM 
			CONNECTION TO DB2 
			(
				SELECT DISTINCT 
					GR10.BF_SSN 
					,GR10.LN_SEQ 
					,GR10.WN_SEQ_GR10 
					,GR10.IC_LON_PGM
					,GR10.LA_CUR_PRI 
					,GR10.LD_LON_1_DSB 
					,GR10.IF_GTR
					,LN10.LF_LON_ALT
					,GR10.WF_LON_ALT
					,LN10.LF_GTR_RFR_XTN
					,GR10.WF_GTR_RFR
					,LN10.LN_LON_ALT_SEQ
				FROM 
					OLWHRM1.LN10_LON LN10
					INNER JOIN OLWHRM1.GR10_RPT_LON_APL GR10 ON
						LN10.BF_SSN = GR10.BF_SSN
						AND LN10.LN_SEQ = GR10.LN_SEQ
				WHERE
					GR10.IF_GTR IN ('000753','000800') 
					AND LN10.LF_LON_ALT <> ''

				FOR READ ONLY WITH UR
			) 
	;
	DISCONNECT FROM DB2;

/*	CREATE DATASET FOR LOANS ON 753 TAB*/
	CREATE TABLE NOC753DATA AS
		SELECT
			ND.BF_SSN 
			,ND.LN_SEQ 
			,ND.WN_SEQ_GR10 
			,ND.IC_LON_PGM
			,ND.LA_CUR_PRI 
			,ND.LD_LON_1_DSB 
			,ND.IF_GTR
			,ND.LF_LON_ALT
			,ND.WF_LON_ALT
			,ND.LF_GTR_RFR_XTN
			,ND.WF_GTR_RFR
			,ND.LN_LON_ALT_SEQ
		FROM
			NOCDATA ND
			INNER JOIN XNOC753 X7
				ON ND.LF_LON_ALT = X7.CLUID
				AND ND.LN_LON_ALT_SEQ = X7.CL_SEQ
	;

/*	CREATE DATASET FOR LOANS ON 800 TAB*/
	CREATE TABLE NOC800DATA AS
		SELECT
			ND.BF_SSN 
			,ND.LN_SEQ 
			,ND.WN_SEQ_GR10 
			,ND.IC_LON_PGM
			,ND.LA_CUR_PRI 
			,ND.LD_LON_1_DSB 
			,ND.IF_GTR
			,ND.LF_LON_ALT
			,ND.WF_LON_ALT
			,ND.LF_GTR_RFR_XTN
			,ND.WF_GTR_RFR
			,ND.LN_LON_ALT_SEQ
		FROM
			NOCDATA ND
			INNER JOIN XNOC800 X8
				ON ND.LF_LON_ALT = X8.CLUID
				AND ND.LN_LON_ALT_SEQ = X8.CL_SEQ
	;

QUIT;

ENDRSUBMIT;

DATA NOCDATA; SET WORKLOCL.NOCDATA; RUN;
DATA NOC753DATA; SET WORKLOCL.NOC753DATA; RUN;
DATA NOC800DATA; SET WORKLOCL.NOC800DATA; RUN;

/*IDENTIFY LOANS FROM XL 753 TAB NOT IN WAREHOUSE*/
PROC SQL;
	CREATE TABLE NO_CL7 AS
		SELECT 
			X7.*
		FROM
			XL753 X7
		WHERE
			X7.CLUID NOT IN (SELECT DISTINCT LF_LON_ALT FROM NOCDATA)
			AND X7.CLUID <> ''
	;
QUIT;


/*IDENTIFY LOANS FROM XL 800 TAB NOT IN WAREHOUSE*/
PROC SQL;
	CREATE TABLE NO_CL8 AS
		SELECT 
			X8.*
		FROM
			XL800 X8
		WHERE
			X8.CLUID NOT IN (SELECT DISTINCT LF_LON_ALT FROM NOCDATA)
			AND X8.CLUID <> ''
	;
QUIT;


PROC EXPORT DATA= WORK.NOC753DATA
     OUTFILE= REPORT7 
     DBMS=XLSX REPLACE;
     SHEET="FOUND"; 
RUN;

PROC EXPORT DATA= WORK.NO_CL7
	OUTFILE= REPORT7 
     DBMS=XLSX;
     SHEET="NOT FOUND"; 
RUN;

PROC EXPORT DATA= WORK.NOC800DATA
     OUTFILE= REPORT8 
     DBMS=XLSX REPLACE;
     SHEET="FOUND"; 
RUN;

PROC EXPORT DATA= WORK.NO_CL8
	OUTFILE= REPORT8 
     DBMS=XLSX;
     SHEET="NOT FOUND"; 
RUN;
