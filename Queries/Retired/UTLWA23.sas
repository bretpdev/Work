*---------------------------------------*
| UTLWA23 Weekly Disbursements > 7/1/08 |
*---------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWA23.LWA23R2";
FILENAME REPORT3 "&RPTLIB/ULWA23.LWA23R3";
FILENAME REPORTZ "&RPTLIB/ULWA23.LWA23RZ";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE WKDSGS1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.LD_LON_1_DSB
	,A.LD_TRM_BEG
	,A.LD_TRM_END
	,B.LN_BR_DSB_SEQ
	,B.LA_DSB - COALESCE(B.LA_DSB_CAN,0) AS DISB_AMT
	,A.LF_LON_CUR_OWN
	,C.IM_OWN_FUL
	,B.LD_DSB
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.OW10_OWN C
	ON A.LF_LON_CUR_OWN = C.IF_OWN
WHERE A.LC_STA_LON10 = 'R'
	AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS','PLUSGB')
	AND A.LD_LON_1_DSB BETWEEN '07/01/2008' AND '06/30/2010'
	AND DAYS(A.LD_TRM_END) >= DAYS('07/01/2008')
	AND B.LA_DSB <> COALESCE(B.LA_DSB_CAN, 0)
	AND B.LC_STA_LON15 IN ('1','3')
	AND 
	(
		B.LC_DSB_TYP = '2'			
		OR 
		(
			B.LC_DSB_TYP = '1'
			AND B.LD_DSB_ROS_PRT IS NOT NULL	
		)
	)

FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWA23.LWA23RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA WKDSGS1 ;*/
/*	SET WORKLOCL.WKDSGS1;*/
/*RUN;*/

PROC SORT DATA=WKDSGS1 NODUPKEY;
	BY BF_SSN LN_SEQ LN_BR_DSB_SEQ;
RUN;

PROC SQL;
CREATE TABLE WKDSGS AS 
SELECT DISTINCT A.*
	,B.MAX_DISB_DT FORMAT=MMDDYY10.
FROM WKDSGS1 A
INNER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,MAX(LD_DSB) AS MAX_DISB_DT
	FROM WKDSGS1
	GROUP BY BF_SSN
		,LN_SEQ
	) B 
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
;
QUIT;

/*REPORT 2 DATA SET*/
PROC SQL;
CREATE TABLE DISB_BY_LENDER AS
SELECT 	IM_OWN_FUL
		,SUM(DISB_AMT) AS DISB_AMT
		,COUNT(LN_BR_DSB_SEQ) AS DISBS
FROM WKDSGS
GROUP BY IM_OWN_FUL;
QUIT;

/*REPORT 3 PROCESSING*/
DATA ECAT;
INFILE CARDS DSD MISSOVER;
INPUT CAT $ 1-10;
CARDS;
ECASLA I
ECASLA II 
NON ECASLA
RUN;
DATA WDECAT (DROP=E1_1ST_DSB_LB E1_1ST_DSB_UB E1_LN_TRM_DT  E1_MX_DSB_LB E1_MX_DSB_UB 
	E2_1ST_DSB_LB E2_LN_TRM_DT  E2_MX_DSB_LB  E2_MX_DSB_UB);
	SET WKDSGS;
	LENGTH CAT $ 10.;
	/*ECASLA 1 DATE VARIABLE INITIALIZATION*/
	E1_1ST_DSB_LB = '01JUL2008'D;
	E1_1ST_DSB_UB = '01JUL2009'D;
	E1_LN_TRM_DT = '01JUL2008'D;
	E1_MX_DSB_LB = '01JUL2008'D;
	E1_MX_DSB_UB = '30SEP2009'D;
	/*ECASLA 2 DATE VARIABLE INITIALIZATION*/
	E2_1ST_DSB_LB = '01MAY2009'D;
	E2_LN_TRM_DT = '01JUL2009'D;
	E2_MX_DSB_LB = '01MAY2009'D;
	E2_MX_DSB_UB = '30SEP2010'D;
	/*ASSIGN ECASLA CATEGORY*/
	IF (
			E1_LN_TRM_DT <= LD_TRM_BEG OR
			LD_TRM_BEG <= E1_LN_TRM_DT <= LD_TRM_END OR
			(LD_TRM_BEG <= E1_LN_TRM_DT AND LD_TRM_END >= E2_LN_TRM_DT)
		) AND 
		E1_1ST_DSB_LB <= LD_LON_1_DSB < E1_1ST_DSB_UB AND	
		E1_MX_DSB_LB <= MAX_DISB_DT <= E1_MX_DSB_UB THEN 
			CAT = 'ECASLA I';
	ELSE IF (
				E2_LN_TRM_DT <= LD_TRM_BEG OR
				LD_TRM_BEG <= E2_LN_TRM_DT <= LD_TRM_END OR
				(LD_TRM_BEG <= E1_LN_TRM_DT AND LD_TRM_END >= E2_LN_TRM_DT)
			) AND
		E2_1ST_DSB_LB <= LD_LON_1_DSB AND
		E2_MX_DSB_LB <= MAX_DISB_DT <= E2_MX_DSB_UB THEN
			CAT = 'ECASLA II';
	ELSE
		CAT = 'NON ECASLA';
RUN;
PROC SQL;
CREATE TABLE ECASLA_DISB AS
SELECT AA.IM_OWN_FUL
	,AA.CAT
	,COALESCE(SUM(DISB_AMT),0) AS DISB_AMT
	,COUNT(LN_BR_DSB_SEQ) AS DISBS
FROM (
	SELECT A.CAT
		,B.IM_OWN_FUL
	FROM ECAT A
	FULL OUTER JOIN (
		SELECT DISTINCT IM_OWN_FUL
		FROM WKDSGS
		) B
		ON A.CAT = A.CAT
	) AA
LEFT OUTER JOIN WDECAT B
	ON AA.CAT = B.CAT
	AND AA.IM_OWN_FUL = B.IM_OWN_FUL
GROUP BY AA.IM_OWN_FUL
	,AA.CAT
ORDER BY AA.IM_OWN_FUL
	,AA.CAT
;
QUIT;

/*CREATE REPORTS*/
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 DATE PAGENO=1 CENTER;
TITLE 'WEEKLY DISBURSEMENT TOTALS';
TITLE2	'LOANS FIRST DISBURSED >= 7/1/08';
FOOTNOTE   'JOB = UTLWA23  	 REPORT = ULWA23.LWA23R2';
PROC PRINT NOOBS SPLIT='/' DATA=DISB_BY_LENDER WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT DISBS COMMA8. DISB_AMT DOLLAR18.2;
VAR IM_OWN_FUL DISBS DISB_AMT;
LABEL IM_OWN_FUL = 'LENDER/NAME'
	DISBS = 'TOTAL #/DISBURSED'
	DISB_AMT = 'TOTAL $/DISBURSED';
SUM DISBS DISB_AMT;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'WEEKLY DISBURSEMENTS > 7/1/08';
TITLE2	"BY ECASLA CATEGORY";
FOOTNOTE   'JOB = UTLWA23  	 REPORT = ULWA23.LWA23R3';
PROC CONTENTS DATA=ECASLA_DISB OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWA23  	 REPORT = ULWA23.LWA23R2";
	END;
RETURN;
RUN;
PROC REPORT DATA=ECASLA_DISB NOWD HEADSKIP SPLIT='/';
	COLUMN IM_OWN_FUL CAT DISBS DISB_AMT;
	DEFINE IM_OWN_FUL / GROUP 'LENDER/NAME' WIDTH=40 ;
	DEFINE CAT / GROUP 'CATEGORY' WIDTH = 10;
	DEFINE DISBS / ANALYSIS 'TOTAL #/DISBURSED' WIDTH=10 FORMAT=COMMA8.;
	DEFINE DISB_AMT / ANALYSIS 'TOTAL $/DISBURSED' WIDTH=18 FORMAT=DOLLAR16.2;
	BREAK AFTER IM_OWN_FUL / SUMMARIZE SUPPRESS SKIP OL;
	COMPUTE AFTER;
		LINE '';
		LINE @27 'GRAND TOTAL # DISBURSED: ' DISBS.SUM COMMA8.;
		LINE @27 'GRAND TOTAL $ DISBURSED: ' DISB_AMT.SUM DOLLAR16.2;
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;

/*DETAIL FILE*/
/*PROC SORT DATA=WKDSGS;*/
/*	BY IM_OWN_FUL LD_DSB;*/
/*RUN;*/
/*DATA _NULL_;*/
/*FILE 'T:\SAS\ULWA23.Detail.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;*/
/*if _n_ = 1 then do;*/
/*	put*/
/*		"BF_SSN"*/
/*		','*/
/*		"LN_SEQ"*/
/*		','*/
/*		"LD_LON_1_DSB"*/
/*		','*/
/*		"LD_TRM_BEG"*/
/*		','*/
/*		"LD_TRM_END"*/
/*		','*/
/*		"LN_BR_DSB_SEQ"*/
/*		','*/
/*		"DISB_AMT"*/
/*		','*/
/*		"LF_LON_CUR_OWN"*/
/*		','*/
/*		"IM_OWN_FUL"*/
/*		','*/
/*		"LD_DSB"*/
/*		','*/
/*		"MAX_DISB_DT";*/
/*	end;*/
/*SET  WORK.WKDSGS;*/
/*   FORMAT BF_SSN $9. ;*/
/*   FORMAT LN_SEQ 6. ;*/
/*   FORMAT LD_LON_1_DSB MMDDYY10. ;*/
/*   FORMAT LD_TRM_BEG MMDDYY10. ;*/
/*   FORMAT LD_TRM_END MMDDYY10. ;*/
/*   FORMAT LN_BR_DSB_SEQ 6. ;*/
/*   FORMAT DISB_AMT 16.2 ;*/
/*   FORMAT LF_LON_CUR_OWN $8. ;*/
/*   FORMAT IM_OWN_FUL $40. ;*/
/*   FORMAT LD_DSB MMDDYY10. ;*/
/*   FORMAT MAX_DISB_DT MMDDYY10. ;*/
/*DO;*/
/*   PUT BF_SSN $ @;*/
/*   PUT LN_SEQ @;*/
/*   PUT LD_LON_1_DSB @;*/
/*   PUT LD_TRM_BEG @;*/
/*   PUT LD_TRM_END @;*/
/*   PUT LN_BR_DSB_SEQ @;*/
/*   PUT DISB_AMT @;*/
/*   PUT LF_LON_CUR_OWN $ @;*/
/*   PUT IM_OWN_FUL $ @;*/
/*   PUT LD_DSB @;*/
/*   PUT MAX_DISB_DT ;*/
/*   ;*/
/*END;*/
/*RUN;*/
