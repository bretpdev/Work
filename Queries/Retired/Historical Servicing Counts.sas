/*This job was withdrawn during testing.  Invalid sale dates and servicer responsibility dates
invalidate the results.  Attempts to approximate these dates with logic based largely on
the NSLDS Date Entered Repayment failed because this date is updated for all loans
when new loans are recieved after earlier loans have already been sold.*/

*date range of last 24 months;
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-24,'beginning'), DATE9.)||"'D");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), DATE9.)||"'D");
     CALL SYMPUT('BG',PUT(INTNX('MONTH',TODAY(),-24,'beginning'), MMDDYY10.));
     CALL SYMPUT('ED',PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYY10.));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);

CREATE TABLE DETAIL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.DF_PRS_ID_BR					as SSN,
		B.AF_APL_ID||B.AF_APL_ID_SFX	as CLUID,
/*		B.AD_LON_SLD,*/
		COALESCE(B.AA_GTE_LON_AMT,0)
		- COALESCE(B.AA_TOT_RFD,0)
		- COALESCE(B.AA_TOT_CAN,0)		AS NETGTY
		,B.AC_LON_TYP
/*		,B.AD_PRC*/
		,A.AF_CUR_APL_OPS_LDR			AS ORGLDR
		,C.AD_NDS_CLC_ENT_RPD			AS RPAYDT
		,D.AD_DSB_ADJ_LST				AS LASTDSB
/*		,B.AF_CUR_LON_OPS_LDR
		,B.AF_CUR_LON_SER_AGY*/
FROM  OLWHRM1.GA01_APP A inner join OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.GA15_NDS_ID C
	ON B.AF_APL_ID = C.AF_APL_ID
	AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
INNER JOIN 
	(SELECT AF_APL_ID
	,AF_APL_ID_SFX
	,MAX(AD_DSB_ADJ) AS AD_DSB_ADJ_LST
	FROM OLWHRM1.GA11_LON_DSB_ATY
	WHERE AC_DSB_ADJ = 'A' /*Actual Disbursement*/
	AND AC_DSB_ADJ_STA = 'A' /*Active Disbursement Record*/
	GROUP BY AF_APL_ID, AF_APL_ID_SFX) D

	ON B.AF_APL_ID = D.AF_APL_ID
	AND B.AF_APL_ID_SFX = D.AF_APL_ID_SFX

WHERE C.AC_STA_GA15 = 'A' /*Active NSLDS Record*/
AND B.AF_CUR_LON_OPS_LDR IN ('826717','829769','830248')
AND B.AF_CUR_LON_SER_AGY = '700121'
AND B.AC_LON_TYP <> 'CL'
/*AND A.DF_PRS_ID_BR < '105208881'*/
);

DISCONNECT FROM DB2;
*endrsubmit  ;

/*DATA DETAIL; SET WORKLOCL.DETAIL; RUN;*/
PROC SORT DATA=DETAIL; BY SSN;RUN;

DATA DETAIL2;
SET DETAIL;
FORMAT NEWSLD DATE9.;
IF AC_LON_TYP IN ('SF','SU','SL') 
	AND ORGLDR NOT IN ('819628','820200','829158') THEN
	NEWSLD = RPAYDT - 60;
ELSE IF AC_LON_TYP = 'PL'
	AND ORGLDR NOT IN ('819628','820200','829158') THEN
	NEWSLD = LASTDSB + 30;
ELSE IF ORGLDR = '819628' THEN /*BANK ONE*/
	DO;
		IF DAY(LASTDSB) <= 20 THEN NEWSLD = LASTDSB;
		ELSE NEWSLD = INTNX('MONTH',LASTDSB,1,'beginning');
	END;
ELSE IF ORGLDR IN ('820200','829158') THEN /*TOOELE FEDERAL CU, WEBER STATE CU*/
	NEWSLD = INTNX('QUARTER',LASTDSB,1,'beginning');
RUN;

PROC SQL;
CREATE TABLE BWRS AS
SELECT *
FROM 
	(SELECT	SSN,
			MIN(NEWSLD) FORMAT=DATE9.	AS NEWSLD_1ST
	FROM DETAIL2
	GROUP BY SSN) X
WHERE X.NEWSLD_1ST BETWEEN &BEGIN AND &END
;QUIT;

PROC SORT DATA=DETAIL2; BY SSN; RUN;
PROC SORT DATA=BWRS; BY SSN;RUN;

DATA DETAILX;
MERGE DETAIL2 (IN=A) BWRS (IN=B);
BY SSN;
*IF A=1 AND B=1;
RUN;

PROC SORT DATA=DETAILX; BY NEWSLD; RUN;

PROC SQL;
CREATE TABLE DETAILMO AS
SELECT * 
FROM (SELECT YEAR(NEWSLD) AS YR,
			MONTH(NEWSLD) AS MO,  
			SUM(NETGTY) AS TOTNETGTY
	FROM DETAILX
	WHERE NEWSLD BETWEEN &BEGIN AND &END
	GROUP BY YR, MO) A
INNER JOIN 
	(SELECT YEAR(NEWSLD) AS YR,
			MONTH(NEWSLD) AS MO,  
			COUNT(SSN) AS NEWACCTS, 
			SUM(NETGTY) AS NEWACCTNETGTY
	FROM DETAILX
	WHERE NEWSLD_1ST BETWEEN &BEGIN AND &END
	GROUP BY YR, MO) B
ON A.YR = B.YR
AND A.MO = B.MO
LEFT OUTER JOIN
	(SELECT YEAR(NEWSLD) AS YR,
			MONTH(NEWSLD) AS MO,  
			SUM(NETGTY) AS OLDACCTNETGTY
	FROM DETAILX
	WHERE NEWSLD_1ST NOT BETWEEN &BEGIN AND &END
	GROUP BY YR, MO) C
ON A.YR = C.YR
AND A.MO = C.MO
;
QUIT;

ENDRSUBMIT;
DATA DETAILMO;SET WORKLOCL.DETAILMO;RUN;
DATA DETAILX;
SET WORKLOCL.DETAILX;
WHERE NEWSLD BETWEEN &BEGIN AND &END;
RUN;

OPTIONS CENTER NODATE NONUMBER PAGENO=1 LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=DETAILMO WIDTH=UNIFORM WIDTH=MIN;
VAR YR MO NEWACCTS NEWACCTNETGTY OLDACCTNETGTY TOTNETGTY;
SUM NEWACCTS NEWACCTNETGTY OLDACCTNETGTY TOTNETGTY;
FORMAT NEWACCTS COMMA9. NEWACCTNETGTY DOLLAR18.2 OLDACCTNETGTY DOLLAR18.2 
TOTNETGTY DOLLAR18.2;
LABEL YR='YEAR' MO='MONTH' NEWACCTS='NEW SERVICING ACCOUNTS'
NEWACCTNETGTY='NET GUARANTY AMOUNT SOLD/NEW SERVICING ACCOUNTS'
OLDACCTNETGTY='NET GUARANTY AMOUNT SOLD/PREVIOUS SERVICING ACCOUNTS'
TOTNETGTY='TOTAL NET GUARANTY AMOUNT SOLD/ALL SERVICING ACCOUNTS';
TITLE1 "HISTORICAL SERVICING TRENDS (PROJECTED)";
TITLE2 "PERIOD: &BG TO &ED";
FOOTNOTE  'JOB = ON DEMAND     REPORT = HISTORICAL SERVICING COUNTS';
RUN;

PROC EXPORT DATA= DETAILMO
            OUTFILE= "C:\WINDOWS\TEMP\Historical Servicing Counts.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;

PROC EXPORT DATA= DETAILX
            OUTFILE= "C:\WINDOWS\TEMP\Historical Servicing Counts - Detail.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;