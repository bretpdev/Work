*-----------------------------------------------*
| FALL SEASON APPLICATION CUSTOMER EFFICIENCIES |
*-----------------------------------------------*
| PROGRAMMER NOTE: IF THIS JOB IS EVER PUT INTO |
| PRODUCTION IT WILL NEED TO BE TUNED FOR       |
| EFFICIENCY.                                   |
*-----------------------------------------------*;
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/FSACE.R2.txt";
FILENAME REPORT3 "&RPTLIB/FSACE.R3.txt";
FILENAME REPORT4 "&RPTLIB/FSACE.R4.txt";
%LET SCHOOL = '00402700'; /*INPUT SINGLE SCHOOL CODE WITH TICKS*/
%LET LENDERS = '813894','817455','811698'; /*INPUT A LENDER ID OR A COMMA DELIMITED LIST WITH TICKS*/
%SYSLPUT SCHOOL = &SCHOOL;
%SYSLPUT LENDERS = &LENDERS;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FSACE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.LN_SEQ 
	,A.LF_DOE_SCL_ORG
	,A.IF_DOE_LDR
	,A.LD_TRM_END
	,C.DM_PRS_1
	,C.DM_PRS_LST
	,D.DC_ADR
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DX_STR_ADR_3
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP_CDE
	,D.DI_VLD_ADR 
	,E.DC_ADR_EML
	,E.DI_VLD_ADR_EML
	,E.DX_ADR_EML
	,F.DC_PHN
	,F.DI_PHN_VLD
	,COALESCE(STA_D.HAS_D_LOANS,0) AS HAS_D_LOANS
	,COALESCE(STA_O.HAS_O_LOANS,0) AS HAS_O_LOANS
	,COALESCE(X_STA.HAS_X_STA,0) AS HAS_X_STA
	,COALESCE(Y_STA.HAS_Y_STA,0) AS HAS_Y_STA
FROM OLWHRM1.LN10_LON A
LEFT OUTER JOIN (
	SELECT BF_SSN
		,'Y' AS HAS_LOAN
	FROM OLWHRM1.LN10_LON
	WHERE LF_DOE_SCL_ORG = &SCHOOL
		AND IF_DOE_LDR IN (&LENDERS)
	) B
	ON A.BF_SSN = B.BF_SSN
INNER JOIN OLWHRM1.PD10_PRS_NME C
	ON A.BF_SSN = C.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD30_PRS_ADR D
	ON A.BF_SSN = D.DF_PRS_ID
	AND D.DI_VLD_ADR = 'Y'
LEFT OUTER JOIN OLWHRM1.PD32_PRS_ADR_EML E
	ON A.BF_SSN = E.DF_PRS_ID
	AND E.DC_STA_PD32 = 'A'
	AND E.DI_VLD_ADR_EML = 'Y'
LEFT OUTER JOIN OLWHRM1.PD42_PRS_PHN F
	ON A.BF_SSN = F.DF_PRS_ID
	AND F.DI_PHN_VLD = 'Y'
LEFT OUTER JOIN OLWHRM1.DW01_DW_CLC_CLU G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ
LEFT OUTER JOIN (
	SELECT BF_SSN
		,1 AS HAS_D_LOANS
	FROM OLWHRM1.LN10_LON 
	WHERE LC_STA_LON10 = 'D'
	) STA_D
	ON A.BF_SSN = STA_D.BF_SSN
LEFT OUTER JOIN (
	SELECT BF_SSN
		,1 AS HAS_O_LOANS
	FROM OLWHRM1.LN10_LON 
	WHERE LC_STA_LON10 ^= 'D'
	) STA_O
	ON A.BF_SSN = STA_O.BF_SSN
LEFT OUTER JOIN (
	SELECT BF_SSN
		,1 AS HAS_X_STA
	FROM OLWHRM1.DW01_DW_CLC_CLU  
	WHERE WC_DW_LON_STA IN ('12','17','22')
	) X_STA
	ON A.BF_SSN = X_STA.BF_SSN
LEFT OUTER JOIN (
	SELECT BF_SSN
		,1 AS HAS_Y_STA
	FROM OLWHRM1.DW01_DW_CLC_CLU  
	WHERE WC_DW_LON_STA NOT IN ('12','17','22')
	) Y_STA
	ON A.BF_SSN = Y_STA.BF_SSN
WHERE B.HAS_LOAN = 'Y'
	AND NOT EXISTS 
	(
		SELECT 1 
		FROM OLWHRM1.LN10_LON X
		WHERE X.BF_SSN = A.BF_SSN
			AND X.LD_TRM_END > '06/30/2009'
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*SPLIT DATA*/
PROC SORT DATA=FSACE OUT=LOANS(
	KEEP = DF_SPE_ACC_ID BF_SSN LN_SEQ LF_DOE_SCL_ORG IF_DOE_LDR LD_TRM_END DM_PRS_1 DM_PRS_LST 
		   HAS_D_LOANS HAS_O_LOANS HAS_X_STA HAS_Y_STA
	) NODUPKEY;
	BY BF_SSN DESCENDING LN_SEQ ;
RUN;
PROC SORT DATA=FSACE OUT=ADR(
	KEEP=BF_SSN DC_ADR DX_STR_ADR_1 DX_STR_ADR_2 DX_STR_ADR_3 DM_CT DC_DOM_ST DF_ZIP_CDE DI_VLD_ADR 
	) NODUPKEY;
	BY BF_SSN DC_ADR;
RUN;
PROC SORT DATA=FSACE OUT=PHN(
	KEEP=BF_SSN DC_PHN DI_PHN_VLD 
	) NODUPKEY;
	BY BF_SSN DC_PHN;
RUN;
PROC SORT DATA=FSACE OUT=EML(
	KEEP=BF_SSN DC_ADR_EML DI_VLD_ADR_EML DX_ADR_EML 
	) NODUPKEY;
	BY BF_SSN DC_ADR_EML;
RUN;
/*DETERMINE WHICH ADDRESS, PHONE # OR EMAIL ADDRESS TO USE IF MULTIPLES EXIST*/
%MACRO SPLIT_DS(DS,VTYP,TYP1,TYP2,TYP3);
DATA &TYP1._&DS &TYP2._&DS &TYP3._&DS &DS._OTH;
SET &DS;
IF &VTYP = "&TYP1" THEN 
	OUTPUT &TYP1._&DS;
ELSE IF &VTYP = "&TYP2" THEN
	OUTPUT &TYP2._&DS;
ELSE IF &VTYP = "&TYP3" THEN 
	OUTPUT &TYP3._&DS;
ELSE 
	OUTPUT &DS._OTH;
RUN;
%MEND SPLIT_DS;
%MACRO ORD(DS);
PROC SORT DATA=&DS;
	BY BF_SSN;
RUN;
%MEND ORD;
%SPLIT_DS(ADR,DC_ADR,L,B,D);
%SPLIT_DS(PHN,DC_PHN,H,A,W);
%SPLIT_DS(EML,DC_ADR_EML,H,A,W);
%ORD(ADR_OTH); 
%ORD(B_ADR); 
%ORD(D_ADR); 
%ORD(L_ADR);
%ORD(PHN_OTH); 
%ORD(W_PHN); 
%ORD(A_PHN); 
%ORD(H_PHN);
%ORD(EML_OTH); 
%ORD(W_EML); 
%ORD(A_EML); 
%ORD(H_EML);
/*CREATE APPROPRIATE LOAN DATA SET*/
DATA LOANS;
	SET LOANS;
	BY BF_SSN;
	IF FIRST.BF_SSN THEN 
		OUTPUT;
RUN;
PROC SQL;
CREATE TABLE LOANSX AS
SELECT DISTINCT A.*
FROM LOANS A
WHERE A.LF_DOE_SCL_ORG = &SCHOOL
	AND A.IF_DOE_LDR IN (&LENDERS)
	AND MDY(1,1,2007) <= A.LD_TRM_END <= MDY(6,30,2009)
ORDER BY A.BF_SSN
	;
QUIT;
/*MERGE DATA INTO REPORTING DATASET*/
DATA LOANS;	/*ADDRESS*/
	MERGE LOANSX (IN=A) ADR_OTH B_ADR D_ADR L_ADR;
	BY BF_SSN;
	IF A;
RUN;
DATA LOANS;	/*PHONE*/
	MERGE LOANS (IN=A) PHN_OTH W_PHN A_PHN H_PHN;
	BY BF_SSN;
	IF A;
RUN;
DATA LOANS;	/*EMAIL*/
	MERGE LOANS (IN=A) EML_OTH W_EML A_EML H_EML;
	BY BF_SSN;
	IF A;
RUN;
ENDRSUBMIT;
DATA LOANS;
	SET WORKLOCL.LOANS;
RUN;
PROC SORT DATA=LOANS;
	BY DF_SPE_ACC_ID;
RUN;
DATA _NULL_;
	SET  WORK.LOANS;
	WHERE DI_VLD_ADR_EML = 'Y';
	FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	   FORMAT DF_SPE_ACC_ID $10. ;
	   FORMAT DM_PRS_1 $13. ;
	   FORMAT DX_ADR_EML $254. ;
	IF _N_ = 1 THEN DO;
	   PUT "DM_PRS_1,DX_ADR_EML,DF_SPE_ACC_ID";
	END;
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT DM_PRS_1 $ @;
		PUT DX_ADR_EML $ ;
	END;
RUN;
DATA _NULL_;
	SET WORK.LOANS;
	WHERE DI_PHN_VLD = 'Y' AND (HAS_D_LOANS <= HAS_O_LOANS) AND (HAS_X_STA <= HAS_Y_STA);
	FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT DM_PRS_1 $13. ;
	FORMAT DX_ADR_EML $254. ;
	FORMAT ARC $5.;
	FORMAT V $1.;
	FORMAT LSN $3.;
	FORMAT DAYS_PLUS_3 MMDDYY6.;
	ARC = 'LNINV';
	V = '';
	LSN = 'ALL';
	DAYS_PLUS_3 = TODAY()+3;
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT ARC $ @;
		PUT DAYS_PLUS_3 @;
		PUT DAYS_PLUS_3 @;
		PUT DAYS_PLUS_3 @;
		PUT V $ @;
		PUT V $ @;
		PUT V $ @;
		PUT LSN $ @;
		PUT V $;
	END;
RUN;
PROC SORT DATA=LOANS;
	BY BF_SSN;
RUN;
DATA _NULL_;
	SET  WORK.LOANS;
	WHERE DI_PHN_VLD = ' ' AND DI_VLD_ADR_EML = ' ' AND DI_VLD_ADR = 'Y';
	FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;
	   FORMAT DF_SPE_ACC_ID $10. ;
	   FORMAT DM_PRS_1 $13. ;
	   FORMAT DM_PRS_LST $23. ;
	   FORMAT DX_STR_ADR_1 $30. ;
	   FORMAT DX_STR_ADR_2 $30. ;
	   FORMAT DX_STR_ADR_3 $30. ;
	   FORMAT DM_CT $20. ;
	   FORMAT DC_DOM_ST $2. ;
	   FORMAT DF_ZIP_CDE $17. ;
	IF _N_ = 1 THEN DO;
	   PUT
	      "DF_SPE_ACC_ID"
	    ','
	      "DM_PRS_1"
	   ','
	      "DM_PRS_LST"
	   ','
	      "DX_STR_ADR_1"
	   ','
	      "DX_STR_ADR_2"
	   ','
	      "DX_STR_ADR_3"
	   ','
	      "DM_CT"
	   ','
	      "DC_DOM_ST"
	   ','
	      "DF_ZIP_CDE";
	END;
	DO;
	   PUT DF_SPE_ACC_ID $ @;
	   PUT DM_PRS_1 $ @;
	   PUT DM_PRS_LST $ @;
	   PUT DX_STR_ADR_1 $ @;
	   PUT DX_STR_ADR_2 $ @;
	   PUT DX_STR_ADR_3 $ @;
	   PUT DM_CT $ @;
	   PUT DC_DOM_ST $ @;
	   PUT DF_ZIP_CDE $ ;
	END;
RUN;
