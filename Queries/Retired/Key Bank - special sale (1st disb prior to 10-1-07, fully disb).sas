*----------------------------------------------------------------*
| Key Bank - special sale (1st disb prior to 10-1-07, fully disb)|
*----------------------------------------------------------------*;
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/KeyBankSpecial.R2";
FILENAME REPORT3 "&RPTLIB/KeyBankSpecial.R3";
FILENAME REPORT4 "&RPTLIB/KeyBankSpecial.R4";
FILENAME REPORT5 "&RPTLIB/KeyBankSpecial.R5.doc";
FILENAME REPORT6 "&RPTLIB/KeyBankSpecial.R6.doc";
FILENAME REPORT7 "&RPTLIB/KeyBankSpecial.R7.doc";
FILENAME REPORT8 "&RPTLIB/KeyBankSpecial.R8";
FILENAME REPORT9 "&RPTLIB/KeyBankSpecial.R9.doc";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE KSPEC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,A.LA_CUR_PRI + COALESCE(A.LA_NSI_OTS,0) AS CUR_BAL
	,B.LD_DSB
	,B.LA_DSB
	,ABS(COALESCE(B.LA_DSB_CAN,0))*-1 AS LA_DSB_CAN
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LF_LON_CUR_OWN = '813760UT'
	AND A.LD_LON_1_DSB < '10/01/2007'
	AND A.LA_CUR_PRI > 0
	AND B.LA_DSB != COALESCE(B.LA_DSB_CAN,0)
	AND NOT EXISTS
	(
		SELECT *
		FROM OLWHRM1.LN15_DSB X
		WHERE X.BF_SSN = A.BF_SSN
		AND X.LN_SEQ = A.LN_SEQ
		AND X.LC_STA_LON15 IN ('1','3')
		AND X.LC_DSB_TYP != '2'
		AND X.LA_DSB != COALESCE(X.LA_DSB_CAN,0)
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA KSPEC ;
	SET WORKLOCL.KSPEC;
RUN;
PROC SORT DATA=KSPEC;
	BY BF_SSN LN_SEQ LD_DSB;
RUN;
DATA KSPEC(DROP=LA_DSB LA_DSB_CAN );
	SET KSPEC;
	LENGTH LID $ 13.;
	BY BF_SSN LN_SEQ;
	IF LAST.LN_SEQ;
	LID = CATT(BF_SSN,PUT(LN_SEQ,Z4.));
RUN;
/********************************************************
* CREATE FILES AND SUMMARY REPORTS
*********************************************************/
%MACRO MKFILES(CRIT,RNO1,RNO2,TITLE1);
DATA REPDS;
	SET KSPEC;
	WHERE &CRIT;
RUN;
PROC SORT DATA=REPDS;
	BY BF_SSN;
RUN;
DATA SREPDS(KEEP=BR_CT LN_CT CR_BL);
	SET REPDS END=LAST;
	BY BF_SSN;
	IF FIRST.BF_SSN THEN B = 1;
	L = 1;
	BR_CT+B;
	LN_CT+L;
	CR_BL+CUR_BAL;
	IF LAST;
RUN;
/*CREATE TXT FILE*/
DATA _NULL_;
	SET REPDS;
	FILE REPORT&RNO1 DLM=',' DSD DROPOVER LRECL=67000;
	FORMAT LID $13.;
	DO;
		PUT LID $;
	END;
RUN;
/*CREATE SUMMARY REPORT*/
ODS LISTING CLOSE;
ODS RTF FILE=REPORT&RNO2;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE "&TITLE1";
FOOTNOTE "JOB = KEY BANK SPECIAL SALE  	 REPORT = R&RNO2";
PROC PRINT DATA=SREPDS NOOBS LABEL SPLIT='/';
	FORMAT BR_CT LN_CT COMMA8. CR_BL DOLLAR14.2;
	VAR BR_CT LN_CT CR_BL;
	LABEL BR_CT = 'TOTAL/BORROWERS/SCHEDULED/TO SELL'
		LN_CT = 'TOTAL/LOANS/SCHEDULED/TO SELL'
		CR_BL = 'TOTAL/DOLLAR/AMOUNT SCHEDULED/TO SELL';
RUN;
ODS RTF CLOSE;
ODS LISTING;
%MEND MKFILES;
%MKFILES(%STR(LID ^= ''),2,5,KEY BANK FULLY DISBURSED REPORT);
%MKFILES(%STR(INTCK('DAY',LD_DSB,'24APR2008'D) > 30),3,6,KEY BANK 30 DAYS FULLY DISBURSED REPORT);
%MKFILES(%STR(INTCK('DAY',LD_DSB,'24APR2008'D) > 90),4,7,KEY BANK 90 DAYS FULLY DISBURSED REPORT);
%MKFILES(%STR(INTCK('DAY',LD_DSB,'24APR2008'D) > 60),8,9,KEY BANK 60 DAYS FULLY DISBURSED REPORT);
