/*Disbursement Reissued after Loan Sale*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG50.LWG50R2";
FILENAME REPORT3 "&RPTLIB/ULWG50.LWG50R3";
FILENAME REPORT4 "&RPTLIB/ULWG50.LWG50R4";
FILENAME REPORT5 "&RPTLIB/ULWG50.LWG50R5";
FILENAME REPORT6 "&RPTLIB/ULWG50.LWG50R6";
FILENAME REPORT7 "&RPTLIB/ULWG50.LWG50R7";
FILENAME REPORT8 "&RPTLIB/ULWG50.LWG50R8";
FILENAME REPORT9 "&RPTLIB/ULWG50.LWG50R9";
FILENAME REPORT10 "&RPTLIB/ULWG50.LWG50R10";
FILENAME REPORT11 "&RPTLIB/ULWG50.LWG50R11";
FILENAME REPORT12 "&RPTLIB/ULWG50.LWG50R12";
FILENAME REPORT13 "&RPTLIB/ULWG50.LWG50R13";
FILENAME REPORT14 "&RPTLIB/ULWG50.LWG50R14";
FILENAME REPORT15 "&RPTLIB/ULWG50.LWG50R15";
FILENAME REPORT16 "&RPTLIB/ULWG50.LWG50R16";
FILENAME REPORT17 "&RPTLIB/ULWG50.LWG50R17";
FILENAME REPORT18 "&RPTLIB/ULWG50.LWG50R18";
FILENAME REPORT19 "&RPTLIB/ULWG50.LWG50R19";
FILENAME REPORT20 "&RPTLIB/ULWG50.LWG50R20";
FILENAME REPORT21 "&RPTLIB/ULWG50.LWG50R21";
FILENAME REPORT22 "&RPTLIB/ULWG50.LWG50R22";
FILENAME REPORT23 "&RPTLIB/ULWG50.LWG50R23";
FILENAME REPORT24 "&RPTLIB/ULWG50.LWG50R24";
FILENAME REPORT25 "&RPTLIB/ULWG50.LWG50R25";
FILENAME REPORTZ "&RPTLIB/ULWG50.LWG50RZ";
options symbolgen;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*LOAN SALE*/
CREATE TABLE D1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT LN10.BF_SSN
	,LN10.LN_SEQ
	,PD10.DM_PRS_1
	,PD10.DM_PRS_LST
	,PD10.DF_SPE_ACC_ID AS ACCTNUM
	,LN10.IF_DOE_LDR 
	,LN15.LN_BR_DSB_SEQ	
	,LN15.LD_DSB 
	,LN15.LA_DSB 			
	,LN15.LA_DSB_CAN		
	,LN10.LF_DOE_SCL_ORG 	
	,LN90A.PC_FAT_TYP 		AS PC_FAT_TYP_A0101
	,LN90A.PC_FAT_SUB_TYP 	AS PC_FAT_SUB_TYP_A0101
	,LN90A.LD_FAT_EFF 		AS LD_FAT_EFF_A0101
	,LN90A.LA_FAT_CUR_PRI 	AS LA_FAT_CUR_PRI_A0101
	,LN90B.PC_FAT_TYP 		AS PC_FAT_TYP_AXX95
	,LN90B.PC_FAT_SUB_TYP 	AS PC_FAT_SUB_TYP_AXX95
	,LN90B.LD_FAT_EFF 		AS LD_FAT_EFF_AXX95
	,LN90B.LA_FAT_CUR_PRI 	AS LA_FAT_CUR_PRI_AXX95
	,LN90C.PC_FAT_TYP 		AS PC_FAT_TYP_A1045
	,LN90C.PC_FAT_SUB_TYP 	AS PC_FAT_SUB_TYP_A1045
	,LN90C.LD_FAT_EFF 		AS LD_FAT_EFF_A1045
	,LN90C.LA_FAT_CUR_PRI 	AS LA_FAT_CUR_PRI_A1045
	,LN90B.LN_FAT_SEQ		AS SALE_FAT_SEQ
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.LN15_DSB LN15
	ON LN10.BF_SSN = LN15.BF_SSN
	AND LN10.LN_SEQ = LN15.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
INNER JOIN (
	SELECT *
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE PC_FAT_TYP = '01'
	AND PC_FAT_SUB_TYP = '01'
	AND LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	 ) LN90A
	ON LN90A.BF_SSN = LN10.BF_SSN
	AND LN90A.LN_SEQ = LN10.LN_SEQ
INNER JOIN (
	SELECT *
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE PC_FAT_TYP IN ('03','04')
	AND PC_FAT_SUB_TYP = '95'
	AND LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	 ) LN90B
	ON LN90B.BF_SSN = LN10.BF_SSN
	AND LN90B.LN_SEQ = LN10.LN_SEQ
LEFT OUTER JOIN (
	SELECT *
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE PC_FAT_TYP = '10'
	AND PC_FAT_SUB_TYP = '45'
	AND LC_STA_LON90 = 'A'
	AND LC_FAT_REV_REA = ''
	 ) LN90C
	ON LN90C.BF_SSN = LN10.BF_SSN
	AND LN90C.LN_SEQ = LN10.LN_SEQ
	AND LN90C.LD_FAT_EFF = LN90A.LD_FAT_EFF
WHERE LN90A.LD_FAT_EFF >= LN90B.LD_FAT_EFF
AND LN15.LC_STA_LON15 IN ('1','3')
AND (LN15.LA_DSB_CAN IS NULL
	OR LN15.LA_DSB_CAN <> LN15.LA_DSB)
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN 
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ
	WHERE Y.BF_SSN = LN90A.BF_SSN
	AND Y.LN_SEQ = LN90A.LN_SEQ
	AND X.PF_REQ_ACT = 'REALS'
	AND X.LC_STA_ACTY10 = 'A'
	AND X.LD_ATY_REQ_RCV > LN90A.LD_FAT_EFF
	)
FOR READ ONLY WITH UR
);

/*DISBURSEMENTS*/
CREATE TABLE D2 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN
	,LN10.LN_SEQ
	,PD10.DM_PRS_1
	,PD10.DM_PRS_LST
	,PD10.DF_SPE_ACC_ID AS ACCTNUM
	,LN10.IF_DOE_LDR
	,SUM (LN15.LA_DSB-COALESCE(LN15.LA_DSB_CAN,0)) AS LA_DSB
	,LN15.LD_DSB	
	,LN10.LF_DOE_SCL_ORG 
	,LN90.LN_FAT_SEQ
	,LN90.SALE_DT
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.LN15_DSB LN15
	ON LN10.BF_SSN = LN15.BF_SSN
	AND LN10.LN_SEQ = LN15.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.AP03_MASTER_APL AP03 
	ON LN15.AF_APL_ID = AP03.AF_APL_ID
LEFT OUTER JOIN (
	SELECT L1.BF_SSN
		,L1.LN_SEQ 
		,L1.LN_FAT_SEQ
		,L1.LD_FAT_EFF AS SALE_DT
	FROM OLWHRM1.LN90_FIN_ATY L1
	INNER JOIN (
		SELECT BF_SSN
			,LN_SEQ 
			,MAX(LD_FAT_EFF) AS LD_FAT_EFF
		FROM OLWHRM1.LN90_FIN_ATY 
		WHERE PC_FAT_TYP = '04'
			AND PC_FAT_SUB_TYP = '95'
			AND LC_STA_LON90 = 'A'
			AND LC_FAT_REV_REA = ''
		GROUP BY BF_SSN
			,LN_SEQ
		) L2
		ON L1.BF_SSN = L2.BF_SSN
		AND L1.LN_SEQ = L2.LN_SEQ
		AND L1.LD_FAT_EFF = L2.LD_FAT_EFF
	WHERE L1.PC_FAT_TYP = '04'
		AND L1.PC_FAT_SUB_TYP = '95'
		AND L1.LC_STA_LON90 = 'A'
		AND L1.LC_FAT_REV_REA = ''
	 ) LN90
	ON LN90.BF_SSN = LN10.BF_SSN
	AND LN90.LN_SEQ = LN10.LN_SEQ
WHERE LN15.LC_STA_LON15 IN ('1','3')
AND (
		LN15.LA_DSB_CAN IS NULL
	OR 
		LN15.LA_DSB_CAN <> LN15.LA_DSB
	)
AND (
		( 
			LN15.LC_DSB_RLS_STA <> 'R' OR LN15.LC_DSB_RLS_STA IS NULL
		)
		OR 
		(
			LN15.LC_DSB_RLS_STA IS NULL AND LN15.LN_SEQ IS NOT NULL
		)
	)
AND LN15.LD_DSB >= '11/01/2001'
AND LN10.LC_STA_LON10 = 'D'
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.LN10_LON X
	INNER JOIN OLWHRM1.LN15_DSB Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_SEQ = Y.LN_SEQ
	WHERE X.LF_LON_ALT||'0'||RTRIM(CHAR(X.LN_LON_ALT_SEQ)) = AP03.AF_CNL||AP03.AF_CNL_SFX 
	AND Y.AN_SEQ IS NULL
	)
GROUP BY LN10.BF_SSN
	,PD10.DF_SPE_ACC_ID
	,LN10.LN_SEQ
	,PD10.DM_PRS_1
	,PD10.DM_PRS_LST
	,LN10.IF_DOE_LDR
	,LN15.LD_DSB	
	,LN10.LF_DOE_SCL_ORG 
	,LN90.LN_FAT_SEQ
	,LN90.SALE_DT
FOR READ ONLY WITH UR
);

CREATE TABLE EX AS
SELECT DISTINCT BF_SSN
	,LN_SEQ
	,LN_FAT_SEQ
FROM CONNECTION TO DB2 (
	SELECT LN90.BF_SSN
		,LN90.LN_SEQ
		,LN90.LN_FAT_SEQ	
	FROM OLWHRM1.LN90_FIN_ATY LN90
	INNER JOIN OLWHRM1.LN99_LON_SLE_FAT LN99
		ON LN99.BF_SSN = LN90.BF_SSN
		AND LN99.LN_SEQ = LN90.LN_SEQ
	WHERE LN90.PC_FAT_TYP = '03'
		AND LN90.PC_FAT_SUB_TYP = '95'
		AND LN90.LC_STA_LON90 = 'A'
		AND (
				(
				 LN99.IF_SLL_OWN_SLD = '813915' AND LN99.IF_BUY_OWN_SLD = '813894'
				 AND LN90.LD_FAT_EFF IN (
						'07/12/2001','07/19/2001'
						)
				 )
			OR (
				LN99.IF_SLL_OWN_SLD = '820043' AND LN99.IF_BUY_OWN_SLD = '829505'
				AND LN90.LD_FAT_EFF = '02/06/2003'
				)
			)
UNION
	SELECT LN99.BF_SSN
		,LN99.LN_SEQ	
		,LN99.LN_FAT_SEQ
	FROM OLWHRM1.LN99_LON_SLE_FAT LN99
	WHERE 
		(	
			LN99.IF_SLL_OWN_SLD IN (&UHEAA_LIST) AND 
			LN99.IF_BUY_OWN_SLD IN (&UHEAA_LIST) 
		)
	OR 
		(	
			LN99.IF_SLL_OWN_SLD = '813760' AND 
			LN99.IF_BUY_OWN_SLD = '813760UT'
		)
UNION
	SELECT A.BF_SSN
      ,A.LN_SEQ
	  ,A.LN_FAT_SEQ
	FROM OLWHRM1.LN90_FIN_ATY A
	INNER JOIN OLWHRM1.LN99_LON_SLE_FAT LN99
	      ON A.BF_SSN = LN99.BF_SSN
	      AND A.LN_SEQ = LN99.LN_SEQ
	      AND A.LN_FAT_SEQ = LN99.LN_FAT_SEQ
	INNER JOIN OLWHRM1.LR10_LDR_DMO LR10
	      ON LN99.IF_SLL_OWN_SLD = LR10.IF_DOE_LDR
	      AND LN99.IF_BUY_OWN_SLD = LR10.IF_LDR_MRG_TO
	WHERE A.PC_FAT_TYP = '03'
	      AND A.PC_FAT_SUB_TYP = '95'
	      AND LR10.IC_LDR_MRG_TYP IN ('F','1')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=UTLWG50.LWG50RZ);*/
/*QUIT;*/
PROC SQL;
CREATE TABLE FIN AS 
SELECT DISTINCT 
	 A.BF_SSN
	,A.ACCTNUM
	,A.LN_SEQ
	,A.DM_PRS_1
	,A.DM_PRS_LST
	,A.IF_DOE_LDR 
	,A.LF_DOE_SCL_ORG 	
	,A.LD_FAT_EFF_A0101
	,A.LA_FAT_CUR_PRI_A0101
	,CASE 
		WHEN B.O495_DT IS NULL 
		THEN A.LD_FAT_EFF_AXX95
		ELSE B.O495_DT
	 END AS SALE_DT
	,a.SALE_FAT_SEQ
FROM D1 A
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,LD_FAT_EFF_AXX95 AS O495_DT
		,'X' AS O405_IND
	FROM D1
	WHERE PC_FAT_TYP_AXX95 = '04'
	AND PC_FAT_SUB_TYP_AXX95 = '95'
	 ) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LD_FAT_EFF_A0101 <> A.LD_FAT_EFF_A1045
;
QUIT;

DATA BASE;
SET FIN D2;
RUN;

PROC SQL;
CREATE TABLE BASE2 AS 
SELECT DISTINCT A.BF_SSN 
	,A.ACCTNUM
	,A.LN_SEQ 
	,A.DM_PRS_1 
	,A.DM_PRS_LST 
	,A.IF_DOE_LDR 
	,A.SALE_DT 
	,CASE 
		WHEN A.LD_FAT_EFF_A0101 = .
		THEN A.LD_DSB
		ELSE A.LD_FAT_EFF_A0101
	 END AS LD_FAT_EFF_A0101
	,CASE 
		WHEN A.LA_FAT_CUR_PRI_A0101 = .
		THEN A.LA_DSB
		ELSE A.LA_FAT_CUR_PRI_A0101
	 END AS LA_FAT_CUR_PRI_A0101
	,A.LF_DOE_SCL_ORG
FROM BASE A
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,LN_FAT_SEQ
		,'Y' AS BYPASS_SALE
	FROM EX
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND A.SALE_FAT_SEQ = B.LN_FAT_SEQ
WHERE B.BYPASS_SALE IS NULL
;
QUIT;
endrsubmit;
data BASE2;
	set worklocl.BASE2;
run;

PROC SORT DATA=BASE2;BY ACCTNUM LN_SEQ;RUN;

%MACRO PRNT_REP(LNDR,REPNO);
%IF &LNDR = ALL %THEN %DO;
	DATA REPDS;
		SET BASE2;
	RUN;
	TITLE 'DISBURSEMENT REISSUED AFTER LOAN SALE';
	TITLE2;
	FOOTNOTE  "JOB = UTLWG50     REPORT = UTLWG50.LWG50R&REPNO";
%END;
%ELSE %DO;
	DATA REPDS;
		SET BASE2;
		WHERE IF_DOE_LDR = "&LNDR";
	RUN;
	TITLE 'DISBURSEMENT REISSUED AFTER LOAN SALE';
	TITLE2 "LENDER ID: &LNDR";
	FOOTNOTE  "JOB = UTLWG50     REPORT = UTLWG50.LWG50R&REPNO";
%END;

PROC SORT DATA=REPDS;
BY IF_DOE_LDR DM_PRS_LST;
RUN;

PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1;
PROC CONTENTS DATA=REPDS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 125*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT /////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWG50  	 REPORT = ULWG50.LWG50R&REPNO";
	END;
RETURN;
RUN;
PROC REPORT DATA=REPDS NOWD HEADSKIP SPLIT='/';
COLUMN ACCTNUM LN_SEQ DM_PRS_LST DM_PRS_1 IF_DOE_LDR SALE_DT LD_FAT_EFF_A0101 LA_FAT_CUR_PRI_A0101;
DEFINE ACCTNUM / DISPLAY "ACCTNUM" WIDTH = 10;
DEFINE LN_SEQ / DISPLAY "Loan Seq#" WIDTH = 10;
DEFINE DM_PRS_LST / DISPLAY "Last Name" WIDTH = 10;
DEFINE DM_PRS_1 / DISPLAY "First Name" WIDTH = 10;
DEFINE IF_DOE_LDR / DISPLAY "Originating LID" WIDTH = 11;
DEFINE SALE_DT / DISPLAY DISPLAY FORMAT = MMDDYY10. "Sale Date" WIDTH = 10;
DEFINE LD_FAT_EFF_A0101 / DISPLAY DISPLAY FORMAT = MMDDYY10. "Disb Date" WIDTH = 10;
DEFINE LA_FAT_CUR_PRI_A0101/ DISPLAY FORMAT = DOLLAR13. "Disb Amount" WIDTH = 10;
RUN;

PROC PRINTTO;
RUN;
%MEND PRNT_REP;
%PRNT_REP(ALL,2);
%PRNT_REP(817575,3);
%PRNT_REP(822373,4);
%PRNT_REP(819628,5);
%PRNT_REP(833828,6);
%PRNT_REP(817440,7);
%PRNT_REP(817545,8);
%PRNT_REP(834265,9);
%PRNT_REP(830791,10);
%PRNT_REP(813760UT,11);
%PRNT_REP(817546,12);
%PRNT_REP(834122,13);
%PRNT_REP(832241,14);
%PRNT_REP(820200,15);
%PRNT_REP(830132,16);
%PRNT_REP(811698,17);
%PRNT_REP(830146,18);
%PRNT_REP(829123,19);
%PRNT_REP(833577,20);
%PRNT_REP(829505,21);
%PRNT_REP(829158,22);
%PRNT_REP(813894,23);
%PRNT_REP(817455,24);
%PRNT_REP(834370,25);
/*PROC EXPORT DATA=WORK.D3*/
/*            OUTFILE= "T:\SAS\DEMO.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
