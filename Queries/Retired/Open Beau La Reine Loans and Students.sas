/*QSASS01 - OPEN BEAU LA REINE LOANS/STUDENTS*/

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*BORROWERS WITH OPEN LOANS IN LOAN SERVICING*/
CREATE TABLE BLR1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN
	,PD10.DM_PRS_LST
	,PD10.DM_PRS_1
	,DW01.WC_DW_LON_STA
	,SUM(LN10.LA_CUR_PRI) AS PRI_SUM
	,max(LN15.ANT_DSB) as ANT_DSB

FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON LN10.BF_SSN = DW01.BF_SSN
	AND LN10.LN_SEQ = DW01.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
LEFT OUTER JOIN
	(SELECT DISTINCT
	BF_SSN
	,LN_SEQ
	,'Y' AS ANT_DSB
	FROM OLWHRM1.LN15_DSB
	WHERE LC_STA_LON15 IN ('1','3')
	AND LC_DSB_TYP = '1'
	AND (LA_DSB_CAN IS NULL
    	OR LA_DSB_CAN <> LA_DSB)
	)LN15
	ON LN15.BF_SSN = LN10.BF_SSN
	AND LN15.LN_SEQ = LN10.LN_SEQ

WHERE LN10.LF_DOE_SCL_ORG = '01147900'
AND DW01.WC_DW_LON_STA IN ('01','02','03','04','05','06','07','08','09',
	'10','11','13','14','15','16','17','18','19','20','21','23')
AND LN10.LA_CUR_PRI > 0
GROUP BY LN10.BF_SSN
	,PD10.DM_PRS_LST
	,PD10.DM_PRS_1
	,DW01.WC_DW_LON_STA
/*	,LN15.ANT_DSB*/
ORDER BY LN10.BF_SSN
);

/*LOANS WITH ANTICIPATED DISBURSEMENTS NOT YET IN LOAN SERVICING*/
CREATE TABLE BLR2 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	LN15.BF_SSN
	,PD10.DM_PRS_LST
	,PD10.DM_PRS_1
	,'Y' AS ANT_DSB

FROM OLWHRM1.LN15_DSB LN15
INNER JOIN OLWHRM1.AP10_APL AP10
	ON LN15.BF_SSN = AP10.BF_SSN
	AND LN15.AN_SEQ = AP10.AN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN15.BF_SSN = PD10.DF_PRS_ID

WHERE LN15.LC_STA_LON15 IN ('1','3')
AND LN15.LC_DSB_TYP = '1'
AND (LN15.LA_DSB_CAN IS NULL
    OR LN15.LA_DSB_CAN <> LN15.LA_DSB)
AND LN15.AN_SEQ IS NULL
AND AP10.AF_DOE_SCL = '01147900'
);

DISCONNECT FROM DB2;

DATA BLR; 
SET BLR1 BLR2; 
RUN;

ENDRSUBMIT;

DATA BLR; 
SET WORKLOCL.BLR; 
RUN;

PROC FORMAT;
VALUE $LONSTA
'01'="IN GRACE"
'02'="IN SCHOOL"
'03'="IN REPAYMENT"
'04'="IN DEFERMENT"
'05'="IN FORBEARANCE"
'06'="IN CURE"
'07'="CLAIM PENDING"
'08'="CLAIM SUBMITTED"
'09'="CLAIM CANCELED"
'10'="CLAIM REJECT"
'11'="CLAIM RETURNED"
'12'="CLAIM PAID"
'13'="PRE-CLAIM PENDING"
'14'="PRE-CLAIM SUBMITTED"
'15'="PRE-CLAIM CANCELLED"
'16'="DEATH ALLEGED"
'17'="DEATH VERIFIED"
'18'="DISABILITY ALLEGED"
'19'="DISABILITY VERIFIED"
'20'="BANKRUPTCY ALLEGED"
'21'="BANKRUPTCY VERIFIED"
'22'="PAID IN FULL"
'23'="NOT FULLY ORIGINATED"
'88'="PROCESSING ERROR"
'98'="UNKNOWN STATUS";
QUIT;

OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=90;
PROC PRINT NOOBS SPLIT='/' DATA=BLR WIDTH=UNIFORM WIDTH=MIN;
VAR BF_SSN DM_PRS_LST DM_PRS_1 WC_DW_LON_STA PRI_SUM ANT_DSB;
LABEL BF_SSN='SSN' DM_PRS_LST='LAST NAME' DM_PRS_1='FIRST NAME'
WC_DW_LON_STA='STATUS' PRI_SUM='TOTAL PRINCIPAL BALANCE'
ANT_DSB='PENDING DISBURSEMENT';
FORMAT PRI_SUM DOLLAR9.2 WC_DW_LON_STA $LONSTA.;
TITLE 'BEAU LA REINE STUDENTS';
FOOTNOTE  'JOB = QSASS01     REPORT = QSASS01.R2';
RUN;