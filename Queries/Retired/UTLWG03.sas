/*UTLWG03 - MONTHLY LOANS GUARANTEED
Lists loans guaranteed during the previous month.  This program must be run 
ASAP after the end of the month to avoid the problem of adjustments to the 
guaranteed amount which happen after the end of the month in which the guarantee 
was processed.*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORTZ "&RPTLIB/ULWG03.LWG03RZ";
FILENAME REPORT2 "&RPTLIB/ULWG03.LWG03R2";
FILENAME REPORT3 "&RPTLIB/ULWG03.LWG03R3";
FILENAME REPORT4 "&RPTLIB/ULWG03.LWG03R4";
FILENAME REPORT5 "&RPTLIB/ULWG03.LWG03R5";
FILENAME REPORT6 "&RPTLIB/ULWG03.LWG03R6";
FILENAME REPORT20 "&RPTLIB/ULWG03.LWG03R20";
FILENAME REPORT30 "&RPTLIB/ULWG03.LWG03R30";
FILENAME REPORT31 "&RPTLIB/ULWG03.LWG03R31";
FILENAME REPORT32 "&RPTLIB/ULWG03.LWG03R32";
FILENAME REPORT33 "&RPTLIB/ULWG03.LWG03R33";
FILENAME REPORT34 "&RPTLIB/ULWG03.LWG03R34";
FILENAME REPORT35 "&RPTLIB/ULWG03.LWG03R35";
FILENAME REPORT36 "&RPTLIB/ULWG03.LWG03R36";
FILENAME REPORT37 "&RPTLIB/ULWG03.LWG03R37";
/*THIS VARIABLE ALLOWS THE INCLUSION OF SPECIFIED SCHOOLS 
WITH OUT-OF-STATE ADDRESSES AS "UTAH SCHOOLS"*/
%LET UTSCL = '02098800'; /*UNIVERSITY OF PHOENIX*/
/*THIS VARIABLE ALLOWS THE EXCLUSION OF SPECIFIED SCHOOLS 
WITH UTAH ADDRESSES FROM THE REPORTED LIST OF "UTAH SCHOOLS"*/
%LET NONUTSCL = ' '; /*NONE CURRENTLY SET*/
/*SET DATE RANGE FOR REPORTING TO PREVIOUS MONTH*/
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY()+3,-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY()+3,-1,'end'), MMDDYYD10.)||"'");
     CALL SYMPUT('EFFDATE',TRIM(LEFT(UPCASE(
		PUT(INTNX('MONTH',TODAY()+3,-1), MONNAME9.)||' '||
		PUT(INTNX('MONTH',TODAY()+3,-1), YEAR4.)))));
RUN;
/*%SYSLPUT BEGIN = &BEGIN;*/
/*%SYSLPUT END = &END;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
OPTIONS MPRINT SYMBOLGEN;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE MOGUARS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.AF_APL_ID||A.AF_APL_ID_SFX	AS CLID,
	B.df_prs_id_br					AS SSN,
	B.af_cur_apl_ops_ldr			AS LENDER,
	D.im_ist_ful					AS LNAME,
	B.af_apl_ops_scl				AS SCHOOL,
	C.im_ist_ful					AS SNAME,
	ROUND(A.aa_gte_lon_amt,0)		AS GROSS,
	ROUND(A.aa_tot_rfd,0)			AS REFUND,
	ROUND(A.aa_tot_can,0)			AS CANCEL,
	A.ac_lon_typ					AS TYPE
	,C.IC_GEN_ST
	,B.AC_ACA_GDE_LEV				AS GRD
	,CASE 
		WHEN B.AF_ORG_APL_OPS_LDR = '828476' THEN F.AF_ORG_APL_OPS_LDR 
		ELSE B.AF_ORG_APL_OPS_LDR
	 END AS OLENDER
	,CASE
		WHEN B.AF_ORG_APL_OPS_LDR = '828476' THEN G.IM_IST_FUL
		ELSE E.IM_IST_FUL
	 END AS OLNAME
FROM OLWHRM1.GA10_LON_APP A 
INNER JOIN OLWHRM1.GA01_APP B 
	ON A.af_apl_id = B.af_apl_id 
	AND	A.ac_prc_sta = 'A' 
INNER JOIN (
	SELECT DISTINCT IF_IST 
		,IM_IST_FUL
		,IC_GEN_ST
	FROM OLWHRM1.SC01_LGS_SCL_INF 
	) C 
	ON B.af_apl_ops_scl = C.if_ist 
INNER JOIN OLWHRM1.LR01_LGS_LDR_INF D 
	ON B.af_cur_apl_ops_ldr = D.if_ist
LEFT OUTER JOIN OLWHRM1.LR01_LGS_LDR_INF E 
	ON B.AF_ORG_APL_OPS_LDR = E.IF_IST
LEFT OUTER JOIN OLWHRM1.GA01_APP F
	ON F.AF_APL_ID = B.AF_BS_MPN_APL_ID
LEFT OUTER JOIN OLWHRM1.LR01_LGS_LDR_INF G 
	ON F.AF_ORG_APL_OPS_LDR = G.IF_IST

WHERE	A.ac_lon_typ <> 'CL'
		AND A.ad_prc BETWEEN &BEGIN AND &END AND
		(A.aa_tot_can IS NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd > 0 OR
		 A.aa_tot_can IS NOT NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd - A.aa_tot_can > 0)
);

CREATE TABLE CONSOL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.AF_APL_ID||A.AF_APL_ID_SFX							AS CLID,
	A.aa_gte_lon_amt										AS GROSS,
	A.aa_tot_rfd											AS REFUND,
	A.aa_tot_can											AS CANCEL,
	A.ac_lon_typ											AS TYPE
FROM	OLWHRM1.GA10_LON_APP A INNER JOIN
		OLWHRM1.GA01_APP B ON
			A.af_apl_id = B.af_apl_id AND
			A.ac_prc_sta = 'A'

WHERE	A.ac_lon_typ = 'CL' AND
		A.ad_prc BETWEEN &BEGIN AND &END AND
		(A.aa_tot_can IS NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd > 0 OR
		 A.aa_tot_can IS NOT NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd - A.aa_tot_can > 0)
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=UTLWG03.LWG03RZ);
QUIT;
DATA MOGUARS;
	SET MOGUARS;
	IF CANCEL = . THEN CANCEL = 0;
	NETAMT = GROSS - REFUND - CANCEL;
RUN;
DATA CONSOL;
	SET CONSOL;
	IF CANCEL = . THEN CANCEL = 0;
	NETAMT = GROSS - REFUND - CANCEL;
RUN;
/*ENDRSUBMIT;*/
/*DATA MOGUARS;*/
/*	SET WORKLOCL.MOGUARS;*/
/*RUN;*/
/*DATA CONSOL;*/
/*	SET WORKLOCL.CONSOL;*/
/*RUN;*/
/******************************************************************************
*******************************************************************************
                               SUMMARY REPORTS                                 
*******************************************************************************
*******************************************************************************/

/*            BY LENDER                */

PROC SQL;
CREATE TABLE LENDER AS
SELECT	LNAME,
		LENDER,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	MOGUARS
GROUP BY LENDER, LNAME;
QUIT;
PROC SORT DATA = LENDER;
BY LNAME;
RUN;

OPTIONS ORIENTATION = PORTRAIT;
OPTIONS NODATE CENTER PAGENO=1;
OPTIONS PS=52 LS=96;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
PROC PRINT DATA = LENDER NOOBS SPLIT='/';
VAR LNAME
	LENDER
	NUMBER
	AMOUNT;
SUM 	NUMBER
		AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
LABEL	LNAME = 'LENDER NAME'
		LENDER = 'LENDER ID';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'BY LENDER';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R2';
RUN;

/*                 BY UTAH SCHOOL                   */

PROC SQL;
CREATE TABLE USCHOOL AS
SELECT	SNAME,
		SCHOOL,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	MOGUARS
WHERE (IC_GEN_ST = 'UT'
	OR SCHOOL IN (&UTSCL))
AND SCHOOL NOT IN (&NONUTSCL)
/*WHERE	SCHOOL IN  */
/*	('02270800', '02270801', '02612200', '00367000', '00367600',*/
/*	'00367100', '02573100', '02504000', '02556800', '02361000', '00367200',*/
/*	'02298500', '03082100', '02360800', '00522000', '02078800',*/
/*	'00367900', '00367800', '02516100', '00367400', '00367404', '00367403',*/
/*	'00367401', '02098800', '00367500', '03030601', '03030602', '03030600',*/
/*	'00367700', '00402700', '02531800', '00368000', '00368100', '01147900',*/
/*	'02579000', '02572700', '03108200', '02178500', '01017300', '02557700',*/
/*	'00367901', '01116600', '02098800', '00522100', '03110400',*/
/*	'00367405', '03617300')*/
GROUP BY SCHOOL, SNAME;
QUIT;
PROC SORT DATA = USCHOOL;
BY SNAME;
RUN;
OPTIONS  PAGENO=1 ;
PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
PROC PRINT DATA = USCHOOL NOOBS SPLIT='/';
VAR SNAME
	SCHOOL
	NUMBER
	AMOUNT;
SUM 	NUMBER
		AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
LABEL	SNAME = 'SCHOOL NAME'
		SCHOOL = 'SCHOOL ID';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'BY UTAH SCHOOL';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R3';
RUN;

/*              BY OUT OF STATE SCHOOL            */

PROC SQL;
CREATE TABLE SCHOOL AS
SELECT	SNAME,
		SCHOOL,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	MOGUARS
WHERE (IC_GEN_ST NE 'UT'
	AND SCHOOL NOT IN (&UTSCL))
OR SCHOOL IN (&NONUTSCL)
/*WHERE	SCHOOL NOT IN  */
/*	('02270800', '02270801', '02612200', '00367000', '00367600',*/
/*	'00367100', '02573100', '02504000', '02556800', '02361000', '00367200',*/
/*	'02298500', '03082100', '02360800', '00522000', '02078800',*/
/*	'00367900', '00367800', '02516100', '00367400', '00367404', '00367403',*/
/*	'00367401', '02098800', '00367500', '03030601', '03030602', '03030600',*/
/*	'00367700', '00402700', '02531800', '00368000', '00368100', '01147900',*/
/*	'02579000', '02572700', '03108200', '02178500', '01017300', '02557700',*/
/*	'00367901', '01116600', '02098800', '00522100', '03110400',*/
/*	'00367405', '03617300')*/
GROUP BY SCHOOL, SNAME;
QUIT;
PROC SORT DATA = SCHOOL;
BY SNAME;
RUN;
OPTIONS  PAGENO=1 ;
PROC PRINTTO PRINT=REPORT4 NEW;
RUN;
PROC PRINT DATA = SCHOOL NOOBS SPLIT='/';
VAR SNAME
	SCHOOL
	NUMBER
	AMOUNT;
SUM 	NUMBER
		AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
LABEL	SNAME = 'SCHOOL NAME'
		SCHOOL = 'SCHOOL ID';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'BY OUT OF STATE SCHOOL';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R4';
RUN;

/*				TOTAL BY TYPE                  */

PROC SQL;
CREATE TABLE TYPE AS
SELECT	TYPE,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	MOGUARS
GROUP BY TYPE;
QUIT;
OPTIONS  PAGENO=1 ;
PROC PRINTTO PRINT=REPORT5 NEW;
RUN;
PROC PRINT DATA = TYPE NOOBS;
VAR	TYPE
	NUMBER
	AMOUNT;
SUM 	NUMBER
		AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'BY LOAN TYPE';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R5';
RUN;

/*            CONSOLIDATIONS            */ 

PROC SQL;
CREATE TABLE CONSUM AS
SELECT	SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	CONSOL;
QUIT;
OPTIONS  PAGENO=1 ;
PROC PRINTTO PRINT=REPORT6 NEW;
RUN;
PROC PRINT DATA = CONSUM NOOBS;
VAR	NUMBER
	AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'CONSOLIDATION LOANS';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R6';
RUN;

/******************************************************************************
*******************************************************************************
                               LENDER REPORTS                                 
*******************************************************************************
*******************************************************************************/
PROC SQL;
CREATE TABLE LENDERPL AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS PLNO,
		SUM(NETAMT) AS PLAMT
FROM MOGUARS
WHERE TYPE = 'PL'
GROUP BY LENDER, LNAME, SCHOOL, SNAME;
RUN;
PROC SORT DATA=LENDERPL;
BY LENDER LNAME SCHOOL SNAME;
RUN;

PROC SQL;
CREATE TABLE LENDERSF AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS SFNO,
		SUM(NETAMT) AS SFAMT
FROM MOGUARS
WHERE TYPE = 'SF'
GROUP BY LENDER, LNAME, SCHOOL, SNAME;
RUN;
PROC SORT DATA=LENDERSF;
BY LENDER LNAME SCHOOL SNAME;
RUN;

PROC SQL;
CREATE TABLE LENDERSU AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS SUNO,
		SUM(NETAMT) AS SUAMT
FROM MOGUARS
WHERE TYPE = 'SU'
GROUP BY LENDER, LNAME, SCHOOL, SNAME;
RUN;
PROC SORT DATA=LENDERSU;
BY LENDER LNAME SCHOOL SNAME;
RUN;

PROC SQL;
CREATE TABLE LENDERGB AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS GBNO,
		SUM(NETAMT) AS GBAMT
FROM MOGUARS
WHERE TYPE = 'GB'
GROUP BY LENDER, LNAME, SCHOOL, SNAME;
RUN;
PROC SORT DATA=LENDERGB;
BY LENDER LNAME SCHOOL SNAME;
RUN;

PROC SQL;
CREATE TABLE LCTTLS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		SUM(NETAMT)	AS TTLAMT,
		COUNT(CLID)	AS TTLNO
FROM MOGUARS
GROUP BY LENDER, LNAME, SCHOOL, SNAME;
RUN;
PROC SORT DATA=LCTTLS;
BY LENDER LNAME SCHOOL SNAME;
RUN;

DATA LBYS;
	MERGE LENDERPL LENDERSF LENDERSU LENDERGB LCTTLS;
	BY LENDER LNAME SCHOOL SNAME;
RUN;
%MACRO FORMAT_ZEROS(DS,NUM);
DATA &DS;
SET &DS;
IF &NUM = . THEN &NUM = 0;
ELSE &NUM = &NUM;
RUN;
%MEND FORMAT_ZEROS;
%FORMAT_ZEROS(LBYS,SFNO);
%FORMAT_ZEROS(LBYS,SFAMT);
%FORMAT_ZEROS(LBYS,SUNO);
%FORMAT_ZEROS(LBYS,SUAMT);
%FORMAT_ZEROS(LBYS,PLNO);
%FORMAT_ZEROS(LBYS,PLAMT);
%FORMAT_ZEROS(LBYS,GBNO);
%FORMAT_ZEROS(LBYS,GBAMT);
%FORMAT_ZEROS(LBYS,TTLNO);
%FORMAT_ZEROS(LBYS,TTLAMT);
PROC PRINTTO PRINT=REPORT20 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=39 LS=132;

PROC REPORT DATA = LBYS NOWD HEADLINE HEADSKIP SPACING = 1 SPLIT = '/';
TITLE;
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R20';
COLUMN LENDER LNAME SCHOOL SNAME SFNO SFAMT SUNO SUAMT PLNO PLAMT GBNO GBAMT TTLNO TTLAMT;
DEFINE	LENDER / GROUP NOPRINT;
DEFINE	LNAME / GROUP NOPRINT;
DEFINE 	SCHOOL / DISPLAY 'SCHOOL ID' WIDTH = 9;
DEFINE	SNAME / DISPLAY 'SCHOOL NAME' WIDTH = 20 FLOW;
DEFINE	SFAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SFNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	SUAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SUNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	PLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	PLNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	GBAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	GBNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	TTLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	TTLNO / FORMAT = BEST. 'NO' WIDTH = 7;

COMPUTE BEFORE _PAGE_ / CENTER;
      LINE '(' LENDER $6. ')  ' LNAME $40.;
	  LINE "LOANS GUARANTEED BY UHEAA FOR THE MONTH OF &EFFDATE";
	  LINE 'BY SCHOOL';
      LINE ' ';
	  LINE @41  'STAFFORD' @60 'STAFFORD';
	  LINE @40 'SUBSIDIZED' @58 'UNSUBSIDIZED' @79 'PLUS'  @97 'GBPLUS' @118 'TOTAL';
ENDCOMP;

BREAK AFTER LENDER / OL SUMMARIZE PAGE;
RUN;
/******************************************************************************
*******************************************************************************
                               MISC REPORTS                                 
*******************************************************************************
*******************************************************************************/

/*    SALLIE MAE AND LOAN SERVICING CENTER LOANS      */

PROC SQL;
CREATE TABLE ANOMS AS
SELECT *
FROM MOGUARS
WHERE LENDER IN ('888885', '826717');
PROC SORT DATA = ANOMS;
BY LENDER LNAME;
RUN;
PROC PRINTTO PRINT=REPORT30 NEW;
RUN;

OPTIONS ORIENTATION = PORTRAIT;
OPTIONS CENTER PAGENO=1;
OPTIONS PS=52 LS=96;
PROC PRINT DATA = ANOMS NOOBS SPLIT='/' N='NUMBER OF LOANS = ' 'TOTAL NUMBER OF LOANS = ' WIDTH = MIN;;
BY LENDER LNAME;
VAR	SSN
	CLID
	TYPE
	NETAMT
	SCHOOL
	SNAME;
SUMBY LENDER;
FORMAT NETAMT DOLLAR13.2;
LABEL	CLID = 'UNIQUE ID'
		TYPE = 'LOAN TYPE'
		NETAMT = 'NET AMOUNT GUARANTEED'
		SCHOOL = 'SCHOOL ID '
		SNAME = 'SCHOOL NAME '
		LENDER = 'LENDER ID'
		LNAME = 'LENDER NAME';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'SALLIE MAE AND LOAN SERVICING CENTER LOANS';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R30';
RUN;

/*               BYU LOANS BY LENDER                       */

PROC SQL;
CREATE TABLE BYU AS 
SELECT	COUNT(CLID) AS NUMBER,
		SUM(NETAMT) AS AMOUNT,
		LNAME,
		LENDER
FROM MOGUARS
WHERE SCHOOL = '00367000'
GROUP BY LNAME, LENDER;
PROC SORT DATA=BYU;
BY LNAME;
RUN;
PROC PRINTTO PRINT=REPORT31 NEW;
RUN;
OPTIONS  PAGENO=1 ;
PROC PRINT DATA = BYU NOOBS SPLIT='/' WIDTH = MIN;;
VAR	LNAME
	LENDER
	NUMBER
	AMOUNT;
SUM NUMBER AMOUNT;
FORMAT AMOUNT DOLLAR13.2;
LABEL	AMOUNT = 'NET AMOUNT GUARANTEED'
		LENDER = 'LENDER ID'
		LNAME = 'LENDER NAME';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'BRIGHAM YOUNG UNIVERSITY';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R31';
RUN;

/*               UVSC LOANS BY LENDER               */

PROC SQL;
CREATE TABLE UVSC AS 
SELECT	COUNT(CLID) AS NUMBER,
		SUM(NETAMT) AS AMOUNT,
		LNAME,
		LENDER
FROM MOGUARS
WHERE SCHOOL = '00402700'
GROUP BY LNAME, LENDER;
PROC SORT DATA=UVSC;
BY LNAME;
RUN;
PROC PRINTTO PRINT=REPORT32 NEW;
RUN;
OPTIONS  PAGENO=1 ;
PROC PRINT DATA = UVSC NOOBS SPLIT='/' WIDTH = MIN;;
VAR	LNAME
	LENDER
	NUMBER
	AMOUNT;
SUM NUMBER AMOUNT;
FORMAT AMOUNT DOLLAR13.2;
LABEL	AMOUNT = 'NET AMOUNT GUARANTEED'
		LENDER = 'LENDER ID'
		LNAME = 'LENDER NAME';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'UTAH VALLEY STATE COLLEGE';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R32';
RUN;

/*                       ZION'S PERCENTAGES                  */

PROC SQL;
CREATE TABLE ZBYSCH AS
SELECT	SUM(NETAMT)	AS ZAMT,
		SNAME,
		SCHOOL
FROM MOGUARS
WHERE	LENDER = '817455' AND
		SCHOOL IN ('00367000', '00367200', '00367500', '00367700', '00368000')
GROUP BY SNAME, SCHOOL;
QUIT;
PROC SORT DATA=ZBYSCH;
BY SCHOOL;
RUN;
PROC SQL;
CREATE TABLE ZSCH AS
SELECT	SUM(NETAMT)	AS TTLAMT,
		SNAME,
		SCHOOL
FROM MOGUARS
WHERE	SCHOOL IN ('00367000', '00367200', '00367500', '00367700', '00368000')
GROUP BY SNAME, SCHOOL;
QUIT;
PROC SORT DATA=ZSCH;
BY SCHOOL;
RUN;
DATA ZCOMP;
	MERGE ZBYSCH ZSCH;
	BY SCHOOL;
RUN;
DATA ZCOMP;
	SET ZCOMP;
	ZPERC = ROUND((ZAMT/TTLAMT)*100,2);
RUN;
PROC PRINTTO PRINT=REPORT33 NEW;
RUN;
OPTIONS PAGENO=1;
PROC PRINT DATA = ZCOMP NOOBS SPLIT='/' WIDTH=MIN;
VAR SCHOOL
	SNAME
	ZAMT
	TTLAMT
	ZPERC;
FORMAT ZAMT DOLLAR13. TTLAMT DOLLAR13. ZPERC BEST.;
LABEL	SCHOOL = 'SCHOOL ID'
		SNAME = 'SCHOOL NAME'
		ZAMT = 'ZIONS AMOUNT'
		TTLAMT = 'SCHOOL TOTAL'
		ZPERC = 'PERCENT';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"NET GUARANTEES FOR THE MONTH OF &EFFDATE";
TITLE3	'ZIONS PERCENTAGES';
FOOTNOTE	'JOB = UTLWG03     REPORT = ULWG03.LWG03R33';
RUN;
/*WELLS FARGO AND UHEAA PROCESSING*/
%MACRO CDLMF(LDR_ID,RNO);
PROC SQL;
CREATE TABLE PL_DS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS PLNO,
		SUM(NETAMT) AS PLAMT,
		GRD
FROM MOGUARS
WHERE TYPE = 'PL'
AND LENDER = "&LDR_ID"
GROUP BY LENDER, LNAME, SCHOOL, SNAME, GRD;
RUN;
PROC SORT DATA=PL_DS;
BY LENDER LNAME SCHOOL SNAME GRD;
RUN;

PROC SQL;
CREATE TABLE SF_DS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS SFNO,
		SUM(NETAMT) AS SFAMT
		,GRD
FROM MOGUARS
WHERE TYPE = 'SF'
AND LENDER = "&LDR_ID"
GROUP BY LENDER, LNAME, SCHOOL, SNAME, GRD;
RUN;
PROC SORT DATA=SF_DS;
BY LENDER LNAME SCHOOL SNAME GRD;
RUN;

PROC SQL;
CREATE TABLE SU_DS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS SUNO,
		SUM(NETAMT) AS SUAMT
		,GRD
FROM MOGUARS
WHERE TYPE = 'SU'
AND LENDER = "&LDR_ID"
GROUP BY LENDER, LNAME, SCHOOL, SNAME, GRD;
RUN;
PROC SORT DATA=SU_DS;
BY LENDER LNAME SCHOOL SNAME GRD;
RUN;

PROC SQL;
CREATE TABLE GB_DS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS GBNO,
		SUM(NETAMT) AS GBAMT,
		GRD
FROM MOGUARS
WHERE TYPE = 'GB'
AND LENDER = "&LDR_ID"
GROUP BY LENDER, LNAME, SCHOOL, SNAME, GRD;
RUN;
PROC SORT DATA=GB_DS;
BY LENDER LNAME SCHOOL SNAME GRD;
RUN;

PROC SQL;
CREATE TABLE LCTTLS AS
SELECT	LENDER,
		LNAME,
		SCHOOL,
		SNAME,
		SUM(NETAMT)	AS TTLAMT,
		COUNT(CLID)	AS TTLNO
		,GRD
FROM MOGUARS
WHERE LENDER = "&LDR_ID"
GROUP BY LENDER, LNAME, SCHOOL, SNAME, GRD;
RUN;
PROC SORT DATA=LCTTLS;
BY LENDER LNAME SCHOOL SNAME GRD;
RUN;
DATA LBYS;
	MERGE PL_DS GB_DS SF_DS SU_DS LCTTLS;
	BY LENDER LNAME SCHOOL SNAME GRD;
RUN;
DATA CDTF;
SET LBYS;
WHERE LENDER="&LDR_ID";
RUN;
%FORMAT_ZEROS(CDTF,PLNO);
%FORMAT_ZEROS(CDTF,PLAMT);
%FORMAT_ZEROS(CDTF,GBNO);
%FORMAT_ZEROS(CDTF,GBAMT);
%FORMAT_ZEROS(CDTF,SFNO); 
%FORMAT_ZEROS(CDTF,SFAMT);
%FORMAT_ZEROS(CDTF,SUNO); 
%FORMAT_ZEROS(CDTF,SUAMT); 
%FORMAT_ZEROS(CDTF,SUNO); 
%FORMAT_ZEROS(CDTF,SUAMT);
%FORMAT_ZEROS(CDTF,TTLAMT);
%FORMAT_ZEROS(CDTF,TTLNO); 

DATA _NULL_;
SET  WORK.CDTF;
FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT SCHOOL $8. ;
	FORMAT SNAME $40. ;
	FORMAT SFNO BEST12. ;
	FORMAT SFAMT BEST12. ;
	FORMAT SUNO BEST12. ;
	FORMAT SUAMT BEST12. ;
	FORMAT PLNO BEST12. ;
	FORMAT PLAMT BEST12. ;
	FORMAT GBNO BEST12. ;
	FORMAT GBAMT BEST12. ;
	FORMAT TTLNO BEST12. ;
	FORMAT TTLAMT BEST12. ;
	FORMAT GRD $4. ;
IF _N_ = 1 THEN        
DO;
	PUT
	'SCHOOL'
	','
	'SCH_NAME'
	','
	'STAF_SUB_NO'
	','
	'STAF_SUB_AMT'
	','
	'STAF_UNS_NO'
	','
	'STAF_UNS_AMT'
	','
	'PLUS_NO'
	','
	'PLUS_AMT'
	','
	'GB_NO'
	','
	'GB_AMT'
	','
	'TOTAL_NO'
	','
	'TOTAL_AMT'
	','
	'GRD'
	;
	END;
DO;
	EFIOUT + 1;
	PUT SCHOOL $ @;
	PUT SNAME $ @;
	PUT SFNO @;
	PUT SFAMT @;
	PUT SUNO @;
	PUT SUAMT @;
	PUT PLNO @;
	PUT PLAMT @;
	PUT GBNO @;
	PUT GBAMT @;
	PUT TTLNO @;
	PUT TTLAMT @;
	PUT GRD ;
	;
	END;
RUN;

PROC PRINTTO;
RUN;
%MEND CDLMF;
%CDLMF(813894,34);
%CDLMF(828476,35);

%MACRO SQL_SPLIT(TAB,LNNO,LNAMT,LNTYP,OLDID);
PROC SQL;
CREATE TABLE &TAB AS
SELECT	OLENDER AS LENDER,
		OLNAME AS LNAME LENGTH=100,
		SCHOOL,
		SNAME,
		COUNT(CLID) AS &LNNO,
		SUM(NETAMT) AS &LNAMT
FROM MOGUARS
WHERE TYPE IN (&LNTYP)
	AND LENDER = '828476'
	AND OLENDER = "&OLDID"
GROUP BY OLENDER, OLNAME, SCHOOL, SNAME;
QUIT;
PROC SORT DATA=&TAB;
BY LENDER LNAME SCHOOL SNAME;
RUN;
%MEND SQL_SPLIT;
%MACRO REFLNDR_REP(DSETS,RNO);
DATA OLBYS;
	MERGE OLENDERPL OLENDERSF OLENDERSU OLENDERGB OLCTTLS;
	BY LENDER LNAME SCHOOL SNAME;
RUN;
%FORMAT_ZEROS(OLBYS,SFNO);
%FORMAT_ZEROS(OLBYS,SFAMT);
%FORMAT_ZEROS(OLBYS,SUNO);
%FORMAT_ZEROS(OLBYS,SUAMT);
%FORMAT_ZEROS(OLBYS,PLNO);
%FORMAT_ZEROS(OLBYS,PLAMT);
%FORMAT_ZEROS(OLBYS,GBNO);
%FORMAT_ZEROS(OLBYS,GBAMT);
%FORMAT_ZEROS(OLBYS,TTLNO);
%FORMAT_ZEROS(OLBYS,TTLAMT);
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=39 LS=132;

PROC CONTENTS DATA=OLBYS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWG03  	 REPORT = ULWG03.LWG03R&RNO";
	END;
RETURN;
RUN;

PROC REPORT DATA = OLBYS NOWD HEADLINE HEADSKIP SPACING = 1 SPLIT = '/';
TITLE;
FOOTNOTE	"JOB = UTLWG03     REPORT = ULWG03.LWG03R&RNO";
COLUMN LENDER LNAME SCHOOL SNAME SFNO SFAMT SUNO SUAMT PLNO PLAMT GBNO GBAMT TTLNO TTLAMT;
DEFINE	LENDER / GROUP NOPRINT;
DEFINE	LNAME / GROUP NOPRINT;
DEFINE 	SCHOOL / DISPLAY 'SCHOOL ID' WIDTH = 9;
DEFINE	SNAME / DISPLAY 'SCHOOL NAME' WIDTH = 20 FLOW;
DEFINE	SFAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SFNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	SUAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	SUNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	PLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	PLNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	GBAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 11;
DEFINE	GBNO / FORMAT = BEST. 'NO' WIDTH = 6;
DEFINE	TTLAMT / FORMAT = DOLLAR13. 'AMOUNT' WIDTH = 12;
DEFINE	TTLNO / FORMAT = BEST. 'NO' WIDTH = 7;

COMPUTE BEFORE _PAGE_ / CENTER;
      LINE '(' LENDER $6. ')  ' LNAME $40.;
	  LINE "LOANS GUARANTEED BY UHEAA FOR THE MONTH OF &EFFDATE";
	  LINE 'BY SCHOOL';
      LINE ' ';
	  LINE @41  'STAFFORD' @60 'STAFFORD';
	  LINE @40 'SUBSIDIZED' @58 'UNSUBSIDIZED' @79 'PLUS'  @97 'GBPLUS' @118 'TOTAL';
ENDCOMP;

BREAK AFTER LENDER / OL SUMMARIZE PAGE;
RUN;

PROC PRINTTO;
RUN;

%MEND REFLNDR_REP;
%SQL_SPLIT(OLENDERPL,PLNO,PLAMT,'PL',817476);
%SQL_SPLIT(OLENDERSF,SFNO,SFAMT,'SF',817476);
%SQL_SPLIT(OLENDERSU,SUNO,SUAMT,'SU',817476);
%SQL_SPLIT(OLENDERGB,GBNO,GBAMT,'GB',817476);
%SQL_SPLIT(OLCTTLS,TTLNO,TTLAMT,%STR('PL','SF','SU','GB'),817476);
%REFLNDR_REP(OLENDERPL OLENDERSF OLENDERSU OLENDERGB OLCTTLS,36);
%SQL_SPLIT(OLENDERPL,PLNO,PLAMT,'PL',832382);
%SQL_SPLIT(OLENDERSF,SFNO,SFAMT,'SF',832382);
%SQL_SPLIT(OLENDERSU,SUNO,SUAMT,'SU',832382);
%SQL_SPLIT(OLENDERGB,GBNO,GBAMT,'GB',832382);
%SQL_SPLIT(OLCTTLS,TTLNO,TTLAMT,%STR('PL','SF','SU','GB'),832382);
%REFLNDR_REP(OLENDERPL OLENDERSF OLENDERSU OLENDERGB OLCTTLS,37);
/*LENDER='813894'*/
/**/
/*PROC EXPORT DATA=MOGUARS*/
/*            OUTFILE= "T:\SAS\UTLWG03_DETAIL.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
/*PROC EXPORT DATA=CONSOL*/
/*            OUTFILE= "T:\SAS\UTLWG03_CONSOL_DETAIL.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/

