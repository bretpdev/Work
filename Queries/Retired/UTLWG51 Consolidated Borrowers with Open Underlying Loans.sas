/*UTLWG51 CONSOLIDATION BORROWERS WITH OPEN UNDERLYING LOANS*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG51.LWG51R2";
FILENAME REPORTZ "&RPTLIB/ULWG51.LWG51RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LS_CON AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN AS SSN
	,RTRIM(PD10.DM_PRS_LST) || ' ' || RTRIM(PD10.DM_PRS_1) AS NAME
	,LN10.LN_SEQ
	,LN10.IC_LON_PGM
	,LN10.LA_CUR_PRI
	,LN10.LD_LON_1_DSB
	,LN10.LC_STA_LON10
	,LN15.LD_DSB
	,PD10.DF_SPE_ACC_ID AS ACCT#
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
ON LN10.BF_SSN = PD10.DF_PRS_ID
LEFT OUTER JOIN (SELECT A.BF_SSN, MAX(A.LD_DSB) AS LD_DSB
				FROM OLWHRM1.LN15_DSB A
				WHERE A.LD_DSB_CAN IS NULL
				GROUP BY A.BF_SSN
				) LN15
ON LN15.BF_SSN = LN10.BF_SSN
WHERE LN10.IC_LON_PGM IN ('SUBCNS','UNCNS')
AND LN10.LC_STA_LON10 = 'R'
AND (SELECT SUM(L10.LA_CUR_PRI) 
	 FROM OLWHRM1.LN10_LON L10
	 WHERE L10.BF_SSN = LN10.BF_SSN
	 AND L10.LN_SEQ = LN10.LN_SEQ 
	 AND L10.IC_LON_PGM IN ('SUBCNS','UNCNS')
	 AND L10.LC_STA_LON10 = 'R') > 0
AND LN15.LD_DSB <= LN10.LD_LON_1_DSB
);

CREATE TABLE LS_NON_CON AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN AS SSN
	,RTRIM(PD10.DM_PRS_LST) || ' ' || RTRIM(PD10.DM_PRS_1) AS NAME
	,LN10.LN_SEQ
	,LN10.IC_LON_PGM
	,LN10.LA_CUR_PRI
	,LN10.LD_LON_1_DSB
	,LN10.LC_STA_LON10
	,CASE 
		WHEN B.MINPRI < 0 THEN
		'*'
		WHEN B.MINPRI >= 0 THEN
		''
	END CREDITBALANCE
	,PD10.DF_SPE_ACC_ID AS ACCT#
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
ON LN10.BF_SSN = PD10.DF_PRS_ID
LEFT OUTER JOIN (SELECT Z.BF_SSN, MIN(Z.LA_CUR_PRI) AS MINPRI
				FROM OLWHRM1.LN10_LON Z
/*				WHERE Z.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')*/
				GROUP BY Z.BF_SSN
				) B
ON LN10.BF_SSN = B.BF_SSN
WHERE /*LN10.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')
AND*/ (SELECT SUM(L10.LA_CUR_PRI) AS TOTAL
			FROM OLWHRM1.LN10_LON L10
			WHERE L10.BF_SSN = LN10.BF_SSN 
			AND L10.LN_SEQ = LN10.LN_SEQ 
			AND L10.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')
			GROUP BY L10.BF_SSN) > 0
);

CREATE TABLE LCODATA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT 	 LCOA.DF_LCO_PRS_SSN_BR as SSN
		,AP1A.AN_LCO_APL_SEQ AS LCO_SEQ
		,SUBSTR(LCOA.LF_UND_LN_ACC_NUM, 8,2) as ACCNUM
	  	,LCOA.LI_UND_LN_CON as CON
		,AP1A.AD_LCO_APL_DSB
FROM OLWHRM1.LC10_UND_LN_INF LCOA
INNER JOIN OLWHRM1.AP1A_LCO_APL AP1A
	ON AP1A.DF_LCO_PRS_SSN_BR = LCOA.DF_LCO_PRS_SSN_BR
	AND AP1A.AN_LCO_APL_SEQ = LCOA.AN_LCO_APL_SEQ
WHERE SUBSTR(LCOA.LF_UND_LN_ACC_NUM,1,4) = 'L0UT' AND
		 LCOA.LI_UND_LN_CON = 'Y'		  
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWG51.LWG51RZ);*/
/*QUIT;*/
DATA LCODATA;
SET LCODATA;
FORMAT ACCNUM 6.;
LN_SEQ = INPUT(ACCNUM,2.);
RUN;

PROC SORT DATA=LS_CON;
BY SSN LD_DSB;
RUN;

PROC SORT DATA=LS_NON_CON;
BY SSN;
RUN;

PROC SQL;
CREATE TABLE DEMO3 AS 
SELECT DISTINCT A.*
	,B.LCO_SEQ
FROM LS_CON A
INNER JOIN LCODATA B
	ON A.SSN = B.SSN
	AND A.LD_LON_1_DSB = B.AD_LCO_APL_DSB
ORDER BY SSN, LN_SEQ;
QUIT;

PROC SQL;
CREATE TABLE COMBINE1 AS 
SELECT DISTINCT C.*
	,B.LCO_SEQ
FROM DEMO3 A
INNER JOIN LCODATA B
	ON A.SSN = B.SSN
	AND A.LCO_SEQ = B.LCO_SEQ
INNER JOIN LS_NON_CON C
	ON B.SSN = C.SSN
	AND B.LN_SEQ = C.LN_SEQ
WHERE C.LA_CUR_PRI > 0
;
QUIT; 

PROC SQL;
CREATE TABLE COMBINE2 AS 
SELECT DISTINCT A.*
FROM DEMO3 A
INNER JOIN LCODATA B
	ON A.SSN = B.SSN
	AND A.LCO_SEQ = B.LCO_SEQ
INNER JOIN COMBINE1 C
	ON B.SSN = C.SSN
	AND B.LN_SEQ = C.LN_SEQ
;
QUIT; 

DATA COMBINE;
SET COMBINE1 COMBINE2;
RUN;

ENDRSUBMIT;
DATA COMBINE; SET WORKLOCL.COMBINE; RUN;
PROC SORT DATA = COMBINE;
BY ACCT_ NAME LN_SEQ;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=39 LS=127;
PROC PRINT NOOBS SPLIT='/' DATA=COMBINE;
BY ACCT_ NAME;
ID ACCT_ NAME;
VAR LCO_SEQ LN_SEQ IC_LON_PGM LA_CUR_PRI LD_LON_1_DSB CREDITBALANCE;
LABEL LCO_SEQ = 'LCO/SEQ #'	
	LN_SEQ = 'LN/SEQ'
	IC_LON_PGM = 'LOAN/PROGRAM'
	LA_CUR_PRI = 'PRINCIPAL'
	LD_LON_1_DSB = 'DISBURSMENT/DATE'
	CREDITBALANCE = 'CREDIT/BALANCE';
TITLE 'Consolidated Borrowers With Open Loans';
FOOTNOTE  'JOB = UTLWG51     REPORT = UTLWG51.LWG51R2';
RUN;
PROC PRINTTO;
RUN;

/*PROC EXPORT DATA=WORKLOCL.DEMO*/
/*            OUTFILE= "T:\SAS\DEMO.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
