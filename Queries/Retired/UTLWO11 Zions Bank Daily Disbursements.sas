/*DAILY DISBURSEMENT REPORT, ZIONS REGION*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWO11.LWO11R2";
FILENAME REPORT3 "&RPTLIB/ULWO11.LWO11R3";
*/

DATA _NULL_;
CALL SYMPUT('ROSTDT',"'"||PUT(INTNX('DAY',TODAY(),-1,'beginning'), MMDDYY10.)||"'");
CALL SYMPUT('DSBDAT', PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT ROSTDT = &ROSTDT;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DADISB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS BF_SSN,
    COALESCE (A.LA_DSB,0) - COALESCE (A.LA_DSB_CAN,0) AS DISBAMT,
    D.LA_DSB_FEE 		AS GTYFEE,
    A.LN_LON_DSB_SEQ,
    A.LC_LDR_DSB_MDM,
    A.IC_LON_PGM,
    B.AF_DOE_SCL,
    C.AF_DOE_LDR,
	E.IM_LDR_SHO
	,E.IM_LDR_FUL
	,A.LC_DSB_TYP
	,A.LC_STA_LON15
FROM OLWHRM1.LN15_DSB_zb A
INNER JOIN OLWHRM1.AP10_APL_zb B
    ON B.BF_SSN = A.BF_SSN
    AND B.AN_SEQ = A.AN_SEQ
INNER JOIN OLWHRM1.AP20_APL_LON_zb C
    ON C.BF_SSN = A.BF_SSN
    AND C.AN_SEQ = A.AN_SEQ
    AND C.IC_LON_PGM = A.IC_LON_PGM
INNER JOIN OLWHRM1.LN18_DSB_FEE_zb D
    ON D.BF_SSN = A.BF_SSN
    AND D.LN_BR_DSB_SEQ = A.LN_BR_DSB_SEQ
    AND D.LC_DSB_FEE = '02'	/*GTY FEE*/
INNER JOIN OLWHRM1.LR10_LDR_DMO_zb E
	ON E.IF_DOE_LDR = C.AF_DOE_LDR
WHERE A.LC_STA_LON15 IN ('1','3')
AND A.LC_DSB_TYP IN ('2','1')
AND A.LD_DSB_ROS_PRT = &ROSTDT
AND (A.LA_DSB_CAN IS NULL
    OR A.LA_DSB_CAN <> A.LA_DSB)
AND A.LC_LDR_DSB_MDM NOT IN ('M','E','F')
ORDER BY C.AF_DOE_LDR, A.IC_LON_PGM, A.BF_SSN
);
DISCONNECT FROM DB2;

DATA DADISB;
SET DADISB;
NETDISB = DISBAMT - GTYFEE;
RUN;

ENDRSUBMIT;
DATA DADISB; 
SET WORKLOCL.DADISB; 
RUN;

PROC SQL;
CREATE TABLE DASUM AS
SELECT AF_DOE_LDR
,IM_LDR_FUL
,SUM(NETDISB) AS NETDISB
FROM DADISB
GROUP BY AF_DOE_LDR, IM_LDR_FUL;
QUIT;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=90;
PROC CONTENTS DATA=DASUM OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
   PUT // 90*'-';
   PUT      ////////
       @31 '**** NO DISBURSEMENTS FOUND ****';
   PUT ////////
       @37 '-- END OF REPORT --';
   PUT ////////////////////////////
   		@25 "JOB = UTLWO11     REPORT = ULWO11.LWO11R2";
   END;
RETURN;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 'DAILY DISBURSEMENT SUMMARY - ZB';
TITLE3 "DISBURSEMENT DATE : &DSBDAT";
run;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=90;
PROC PRINT NOOBS SPLIT='/' DATA=DASUM WIDTH=UNIFORM WIDTH=MIN;
VAR IM_LDR_FUL AF_DOE_LDR NETDISB;
SUM NETDISB;
LABEL AF_DOE_LDR = "LENDER ID" IM_LDR_FUL = "LENDER NAME"
NETDISB= "TOTAL NET DISBURSEMENT AMOUNT";;
FORMAT NETDISB DOLLAR18.2;
TITLE 'DAILY DISBURSEMENT SUMMARY - ZB';
TITLE2 "DISBURSEMENT DATE : &DSBDAT";
FOOTNOTE "JOB = UTLWO11     REPORT = ULWO11.LWO11R2";
RUN;

%LET TTL = ;

%MACRO PRINT(LENDER=,REPNO=);

DATA DADISBLDR;
SET DADISB;
WHERE AF_DOE_LDR = "&LENDER";
TYPESORT = IC_LON_PGM;
RUN;

DATA _NULL_;
SET DADISBLDR;
CALL SYMPUT('TTL',TRIM(IM_LDR_FUL));
RUN;

/*PROC PRINTTO PRINT=REPORT&REPNO;
RUN;*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=90;
PROC CONTENTS DATA=DADISBLDR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
   PUT // 90*'-';
   PUT      ////////
       @31 '**** NO DISBURSEMENTS FOUND ****';
   PUT ////////
       @37 '-- END OF REPORT --';
   PUT ////////////////////////////
   		@25 "JOB = UTLWO11     REPORT = ULWO11.LWO11R&REPNO";
   END;
RETURN;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 'DAILY DISBURSEMENT REPORT - ZB';
TITLE3 "LENDER ID # &LENDER";
TITLE4 "DISBURSEMENT DATE : &DSBDAT";
run;

PROC REPORT DATA=DADISBLDR NOWD HEADSKIP SPLIT='/';
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 'DAILY DISBURSEMENT REPORT - ZB';
TITLE3 "LENDER ID # &LENDER - &TTL";
TITLE4 "DISBURSEMENT DATE : &DSBDAT";
FOOTNOTE "JOB = UTLWO11     REPORT = ULWO11.LWO11R&REPNO";
COLUMN TYPESORT BF_SSN LN_LON_DSB_SEQ DISBAMT GTYFEE NETDISB
LC_LDR_DSB_MDM IC_LON_PGM AF_DOE_SCL N;
DEFINE TYPESORT / GROUP ORDER NOPRINT;
DEFINE BF_SSN / DISPLAY "SSN" WIDTH=9 FORMAT=SSN11.;
DEFINE LN_LON_DSB_SEQ / DISPLAY "DISB/#";
DEFINE DISBAMT / ANALYSIS FORMAT=COMMA14.2 "DISB/AMOUNT";
DEFINE GTYFEE / ANALYSIS FORMAT=COMMA11.2 "ORIGINATION/FEE";
DEFINE NETDISB / ANALYSIS FORMAT=COMMA14.2 "NET DISB/AMOUNT";
DEFINE LC_LDR_DSB_MDM / DISPLAY WIDTH=6 "DISB/MEDIUM";
DEFINE IC_LON_PGM / DISPLAY "LOAN/TYPE" WIDTH=6;
DEFINE AF_DOE_SCL / DISPLAY "SCHOOL/CODE";
DEFINE N / NOPRINT;

COMPUTE AFTER TYPESORT;
LINE @1 9*'-' @21 14*'-' @37 11*'-' @50 14*'-' @74 16*'-';
LINE @1 'SUBTOTAL: ' @21 DISBAMT.SUM DOLLAR14.2 @37 GTYFEE.SUM DOLLAR11.2 
@50 NETDISB.SUM DOLLAR14.2 @74 'DISB COUNT:' N COMMA5.;
LINE ' ';
LINE ' ';
ENDCOMP;

COMPUTE AFTER;
LINE ' ';
LINE @1 'TOTAL NUMBER OF DISBURSEMENTS:' @40 N COMMA7.;
LINE @1 'TOTAL DISBURSEMENT AMOUNT:' @33 DISBAMT.SUM DOLLAR14.2;
LINE @1 'TOTAL ORIGINATION FEE:' @36 GTYFEE.SUM DOLLAR11.2;
LINE @1 'TOTAL NET DISBURSEMENT AMOUNT:' @33 NETDISB.SUM DOLLAR14.2;
ENDCOMP;
RUN;
%MEND PRINT;

%PRINT(LENDER=817455,REPNO=3);
