*----------------------------------------------------------*
| UTLWM34 - Increase Collection Revenue - AWG Work Address |
*----------------------------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = C:\WINDOWS\TEMP;*/
FILENAME REPORT2 "&RPTLIB/ULWM34.LWM34R2";
FILENAME REPORT3 "&RPTLIB/ULWM34.LWM34R3";
FILENAME REPORT4 "&RPTLIB/ULWM34.LWM34R4";
FILENAME REPORTZ "&RPTLIB/ULWM34.LWM34RZ";
DATA _NULL_;
	CALL SYMPUT('DAYS_AGO_30',INTNX('DAY',TODAY(),-30,'BEGINNING'));
	CALL SYMPUT('DAYS_AGO_180',INTNX('DAY',TODAY(),-180,'BEGINNING'));
RUN;
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ICRAWA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,D.DF_SPE_ACC_ID
	,D.DM_PRS_1
	,D.DM_PRS_LST
	,E.IX_GEN_STR_ADR_1
	,E.IX_GEN_STR_ADR_2
	,E.IM_GEN_CT
	,E.IC_GEN_ST
	,E.IF_GEN_ZIP
	,E.IM_GEN_FGN_CNY
	,'MA2329' AS COST_CENTER_CODE
	,F.PF_ACT
	,F.BD_ATY_CLO
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN (
	SELECT EMP1.BF_SSN
		,EMP2.IX_GEN_STR_ADR_1
		,EMP2.IX_GEN_STR_ADR_2
		,EMP2.IM_GEN_CT
		,EMP2.IC_GEN_ST
		,EMP2.IF_GEN_ZIP
		,EMP2.IM_GEN_FGN_CNY
	FROM OLWHRM1.BR02_BR_EMP EMP1
	INNER JOIN OLWHRM1.IN01_LGS_IDM_MST EMP2
		ON EMP1.BF_EMP_ID_1 = EMP2.IF_IST
	WHERE EMP2.IC_IST_TYP = '006'
		AND EMP2.IX_GEN_STR_ADR_1 != ''
		AND EMP2.IX_GEN_STR_ADR_2 != ''
		AND EMP2.IM_GEN_CT != ''
		AND EMP2.IC_GEN_ST != ''
		AND EMP2.IF_GEN_ZIP != ''
		AND EMP1.BF_EMP_ID_1 NOT IN 
		(
			'A9100010','GN001174','GN00318','GN004957',
			'9080040','9100040','0403270','GN00025'
		)
	) E
	ON D.DF_PRS_ID = E.BF_SSN
LEFT OUTER JOIN OLWHRM1.LA10_LEG_ACT B
	ON A.BF_SSN = B.DF_PRS_ID_BR
LEFT OUTER JOIN OLWHRM1.LA11_LEG_ACT_ATY C
	ON A.BF_SSN = C.DF_PRS_ID_BR
LEFT OUTER JOIN (
	SELECT DF_PRS_ID
		,PF_ACT
		,MAX(BD_ATY_CLO) AS BD_ATY_CLO
	FROM OLWHRM1.AY01_BR_ATY
	WHERE PF_ACT IN (
		'DLWH1','DLWH2','DLWH3'
		)
	GROUP BY DF_PRS_ID
		,PF_ACT
	) F
	ON A.BF_SSN = F.DF_PRS_ID
WHERE A.LC_STA_DC10 = '03'
	AND A.LD_CLM_ASN_DOE IS NULL
	AND A.LC_AUX_STA = ''
	AND 
	(
		(
			A.LC_GRN = '02' 
			AND B.BD_WDR IS NULL
		)
	OR
		(	
			A.LC_GRN = '07' 
			AND C.BC_EXE_TYP = '05'
		)
	)
	AND NOT EXISTS 
	(
		SELECT 1
		FROM OLWHRM1.AY01_BR_ATY X
		WHERE X.DF_PRS_ID = A.BF_SSN
			AND X.PF_ACT = 'DLSMR'
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWM34.LWM34RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA ICRAWA ;*/
/*SET WORKLOCL.ICRAWA;*/
/*RUN;*/
DATA ICRAWA (DROP=PF_ACT BD_ATY_CLO) ACT_CDS(KEEP=BF_SSN PF_ACT BD_ATY_CLO WHERE=(PF_ACT ^= ''));
SET ICRAWA;
RUN;

PROC SORT DATA = ACT_CDS NODUPKEY; BY PF_ACT BD_ATY_CLO; RUN;
PROC SORT DATA = ICRAWA NODUPKEY; BY BF_SSN; RUN;

PROC SQL;
CREATE TABLE ICRAWA_WACT AS
SELECT DISTINCT A.*
	,B.BD_ATY_CLO AS D1DT
	,C.BD_ATY_CLO AS D2DT
	,D.BD_ATY_CLO AS D3DT
FROM ICRAWA A
LEFT OUTER JOIN ACT_CDS B
	ON A.BF_SSN = B.BF_SSN
	AND B.PF_ACT = 'DLWH1'
LEFT OUTER JOIN ACT_CDS C
	ON A.BF_SSN = C.BF_SSN
	AND C.PF_ACT = 'DLWH2'
LEFT OUTER JOIN ACT_CDS D
	ON A.BF_SSN = D.BF_SSN
	AND D.PF_ACT = 'DLWH3'
;
QUIT;

DATA ICRAWA;
SET ICRAWA_WACT;
IF D1DT = . OR 
	D1DT < &DAYS_AGO_180 
		THEN RFILE = 2;
ELSE IF D1DT ^= . AND 
	D1DT < &DAYS_AGO_30 AND 
	(
		D2DT = . OR
		D2DT < &DAYS_AGO_180
	)
		THEN RFILE = 3;
IF D2DT ^= . AND 
	D2DT < &DAYS_AGO_30 AND 
	(
		D3DT = . OR
		D3DT < &DAYS_AGO_180
	)
		THEN RFILE = 4;
RUN;

%MACRO CRT_REPS(REPNO);
DATA _NULL_;
SET ICRAWA;
WHERE RFILE = &REPNO ;
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT DM_PRS_1 $12. ;
   FORMAT DM_PRS_LST $35. ;
   FORMAT IX_GEN_STR_ADR_1 $40. ;
   FORMAT IX_GEN_STR_ADR_2 $40. ;
   FORMAT IM_GEN_CT $30. ;
   FORMAT IC_GEN_ST $2. ;
   FORMAT IF_GEN_ZIP $14. ;
   FORMAT IM_GEN_FGN_CNY $25.;
   FORMAT COST_CENTER_CODE $6. ;
IF _N_ = 1 THEN DO;
	PUT
		'BF_SSN'
		','
		'DF_SPE_ACC_ID'
		','
		'DM_PRS_1'
		','
		'DM_PRS_LST'
		','
		'IX_GEN_STR_ADR_1'
		','
		'IX_GEN_STR_ADR_2'
		','
		'IM_GEN_CT'
		','
		'IC_GEN_ST'
		','
		'IF_GEN_ZIP'
		','
		'IM_GEN_FGN_CNY'
		','
		'STATE_INDICATOR'
		','
		'COST_CENTER_CODE'
		;
END;
DO;
	PUT BF_SSN $ @;
	PUT DF_SPE_ACC_ID $ @;
	PUT DM_PRS_1 $ @;
	PUT DM_PRS_LST $ @;
	PUT IX_GEN_STR_ADR_1 $ @;
	PUT IX_GEN_STR_ADR_2 $ @;
	PUT IM_GEN_CT $ @;
	PUT IC_GEN_ST $ @;
	PUT IF_GEN_ZIP $ @;
	PUT IM_GEN_FGN_CNY $ @;
	PUT IC_GEN_ST $ @;
	PUT COST_CENTER_CODE $ ;
END;
RUN;
%MEND CRT_REPS;

%CRT_REPS(2);
%CRT_REPS(3);
%CRT_REPS(4);
