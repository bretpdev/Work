/*UTLWDR6 - CLAIMS PAID OVER THE LIMITS -PHEAA SAS JOB RECEIVED AT CONVERSION*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWDR6.LWDR6R2";*/

DATA _NULL_ ;
        BEGIN1=INTNX('DAY',TODAY()+3,-120,'BEGINNING') ;
        CALL SYMPUT ('BEGIN',"'"||PUT(BEGIN1,YYMMDD10.)||"'") ;
RUN;

%SYSLPUT BEGIN = &BEGIN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL  ;
CONNECT TO DB2 (DATABASE=dlgsutwh) ;
CREATE TABLE PYMENTQA AS
SELECT SSN,
	FILE,
	PAYOFF,
	OOP,
	DCR,
	DRS,
	TIMEPART(TIME2)  AS  TIME,
	PREAS,
	VER,
	HOLDER,
	SERVIC
FROM CONNECTION TO DB2(
SELECT DISTINCT
	BF_SSN                          AS      SSN,
	AF_APL_ID||AF_APL_ID_SFX        AS      FILE,
	LD_LDR_POF                      AS      PAYOFF,
	LC_CLM_OUT_PUR_REA              AS      OOP,
	BD_CLM_PKG_RCV                  AS      DCR,
	BD_CLM_PKG_RTN                  AS      DRT,
	BD_CLM_PKG_RS                   AS      DRS,
	BF_CRT_DTS_DC01                 AS      TIME2,
	LC_PCL_REA                      AS      PREAS,
	BD_VER_CLM_PAY                  AS      VER,
	IF_OPS_LDR                      AS      HOLDER,
	LF_CUR_LON_SER_AGY              AS      SERVIC
FROM OLWHRM1.ZD03_DFT_CLM
WHERE BD_CLM_PKG_RCV > &BEGIN
OR BD_CLM_PKG_RS > &BEGIN   
);

CREATE TABLE PYMENTQA2 AS
SELECT SSN,
	FILE,
	PAYOFF,
	OOP,
	DCR,
	DRS,
	TIMEPART(TIME2)  AS  TIME,
	PREAS,
	VER,
	HOLDER,
	SERVIC
FROM CONNECTION TO DB2(
SELECT DISTINCT
	BF_SSN                          AS      SSN,
	AF_APL_ID||AF_APL_ID_SFX        AS      FILE,
	LD_LDR_POF                      AS      PAYOFF,
	LC_CLM_OUT_PUR_REA              AS      OOP,
	BD_CLM_PKG_RCV                  AS      DCR,
	BD_CLM_PKG_RTN                  AS      DRT,
	BD_CLM_PKG_RS                   AS      DRS,
	BF_CRT_DTS_DC01                 AS      TIME2,
	LC_PCL_REA                      AS      PREAS,
	BD_VER_CLM_PAY                  AS      VER,
	IF_OPS_LDR                      AS      HOLDER,
	LF_CUR_LON_SER_AGY              AS      SERVIC
FROM    OLWHRM1.ZD04_DFT_STS
WHERE BD_CLM_PKG_RCV > &BEGIN
OR BD_CLM_PKG_RS > &BEGIN 
);
DISCONNECT FROM DB2 ;
ENDRSUBMIT;

PROC FORMAT;
VALUE $IND       '1'='DEFAULT REASONS        '
                 '2'='DDBS                   '
                 '3'='SCHL CLOSURE/FALSE CERT';
RUN;

DATA PYMENTQA;SET WORKLOCL.PYMENTQA;RUN;
DATA PYMENTQA2;SET WORKLOCL.PYMENTQA2;RUN;

PROC SORT DATA=PYMENTQA ;
BY SSN ;
RUN ;

PROC SORT DATA=PYMENTQA2 ;
BY SSN ;
RUN ;

DATA PYMENTQA ;
MERGE PYMENTQA (IN=A) PYMENTQA2 (IN=B)  ;
BY SSN ;
IF A OR B;
RUN;

DATA MONTHLY DDB SPECIAL;
SET PYMENTQA ;
IF VER=. THEN DELETE ;
IF DRS > DCR THEN DATE=DRS;
	ELSE DATE=DCR;
IF PREAS IN ('DQ','DF','RS','IN','DU','DB') THEN OUTPUT MONTHLY;
	ELSE IF PREAS IN ('DE','BO','BH','BC') THEN OUTPUT DDB;
	ELSE IF PREAS IN ('CS','DI','FC') THEN OUTPUT SPECIAL;
RUN;

PROC SORT DATA=MONTHLY NODUPKEY;
BY SSN TIME;
RUN ;

DATA MONTHLY;
SET MONTHLY;
TSPAN=INTCK('DAY',DATE,PAYOFF);
IND='1';
IF TSPAN > 60 THEN OUTPUT;
RUN;

PROC SORT DATA=MONTHLY;
BY IND;
RUN ;

PROC SORT DATA=DDB NODUPKEY;
BY SSN TIME;
RUN ;

DATA DDB;
SET DDB;
TSPAN=INTCK('DAY',DATE,PAYOFF);
IND='2';
IF TSPAN > 45 THEN OUTPUT;
RUN;

PROC SORT DATA=DDB;
BY IND;
RUN ;

PROC SORT DATA=SPECIAL NODUPKEY;
BY SSN TIME;
RUN ;

DATA SPECIAL;
SET SPECIAL;
TSPAN=INTCK('DAY',DATE,PAYOFF);
IND='3';
IF TSPAN > 90 THEN OUTPUT;
RUN;

PROC SORT DATA=SPECIAL;
BY IND;
RUN ;

DATA ALL;
SET MONTHLY DDB SPECIAL;
BY IND;
IF PAYOFF > TODAY()-15 THEN NEW='*' ;
RUN;

PROC SORT DATA=ALL;
BY IND DESCENDING PAYOFF ;
RUN  ;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/

%MACRO RPTPRNT(DSNAME) ;
%LET DSID=%SYSFUNC(OPEN(&DSNAME)) ;
%LET HASOBS=%SYSFUNC(ATTRN(&DSID,ANY)) ;
%LET RC=%SYSFUNC(CLOSE(&DSID)) ;
%IF &HASOBS=1 %THEN %DO;

OPTIONS PAGENO=1 LS=126;
PROC PRINT DATA=ALL ;
BY IND;
VAR SSN HOLDER SERVIC DATE PAYOFF TSPAN TIME NEW ;
FORMAT  DATE PAYOFF MMDDYY10. IND $IND. TIME TIME14.6 ;
TITLE 'PAYMENT QUALITY ASSURANCE';
TITLE2 'CLAIMS PAID OVER LIMITS';
TITLE3 "RUN ON &SYSDATE" ;
FOOTNOTE 'PYMENTQA';
RUN ;

%END ;
%ELSE %IF &HASOBS=0 %THEN
%DO ;
DATA _NULL_ ;
FILE PRINT NOTITLES ;
PUT // 126*'-';
   PUT      ////////
       @51 '**** NO OBSERVATIONS FOUND ****';
   PUT ////////
       @57 '-- END OF REPORT --';
   PUT ///////////////
   		@49 "JOB = UTLWDR6     REPORT = ULWDR6.LWDR6R2";
RUN ;
%END ;
%MEND RPTPRNT ;
%RPTPRNT(ALL) ;
