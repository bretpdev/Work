/*UTLWO18 - PLUS INTEREST CREDIT*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*FILENAME REPORT2 "&RPTLIB/ULWO18.LWO18R2";*/
/*FILENAME REPORT3 "&RPTLIB/ULWO18.LWO18R3";*/

FILENAME REPORT2 "C:\WINDOWS\TEMP\ULWO18.LWO18R2";
FILENAME REPORT3 "C:\WINDOWS\TEMP\ULWO18.LWO18R3";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PIC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN
	,LN10.LN_SEQ

	,PD10.DF_SPE_ACC_ID
	,PD10.DM_PRS_1
	,PD10.DM_PRS_LST
	,PD30.DC_ADR
	,PD30.DI_VLD_ADR
	,PD30.DX_STR_ADR_1
	,PD30.DX_STR_ADR_2
	,PD30.DX_STR_ADR_3
	,PD30.DM_CT
	,PD30.DC_DOM_ST
	,PD30.DF_ZIP_CDE
	,PD30.DM_FGN_CNY
	,PD30.DM_FGN_ST

	,LN10.LD_LON_1_DSB
	,LN10.LA_LON_AMT_GTR
,LN90.LD_FAT_EFF
	,ABS(LN90.LA_FAT_NSI) AS LA_FAT_NSI

FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
	ON LN10.BF_SSN = PD30.DF_PRS_ID
	AND PD30.DC_ADR = 'L'
INNER JOIN OLWHRM1.LN85_LON_ATY LN85
	ON LN10.BF_SSN = LN85.BF_SSN
	AND LN10.LN_SEQ = LN85.LN_SEQ
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON LN85.BF_SSN = AY10.BF_SSN
	AND LN85.LN_ATY_SEQ = AY10.LN_ATY_SEQ
	AND AY10.LC_STA_ACTY10 = 'A'
INNER JOIN OLWHRM1.AY15_ATY_CMT AY15
	ON AY10.BF_SSN = AY15.BF_SSN
	AND AY10.LN_ATY_SEQ = AY15.LN_ATY_SEQ
	AND AY15.LC_STA_AY15 = 'A'
INNER JOIN OLWHRM1.AY20_ATY_TXT AY20
	ON AY15.BF_SSN = AY20.BF_SSN
	AND AY15.LN_ATY_SEQ = AY20.LN_ATY_SEQ
	AND AY15.LN_ATY_CMT_SEQ = AY20.LN_ATY_CMT_SEQ

INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
	ON LN10.BF_SSN = LN90.BF_SSN
	AND LN10.LN_SEQ = LN90.LN_SEQ
	AND LN90.LC_STA_LON90 = 'A'
	AND LN90.LN_FAT_SEQ_REV IS NULL

WHERE LN10.IC_LON_PGM = 'PLUS'
AND LN10.LD_LON_1_DSB BETWEEN '07/01/1999' AND '06/30/2004'
AND LN10.LF_LON_CUR_OWN = '828476'
AND AY10.PF_REQ_ACT = 'UTIME'
AND AY20.LN_ATY_CMT_SEQ = 1
AND LN90.PC_FAT_TYP = '10'
AND LN90.PC_FAT_SUB_TYP IN ('10','11')
AND DATE(SUBSTR(AY20.LX_ATY,7,4)
	||'-'||SUBSTR(AY20.LX_ATY,1,2)
	||'-'||SUBSTR(AY20.LX_ATY,4,2)) = LN90.LD_FAT_EFF

/*NO ACTIVE ARC = UPINT, ULATE, UPICD*/
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ
	WHERE Y.BF_SSN = LN10.BF_SSN
	AND Y.LN_SEQ = LN10.LN_SEQ
	AND X.LC_STA_ACTY10 = 'A'
	AND X.PF_REQ_ACT IN ('UPINT','ULATE','UPICD'))
/*DOES NOT HAVE 4 OR MORE ACTIVE UTPHE ARCS*/
AND NOT EXISTS
	(SELECT X.BF_SSN
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ
	WHERE Y.BF_SSN = LN10.BF_SSN
	AND Y.LN_SEQ = LN10.LN_SEQ
	AND X.LC_STA_ACTY10 = 'A'
	AND X.PF_REQ_ACT = 'UTPHE'
	GROUP BY X.BF_SSN
	HAVING COUNT(*) >= 4)
/*LOAN HAS EXACTLY 12 ACTIVE UTIME + UTPHE COMMENTS*/
AND EXISTS
	(SELECT X.BF_SSN
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ
	WHERE Y.BF_SSN = LN10.BF_SSN
	AND Y.LN_SEQ = LN10.LN_SEQ
	AND X.LC_STA_ACTY10 = 'A'
	AND X.PF_REQ_ACT IN ('UTIME','UTPHE')
	GROUP BY X.BF_SSN
	HAVING COUNT(*) = 12)

);
DISCONNECT FROM DB2;

ENDRSUBMIT;
DATA PIC; 
SET WORKLOCL.PIC; 
RUN;

PROC SORT DATA=PIC;
BY BF_SSN LN_SEQ;
RUN;

/*SUMMARIZE TOTAL INTEREST AMOUNT*/
DATA PIC;
SET PIC;
BY BF_SSN LN_SEQ;
IF FIRST.LN_SEQ THEN TOT_INT_AMT = 0;
TOT_INT_AMT + LA_FAT_NSI;
IF LAST.LN_SEQ THEN OUTPUT;
RUN;

%MACRO KEY_CLC(TBL);
*CALCULATE KEYLINE;
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%KEY_CLC(PIC);

PROC SORT DATA=PIC;
BY BF_SSN LN_SEQ;
RUN;

DATA _NULL_;
SET PIC;
WHERE DI_VLD_ADR = 'Y';
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT LN_SEQ 6. ;
	FORMAT DM_PRS_1 $13. ;
	FORMAT DM_PRS_LST $23. ;
	FORMAT DX_STR_ADR_1 $30. ;
	FORMAT DX_STR_ADR_2 $30. ;
	FORMAT DX_STR_ADR_3 $30. ;
	FORMAT DM_CT $20. ;
	FORMAT DC_DOM_ST $2. ;
	FORMAT DF_ZIP_CDE $9. ;
	FORMAT DM_FGN_CNY $15. ;
	FORMAT DM_FGN_ST $15. ;
	FORMAT LD_LON_1_DSB MMDDYY10. ;
	FORMAT LA_LON_AMT_GTR 10.2 ;
	FORMAT TOT_INT_AMT COMMA14.2 ;
	FORMAT ACSKEY $18. ;
IF _N_ = 1 THEN        /* WRITE COLUMN NAMES */
	DO;
	PUT
	'BF_SSN'
	','
	'DF_SPE_ACC_ID'
	','
	'LN_SEQ'
	','
	'DM_PRS_1'
	','
	'DM_PRS_LST'
	','
	'DX_STR_ADR_1'
	','
	'DX_STR_ADR_2'
	','
	'DX_STR_ADR_3'
	','
	'DM_CT'
	','
	'DC_DOM_ST'
	','
	'DF_ZIP_CDE'
	','
	'DM_FGN_CNY'
	','
	'DM_FGN_ST'
	','
	'LD_LON_1_DSB'
	','
	'LA_LON_AMT_GTR'
	','
	'TOT_INT_AMT'
	','
	'ACSKEY'
	;
	END;
DO;
	PUT BF_SSN $ @;
	PUT DF_SPE_ACC_ID $ @;
	PUT LN_SEQ @;
	PUT DM_PRS_1 $ @;
	PUT DM_PRS_LST $ @;
	PUT DC_ADR $ @;
	PUT DI_VLD_ADR $ @;
	PUT DX_STR_ADR_1 $ @;
	PUT DX_STR_ADR_2 $ @;
	PUT DX_STR_ADR_3 $ @;
	PUT DM_CT $ @;
	PUT DC_DOM_ST $ @;
	PUT DF_ZIP_CDE $ @;
	PUT DM_FGN_CNY $ @;
	PUT DM_FGN_ST $ @;
	PUT LD_LON_1_DSB @;
	PUT LA_LON_AMT_GTR @;
	PUT TOT_INT_AMT @;
	PUT ACSKEY $ ;
	;
	END;
RUN;

DATA _NULL_;
SET PIC;
WHERE DI_VLD_ADR = 'N';
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
	FORMAT LN_SEQ 6. ;
	FORMAT TOT_INT_AMT COMMA14.2 ;
IF _N_ = 1 THEN        /* WRITE COLUMN NAMES */
DO;
	PUT
	'BF_SSN'
	','
	'LN_SEQ'
	','
	'TOT_INT_AMT'
	;
	END;
DO;
	PUT BF_SSN $ @;
	PUT LN_SEQ @;
	PUT TOT_INT_AMT;
	END;
RUN;
