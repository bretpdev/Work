LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORTZ "&RPTLIB/ULWS45.LWS45RZ";
FILENAME REPORT2 "&RPTLIB/ULWS45.LWS45R2";
FILENAME REPORT3 "&RPTLIB/ULWS45.LWS45R3";
FILENAME REPORT4 "&RPTLIB/ULWS45.LWS45R4";
FILENAME REPORT5 "&RPTLIB/ULWS45.LWS45R5";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT; */
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT E.DF_SPE_ACC_ID
	,TRIM(E.DM_PRS_1) || ' ' || TRIM(E.DM_PRS_LST) AS BOR_NAME
	,E.DX_EML_ADR
	,A.LD_LDR_POF
	,X.PF_ACT
FROM OLWHRM1.PD01_PDM_INF E
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN B
	ON E.DF_PRS_ID = B.DF_PRS_ID 
INNER JOIN OLWHRM1.DC01_LON_CLM_INF A
	ON E.DF_PRS_ID = A.BF_SSN
INNER JOIN OLWHRM1.DC02_BAL_INT C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT AF_APL_ID
					,AF_APL_ID_SFX
					,LC_TRX_TYP
					,LD_TRX_EFF
				FROM OLWHRM1.DC11_LON_FAT 
				WHERE LC_TRX_TYP IN ('BR','CS','EP')
) Y
	ON A.AF_APL_ID = Y.AF_APL_ID
	AND A.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
LEFT OUTER JOIN (SELECT AF_APL_ID
					,AF_APL_ID_SFX
					,MAX(LD_TRX_EFF) AS LD_TRX_EFF
				FROM OLWHRM1.DC11_LON_FAT
				GROUP BY AF_APL_ID, AF_APL_ID_SFX
) Z
	ON Z.AF_APL_ID = Y.AF_APL_ID
	AND Z.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
	AND Z.LD_TRX_EFF = Y.LD_TRX_EFF
LEFT OUTER JOIN (SELECT DF_PRS_ID
				,SUBSTR(PF_ACT,5,1) AS PF_ACT
				FROM OLWHRM1.AY01_BR_ATY
				WHERE SUBSTR(PF_ACT,1,4) = 'DEMN') X
	ON E.DF_PRS_ID = X.DF_PRS_ID
WHERE A.LC_STA_DC10 = '03'
	AND A.LC_GRN != '02'
	AND A.LC_AUX_STA = ''
	AND A.LC_PCL_REA IN ('DF','DQ','DB')
	AND A.LD_CLM_ASN_DOE IS NULL
	AND A.LA_CLM_PRI+A.LA_CLM_INT-A.LA_PRI_COL+A.LA_INT_ACR+C.LA_CLM_INT_ACR
		-A.LA_INT_COL+A.LA_LEG_CST_ACR-A.LA_LEG_CST_COL+A.LA_OTH_CHR_ACR
		-A.LA_OTH_CHR_COL+A.LA_COL_CST_ACR-A.LA_COL_CST_COL+C.LA_CLM_PRJ_COL_CST > 25
	AND A.LI_COL_CST_ASS = 'Y'
	AND A.LA_COL_CST_ACR
		-A.LA_COL_CST_COL
		+C.LA_CLM_PRJ_COL_CST > 25
	AND A.LI_DFL_LTR_1_IVL != 'Y'
	AND (A.LD_LST_PAY IS NULL OR Y.LC_TRX_TYP NOT IN ('BR','CS','EP'))
	AND B.DI_EML_ADR_VAL = 'Y'
	AND DAYS(A.LD_LDR_POF) <= DAYS(CURRENT DATE) - 60
/*	ALTERNATIVE WAYS TO FIND WAGE GARNISHMENTS*/
	AND E.DF_PRS_ID NOT IN (SELECT DF_PRS_ID_BR
						FROM OLWHRM1.LA11_LEG_ACT_ATY
						WHERE BC_LEG_ACT_ATY_TYP IN ('GG','JD')
							AND BC_ATY_WDR_REA = '')
	AND E.DF_PRS_ID NOT IN (SELECT DF_PRS_ID_BR
						FROM OLWHRM1.CT30_CALL_QUE 
						WHERE IF_WRK_GRP = 'WG000016')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK;
QUIT;
/*ENDRSUBMIT;*/
/*DATA DEMO;*/
/*	SET WORKLOCL.DEMO;*/
/*RUN;*/
PROC SORT DATA=DEMO ;
BY DF_SPE_ACC_ID;
RUN;
%MACRO REPORTS(R, DEMN, MAX, MIN);
DATA _NULL_;
SET DEMO ;
BY DF_SPE_ACC_ID;
WHERE PF_ACT ^= &DEMN
	AND TODAY() - &MIN <= LD_LDR_POF <= TODAY() - &MAX ;
FORMAT DEADLINE MMDDYY10.;
DEADLINE = TODAY() + 45;
FILE REPORT&R DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNTNUMBER,NAME,RECIPIENT,DEADLINE";
END;
IF FIRST.DF_SPE_ACC_ID THEN DO;
   PUT DF_SPE_ACC_ID $ @;
   PUT BOR_NAME @;
   PUT DX_EML_ADR @;
   PUT DEADLINE $ ;
END;
RUN; 
%MEND;
%REPORTS(2,'1',60,239);
%REPORTS(3,'2',240,419);
%REPORTS(4,'3',420,599);
%REPORTS(5,'4',600,50000);
