/*LPP STALE ANTICIPATED DISBURSEMENT REPORT*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWL07.LWL07R2";
FILENAME REPORTZ "&RPTLIB/ULWL07.LWL07RZ";
OPTIONS SYMBOLGEN;
*Resolves to last day of previous month.  TODAY()+3 is used incase EOM 
processing rolls to an earlier day due to weekend or holiday.;
DATA _NULL_;
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY()+3,-2,'end'), MMDDYYD10.)||"'");
RUN;
/*%SYSLPUT END = &END;*/
/*libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS SSN
	,A.AN_SEQ AS APP
	,A.LN_LON_DSB_SEQ AS DISB
	,COALESCE(A.LA_DSB,0) - COALESCE(A.LA_DSB_CAN,0) AS DISBAMT
	,A.LD_DSB AS DISBDATE
	,C.AF_DOE_SCL
	,D.IM_SCL_FUL
	,A.IC_LON_PGM
	,A.LC_CMN_LN_HLD_RLS
    ,C.AF_CNL||''||C.AF_CNL_SFX AS CLUID 

FROM OLWHRM1.LN15_DSB A 
INNER JOIN OLWHRM1.AP03_MASTER_APL C 
	ON A.AF_APL_ID = C.AF_APL_ID
INNER JOIN OLWHRM1.SC10_SCH_DMO D
	ON D.IF_DOE_SCL = C.AF_DOE_SCL
WHERE COALESCE(A.LA_DSB_CAN,0) <> COALESCE(A.LA_DSB,0)
AND A.LD_DSB <= &END
AND A.LC_LDR_DSB_MDM = ' '
AND A.LC_DSB_TYP = '1'	/*ANTICIPATED DISBS*/
AND A.LC_STA_LON15 IN ('1','3') /*ACTIVE, ACTIVE REISSUE DISBS*/
AND C.AF_CNL NOT LIKE ('%REALLO%')
ORDER BY A.LC_CMN_LN_HLD_RLS, C.AF_DOE_SCL, A.LD_DSB, A.BF_SSN
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWL07.LWL07RZ);
QUIT;
/*ENDRSUBMIT  ;*/
/*DATA DEMO; */
/*SET WORKLOCL.DEMO; */
/*RUN;*/
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS NOCENTER NODATE NONUMBER PAGENO=1 LS=126;
PROC PRINT NOOBS SPLIT='+' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN;
VAR SSN DISB DISBAMT DISBDATE /*AF_DOE_SCL IM_SCL_FUL*/ IC_LON_PGM 
CLUID LC_CMN_LN_HLD_RLS;
BY LC_CMN_LN_HLD_RLS AF_DOE_SCL IM_SCL_FUL;
PAGEBY AF_DOE_SCL;
LABEL DISB="DISB SEQ #" DISBAMT="DISB AMT" DISBDATE="DISB DATE" 
AF_DOE_SCL="SCHOOL CODE" IM_SCL_FUL="SCHOOL NAME" IC_LON_PGM="LOAN TYPE" 
CLUID="COMMONLINE ID" LC_CMN_LN_HLD_RLS="HOLD/+RELEASE";
FORMAT SSN SSN11. DISBAMT COMMA9.2 DISBDATE MMDDYY10.;
TITLE  "                                            UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "                                                 ANTICIPATED DISBURSEMENT REPORT";
TITLE3 "                                                    HOLD OR NOT YET DISBURSED";
TITLE4 "                                                        AS OF &END";
FOOTNOTE  '                                            JOB = UTLWL07     REPORT = ULWL07.LWL07R2';
RUN;

PROC PRINTTO;
RUN;
