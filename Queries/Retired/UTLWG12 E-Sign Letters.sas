/*UTLWG12 - BORROWERS TO RECEIVE ESIGN LETTERS*/
/*RESERVE REPORTS FOR PLUS LOANS EVEN IF THE SCHOOL DOESN'T E-SIGN THEM WHEN THE STAFFORD LOANS ARE ADDED*/
/*ULWG12.LWG12R2 - UNIVERSITY OF UTAH STAFFORD LOANS*/
/*ULWG12.LWG12R3 - UNIVERSITY OF UTAH PLUS LOANS*/
/*ULWG12.LWG12R4 - UTAH STATE STAFFORD LOANS*/
/*ULWG12.LWG12R5 - UTAH STATE PLUS LOANS*/
/*ULWG12.LWG12R6 - WESTMINSTER STAFFORD LOANS*/
/*ULWG12.LWG12R7 - reserved for WESTMINSTER PLUS LOANS*/
/*ULWG12.LWG12R8 - WEBER STAFFORD LOANS*/
/*ULWG12.LWG12R9 - WEBER PLUS LOANS*/
/*ULWG12.LWG12R10 - SLCC STAFFORD LOANS*/
/*ULWG12.LWG12R11 - SLCC PLUS LOANS*/
/*ULWG12.LWG12R12 - DIXIE STATE STAFFORD LOANS*/
/*ULWG12.LWG12R13 - DIXIE STATE PLUS LOANS*/
/*ULWG12.LWG12R14 - BYU STAFFORD LOANS*/
/*ULWG12.LWG12R15 - BYU PLUS LOANS*/
/*ULWG12.LWG12R16 - UVSC STAFFORD LOANS*/
/*ULWG12.LWG12R17 - UVSC PLUS LOANS*/
/*ULWG12.LWG12R18 - SNOW COLLEGE STAFFORD LOANS*/
/*ULWG12.LWG12R19 - SNOW COLLEGE PLUS LOANS*/
/*ULWG12.LWG12R20 - LDS BUSINESS COLLEGE STAFFORD LOANS*/
/*ULWG12.LWG12R21 - reserved for LDS BUSINESS COLLEGE PLUS LOANS*/
/*ULWG12.LWG12R22 - CEU STAFFORD LOANS*/
/*ULWG12.LWG12R23 - CEU PLUS LOANS*/
/*ULWG12.LWG12R26 - Argosy STAFFORD LOANS*/
/*ULWG12.LWG12R27 - Argosy PLUS LOANS*/
/*ULWG12.LWG12R28 - PMTS Provo STAFFORD LOANS*/
/*ULWG12.LWG12R29 - PMTS Provo PLUS LOANS*/
/*ULWG12.LWG12R99 - HARD COPY ESIGN LETTERS - INVALID ADDRESS BORROWERS*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWG12.LWG12RZ";
FILENAME REPORT2 "&RPTLIB/ULWG12.LWG12R2";
FILENAME REPORT3 "&RPTLIB/ULWG12.LWG12R3";
FILENAME REPORT4 "&RPTLIB/ULWG12.LWG12R4";
FILENAME REPORT5 "&RPTLIB/ULWG12.LWG12R5";
FILENAME REPORT6 "&RPTLIB/ULWG12.LWG12R6";
FILENAME REPORT8 "&RPTLIB/ULWG12.LWG12R8";
FILENAME REPORT9 "&RPTLIB/ULWG12.LWG12R9";
FILENAME REPORT10 "&RPTLIB/ULWG12.LWG12R10";
FILENAME REPORT11 "&RPTLIB/ULWG12.LWG12R11";
FILENAME REPORT12 "&RPTLIB/ULWG12.LWG12R12";
FILENAME REPORT13 "&RPTLIB/ULWG12.LWG12R13";
FILENAME REPORT14 "&RPTLIB/ULWG12.LWG12R14";
FILENAME REPORT15 "&RPTLIB/ULWG12.LWG12R15";
FILENAME REPORT16 "&RPTLIB/ULWG12.LWG12R16";
FILENAME REPORT17 "&RPTLIB/ULWG12.LWG12R17";
FILENAME REPORT18 "&RPTLIB/ULWG12.LWG12R18";
FILENAME REPORT19 "&RPTLIB/ULWG12.LWG12R19";
FILENAME REPORT20 "&RPTLIB/ULWG12.LWG12R20";
FILENAME REPORT22 "&RPTLIB/ULWG12.LWG12R22";
FILENAME REPORT23 "&RPTLIB/ULWG12.LWG12R23";
FILENAME REPORT26 "&RPTLIB/ULWG12.LWG12R26";
FILENAME REPORT27 "&RPTLIB/ULWG12.LWG12R27";
FILENAME REPORT28 "&RPTLIB/ULWG12.LWG12R28";
FILENAME REPORT29 "&RPTLIB/ULWG12.LWG12R29";
FILENAME REPORT99 "&RPTLIB/ULWG12.LWG12R99";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
OPTIONS SYMBOLGEN;
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
DATA _NULL_;
PVS_DT = TODAY() - 7;
CALL SYMPUT('PVS_DT',"'"||PUT(PVS_DT,MMDDYYD10.)||"'");
RUN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ESLTRS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID_BR
	,A.AX_SCL_USB_CER_IAA
	,D.DM_PRS_1
	,D.DM_PRS_LST
	,D.DF_SPE_ACC_ID
	,CASE
	 	 WHEN B.AC_LON_TYP IN ('SF','SU')
	     THEN ' '
		 ELSE E.DF_SPE_ACC_ID
	 END AS DF_SPE_ACC_ID_STU
	,CASE
		 WHEN B.AC_LON_TYP IN ('SF','SU')
		 THEN ' '
		 ELSE E.DM_PRS_1
	 END AS DM_PRS_1_STU
	,CASE
		 WHEN B.AC_LON_TYP IN ('SF','SU')
		 THEN ' '
		 ELSE E.DM_PRS_LST
	 END AS DM_PRS_LST_STU
	,CASE 
		 WHEN B.AC_LON_TYP IN ('SF','SU')
		 THEN '1'
		 WHEN B.AC_LON_TYP IN ('PL','GB')
	 	 THEN '2'
	 END AS LN_IND

	,AT.DC_ADR AS DC_ADR
	,AT.DI_VLD_ADR AS DI_VLD_ADR
	,AT.DX_STR_ADR_1 AS DX_STR_ADR_1
	,AT.DX_STR_ADR_2 AS DX_STR_ADR_2
	,AT.DM_CT AS DM_CT
	,AT.DC_DOM_ST AS DC_DOM_ST
	,AT.DF_ZIP AS DF_ZIP
	,AT.DM_FGN_CNY AS DM_FGN_CNY
	,AT.DC_ADR 
	,AT.DI_VLD_ADR 

	,AL.DC_ADR AS AL_DC_ADR
	,AL.DI_VLD_ADR AS AL_DI_VLD_ADR
	,AL.DX_STR_ADR_1 AS AL_DX_STR_ADR_1
	,AL.DX_STR_ADR_2 AS AL_DX_STR_ADR_2
	,AL.DM_CT AS AL_DM_CT
	,AL.DC_DOM_ST AS AL_DC_DOM_ST
	,AL.DF_ZIP AS AL_DF_ZIP
	,AL.DM_FGN_CNY AS AL_DM_FGN_CNY
	,AL.DC_ADR
	,AL.DI_VLD_ADR

	,A.AF_APL_ID
	,A.AF_APL_OPS_SCL
	,B.AC_LON_TYP 

FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B 
	ON B.AF_APL_ID = A.AF_APL_ID
INNER JOIN OLWHRM1.GA20_CNL_DAT C
	ON C.AF_APL_ID = A.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.DF_PRS_ID_BR = D.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD03_PRS_ADR_PHN AT
	ON AT.DF_PRS_ID = D.DF_PRS_ID
	AND AT.DC_ADR = 'T'
	AND AT.DI_VLD_ADR = 'Y'
LEFT OUTER JOIN OLWHRM1.PD03_PRS_ADR_PHN AL
	ON AL.DF_PRS_ID = D.DF_PRS_ID
	AND AL.DC_ADR = 'L'
	AND AL.DI_VLD_ADR = 'Y'
LEFT OUTER JOIN OLWHRM1.PD01_PDM_INF E
	ON A.DF_PRS_ID_STU = E.DF_PRS_ID
WHERE A.AF_APL_OPS_SCL IN (
	'00367500','00367700','00368100',
	'00368000','00522000','00367100',
	'00367000','00402700','00367900',
	'00367200','00367600','02179935',
	'02531800'
	)
AND B.AC_PRC_STA <> 'A'
AND A.AD_APL_CRT > &PVS_DT
AND (
		(
		 B.AC_LON_TYP = 'SF'
		 AND INTEGER(REPLACE(A.AX_SCL_SUB_CER_IAA,' ','0')) > 0
	 	)
	OR
		(
		 B.AC_LON_TYP = 'SU'
		 AND INTEGER(REPLACE(A.AX_SCL_USB_CER_IAA,' ','0')) > 0
		 )
	OR
		(
		 B.AC_LON_TYP IN ('PL','GB')
		 AND INTEGER(REPLACE(A.AX_SCL_PS_CER_IAA,' ','0')) > 0
		)
	)
AND C.AC_CNL_PRC_TYP = 'GO'
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.AY01_BR_ATY X
	WHERE X.PF_ACT = 'SLBSL'
	AND X.DF_PRS_ID = A.DF_PRS_ID_BR
	AND SUBSTR(X.BX_CMT,1,17) = A.AF_APL_ID)

AND (
	C.AC_MPN_SRL_LON = 'N' 
	OR (
		C.AC_MPN_SRL_LON IN ('S','')
		AND NOT EXISTS (
			SELECT *
			FROM OLWHRM1.GA01_APP X
			INNER JOIN OLWHRM1.GA40_BS_MPN_CTL Y
				ON X.AF_APL_ID = Y.AF_BS_MPN_APL_ID
			WHERE Y.AF_BS_MPN_APL_ID = A.AF_BS_MPN_APL_ID
			AND Y.AD_EXP > CURRENT DATE
			AND Y.AC_MPN_STA = 'A'
			AND (
				X.AF_CUR_APL_OPS_LDR = A.AF_CUR_APL_OPS_LDR
				OR (X.AF_CUR_APL_OPS_LDR = '813915'
					AND A.AF_CUR_APL_OPS_LDR = '813894')
				OR (X.AF_CUR_APL_OPS_LDR = '820043'
					AND A.AF_CUR_APL_OPS_LDR = '829505')
				)
			)
		)
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
ENDRSUBMIT;
DATA ESLTRS; 
SET WORKLOCL.ESLTRS; 
RUN;
PROC SORT DATA=ESLTRS;
BY AL_DC_DOM_ST AF_APL_OPS_SCL DM_FGN_CNY DF_PRS_ID_BR;
RUN;
/*DECIDE BETWEEN LEGAL AND TEMPORARY ADDRESS DEPENDING ON VALIDITY*/
DATA UERR ESLTRS (DROP=AL_DC_ADR AL_DI_VLD_ADR AL_DX_STR_ADR_1 AL_DX_STR_ADR_2 
                  AL_DM_CT AL_DC_DOM_ST AL_DF_ZIP AL_DM_FGN_CNY);
SET ESLTRS; 
COST_CENTER_CODE = 'MA2327';
IF DI_VLD_ADR = 'Y' THEN DO;
	STATE_IND = DC_DOM_ST;
	OUTPUT ESLTRS;
	END;
ELSE IF DI_VLD_ADR NE 'Y' AND AL_DI_VLD_ADR EQ 'Y' THEN 
	DO;
		DC_ADR = AL_DC_ADR;
		DI_VLD_ADR = AL_DI_VLD_ADR;
		DX_STR_ADR_1 = AL_DX_STR_ADR_1;
		DX_STR_ADR_2 = AL_DX_STR_ADR_2;
		DM_CT = AL_DM_CT;
		DC_DOM_ST = AL_DC_DOM_ST;
		DF_ZIP = AL_DF_ZIP;
		DM_FGN_CNY = AL_DM_FGN_CNY;
		STATE_IND = AL_DC_DOM_ST;
		OUTPUT ESLTRS;
	END;
ELSE IF DI_VLD_ADR NE 'Y' AND AL_DI_VLD_ADR NE 'Y' THEN OUTPUT UERR;
RUN;

*CALCULATE KEYLINE;
DATA ESLTRS (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET ESLTRS;
KEYSSN = TRANSLATE(DF_PRS_ID_BR,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

%MACRO ESIG(SCL=,IND=,REPNO=);

DATA ESLTR;
SET ESLTRS;
WHERE AF_APL_OPS_SCL = &SCL AND LN_IND = &IND;
RUN;

DATA _NULL_;
SET ESLTR;
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DF_PRS_ID_BR $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT DM_FGN_CNY $25. ;
FORMAT AF_APL_ID $17. ;
FORMAT ACSKEY $18. ;
FORMAT DF_SPE_ACC_ID $10.;
FORMAT DF_SPE_ACC_ID_STU $10.;
FORMAT DM_PRS_1_STU $12.;
FORMAT DM_PRS_LST_STU $35.;
FORMAT AC_LON_TYP $2.;
IF _N_ = 1 THEN DO;       /* WRITE COLUMN NAMES */
	PUT
	'DF_PRS_ID_BR'		','
	'DM_PRS_1'			','
	'DM_PRS_LST'		','
	'DX_STR_ADR_1'		','
	'DX_STR_ADR_2'		','
	'DM_CT'				','
	'DC_DOM_ST'			','
	'DF_ZIP'			','
	'DM_FGN_CNY'		','
	'AF_APL_ID'			','
	'ACSKEY'			','
	'DF_SPE_ACC_ID'		','
	'DF_SPE_ACC_ID_STU'	','
	'DM_PRS_1_STU'		','
	'DM_PRS_LST_STU'	','
	'AC_LON_TYP'		','
	'STATE_IND'			','
	'COST_CENTER_CODE'
	;
	END;
DO;
	PUT DF_PRS_ID_BR $ @;
	PUT DM_PRS_1 $ @;
	PUT DM_PRS_LST $ @;
	PUT DX_STR_ADR_1 $ @;
	PUT DX_STR_ADR_2 $ @;
	PUT DM_CT $ @;
	PUT DC_DOM_ST $ @;
	PUT DF_ZIP $ @;
	PUT DM_FGN_CNY $ @;
	PUT AF_APL_ID $ @;
	PUT ACSKEY $ @;
	PUT DF_SPE_ACC_ID $ @;
	PUT DF_SPE_ACC_ID_STU $ @;
	PUT DM_PRS_1_STU $ @;
	PUT DM_PRS_LST_STU $ @;
	PUT AC_LON_TYP $ @;
	PUT STATE_IND $ @;
	PUT COST_CENTER_CODE $ ;
	END;
RUN;
%MEND ;

/****NOTE: IND IS DETERMINED IN INITIAL SQL SELECT STATEMENT****/
%ESIG(SCL='00367500',IND='1',REPNO=2);/*UNIVERSITY OF UTAH STAFFORD LOANS*/
%ESIG(SCL='00367500',IND='2',REPNO=3);/*UNIVERSITY OF UTAH PLUS LOANS*/
%ESIG(SCL='00367700',IND='1',REPNO=4);/*UTAH STATE STAFFORD LOANS*/
%ESIG(SCL='00367700',IND='2',REPNO=5);/*UTAH STATE PLUS LOANS*/
%ESIG(SCL='00368100',IND='1',REPNO=6);/*WESTMINSTER STAFFORD LOANS*/
%ESIG(SCL='00368000',IND='1',REPNO=8);/*WEBER STAFFORD LOANS*/
%ESIG(SCL='00368000',IND='2',REPNO=9);/*WEBER PLUS LOANS*/
%ESIG(SCL='00522000',IND='1',REPNO=10);/*SLCC STAFFORD LOANS*/
%ESIG(SCL='00522000',IND='2',REPNO=11);/*SLCC PLUS LOANS*/
%ESIG(SCL='00367100',IND='1',REPNO=12);/*DIXIE STATE STAFFORD LOANS*/
%ESIG(SCL='00367100',IND='2',REPNO=13);/*DIXIE STATE PLUS LOANS*/
%ESIG(SCL='00367000',IND='1',REPNO=14);/*BYU STAFFORD LOANS*/
%ESIG(SCL='00367000',IND='2',REPNO=15);/*BYU PLUS LOANS*/
%ESIG(SCL='00402700',IND='1',REPNO=16);/*UVSC STAFFORD LOANS*/
%ESIG(SCL='00402700',IND='2',REPNO=17);/*UVSC PLUS LOANS*/
%ESIG(SCL='00367900',IND='1',REPNO=18);/*SNOW COLLEGE STAFFORD LOANS*/
%ESIG(SCL='00367900',IND='2',REPNO=19);/*SNOW COLLEGE PLUS LOANS*/
%ESIG(SCL='00367200',IND='1',REPNO=20);/*LDS BUSINESS COLLEGE STAFFORD LOANS*/
%ESIG(SCL='00367600',IND='1',REPNO=22);/*CEU STAFFORD LOANS*/
%ESIG(SCL='00367600',IND='2',REPNO=23);/*CEU PLUS LOANS*/
%ESIG(SCL='02179935',IND='1',REPNO=26);/*ARGOSY STAFFORD LOANS*/
%ESIG(SCL='02179935',IND='2',REPNO=27);/*ARGOSY PLUS LOANS*/
%ESIG(SCL='02531800',IND='1',REPNO=28);/*PMTS Provo STAFFORD LOANS*/
%ESIG(SCL='02531800',IND='2',REPNO=29);/*PMTS Provo PLUS LOANS*/

DATA UERR;
SET UERR;
SSN = INPUT(DF_PRS_ID_BR,9.);
RUN;

PROC PRINTTO PRINT=REPORT99;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=96 PS=52 ;
PROC PRINT NOOBS SPLIT='/' DATA=UERR WIDTH=UNIFORM WIDTH=MIN;
TITLE "ESIGN LETTERS - INVALID ADDRESS BORROWERS";
FOOTNOTE  'JOB = UTLWG12     REPORT = ULWG12.LWG12R99';
VAR SSN	DM_PRS_1 DM_PRS_LST AF_APL_ID AF_APL_OPS_SCL;
LABEL SSN='BORROWER SSN' DM_PRS_1="FIRST NAME" DM_PRS_LST="LAST NAME" 
AF_APL_ID="COMMONLINE UNIQUE ID" AF_APL_OPS_SCL="SCHOOL ID";
FORMAT SSN SSN11.;
RUN;
PROC PRINTTO;
RUN;
