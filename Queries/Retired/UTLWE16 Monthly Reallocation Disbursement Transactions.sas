/*UTLWE16 MONTHLY REALLOCATIONS DISBURSEMENT TRANSACTIONS*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWE16.LWE16R2";
FILENAME REPORT3 "&RPTLIB/ULWE16.LWE16R3";
FILENAME REPORT4 "&RPTLIB/ULWE16.LWE16R4";
FILENAME REPORT5 "&RPTLIB/ULWE16.LWE16R5";
FILENAME REPORT6 "&RPTLIB/ULWE16.LWE16R6";
FILENAME REPORT7 "&RPTLIB/ULWE16.LWE16R7";
FILENAME REPORT8 "&RPTLIB/ULWE16.LWE16R8";
FILENAME REPORT9 "&RPTLIB/ULWE16.LWE16R9";
FILENAME REPORT10 "&RPTLIB/ULWE16.LWE16R10";
FILENAME REPORT11 "&RPTLIB/ULWE16.LWE16R11";
FILENAME REPORT12 "&RPTLIB/ULWE16.LWE16R12";
FILENAME REPORT13 "&RPTLIB/ULWE16.LWE16R13";
FILENAME REPORT14 "&RPTLIB/ULWE16.LWE16R14";
FILENAME REPORT15 "&RPTLIB/ULWE16.LWE16R15";
FILENAME REPORT16 "&RPTLIB/ULWE16.LWE16R16";
FILENAME REPORT17 "&RPTLIB/ULWE16.LWE16R17";
FILENAME REPORT18 "&RPTLIB/ULWE16.LWE16R18";
FILENAME REPORT19 "&RPTLIB/ULWE16.LWE16R19";
FILENAME REPORT20 "&RPTLIB/ULWE16.LWE16R20";
FILENAME REPORT21 "&RPTLIB/ULWE16.LWE16R21";
FILENAME REPORT22 "&RPTLIB/ULWE16.LWE16R22";
FILENAME REPORT23 "&RPTLIB/ULWE16.LWE16R23";
FILENAME REPORT24 "&RPTLIB/ULWE16.LWE16R24";
FILENAME REPORT25 "&RPTLIB/ULWE16.LWE16R25";
FILENAME REPORT26 "&RPTLIB/ULWE16.LWE16R26";
FILENAME REPORT27 "&RPTLIB/ULWE16.LWE16R27";
FILENAME REPORT28 "&RPTLIB/ULWE16.LWE16R28";
FILENAME REPORT29 "&RPTLIB/ULWE16.LWE16R29";
FILENAME REPORT30 "&RPTLIB/ULWE16.LWE16R30";
FILENAME REPORTZ "&RPTLIB/ULWE16.LWE16RZ";
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY()+3,-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY()+3,-1,'end'), MMDDYYD10.)||"'");
     CALL SYMPUT('EFFDATE',TRIM(LEFT(UPCASE(
		PUT(INTNX('MONTH',TODAY()+3,-1), MONNAME9.)||' '||
		PUT(INTNX('MONTH',TODAY()+3,-1), YEAR4.)))));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE MRDTRAN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,F.AF_LDR_ORG 
	,G.IM_LDR_FUL AS ORG_LDR_FUL
	,A.LF_LON_CUR_OWN
	,E.IM_LDR_FUL
	,CASE 
		WHEN D.DM_PRS_MID IS NULL
		THEN RTRIM(D.DM_PRS_LST)||' '||D.DM_PRS_1
		ELSE RTRIM(D.DM_PRS_LST)||' '||RTRIM(D.DM_PRS_1)||' '||DM_PRS_MID
	 END AS NAME
	,B.LN_BR_DSB_SEQ
	,B.LC_LDR_DSB_MDM
	,E.PC_FAT_TYP||E.PC_FAT_SUB_TYP AS TXTYP
	,E.LD_FAT_PST
	,E.LA_FAT_CUR_PRI
	,B.LD_DSB
	,A.LF_RGL_CAT_LP20
	,A.IF_TIR_PCE
	,RLCTN.REALLO_PST_DT
	,SALE.SALE_EFF_DT
	,A.IC_LON_PGM
	,H.LC_DSB_FEE AS LC_DSB_FEE_02
	,H.LA_DSB_FEE AS LA_DSB_FEE_02
	,I.LC_DSB_FEE AS LC_DSB_FEE_21
	,I.LA_DSB_FEE AS LA_DSB_FEE_21
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.LN90_FIN_ATY E
	ON B.BF_SSN = E.BF_SSN
	AND B.LN_SEQ = E.LN_SEQ
	AND B.LD_DSB = E.LD_FAT_EFF
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON E.IF_DOE_LDR = A.LF_LON_CUR_OWN
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,LD_FAT_PST AS REALLO_PST_DT
	FROM OLWHRM1.LN90_FIN_ATY 
	WHERE PC_FAT_TYP = '06'
	AND PC_FAT_SUB_TYP = '85'
	 ) RLCTN
	ON A.BF_SSN = RLCTN.BF_SSN
LEFT OUTER JOIN (
	SELECT LN90.BF_SSN
		,LN90.LN_SEQ
		,LN90.LD_FAT_EFF AS SALE_EFF_DT
		,LN99.IF_SLL_OWN_SLD
		,LN99.IF_BUY_OWN_SLD
	FROM OLWHRM1.LN90_FIN_ATY LN90
	INNER JOIN OLWHRM1.LN99_LON_SLE_FAT LN99
		ON LN90.BF_SSN = LN99.BF_SSN
		AND LN90.LN_SEQ = LN99.LN_SEQ
		AND LN90.LN_FAT_SEQ = LN99.LN_FAT_SEQ
	/*----------LENDER MERGE----------*/
	INNER JOIN OLWHRM1.LR10_LDR_DMO LR10
		ON LN99.IF_SLL_OWN_SLD = LR10.IF_DOE_LDR
	WHERE LN90.PC_FAT_TYP = '03'
	AND LN90.PC_FAT_SUB_TYP = '95'
	AND NOT
		( 
			(
				LN99.IF_BUY_OWN_SLD = LR10.IF_LDR_MRG_TO 
				AND LR10.IC_LDR_MRG_TYP IN ('F','1')
			)
		OR 
			(
				LN99.IF_SLL_OWN_SLD IN (&UHEAA_LIST) 
				AND LN99.IF_BUY_OWN_SLD IN (&UHEAA_LIST)
			)
		)
	 ) SALE
	ON A.BF_SSN = SALE.BF_SSN
INNER JOIN OLWHRM1.AP03_MASTER_APL F
	ON B.AF_APL_ID = F.AF_APL_ID
INNER JOIN OLWHRM1.LR10_LDR_DMO G
	ON G.IF_DOE_LDR = F.AF_LDR_ORG
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE H
	ON B.BF_SSN = H.BF_SSN
	AND B.LN_BR_DSB_SEQ = H.LN_BR_DSB_SEQ
	AND H.LC_DSB_FEE = '02'
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE I
	ON B.BF_SSN = I.BF_SSN
	AND B.LN_BR_DSB_SEQ = I.LN_BR_DSB_SEQ
	AND I.LC_DSB_FEE = '21'


WHERE E.LC_STA_LON90 = 'A'
AND E.LD_FAT_PST BETWEEN &BEGIN AND &END
AND (B.LC_STA_LON15 IN ('1','3')
	 AND B.LC_LDR_DSB_MDM IN('A','E')
	 AND E.PC_FAT_TYP = '01'
	 AND E.PC_FAT_SUB_TYP = '01'
	 OR
	 E.PC_FAT_TYP = '06'
	 AND E.PC_FAT_SUB_TYP = '85')
);
DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ; *includes error messages to SAS log;*/
/*%sqlcheck;*/
/*quit;*/
DATA MRDTRAN (DROP=AF_LDR_ORG ORG_LDR_FUL REALLO_PST_DT SALE_EFF_DT);
SET MRDTRAN;
IF LF_LON_CUR_OWN IN (&UHEAA_LIST) AND REALLO_PST_DT < SALE_EFF_DT 
	THEN DO;
		LF_LON_CUR_OWN = AF_LDR_ORG;
		IM_LDR_FUL = ORG_LDR_FUL;
		END;
RUN;
ENDRSUBMIT;
DATA MRDTRAN;
SET WORKLOCL.MRDTRAN;
RUN;

DATA MRDTRAN;
SET MRDTRAN;
LENGTH LOC1 $ 4. LOC2 $ 35.;
IF INPUT(LF_RGL_CAT_LP20,BEST12.) <= 1999030 THEN
	LOC1 = '3%';
ELSE IF INPUT(LF_RGL_CAT_LP20,BEST12.) = 2006020 THEN
	LOC1 = '2%';
ELSE IF INPUT(LF_RGL_CAT_LP20,BEST12.) = 2007020 THEN
	LOC1 = '1.5%';
ELSE IF INPUT(LF_RGL_CAT_LP20,BEST12.) = 2008020 THEN
	LOC1 = '1.%';
ELSE IF INPUT(LF_RGL_CAT_LP20,BEST12.) = 2009020 THEN DO;
	IF IC_LON_PGM IN ('PLUS','PLUSGB') THEN 
		LOC1 = '3%';
	ELSE 
		LOC1 = '0.5%';
END;

IF IF_TIR_PCE ^= '' THEN DO;
	IF INPUT(LF_RGL_CAT_LP20,BEST12.) = 2008020 THEN
		LOC2 = 'UHEAA PAID ORIGINATION FEE';
	ELSE 
		LOC2 = 'LOAN WITH ZERO ORIGINATION FEE';
END;

IF LA_DSB_FEE_02 > 0 THEN DO;
	IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 AND IC_LON_PGM IN ('PLUS', 'PLUSGB') THEN DO;
		IF LA_DSB_FEE_21 = . THEN
			LOC2 = 'LOAN WITH ORIGINATION FEE 3%';
		ELSE IF LA_DSB_FEE_21 > 0 THEN
			LOC2 = 'LOAN WITH ORIG/GUAR FEE 3%/1%';
	END;
	ELSE DO;
		IF LA_DSB_FEE_21 = . THEN
			LOC2 = 'LOAN WITH ORIGINATION FEE 0.5%';
		ELSE IF LA_DSB_FEE_21 > 0 THEN
			LOC2 = 'LOAN WITH ORIG/GUAR FEE 0.5%/1%';
	END;
END;
RUN;

PROC SORT DATA=MRDTRAN NODUPKEY;
BY BF_SSN LN_BR_DSB_SEQ;
RUN;

%MACRO PRINT(LENDER=,REPNO=);

%LET LNDR_NAME = ;

DATA MRDTRANLDR;
SET MRDTRAN;
WHERE LF_LON_CUR_OWN = "&LENDER";
BRWS = 0;
DISBS = 0;
RUN;

DATA _NULL_;
SET MRDTRANLDR;
CALL SYMPUT('LNDR_NAME',TRIM(IM_LDR_FUL));
RUN;
%PUT &LNDR_NAME;

PROC SQL;
CREATE TABLE BRWS AS
SELECT DISTINCT
	1 AS CNT
	,A.LN_SEQ
	,A.LD_DSB
	,A.BF_SSN
FROM MRDTRANLDR A
WHERE LD_DSB = (SELECT MAX(B.LD_DSB)
				FROM MRDTRANLDR B
				WHERE A.BF_SSN = B.BF_SSN
				GROUP BY BF_SSN)
AND   LN_SEQ = (SELECT MAX(C.LN_SEQ)
				FROM MRDTRANLDR C
				WHERE A.BF_SSN = C.BF_SSN
				GROUP BY BF_SSN);
QUIT;

PROC SQL;
CREATE TABLE MRDTRANLDR2 AS
SELECT
	A.LF_LON_CUR_OWN
	,A.BF_SSN
	,A.NAME
	,A.LN_SEQ
	,A.LD_DSB 
	,A.LD_FAT_PST
	,A.LA_FAT_CUR_PRI
	,A.TXTYP
	,A.LC_LDR_DSB_MDM
	,B.CNT
	,A.LOC1
	,A.LOC2
FROM MRDTRANLDR A
LEFT OUTER JOIN BRWS B ON
 	A.BF_SSN = B.BF_SSN
	AND A.LD_DSB = B.LD_DSB
	AND A.LN_SEQ = B.LN_SEQ;
QUIT;

PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;

OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO = 1 NODATE CENTER;
PROC CONTENTS DATA=MRDTRANLDR2 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 128*'-';
	PUT      ////////
		@51 '**** NO DISBURSEMENTS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT /////////////
		@46 "JOB = UTLWE16     REPORT = ULWE16.LWE16R&REPNO";
	END;
RETURN;
TITLE 'MONTHLY REALLOCATION DISBURSEMENT TRANSACTIONS';
TITLE2 "LENDER ID # &LENDER";
TITLE3	"FOR &EFFDATE";
RUN;
PROC REPORT DATA=MRDTRANLDR2 NOWD HEADSKIP SPLIT='/' SPACING=2;
TITLE 'MONTHLY REALLOCATION DISBURSEMENT TRANSACTIONS';
TITLE2 "&LNDR_NAME";
TITLE3 "LENDER ID # &LENDER";
TITLE4	"FOR &EFFDATE";
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	"Please take appropriate precautions to safeguard this information.";
FOOTNOTE3	;
FOOTNOTE4   "JOB = UTLWE16     REPORT = ULWE16.LWE16R&REPNO";
COLUMN LOC2 LOC1 BF_SSN NAME LN_SEQ	LD_DSB LD_FAT_PST TXTYP LC_LDR_DSB_MDM LA_FAT_CUR_PRI CNT N;
DEFINE LOC2 / GROUP "CATEGORY" WIDTH=15 FLOW;
DEFINE LOC1 / GROUP "ED ORIGINATION FEE %" WIDTH=11;
DEFINE BF_SSN / ORDER "SSN" WIDTH=9;
DEFINE NAME / ORDER "NAME" WIDTH=23 FLOW;
DEFINE LN_SEQ / ORDER "LN/SEQ" WIDTH=3;
DEFINE LD_DSB / ORDER FORMAT=MMDDYY10. "DISB DATE" WIDTH=10;
DEFINE LD_FAT_PST / ORDER FORMAT=MMDDYY10. "TRANSACTION/POSTED DATE" WIDTH=11;
DEFINE TXTYP / ORDER "TRANSACTION/TYPE" WIDTH=11;
DEFINE LC_LDR_DSB_MDM / ORDER "DISB/MEDIUM/CODE" WIDTH=6;
DEFINE LA_FAT_CUR_PRI /ANALYSIS FORMAT=DOLLAR14.2 "DISB/AMOUNT" WIDTH=14;
DEFINE CNT / ANALYSIS NOPRINT;
DEFINE N / NOPRINT;
BREAK AFTER LOC2 / SUMMARIZE SUPPRESS SKIP OL;
COMPUTE AFTER;
LINE ' ';
LINE ' ';
LINE @1 'TOTAL NUMBER OF BORROWERS:' @33 CNT.SUM COMMA7.;
LINE @1 'TOTAL NUMBER OF DISBURSEMENTS:' @33 N COMMA7.;
LINE @1 'TOTAL DISBURSEMENT AMOUNT:' @33 LA_FAT_CUR_PRI.SUM DOLLAR14.2;
ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
%MEND PRINT;

%PRINT(LENDER=811698,REPNO=2);
%PRINT(LENDER=813760,REPNO=3);
%PRINT(LENDER=813894,REPNO=4);
%PRINT(LENDER=817440,REPNO=5);
%PRINT(LENDER=817545,REPNO=6);
%PRINT(LENDER=817546,REPNO=7);
%PRINT(LENDER=819628,REPNO=8);
%PRINT(LENDER=829505,REPNO=9);
%PRINT(LENDER=820200,REPNO=10);
%PRINT(LENDER=822373,REPNO=11);
%PRINT(LENDER=829123,REPNO=12);
%PRINT(LENDER=829158,REPNO=13);
%PRINT(LENDER=830132,REPNO=14);
%PRINT(LENDER=830146,REPNO=15);
%PRINT(LENDER=830791,REPNO=16);
%PRINT(LENDER=832241,REPNO=17);
%PRINT(LENDER=833828,REPNO=18);
%PRINT(LENDER=817455,REPNO=19);
%PRINT(LENDER=817575,REPNO=20);
%PRINT(LENDER=834122,REPNO=21);
%PRINT(LENDER=833577,REPNO=22);
%PRINT(LENDER=834265,REPNO=23);
%PRINT(LENDER=828476,REPNO=24);
%PRINT(LENDER=834370,REPNO=25);
%PRINT(LENDER=834437,REPNO=26);
%PRINT(LENDER=834396,REPNO=27);
%PRINT(LENDER=82847601,REPNO=28);
%PRINT(LENDER=834493,REPNO=29);
%PRINT(LENDER=834529,REPNO=30);
