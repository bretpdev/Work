*-----------------------------------------*
| UTLWM19 SPECIAL CAMPAIGN -  NEW DROPOUT |
*-----------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = C:\WINDOWS\TEMP;
FILENAME REPORT2 "&RPTLIB/ULWM19.LWM19R2";
FILENAME REPORT3 "&RPTLIB/ULWM19.LWM19R3";
FILENAME REPORT4 "&RPTLIB/ULWM19.LWM19R4";
FILENAME REPORTZ "&RPTLIB/ULWM19.LWM19RZ";
DATA _NULL_;
	CALL SYMPUT('INT_DAYS_AHEAD_15',INTNX('DAY',TODAY(),+15,'BEGINNING'));
	CALL SYMPUT('INT_DAYS_AHEAD_00',INTNX('DAY',TODAY(),0,'BEGINNING'));
	CALL SYMPUT('CHAR_DAYS_AGO_00',"'"||PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('CHAR_DAYS_AGO_15',"'"||PUT(INTNX('DAY',TODAY(),-15,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('CHAR_DAYS_AGO_30',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('CHAR_DAYS_AGO_60',"'"||PUT(INTNX('DAY',TODAY(),-60,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT CHAR_DAYS_AGO_00 = &CHAR_DAYS_AGO_00;
%SYSLPUT CHAR_DAYS_AGO_15 = &CHAR_DAYS_AGO_15;
%SYSLPUT CHAR_DAYS_AGO_30 = &CHAR_DAYS_AGO_30;
%SYSLPUT CHAR_DAYS_AGO_60 = &CHAR_DAYS_AGO_60;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/************************************************************
* GET INITAIL POPULATION OF BORROWERS
*************************************************************/
CREATE TABLE SCD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,B.DF_SPE_ACC_ID
	,B.DM_PRS_1
	,B.DM_PRS_LST
	,A.LD_END_GRC_PRD
	,G.PF_ACT
	,H.PF_ACT AS ASCON_ARC
	,F.DC_ADR
	,F.DI_VLD_ADR
	,F.DX_STR_ADR_1
	,F.DX_STR_ADR_2
	,F.DM_CT
	,F.DC_DOM_ST
	,F.DF_ZIP_CDE
	,F.DM_FGN_CNY
	,'MA2329' AS COST_CENTER_CODE
	,SUM(A.LA_CUR_PRI) AS LA_CUR_PRI
	,MAX(E.LD_SCL_SPR) AS LD_SCL_SPR
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD42_PRS_PHN C
	ON B.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.LN13_LON_STU_OSD D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.SD10_STU_SPR E
	ON D.LF_STU_SSN = E.LF_STU_SSN
	AND D.LN_STU_SPR_SEQ = E.LN_STU_SPR_SEQ
LEFT OUTER JOIN OLWHRM1.PD30_PRS_ADR F
	ON A.BF_SSN = F.DF_PRS_ID
	AND F.DC_ADR = 'L'
	AND F.DI_VLD_ADR = 'Y'
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY G
	ON A.BF_SSN = G.DF_PRS_ID
	AND G.PF_ACT = 'ADROP'
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY H
	ON A.BF_SSN = H.DF_PRS_ID
	AND H.PF_ACT IN ('ASCON','ALBNC')
WHERE A.LA_CUR_PRI > 0
AND A.LC_STA_LON10 = 'R'
AND A.IC_LON_PGM IN (
	'STFFRD','PLUSGB','PLUS','UNSTFD'
	)
AND C.DC_PHN = 'H'
AND C.DI_PHN_VLD = 'Y'
AND D.LC_STA_LON13 = 'A'
AND E.LC_STA_STU10 = 'A'
AND E.LC_REA_SCL_SPR = '02'
AND DAYS(CURRENT DATE) > DAYS(E.LD_SCL_SPR) + 60 
AND A.LD_END_GRC_PRD > &CHAR_DAYS_AGO_00
GROUP BY A.BF_SSN
	,B.DF_SPE_ACC_ID
	,B.DM_PRS_1
	,B.DM_PRS_LST
	,A.LD_END_GRC_PRD
	,G.PF_ACT
	,H.PF_ACT 
	,F.DC_ADR
	,F.DI_VLD_ADR
	,F.DX_STR_ADR_1
	,F.DX_STR_ADR_2
	,F.DM_CT
	,F.DC_DOM_ST
	,F.DF_ZIP_CDE
	,F.DM_FGN_CNY
	,'MA2329' 
FOR READ ONLY WITH UR
);
/************************************************************
* CREATE EXCLUSION DATA SET
*************************************************************/
CREATE TABLE EXCLD AS 
SELECT DISTINCT BF_SSN, PF_ACT
FROM CONNECTION TO DB2(
	SELECT DF_PRS_ID AS BF_SSN, PF_ACT AS PF_ACT
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'AUCON'
	AND BD_ATY_PRF >= &CHAR_DAYS_AGO_15
UNION
	SELECT DF_PRS_ID AS BF_SSN, PF_ACT AS PF_ACT
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT IN ('ASCON','ALBNC')
	AND BD_ATY_PRF >= &CHAR_DAYS_AGO_30
UNION
	SELECT DF_PRS_ID_BR AS BF_SSN, IF_WRK_GRP AS PF_ACT
	FROM OLWHRM1.CT30_CALL_QUE 
	WHERE IF_WRK_GRP = 'PSCWDRW'
);
DISCONNECT FROM DB2;
/************************************************************
* EXCLUDE FROM DATASET
*************************************************************/
PROC SQL;
CREATE TABLE SCDRPO AS
SELECT DISTINCT A.*
FROM SCD A
WHERE BF_SSN NOT IN (
	SELECT BF_SSN
	FROM EXCLD 
	)
;
QUIT;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWM19.LWM19RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA SCDRPO;
SET WORKLOCL.SCDRPO;
RUN;

PROC SORT DATA=SCDRPO NODUPKEY;BY BF_SSN;RUN;

/*CALCULATE KEYLINE*/
DATA SCDRPO (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET SCDRPO;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
/*CREATE SORT VALS*/
DATA SCDRPO;
SET SCDRPO;
IF DM_FGN_CNY ^= '' THEN SVAR=1;
	ELSE SVAR=2;
RUN;

DATA _NULL_;
SET  WORK.SCDRPO;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
DO;
	PUT BF_SSN $ ;
END;
RUN;

%MACRO FCRT(CRIT,REPNO);
DATA REPDS;
SET SCDRPO;
WHERE &CRIT ;
RUN;

PROC SORT DATA=REPDS;BY SVAR DC_DOM_ST;RUN;

DATA _NULL_;
SET REPDS;
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $9. ;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT DM_PRS_1 $13. ;
   FORMAT DM_PRS_LST $23. ;
   FORMAT DX_STR_ADR_1 $30. ;
   FORMAT DX_STR_ADR_2 $30. ;
   FORMAT DM_CT $20. ;
   FORMAT DC_DOM_ST $2. ;
   FORMAT DF_ZIP_CDE $17. ;
   FORMAT DM_FGN_CNY $25. ;
   FORMAT COST_CENTER_CODE $6. ;
   FORMAT ACSKEY $18. ;
   FORMAT LA_CUR_PRI 12.2 ;
   FORMAT LD_SCL_SPR MMDDYY10.;
IF _N_ = 1 THEN        
DO;
  PUT
   'BF_SSN'
   ','
   'DF_SPE_ACC_ID'
   ','
   'DM_PRS_1'
   ','
   'DM_PRS_LST'
   ','
   'DX_STR_ADR_1'
   ','
   'DX_STR_ADR_2'
   ','
   'DM_CT'
   ','
   'DC_DOM_ST'
   ','
   'DF_ZIP_CDE'
   ','
   'ACSKEY'
   ','
   'DM_FGN_CNY'
   ','
   'LA_CUR_PRI'
   ','
   'LD_SCL_SPR'
   ','
   'STATE_IND'
   ','
   'COST_CENTER_CODE'
   ;
END;
DO;
   PUT BF_SSN $ @;
   PUT DF_SPE_ACC_ID $ @;
   PUT DM_PRS_1 $ @;
   PUT DM_PRS_LST $ @;
   PUT DX_STR_ADR_1 $ @;
   PUT DX_STR_ADR_2 $ @;
   PUT DM_CT $ @;
   PUT DC_DOM_ST $ @;
   PUT DF_ZIP_CDE $ @;
   PUT ACSKEY $ @;
   PUT DM_FGN_CNY $ @;
   PUT LA_CUR_PRI @;
   PUT LD_SCL_SPR @;
   PUT DC_DOM_ST $ @;
   PUT COST_CENTER_CODE $ ;
END;
RUN;
%MEND FCRT;
%FCRT(DC_ADR EQ 'L' AND DI_VLD_ADR EQ 'Y' AND PF_ACT EQ '',3);
%FCRT(DC_ADR EQ 'L' AND DI_VLD_ADR EQ 'Y' AND ASCON_ARC EQ '' AND LD_END_GRC_PRD BETWEEN &INT_DAYS_AHEAD_00 AND &INT_DAYS_AHEAD_15,4);
