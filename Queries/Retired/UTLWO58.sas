*--------------------------*
| UTLWO58 CREDIT APPROVALS |
*--------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWO58.LWO58R2";
FILENAME REPORT3 "&RPTLIB/ULWO58.LWO58R3";
FILENAME REPORTZ "&RPTLIB/ULWO58.LWO58RZ";
DATA _NULL_;
     CALL SYMPUT('DAYS_AGO_1',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.));
RUN;
/*%SYSLPUT DAYS_AGO_1 = &DAYS_AGO_1;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CREDAPRV AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT C.DF_PRS_ID
	,A.AF_APL_ID
	,C.DC_ADR
	,D.DF_SPE_ACC_ID
	,D.DM_PRS_1
	,D.DM_PRS_MID
	,D.DM_PRS_LST
	,C.DX_STR_ADR_1
	,C.DX_STR_ADR_2
	,C.DM_CT
	,C.DC_DOM_ST
	,C.DF_ZIP
	,C.DM_FGN_CNY
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON C.DF_PRS_ID = D.DF_PRS_ID
WHERE A.AC_CRD_CHK_PRF = 'Y'
AND A.AD_CRD_CHK_PRF = &DAYS_AGO_1
AND B.AC_PRC_STA <> 'A'
AND C.DC_ADR = 'L'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWO58.LWO58RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA CREDAPRV;*/
/*SET WORKLOCL.CREDAPRV;*/
/*RUN;*/
/*SEPARATE DATA FOR LETTER AND SCRIPT FILES*/
DATA LET(DROP=AF_APL_ID) FIL(KEEP=DF_PRS_ID AF_APL_ID);
SET CREDAPRV;
RUN;
PROC SORT DATA=FIL NODUPKEY;BY DF_PRS_ID AF_APL_ID;RUN;
PROC SORT DATA=LET NODUPKEY;BY DF_PRS_ID;RUN;
/*GET APPLICATION DATA ON ONE LINE*/
DATA FIL (DROP=AF_APL_ID);
SET FIL;
BY DF_PRS_ID;
LENGTH APP_LIST $ 600;
RETAIN APP_LIST;
IF FIRST.DF_PRS_ID THEN APP_LIST = AF_APL_ID;
ELSE APP_LIST = TRIM(APP_LIST)||', '||AF_APL_ID;
IF LAST.DF_PRS_ID THEN OUTPUT;
RUN;
/*INITIALIZE VARIALBES NEEDED FOR LETTERS AND SORTING*/
DATA LET;
SET LET;
COST_CENTER_CODE = 'MA2327';
STATE_IND = DC_DOM_ST;
IF DC_DOM_ST = 'FC' 
	THEN SVAR = 1;
ELSE SVAR = 2;
RUN;
/*CALCULATE KEYLINE;*/
DATA LET (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET LET;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

PROC SORT DATA=LET; BY SVAR DC_DOM_ST DF_PRS_ID; RUN;
PROC SORT DATA=FIL; BY DF_PRS_ID; RUN;

DATA _NULL_;
SET  WORK.LET;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_PRS_ID $9. ;
   FORMAT DC_ADR $1. ;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT DM_PRS_1 $12. ;
   FORMAT DM_PRS_MID $1. ;
   FORMAT DM_PRS_LST $35. ;
   FORMAT DX_STR_ADR_1 $35. ;
   FORMAT DX_STR_ADR_2 $35. ;
   FORMAT DM_CT $30. ;
   FORMAT DC_DOM_ST $2. ;
   FORMAT DF_ZIP $14. ;
   FORMAT DM_FGN_CNY $25. ;
   FORMAT COST_CENTER_CODE $6. ;
   FORMAT STATE_IND $2. ;
   FORMAT SVAR BEST12. ;
   FORMAT ACSKEY $18. ;
IF _N_ = 1 THEN 
 DO;
   PUT
   'DF_PRS_ID'
   ','
   'DF_SPE_ACC_ID'
   ','
   'DM_PRS_1'
   ','
   'DM_PRS_MID'
   ','
   'DM_PRS_LST'
   ','
   'DX_STR_ADR_1'
   ','
   'DX_STR_ADR_2'
   ','
   'DM_CT'
   ','
   'DC_DOM_ST'
   ','
   'DF_ZIP'
   ','
   'DM_FGN_CNY'
   ','
   'ACSKEY'
   ','
   'STATE_IND'
   ','
   'COST_CENTER_CODE'
;
END;
DO;
   PUT DF_PRS_ID $ @;
   PUT DF_SPE_ACC_ID $ @;
   PUT DM_PRS_1 $ @;
   PUT DM_PRS_MID $ @;
   PUT DM_PRS_LST $ @;
   PUT DX_STR_ADR_1 $ @;
   PUT DX_STR_ADR_2 $ @;
   PUT DM_CT $ @;
   PUT DC_DOM_ST $ @;
   PUT DF_ZIP $ @;
   PUT DM_FGN_CNY $ @;
   PUT ACSKEY $ @; 
   PUT STATE_IND $ @;
   PUT COST_CENTER_CODE $ 
;
END;
RUN;

DATA _NULL_;
SET  WORK.FIL;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_PRS_ID $9. ;
   FORMAT APP_LIST $600. ;
IF _N_ = 1 THEN 
 DO;
   PUT
   'DF_PRS_ID'
   ','
   'APP_LIST';
END;
DO;
   PUT DF_PRS_ID $ @;
   PUT APP_LIST $ ;
END;
RUN;

