/*
GUARANTEE AMOUNTS FOR BYU, BYU HAWAII, BYU IDAHO, LDS BUS COLLEGE

Lists the gross and net amount of loans guaranteed during 2000 for the LDS church affiliated
schools listed above.

*/


/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWG05.LWG05R2";
FILENAME REPORT3 "&RPTLIB/ULWG05.LWG05R3";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
OPTIONS MPRINT SYMBOLGEN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE YRGUARS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.AF_APL_ID||A.AF_APL_ID_SFX							AS CLID,
	A.af_cur_lon_ops_ldr									AS LENDER,
	D.im_ist_ful											AS LNAME,
	B.af_apl_ops_scl										AS SCHOOL,
	C.im_ist_ful											AS SNAME,
	A.aa_gte_lon_amt										AS GROSS,
	A.aa_tot_rfd											AS REFUND,
	A.aa_tot_can											AS CANCEL,
	A.ac_lon_typ											AS TYPE
FROM	OLWHRM1.GA10_LON_APP A INNER JOIN
		OLWHRM1.GA01_APP B ON
			A.af_apl_id = B.af_apl_id AND
			A.ac_prc_sta = 'A' INNER JOIN
		OLWHRM1.SC01_LGS_SCL_INF C ON
			B.af_apl_ops_scl = C.if_ist INNER JOIN
		OLWHRM1.LR01_LGS_LDR_INF D ON
			A.af_cur_lon_ops_ldr = D.if_ist

WHERE	A.ac_lon_typ in ('SF', 'SU')
		AND A.ad_prc BETWEEN '2000-01-01' AND '2000-12-31' AND
		(A.aa_tot_can IS NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd > 0 OR
		 A.aa_tot_can IS NOT NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd - A.aa_tot_can > 0) and
		B.af_apl_ops_scl in ('00160600', '00367000', '00162500', '00367200')
);
DISCONNECT FROM DB2;


DATA YRGUARS;
	SET YRGUARS;
	IF CANCEL = . THEN CANCEL = 0;
	NETAMT = GROSS - REFUND - CANCEL;
RUN;

PROC SQL;
CREATE TABLE SUMSCHOOL AS
SELECT	SNAME,
		SCHOOL,
		TYPE,
		SUM(NETAMT)	AS AMOUNT,
		SUM(GROSS)	AS GSAMT
FROM	YRGUARS
GROUP BY SCHOOL, SNAME, TYPE;
QUIT;
PROC SORT DATA=SUMSCHOOL;
BY SNAME SCHOOL TYPE;
RUN;
ENDRSUBMIT;
OPTIONS NUMBER NODATE CENTER PAGENO=1 LS=80;
/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
PROC PRINT DATA = WORKLOCL.SUMSCHOOL NOOBS SPLIT='/';
VAR TYPE
	AMOUNT
	GSAMT;
BY	SNAME
	SCHOOL;
SUMBY SNAME;
FORMAT	AMOUNT DOLLAR13.
		GSAMT DOLLAR13.;
LABEL	SNAME = 'SCHOOL NAME'
		SCHOOL = 'SCHOOL ID'
		AMOUNT = 'NET AMOUNT'
		GSAMT = 'GROSS AMOUNT';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	'NET AND GROSS GUARANTEES FOR THE YEAR 2000';
TITLE3	'BY SCHOOL';
RUN;
