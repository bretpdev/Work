/*UTLWG25 UNDERLYING CONSOLIDATION LOANS SOLD*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWG25.LWG25R2";
FILENAME REPORT3 "&RPTLIB/ULWG25.LWG25R3";
FILENAME REPORTZ "&RPTLIB/ULWG2Z.LWG25RZ";

DATA _NULL_;
	 CALL SYMPUT('DATE',"'"||PUT(INTNX('DAY',TODAY(),-3,'BEGINNING'), MMDDYYD10.)||"'");
RUN;

/*%SYSLPUT DATE = &DATE;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LCO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT X.DF_LCO_PRS_SSN_BR AS BF_SSN
	,INTEGER(SUBSTR(Y.LF_UND_LN_ACC_NUM,8,2)) AS LN_SEQ
	,Y.IF_UND_LN_CDR
FROM OLWHRM1.AP1A_LCO_APL X
INNER JOIN OLWHRM1.LC10_UND_LN_INF Y
	ON X.DF_LCO_PRS_SSN_BR = Y.DF_LCO_PRS_SSN_BR
	AND X.AN_LCO_APL_SEQ = Y.AN_LCO_APL_SEQ
WHERE X.AD_LCO_APL_DSB IS NULL
AND X.AC_LCO_ACC_STA NOT IN (
	'D1','D3','D4','D6','D7','D8'
	)
AND Y.LI_UND_LN_CON = 'Y'
AND Y.LI_UND_LN_SER_ORG = 'Y'
AND SUBSTR(Y.LF_UND_LN_ACC_NUM,1,4) = 'L0UT'
FOR READ ONLY WITH UR
);

CREATE TABLE DCON AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,CASE 
		 WHEN D.IF_BUY_OWN_SLD = '888885' 
		 THEN '700191'
		 WHEN D.IF_BUY_OWN_SLD = '828476'
		 THEN '828476'
		 ELSE '700121'
	 END AS BUYER
	 ,'D' AS SIND
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN99_LON_SLE_FAT D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
WHERE A.LD_STA_LON10 >= &DATE
AND A.LC_STA_LON10 = 'D'
FOR READ ONLY WITH UR
);

CREATE TABLE RELS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,CASE 
		 WHEN C.IF_BUY_OWN_SLD = '888885' 
		 THEN '700191'
		 WHEN C.IF_BUY_OWN_SLD = '828476'
		 THEN '828476'
		 ELSE '700121'
	 END AS BUYER
	,'R' AS SIND
	,A.LF_LON_CUR_OWN
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN90_FIN_ATY B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.LN99_LON_SLE_FAT C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
	AND B.LN_FAT_SEQ = C.LN_FAT_SEQ
WHERE A.LC_STA_LON10 = 'R'
AND B.PC_FAT_TYP = '03'
AND B.PC_FAT_SUB_TYP = '95'
AND B.LD_FAT_EFF >= &DATE  
FOR READ ONLY WITH UR
);

CREATE TABLE BDSWP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT B.BF_SSN
	,B.LN_SEQ
FROM OLWHRM1.LN90_FIN_ATY B
INNER JOIN OLWHRM1.LN99_LON_SLE_FAT C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
	AND B.LN_FAT_SEQ = C.LN_FAT_SEQ
INNER JOIN OLWHRM1.LR10_LDR_DMO LR10
	ON C.IF_SLL_OWN_SLD = LR10.IF_DOE_LDR
WHERE B.PC_FAT_TYP = '03'
AND B.PC_FAT_SUB_TYP = '95'
AND B.LD_FAT_EFF >= &DATE  
AND (
		(
			C.IF_SLL_OWN_SLD = '828476' AND 
			C.IF_BUY_OWN_SLD = '828476'
		)
	OR
		(
			C.IF_BUY_OWN_SLD = LR10.IF_LDR_MRG_TO AND
			LR10.IC_LDR_MRG_TYP IN ('F','1') 
		)
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWG25.LWG25RZ);
QUIT;
DATA COMP;
SET DCON RELS;
RUN;

PROC SQL;
CREATE TABLE UCONLSO AS 
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,A.BUYER 
	,A.SIND
FROM COMP A
INNER JOIN LCO B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE (
	 	(
		 A.SIND = 'R' AND 
		 A.LF_LON_CUR_OWN <> B.IF_UND_LN_CDR
		 ) 
	OR (
		 A.SIND = 'D'
		)
	)
AND NOT EXISTS (
	SELECT *
	FROM BDSWP X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.LN_SEQ = X.LN_SEQ
	)
;
QUIT;
/*ENDRSUBMIT;*/
/**/
/*DATA UCONLSO;*/
/*SET WORKLOCL.UCONLSO;*/
/*RUN;*/

%MACRO BLDFL(STAT,REPNO);
DATA _NULL_;
SET WORK.UCONLSO;
WHERE SIND = "&STAT";
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT BF_SSN $10.;
   FORMAT LN_SEQ 5.;
   FORMAT BUYER $35.;
DO;
   PUT BF_SSN $ @;
   PUT LN_SEQ $ @;
   PUT BUYER $ ;
END;
RUN;
%MEND BLDFL;
%BLDFL(D,2)
%BLDFL(R,3)
