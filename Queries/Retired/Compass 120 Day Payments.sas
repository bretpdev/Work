/*BORROWER PAYMENTS WITHIN 120 DAYS OF A DISBURSEMENT 
WITHOUT A 1401 REFUND OF ORIG FEE EFFECTIVE THE SAME DAY*/

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PMT120 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS BF_SSN
,E.DM_PRS_LST
,A.LF_LON_CUR_OWN
,A.IC_LON_PGM
,D.LD_FAT_EFF
,D.LD_FAT_PST
,D.PC_FAT_TYP
,D.PC_FAT_SUB_TYP
,D.LA_FAT_CUR_PRI
,B.LD_DSB

,A.LN_SEQ
,B.LN_LON_DSB_SEQ
,A.LC_STA_LON10
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.LN90_FIN_ATY D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME E
	ON A.BF_SSN = E.DF_PRS_ID
WHERE B.LC_STA_LON15 IN ('1','3')  	/*DISB IS ACTIVE OR ACTIVE REISSUE*/
AND D.LC_STA_LON90 = 'A' 			/*ACTIVE TRANSACTION*/
AND A.IC_LON_PGM IN ('STFFRD','UNSTFD')
AND D.PC_FAT_TYP = '10'
AND D.PC_FAT_SUB_TYP IN ('10','11','12')
AND DAYS(D.LD_FAT_EFF) BETWEEN DAYS(B.LD_DSB) AND DAYS(B.LD_DSB) + 120
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.LN90_FIN_ATY X
	WHERE D.BF_SSN = X.BF_SSN
	AND D.LN_SEQ = X.LN_SEQ
	AND D.LD_FAT_EFF = X.LD_FAT_EFF
	AND X.LC_STA_LON90 = 'A'
	AND X.PC_FAT_TYP = '14'
	AND X.PC_FAT_SUB_TYP = '01')
AND COALESCE(D.LA_FAT_CUR_PRI,0) <> 0
/*EXCLUDE REVERSALS*/
AND D.LN_FAT_SEQ_REV IS NULL
/*EXCLUDE FULLY CANCELED DISBURSEMENTS*/
AND (B.LD_DSB_CAN IS NULL
	OR B.LA_DSB_CAN <> B.LA_DSB)
ORDER BY A.LF_LON_CUR_OWN, A.BF_SSN, A.LN_SEQ, B.LN_LON_DSB_SEQ
);
DISCONNECT FROM DB2;

endrsubmit  ;
DATA PMT120; 
SET WORKLOCL.PMT120; 
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=40 LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=PMT120 WIDTH=UNIFORM WIDTH=MIN N="SUB-TOTAL:  ";
WHERE LC_STA_LON10 NE 'D';
BY LF_LON_CUR_OWN;
VAR BF_SSN DM_PRS_LST LN_SEQ LF_LON_CUR_OWN IC_LON_PGM LD_FAT_EFF LD_FAT_PST
PC_FAT_TYP PC_FAT_SUB_TYP LA_FAT_CUR_PRI LD_DSB;
LABEL BF_SSN="BORROWER SSN" DM_PRS_LST="LAST NAME" LN_SEQ="LOAN SEQ" 
LF_LON_CUR_OWN="OWNER ID" IC_LON_PGM="LOAN PGM" LD_FAT_EFF="PMT EFF DATE" 
LD_FAT_PST="PMT POST DATE" PC_FAT_TYP="PMT TRANS TYPE" 
PC_FAT_SUB_TYP="PMT TRANS SUB TYPE" LA_FAT_CUR_PRI="TRANS AMOUNT (PRIN)"
LD_DSB="DISBURSEMENT DATE";
FORMAT BF_SSN SSN11. LD_FAT_EFF LD_FAT_PST LD_DSB MMDDYY10. LA_FAT_CUR_PRI DOLLAR10.2;
TITLE 'BORROWER PAYMENTS WITHIN 120 DAYS TO BE REVIEWED';
TITLE2 'LPP REGION';
TITLE3 "DATE &RUNDATE";
FOOTNOTE  'JOB = ON DEMAND     REPORT = COMPASS 120 DAY PAYMENTS';
RUN;

/*DATA PMT120D
(RENAME = (BF_SSN=BORROWER_SSN DM_PRS_LST=LAST_NAME LN_SEQ=LOAN_SEQ
LF_LON_CUR_OWN=OWNER_ID IC_LON_PGM=LOAN_PGM LD_FAT_EFF=PMT_EFF_DATE
LD_FAT_PST=PMT_POST_DATE PC_FAT_TYP=PMT_TRANS_TYP PC_FAT_SUB_TYP=PMT_TRANS_SUB_TYPE
LA_FAT_CUR_PRI=TRANS_AMT LD_DSB=DISB_DATE) KEEP = BF_SSN DM_PRS_LST LN_SEQ 
LF_LON_CUR_OWN IC_LON_PGM LD_FAT_EFF LD_FAT_PST
PC_FAT_TYP PC_FAT_SUB_TYP LA_FAT_CUR_PRI LD_DSB);
SET PMT120 ;
WHERE LC_STA_LON10 = 'D';
RUN;

PROC EXPORT DATA= PMT120D
            OUTFILE= "T:\SAS\PMT120 DCNV.xls" 
            DBMS=EXCEL2000 REPLACE;
RUN;*/
