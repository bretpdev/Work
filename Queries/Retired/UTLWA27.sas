*---------------------------------------------------*
| UTLWA27 Scheduled Fully Disbursed Loans GT 7-1-08 |
*---------------------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWA27.LWA27R2";
FILENAME REPORTZ "&RPTLIB/ULWA27.LWA27RZ";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SFDLF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT D.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.LN_SEQ
	,A.LF_LON_CUR_OWN
	,A.LA_CUR_PRI
	,C.WA_TOT_BRI_OTS
	,A.IC_LON_PGM
	,A.LF_DOE_SCL_ORG
	,C.WC_DW_LON_STA  
	,A.LD_LON_1_DSB
	,A.LA_LON_AMT_GTR 
	,B.LC_DSB_TYP 
	,B.LC_STA_LON15
	,B.LA_DSB
	,COALESCE(B.LA_DSB_CAN,0) AS CAN_DSB
	,B.LD_DSB
	,B.LN_BR_DSB_SEQ
	,E.ANT_IND
	,A.LF_LON_CUR_OWN	
	,F.IM_SCL_FUL
	,G.LR_ITR
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN	
	AND A.LN_SEQ = B.LN_SEQ 
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ 
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,'Y' AS ANT_IND
	FROM OLWHRM1.LN15_DSB
	WHERE LC_DSB_TYP = '1'
		AND LC_STA_LON15 IN ('1','3')
		AND LA_DSB <> COALESCE(LA_DSB_CAN,0) 
	) E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
INNER JOIN OLWHRM1.SC10_SCH_DMO F
	ON A.LF_DOE_SCL_ORG = F.IF_DOE_SCL
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,LR_ITR
	FROM OLWHRM1.LN72_INT_RTE_HST 
	WHERE LC_STA_LON72 = 'A'
		AND LD_ITR_EFF_BEG <= CURRENT DATE
		AND LD_ITR_EFF_END >= CURRENT DATE
	) G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ

WHERE A.LD_LON_1_DSB >= '07/01/2008'
	AND A.LC_STA_LON10 = 'R'
	AND E.ANT_IND = 'Y'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWA27.LWA27RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA SFDLF;*/
/*	SET WORKLOCL.SFDLF;*/
/*RUN;*/
PROC SORT DATA=SFDLF;
	BY BF_SSN LN_SEQ LD_DSB;
RUN;
PROC SQL;
	CREATE TABLE DSFDLF AS 
		SELECT DISTINCT A.DF_SPE_ACC_ID	
			,A.LN_SEQ
			,A.LF_LON_CUR_OWN
			,A.LA_CUR_PRI
			,COALESCE(A.WA_TOT_BRI_OTS,0) AS CUR_INT
			,A.IC_LON_PGM
			,A.LF_DOE_SCL_ORG
			,A.IM_SCL_FUL
			,A.WC_DW_LON_STA 
			,B.FUL_DSB_DT
			,A.LD_LON_1_DSB
			,A.LA_LON_AMT_GTR - C.TCAN_DSB AS LN_AMT
			,A.LR_ITR
		FROM SFDLF A
		INNER JOIN (
			SELECT BF_SSN
				,LN_SEQ
				,MAX(LD_DSB) AS FUL_DSB_DT
			FROM SFDLF
			WHERE LC_DSB_TYP = '1'
				AND LC_STA_LON15 IN ('1','3')
			GROUP BY BF_SSN
				,LN_SEQ
			) B
			ON A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
		INNER JOIN (
			SELECT BF_SSN
				,LN_SEQ
				,SUM(CAN_DSB) AS TCAN_DSB
			FROM SFDLF
			GROUP BY BF_SSN
				,LN_SEQ
			) C
			ON A.BF_SSN = C.BF_SSN
			AND A.LN_SEQ = C.LN_SEQ
		ORDER BY DF_SPE_ACC_ID 
			,LN_SEQ
	;
QUIT;
PROC FORMAT;
VALUE $LNSTA 
	'01' = 'IN GRACE'
	'02' = 'IN SCHOOL' 
	'03' = 'IN REPAYMENT'
	'04' = 'IN DEFERMENT' 
	'05' = 'IN FORBEARANCE' 
	'06' = 'IN CURE' 
	'07' = 'CLAIM PENDING' 
	'08' = 'CLAIM SUBMITTED' 
	'09' = 'CLAIM CANCLED'
	'10' = 'CLAIM REJECT' 
	'11' = 'CLAIM RETURNED' 
	'12' = 'CLAIM PAID' 
	'13' = 'PRE-CLAIM PENDING' 
	'14' = 'PRE-CLAIM SUBMITTED' 
	'15' = 'PRE-CLAIM CANCELLED' 
	'16' = 'DEATH ALLEGED' 
	'17' = 'DEATH VERIFIED' 
	'18' = 'DISABILITY ALLEGED' 
	'19' = 'DISABILITY VERIFIED' 
	'20' = 'BANKRUPTCY ALLEGED' 
	'21' = 'BANKRUPTCY VERIFIED' 
	'22' = 'PAID IN FULL' 
	'23' = 'NOT FULLY ORIGINATED' 
	'88' = 'PROCESSING ERROR';
QUIT;
DATA _NULL_;
SET  WORK.DSFDLF;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT LN_SEQ 6. ;
   FORMAT LF_LON_CUR_OWN $8. ;
   FORMAT LA_CUR_PRI 10.2 ;
   FORMAT CUR_INT BEST12. ;
   FORMAT IC_LON_PGM $6. ;
   FORMAT LF_DOE_SCL_ORG $8. ;
   FORMAT WC_DW_LON_STA $LNSTA. ;
   FORMAT FUL_DSB_DT MMDDYY10. ;
   FORMAT LD_LON_1_DSB MMDDYY10. ;
   FORMAT LN_AMT BEST12. ;
   FORMAT LR_ITR 7.3 ;
IF _N_ = 1 THEN        
 DO;
   PUT
   'DF_SPE_ACC_ID'
   ','
   'LN_SEQ'
   ','
   'LF_LON_CUR_OWN'
   ','
   'LA_CUR_PRI'
   ','
   'CUR_INT'
   ','
   'IC_LON_PGM'
   ','
   'LF_DOE_SCL_ORG'
   ','
   'IM_SCL_FUL'
   ','
   'WC_DW_LON_STA'
   ','
   'FUL_DSB_DT'
   ','
   'LD_LON_1_DSB'
   ','
   'LN_AMT'
   ','
   'LR_ITR'
   ;
END;
DO;
   PUT DF_SPE_ACC_ID $ @;
   PUT LN_SEQ @;
   PUT LF_LON_CUR_OWN $ @;
   PUT LA_CUR_PRI @;
   PUT CUR_INT @;
   PUT IC_LON_PGM $ @;
   PUT LF_DOE_SCL_ORG $ @;
   PUT IM_SCL_FUL $ @;
   PUT WC_DW_LON_STA $ @;
   PUT FUL_DSB_DT @;
   PUT LD_LON_1_DSB @;
   PUT LN_AMT @;
   PUT LR_ITR ;
END;
RUN;
