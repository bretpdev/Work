/*UTLWO15 - POTENTIAL CANCEL/REISSUE REPORT*/

LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWO15.LWO15R2";

/*libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;*/
/*RSUBMIT;*/
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CANREI AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.DF_PRS_ID_BR) AS DF_PRS_ID_BR
	,RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_LST) AS NAME
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,C.AD_DSB_ADJ
	,C.AA_DSB_ADJ
	,E.BD_ATY_PRF
	,E.BX_CMT

FROM  OLWHRM1.GA01_APP A 
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.GA11_LON_DSB_ATY C
	ON B.AF_APL_ID = C.AF_APL_ID
	AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.DF_PRS_ID_BR = D.DF_PRS_ID
INNER JOIN OLWHRM1.AY01_BR_ATY E
	ON A.DF_PRS_ID_BR = E.DF_PRS_ID

WHERE C.AC_DSB_ADJ_STA = 'A'
AND C.AC_DSB_ADJ IN ('C','U','S')
AND DAYS(C.AD_DSB_ADJ) = DAYS(CURRENT DATE) - 1
/*AND DAYS(C.AD_DSB_ADJ) > DAYS(CURRENT DATE) - 15*/
AND E.PF_ACT = 'GRTCR'
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.AY01_BR_ATY X
	WHERE E.DF_PRS_ID = X.DF_PRS_ID
	AND X.PF_ACT = 'GRTCP'
	AND X.BD_ATY_PRF > E.BD_ATY_PRF)
);
DISCONNECT FROM DB2;

/*ENDRSUBMIT  ;*/
/*DATA CANREI; */
/*SET WORKLOCL.CANREI; */
/*RUN;*/

PROC SORT DATA=CANREI;
BY DF_PRS_ID_BR CLUID BD_ATY_PRF;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2;
RUN;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
TITLE 'POTENTIAL CANCEL/REISSUE REPORT';
TITLE2 "&RUNDATE";

PROC CONTENTS DATA=CANREI OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132 *'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT ////////////////
		@46 "JOB = UTLWO15     REPORT = ULWO15.LWO15R2";
	END;
RETURN;
run;

PROC REPORT DATA=CANREI NOWD SPACING=1 HEADSKIP SPLIT='*';
FOOTNOTE 'JOB = UTLWO15     REPORT = ULWO15.LWO15R2';
COLUMN DF_PRS_ID_BR NAME CLUID AD_DSB_ADJ AA_DSB_ADJ 
	BD_ATY_PRF BX_CMT;
DEFINE DF_PRS_ID_BR / DISPLAY 'BORROWER*SSN' FORMAT=SSN11.;
DEFINE NAME / DISPLAY 'BORROWER NAME' WIDTH=22;
DEFINE CLUID / DISPLAY 'COMMONLINE ID';
DEFINE AD_DSB_ADJ / DISPLAY 'CANCEL/*REFUND*DATE' FORMAT=MMDDYY10.;
DEFINE AA_DSB_ADJ / DISPLAY 'CANCEL/*REFUND*AMOUNT' FORMAT=COMMA9.2;
DEFINE BD_ATY_PRF / DISPLAY FORMAT=MMDDYY10. NOPRINT;
DEFINE BX_CMT / DISPLAY "COMMENT" WIDTH=55 FLOW;
RUN;
