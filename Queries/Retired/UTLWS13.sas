/*=====================================*/
/*UTLWS13 NEW DISCLOSURE COURTESY CALLS*/
/*=====================================*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWS13.LWS13R2";

/*MACRO DATE INITIALIZATION DONE IN DATA NULL TO ENHANCE PERFORMANCE*/
DATA _NULL_;
	CALL SYMPUT('MNS60',"'"||PUT(INTNX('DAY',TODAY(),-60,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('MNS45',"'"||PUT(INTNX('DAY',TODAY(),-45,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('MNS35',"'"||PUT(INTNX('DAY',TODAY(),-35,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('MNS7',"'"||PUT(INTNX('DAY',TODAY(),-7,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('MNS8',"'"||PUT(INTNX('DAY',TODAY(),-8,'BEGINNING'), MMDDYYD10.)||"'");
RUN;

/*%SYSLPUT MNS7 = &MNS7;*/
/*%SYSLPUT MNS8 = &MNS8;*/
/*%SYSLPUT MNS35 = &MNS35;*/
/*%SYSLPUT MNS45 = &MNS45;*/
/*%SYSLPUT MNS60 = &MNS60;*/
/*FILENAME REPORT2 "C:\WINDOWS\Temp\ULWS13.LWS13R2";*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO NDSCL(DATA1,DATA2,VALS);
PROC SORT DATA=&DATA1;
	BY &VALS;
RUN;
PROC SORT DATA=&DATA2;
	BY &VALS;
RUN;
DATA &DATA1;
	MERGE &DATA1 (IN=A) &DATA2 (IN=B);
	BY &VALS;
	IF A;
RUN;
%MEND;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE NDISCC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN10.BF_SSN AS SSN
FROM OLWHRM1.LN10_LON LN10 
INNER JOIN OLWHRM1.PD42_PRS_PHN PD42
	ON LN10.BF_SSN = PD42.DF_PRS_ID 
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
 	ON LN10.BF_SSN = DW01.BF_SSN
	AND LN10.LN_SEQ = DW01.LN_SEQ 
INNER JOIN OLWHRM1.SD10_STU_SPR SD10
	ON LN10.LF_STU_SSN = SD10.LF_STU_SSN 
	AND SD10.LC_STA_STU10 = 'A'
INNER JOIN OLWHRM1.LN65_LON_RPS LN65
	ON LN10.BF_SSN = LN65.BF_SSN
	AND LN10.LN_SEQ = LN65.LN_SEQ 
INNER JOIN OLWHRM1.LN66_LON_RPS_SPF LN66
	ON LN65.BF_SSN = LN66.BF_SSN
	AND LN65.LN_SEQ = LN66.LN_SEQ
	AND LN65.LN_RPS_SEQ = LN66.LN_RPS_SEQ 
INNER JOIN OLWHRM1.RS10_BR_RPD RS10
	ON LN65.BF_SSN = RS10.BF_SSN
	AND LN65.LN_RPS_SEQ = RS10.LN_RPS_SEQ 
WHERE LN10.LC_STA_LON10 = 'R'
AND LN10.LA_CUR_PRI > 0
AND PD42.DC_PHN = 'H'
AND PD42.DI_PHN_VLD = 'Y'
AND PD42.DN_DOM_PHN_LCL <> ' '
AND PD42.DN_DOM_PHN_XCH <> ' '
AND PD42.DN_DOM_PHN_ARA <> ' '
AND DW01.WD_LON_RPD_SR < &MNS60
AND SD10.LD_SCL_SPR < &MNS60
AND RS10.LC_STA_RPST10 = 'A'
AND LN66.LN_GRD_RPS_SEQ = 1
AND RS10.LD_SNT_RPD_DIS BETWEEN &MNS35 AND &MNS7
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06') /*LOAN NOT IN DEFERMENT, FORBEARANCE, OR CURE STATUS*/
AND (
	 NOT EXISTS
	 	(SELECT * 
	 	 FROM OLWHRM1.AY10_BR_LON_ATY X
	 	 WHERE X.BF_SSN = LN10.BF_SSN
	 	 AND X.PF_REQ_ACT = 'XCNWD'
		 AND X.LD_ATY_RSP >= &MNS45)
	 OR
	  NOT EXISTS
	  	 (SELECT Y.BF_SSN
		  FROM OLWHRM1.AY10_BR_LON_ATY Y
		  WHERE Y.BF_SSN = LN10.BF_SSN
		  AND Y.PF_REQ_ACT = 'XCNWD'
		  AND Y.LD_ATY_RSP >= &MNS45
		  AND PF_RSP_ACT NOT IN ('CNTCT','COMPL')
		  AND NOT EXISTS
		  		(SELECT *
				 FROM OLWHRM1.AY10_BR_LON_ATY Z
				 WHERE Z.BF_SSN = Y.BF_SSN
				 AND Z.PF_REQ_ACT = 'XCNWD'
				 AND Z.LD_ATY_RSP >= &MNS8)
		  GROUP BY Y.BF_SSN
		  HAVING COUNT(*) <= 4)
		)
/*NO OPEN BANKRUPTCY */
AND NOT EXISTS
	(SELECT * 
	 FROM OLWHRM1.PD24_PRS_BKR V
	 WHERE V.DF_PRS_ID = LN10.BF_SSN
	 AND V.DC_BKR_STA = '06')

/*NO TASK IN X1 X2 X3 QUEUE FOR BORROWER*/
AND LN10.BF_SSN NOT IN 
	(SELECT DISTINCT BF_SSN
	 FROM OLWHRM1.WQ20_TSK_QUE
	 WHERE WF_QUE IN ('X1','X2','X3'))

/*NO CONTACT FOR NEW DISCLOSURE WITHIN PAST 45 DAYS - ONLY USE ARC WITH DATE > DISC PRNT DATE*/
AND LN10.BF_SSN NOT IN 
	(SELECT DISTINCT A.BF_SSN
	 FROM OLWHRM1.RS10_BR_RPD A
	 INNER JOIN OLWHRM1.AY10_BR_LON_ATY B
	 	ON B.BF_SSN = A.BF_SSN
		AND B.LD_REQ_RSP_ATY_PRF > A.LD_SNT_RPD_DIS
	 WHERE B.PF_REQ_ACT = 'XCNWD'
	 AND B.PF_RSP_ACT = 'CNTCT'
	 AND B.LC_STA_ACTY10 = 'A'
	 AND B.LD_ATY_RSP >= &MNS45 )

/*NO P200C ACTION CODE EXISTS WITH A GREATER RESPONSE DATE THAN A XCNWD ACTION CODE*/
AND NOT EXISTS
	(SELECT *
	 FROM OLWHRM1.AY10_BR_LON_ATY K
	 WHERE K.BF_SSN = LN10.BF_SSN
	 AND K.PF_REQ_ACT = 'P200C'
	 AND K.LD_ATY_RSP >=
	 		(SELECT MAX(J.LD_ATY_RSP)
			 FROM OLWHRM1.AY10_BR_LON_ATY J
			 WHERE J.BF_SSN = K.BF_SSN
			 AND J.PF_REQ_ACT = 'XCNWD'))
FOR READ ONLY WITH UR
);
 
DISCONNECT FROM DB2;

/*GET EXISTING SSNS IN X3 QUEUE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE X3Q AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	 WQ20.BF_SSN AS SSN
	,MAX(AY10.LD_REQ_RSP_ATY_PRF) AS LD_REQ_RSP_ATY_PRF
	,'X' AS WQ20_IND
FROM OLWHRM1.WQ20_TSK_QUE WQ20 
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON WQ20.BF_SSN = AY10.BF_SSN 
	AND AY10.PF_REQ_ACT = 'XCNWD'
	AND AY10.LC_STA_ACTY10 = 'A'
WHERE WQ20.WF_QUE = 'X3'
GROUP BY WQ20.BF_SSN
FOR READ ONLY WITH UR
);

CREATE TABLE WQ10 AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT WN_DAY_PRD_GOA_WRK
	,'Y' AS X3_IND
FROM OLWHRM1.WQ10_TSK_QUE_DFN
WHERE WF_QUE = 'X3'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

DATA NDISCC;
SET NDISCC;
X3_IND = 'Y';
RUN;

DATA NDISCC (DROP=X3_IND);
MERGE NDISCC WQ10;
BY X3_IND;
RUN;

/*CREATE MASTER NDISCC DATA SET*/
DATA NDISCC;
SET NDISCC X3Q;
RUN;

PROC SORT DATA=NDISCC NODUPKEY;BY SSN;RUN;

/*GET DEMOGRAPHIC INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO_INFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	LN10.BF_SSN AS SSN
	,CASE 
		WHEN PD10.DM_PRS_MID = ' '
		THEN RTRIM(PD10.DM_PRS_1)||' '||PD10.DM_PRS_LST
		WHEN PD10.DM_PRS_MID <> ' '
		THEN RTRIM(PD10.DM_PRS_1)||' '||RTRIM(PD10.DM_PRS_MID)||' '||PD10.DM_PRS_LST
	 END AS NAME
	,SUBSTR(PD10.DM_PRS_LST,1,2) AS NAME_SORT
	,PD10.DD_BRT
/*ADDRESS INFO*/
	,PD30.DX_STR_ADR_1 	 
	,PD30.DX_STR_ADR_2 	 
	,PD30.DX_STR_ADR_3 	 
	,PD30.DM_CT
	,PD30.DC_DOM_ST
	,PD30.DF_ZIP_CDE
	,PD30.DM_FGN_CNY
	,PD30.DM_FGN_ST
	,PD30.DI_VLD_ADR
/*	PHONE INFO*/
	,PD42.DC_PHN
	,PD42.DN_DOM_PHN_ARA||PD42.DN_DOM_PHN_XCH||PD42.DN_DOM_PHN_LCL AS HME_PHN
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON LN10.BF_SSN = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
	ON LN10.BF_SSN = PD30.DF_PRS_ID
	AND PD30.DC_ADR = 'L'
INNER JOIN OLWHRM1.PD42_PRS_PHN PD42
	ON LN10.BF_SSN = PD42.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = LN10.BF_SSN
	AND DW01.LN_SEQ = LN10.LN_SEQ
WHERE LN10.LC_STA_LON10 = 'R'
AND LN10.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06') /*LOAN NOT IN DEFERMENT, FORBEARANCE, OR CURE STATUS*/
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,DEMO_INFO,SSN);

/*GET LOAN COUNT AND TOTAL INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LN10_INFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN AS SSN
	,COUNT(*) AS COUNT
	,SUM(A.LA_CUR_PRI) AS LA_CUR_PRI
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
	AND DW01.LN_SEQ = A.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
AND A.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06') 
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,LN10_INFO,SSN);

/*GET AMOUNT DUE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AMTDU AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT X.BF_SSN AS SSN
	,SUM(COALESCE(X.LA_BIL_CUR_DU,0) +
		 COALESCE(X.LA_BIL_PAS_DU,0) +
		 COALESCE(X.LA_LTE_FEE_ASS,0)) AS AMTDU
FROM OLWHRM1.LN80_LON_BIL_CRF X
INNER JOIN OLWHRM1.LN10_LON Y
	ON X.BF_SSN = Y.BF_SSN
	AND X.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND X.LC_STA_LON80 = 'A'
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06')
AND X.LD_BIL_CRT = 
		(SELECT MAX(Y.LD_BIL_CRT) 
		 FROM OLWHRM1.BL10_BR_BIL Y
		 WHERE Y.BF_SSN = X.BF_SSN
		 AND Y.LC_STA_BIL10 = 'A'
		 AND Y.LC_BIL_TYP = 'P')
GROUP BY X.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,AMTDU,SSN);

/*GET MAX EFF DATE FOR LN90 INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TRX_DT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN90.BF_SSN AS SSN
	,MAX(LN90.LD_FAT_EFF) AS MX_EFFDT
FROM OLWHRM1.LN10_LON Y
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
	ON Y.BF_SSN = LN90.BF_SSN
	AND Y.LN_SEQ = LN90.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06')
AND LN90.PC_FAT_TYP = '10'
AND LN90.PC_FAT_SUB_TYP IN ('10','11','12')
AND LN90.LC_FAT_REV_REA = ' ' 
AND LN90.LC_STA_LON90 = 'A'
GROUP BY LN90.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,TRX_DT,SSN);

/*GET DAYS DELINQUENT*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DLQ_DYS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN16.BF_SSN AS SSN 
	,MAX(LN16.LN_DLQ_MAX) AS LN_DLQ_MAX
FROM OLWHRM1.LN10_LON Y
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
	ON Y.BF_SSN = LN16.BF_SSN
	AND Y.LN_SEQ = LN16.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06')
AND LN16.LC_STA_LON16 = '1' 
GROUP BY LN16.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,DLQ_DYS,SSN);

/*LAST ATTEMPTED CONTACT DATE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ATY_ATT AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,MAX(AY10.LD_ATY_RSP) AS LD_ATY_RSP_ATT
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON AY10.BF_SSN = A.BF_SSN
/*INNER JOIN OLWHRM1.AC10_ACT_REQ AC10*/
/* 	ON AY10.PF_REQ_ACT = AC10.PF_REQ_ACT*/
WHERE A.LC_STA_LON10 = 'R'
AND D.DC_PHN = 'H'
AND D.DI_PHN_VLD = 'Y'
AND D.DN_DOM_PHN_XCH <> ' '
AND D.DN_DOM_PHN_LCL <> ' '
AND D.DN_PHN_XTN <> ' '
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06') 
AND AY10.PF_RSP_ACT IN ('NOCTC','INVPH')
/*AND AC10.PC_DD_ACT_STA = 'D'*/
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,ATY_ATT,SSN);

/*LAST CONTACT DATE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ATY_CTC AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,MAX(AY10.LD_ATY_RSP) AS LD_ATY_RSP_CTC
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON AY10.BF_SSN = A.BF_SSN
/*INNER JOIN OLWHRM1.AC10_ACT_REQ AC10*/
/* 	ON AY10.PF_REQ_ACT = AC10.PF_REQ_ACT*/
WHERE A.LC_STA_LON10 = 'R'
AND D.DC_PHN = 'H'
AND D.DI_PHN_VLD = 'Y'
AND D.DN_DOM_PHN_XCH <> ' '
AND D.DN_DOM_PHN_LCL <> ' '
AND D.DN_PHN_XTN <> ' '
AND DW01.WC_DW_LON_STA NOT IN ('04','05','06') 
AND AY10.PF_RSP_ACT = 'CNTCT'
/*AND AC10.PC_DD_ACT_STA = 'D'*/
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%NDSCL(NDISCC,ATY_CTC,SSN);

%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>>  ;  ** includes error messages to SAS log  ;

%macro sqlcheck (sqlrpt= )  ;
  %if  &sqlxrc ne 0  %then  %do  ;
        FILENAME REPORTZ  "/SAS/SBLOOMFI/&SQLRPT";
		*FILENAME REPORTZ  "&RPTLIB/&SQLRPT";
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS log in &sqlrpt should be reviewed.          **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
%sqlcheck (sqlrpt=ULWS13.LWS13RZ)  ;


/*ENDRSUBMIT;*/
/*DATA NDISCC;*/
/*SET WORKLOCL.NDISCC;*/
/*RUN;*/

/*BREAK OUT AND REASSEMBLE PHONE INFO*/
%MACRO PHN(DSET,PHNVAL);
DATA PHN_&DSET (KEEP=SSN DC_PHN_&DSET PHN_&DSET);
SET NDISCC;
WHERE DC_PHN = &PHNVAL;
DC_PHN_&DSET = DC_PHN;
PHN_&DSET = HME_PHN;
RUN;
PROC SORT DATA = PHN_&DSET;
BY SSN;
RUN;
%MEND;
%PHN(H,'H');
%PHN(A,'A');
%PHN(W,'W');
DATA PHN;
MERGE PHN_H PHN_A PHN_W;
BY SSN;
RUN;

DATA NDISCC (DROP=DC_PHN HME_PHN);
SET NDISCC;
RUN;

PROC SORT DATA=PHN;BY SSN;RUN;
PROC SORT DATA=NDISCC NODUPRECS;BY SSN;RUN;

DATA NDISCC;
MERGE NDISCC PHN;
BY SSN;
RUN;

/*VARIABLE INITIALIZATION*/
DATA NDISCC;
SET NDISCC;
QUEUE = 'X3BS';
REGION = 'N';
LENDER_CODE = '        ';
CUR_SCL_CODE = '                    ';
PHN_IND = 'D';
REL = 'BORROWER';
REL_CODE = 'B';
GEO_PHN_IND = 'D';
BI_ATY_3_PTY = ' ';
FILLER = '                                                                                                                  ';
RUN;

/*CALCULATE DAYS LEFT TO COMPLETE*/
DATA NDISCC;
SET NDISCC;
IF WN_DAY_PRD_GOA_WRK NE . THEN DTC = WN_DAY_PRD_GOA_WRK;
ELSE IF LD_REQ_RSP_ATY_PRF NE . THEN DO;
	IF INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF) =< 0
		THEN DTC = 0;
	ELSE IF INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF) > 0
		THEN DTC = INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF);
	END;
RUN;

*WRITE CURRENT RUN DATE FOR THIS JOB TO CYPRUS DATASET;
DATA NDISCC;
SET NDISCC;
RUNDT = DATETIME();
FORMAT RUNDT DATETIME.;
RUN;

/*ISOLATE NEWLY SELECTED SSNS FOR QUEUE CONSTRUCTION*/
PROC SQL;
CREATE TABLE NDISCC2 AS 
SELECT DISTINCT SSN
FROM NDISCC
WHERE WQ20_IND NE 'X';
QUIT;
RUN;

/*ONLY UNCOMMENT THIS IF YOU WANT TO OVERWRITE THE CYPRUS DATA SET FROM A LOCAL RUN!*/
/***********************************************************************************/
/*=======================*/
/*DATA WORKLOCL.NDISCC;*/
/*SET NDISCC;*/
/*RUN;*/
/*RSUBMIT;*/
/*=======================*/

/*THIS MUST BE UNCOMMENTED FOR PRODUCTION*/
LIBNAME OLRPLD V8 '/uheaa/whse/olrp_lookup_directory'; /*CYPRUS DATA SET*/
DATA OLRPLD.ULWS13_LWS13R3;
SET NDISCC;
RUN;

/*=======================*/
/*ENDRSUBMIT;*/
/*=======================*/
/***********************************************************************************/

/*FILE TO BUILD QUEUE*/
DATA _NULL_;
SET  WORK.NDISCC2;  
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT SSN $9. ;
PUT SSN $ ;
RUN;

/*PROC EXPORT DATA=WORK.NDISCC*/
/*            OUTFILE= "C:\WINDOWS\TEMP\ULWS13_LWS13R3.XLS" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/


