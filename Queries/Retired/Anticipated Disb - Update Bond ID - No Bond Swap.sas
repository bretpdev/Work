*--------------------------------------------------*
| ANTICIPATED DISB - UPDATE BOND ID - NO BOND SWAP |
*--------------------------------------------------*;
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/Anticipated Disb - Bond Swap.txt";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%LET SCHL_LIST = '00367403','00367401';
%LET BOND_ID = '0000STNF';
%LET DISB_BEG = '12/07/2009';
%LET DISB_END = '12/07/2009';
/*INPUT LOAN TYPES FOR PRIVATE AND FFEL LOANS*/
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "/sas/whse/progrevw/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
/*CREATE MACRO VARIALBE LISTS OF LOAN PROGRAMS(FFEL AND PRIVATE LOANS)*/
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY "," /*FFEL LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';
QUIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ADUBINBS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT D.DF_SPE_ACC_ID
	,A.AF_APL_ID
	,C.LN_SEQ
	,B.AF_LDR_BND_ISS
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.LN10_LON C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON C.BF_SSN = D.DF_PRS_ID
WHERE A.LC_DSB_TYP = '1'
	AND A.LC_STA_LON15 IN ('1','3')
	AND A.LA_DSB != COALESCE(LA_DSB_CAN,0)
	AND A.LD_DSB_ROS_PRT IS NULL
	AND A.IC_LON_PGM IN (&FFELP_LIST)
	AND DAYS(A.LD_DSB) BETWEEN DAYS(&DISB_BEG) AND DAYS(&DISB_END)
	AND B.AF_DOE_SCL IN (&SCHL_LIST)
	AND B.AF_CNL NOT LIKE '%REALLO%'
	AND B.AF_LDR_BND_ISS = &BOND_ID
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA ADUBINBS  ;
	SET WORKLOCL.ADUBINBS ;
RUN;
PROC SORT DATA=ADUBINBS NODUPKEY;
	BY DF_SPE_ACC_ID LN_SEQ;
RUN;
DATA _NULL_;
SET  WORK.ADUBINBS;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
PUT
	"DF_SPE_ACC_ID"
	','
	"AF_APL_ID"
	','
	"LN_SEQ"
	','
	"IF_BND_ISS";
END;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT AF_APL_ID $9. ;
	FORMAT LN_SEQ 6. ;
	FORMAT AF_LDR_BND_ISS $8. ;
DO;
	PUT DF_SPE_ACC_ID $ @;
	PUT AF_APL_ID $ @;
	PUT LN_SEQ @;
	PUT AF_LDR_BND_ISS $ ;
END;
RUN;

