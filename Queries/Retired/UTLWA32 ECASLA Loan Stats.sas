*---------------------------*
| UTLWA32 ECASLA Loan Stats |
*---------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS\Output;
FILENAME REPORT2 "&RPTLIB/ULWA32.LWA32R2";
FILENAME REPORT3 "&RPTLIB/ULWA32.LWA32R3";
FILENAME REPORT4 "&RPTLIB/ULWA32.LWA32R4";
FILENAME REPORT5 "&RPTLIB/ULWA32.LWA32R5";
FILENAME REPORT6 "&RPTLIB/ULWA32.LWA32R6";
FILENAME REPORT7 "&RPTLIB/ULWA32.LWA32R7";
FILENAME REPORT8 "&RPTLIB/ULWA32.LWA32R8";
FILENAME REPORT9 "&RPTLIB/ULWA32.LWA32R9";
FILENAME REPORTZ "&RPTLIB/ULWA32.LWA32RZ";
DATA OWN_DAT;
INFILE CARDS DLM=',' DSD MISSOVER;
INFORMAT OWNER $10. IR $3.;
INPUT OWNER $ IR;
CARDS; 
LENDER,01A
LENDER,01B
LENDER,01C
LENDER,02A
LENDER,03A
LENDER,04A
LENDER,04B
LENDER,05A
UHEAA,01A
UHEAA,01B
UHEAA,01C
UHEAA,02A
UHEAA,03A
UHEAA,04A
UHEAA,04B
UHEAA,05A
RUN;
PROC SORT DATA=OWN_DAT OUT=REP_CAT(KEEP=OWNER) NODUPKEY;
	BY OWNER;
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
SELECT "'"||TRIM(LENDER_ID)||"'"
	INTO :UHEAA_LIST SEPARATED BY ","
FROM SAS_TAB.LDR_AFF
WHERE AFFILIATION = 'UHEAA'
;
QUIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DISB_BTWN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT B.BF_SSN
	,B.LN_SEQ
	,B.LN_BR_DSB_SEQ
	,B.LD_DSB 
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE A.LC_STA_LON15 IN ('1','3')
	AND A.LC_DSB_TYP IN ('1','2')
	AND A.LA_DSB != COALESCE(A.LA_DSB_CAN,0) 
	AND A.LD_DSB BETWEEN '07/01/2008' AND '06/30/2010'
	AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS','PLUSGB')
	AND B.LC_STA_LON15 IN ('1','3')
	AND B.LC_DSB_TYP IN ('1','2')
	AND B.LA_DSB != COALESCE(B.LA_DSB_CAN,0) 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWA32.LWA32RZ);*/
/*QUIT;*/
PROC SORT DATA=DISB_BTWN NODUPKEY;
	BY BF_SSN LN_SEQ LD_DSB LN_BR_DSB_SEQ;
RUN;
DATA DISB_BTWN (DROP=LD_DSB LN_BR_DSB_SEQ);
	SET DISB_BTWN;
	BY BF_SSN LN_SEQ;
	IF FIRST.LN_SEQ THEN DO;
		IS_SEL_LOAN = 1;
		IF MDY(7,1,2008) <= LD_DSB <= MDY(6,30,2010) THEN
			OUTPUT DISB_BTWN;
		ELSE 
			DELETE;
	END;
RUN;
PROC SORT DATA=DISB_BTWN OUT=UBORS(KEEP=BF_SSN) NODUPKEY ;
	BY BF_SSN;
RUN;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ELSA AS
SELECT DISTINCT  B.*
	,COALESCE(C.IS_SEL_LOAN,0) AS IS_SEL_LOAN
FROM DISB_BTWN A
INNER JOIN CONNECTION TO DB2 (
	SELECT A.BF_SSN	
		,A.LN_SEQ 
		,A.IC_LON_PGM
		,A.LF_LON_CUR_OWN
		,C.WC_DW_LON_STA
		,COALESCE(A.LA_CUR_PRI,0) AS LA_CUR_PRI
		,COALESCE(C.LA_NSI_OTS,0) AS LA_NSI_OTS
		,COALESCE(A.LA_LTE_FEE_OTS,0) AS LA_LTE_FEE_OTS
		,D.LD_DLQ_OCC 
		,E.LF_ECA_PGM_YR
		,B.LN_BR_DSB_SEQ
		,B.LD_DSB 
		,B.LC_STA_LON15
		,B.LC_DSB_TYP 
		,B.LA_DSB 
		,COALESCE(B.LA_DSB_CAN,0) AS LA_DSB_CAN
		,F.DF_SPE_ACC_ID
	FROM OLWHRM1.LN10_LON A
	INNER JOIN OLWHRM1.LN15_DSB B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C
		ON A.BF_SSN = C.BF_SSN
		AND A.LN_SEQ = C.LN_SEQ
	LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST D
		ON A.BF_SSN = D.BF_SSN
		AND A.LN_SEQ = D.LN_SEQ
		AND D.LC_STA_LON16 = '1'
	LEFT OUTER JOIN OLWHRM1.LNEC_LON_ECA E
		ON A.BF_SSN = E.BF_SSN
		AND A.LN_SEQ = E.LN_SEQ
		AND E.LN_ECA_SEQ = 1 
	INNER JOIN OLWHRM1.PD10_PRS_NME F
		ON A.BF_SSN = F.DF_PRS_ID
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN
LEFT OUTER JOIN DISB_BTWN C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
;

CREATE TABLE LN40 AS
SELECT DISTINCT B.*
FROM DISB_BTWN A
INNER JOIN CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,1 AS HAS_03
	,COALESCE((E.LA_TOT_ITL_CLM_PD - E.LA_PRI_COL) + 
		(E.LA_INT_ACR- E.LA_INT_COL) +  
		(E.LA_LEG_CST_ACR - E.LA_LEG_CST_COL) + 
		(E.LA_OTH_CHR_ACR - E.LA_OTH_CHR_COL) + 
		(E.LA_COL_CST_ACR - E.LA_COL_CST_COL),0) AS OL_AMT
FROM OLWHRM1.LN40_LON_CLM_PCL A
INNER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,MAX(LN_SEQ_CLM_PCL) AS LN_SEQ_CLM_PCL
	FROM OLWHRM1.LN40_LON_CLM_PCL 
	GROUP BY BF_SSN
		,LN_SEQ
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND A.LN_SEQ_CLM_PCL = B.LN_SEQ_CLM_PCL
INNER JOIN OLWHRM1.LN10_LON C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
LEFT OUTER JOIN (
	SELECT AF_APL_ID
		,AF_APL_ID_SFX
		,MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
	FROM OLWHRM1.DC01_LON_CLM_INF
	GROUP BY AF_APL_ID
		,AF_APL_ID_SFX
	) D
	ON C.LF_LON_ALT = D.AF_APL_ID
	AND C.LN_LON_ALT_SEQ = INT(D.AF_APL_ID_SFX)
LEFT OUTER JOIN OLWHRM1.DC01_LON_CLM_INF E
	ON D.AF_APL_ID = E.AF_APL_ID
	AND D.AF_APL_ID_SFX = E.AF_APL_ID_SFX
	AND D.LF_CRT_DTS_DC10 = E.LF_CRT_DTS_DC10
WHERE A.LC_REA_CLP_LON = '03'
FOR READ ONLY WITH UR
) B
ON A.BF_SSN = B.BF_SSN
AND A.LN_SEQ = B.LN_SEQ
;
DISCONNECT FROM DB2;

CREATE TABLE ELS AS
SELECT DISTINCT A.*
	,COALESCE(B.HAS_03,0) AS HAS_03
	,COALESCE(B.OL_AMT,0) AS OL_AMT
FROM ELSA A
LEFT OUTER JOIN LN40 B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ

;
QUIT;
PROC SORT DATA=ELS;
	BY BF_SSN LN_SEQ LN_BR_DSB_SEQ;
RUN;
%SYSRPUT UHEAA_LIST = &UHEAA_LIST;
ENDRSUBMIT;
DATA ELS;
	SET WORKLOCL.ELS;
RUN;
/*CALCULATE VARIABLES NEEDED FOR PROCESSING*/
PROC SQL;
CREATE TABLE ELS2 AS 
SELECT DISTINCT A.BF_SSN	
	,A.LN_SEQ 
	,A.DF_SPE_ACC_ID
	,A.IC_LON_PGM
	,A.LF_LON_CUR_OWN
	,A.WC_DW_LON_STA
	,A.LA_CUR_PRI + A.LA_NSI_OTS + A.LA_LTE_FEE_OTS AS LN_BAL
	,A.LD_DLQ_OCC
	,TODAY() - 255 AS DLQ_CUT_OFF FORMAT=MMDDYY10. 
	,A.LF_ECA_PGM_YR
	,A.IS_SEL_LOAN
	,A.HAS_03
	,A.OL_AMT
	,ACT.ACT_MAX_DD	FORMAT=MMDDYY10. 
	,ACT.ACT_MIN_DD	FORMAT=MMDDYY10. 
	,ACT.ACT_DIS_AMT
	,ANT.ANT_MAX_DD	FORMAT=MMDDYY10. 
	,ANT.ANT_MIN_DD	FORMAT=MMDDYY10. 
	,ANT.ANT_DIS_AMT
	,COALESCE(S.IS_SPLIT,0) AS IS_SPLIT
FROM ELS A
LEFT OUTER JOIN (
		SELECT BF_SSN
		,LN_SEQ
		,MAX(LD_DSB) AS ACT_MAX_DD
		,MIN(LD_DSB) AS ACT_MIN_DD
		,SUM(LA_DSB-LA_DSB_CAN) AS ACT_DIS_AMT
	FROM ELS
	WHERE LC_STA_LON15 IN ('1','3')
		AND LC_DSB_TYP = '2'
		AND LA_DSB ^= LA_DSB_CAN
	GROUP BY BF_SSN
		,LN_SEQ
		,LC_DSB_TYP
	) ACT
	ON A.BF_SSN = ACT.BF_SSN
	AND A.LN_SEQ = ACT.LN_SEQ
LEFT OUTER JOIN (
		SELECT BF_SSN
		,LN_SEQ
		,MAX(LD_DSB) AS ANT_MAX_DD
		,MIN(LD_DSB) AS ANT_MIN_DD
		,SUM(LA_DSB-LA_DSB_CAN) AS ANT_DIS_AMT
	FROM ELS
	WHERE LC_STA_LON15 IN ('1','3')
		AND LC_DSB_TYP = '1'
		AND LA_DSB ^= LA_DSB_CAN
	GROUP BY BF_SSN
		,LN_SEQ
		,LC_DSB_TYP
	) ANT
	ON A.BF_SSN = ANT.BF_SSN
	AND A.LN_SEQ = ANT.LN_SEQ
LEFT OUTER JOIN (
	SELECT BF_SSN
		,1 AS IS_SPLIT
	FROM ELS
	WHERE IS_SEL_LOAN ^= 1
		AND LA_CUR_PRI + LA_NSI_OTS + LA_LTE_FEE_OTS > 0
	) S
	ON A.BF_SSN = S.BF_SSN
WHERE IS_SEL_LOAN = 1
;
QUIT;
PROC DATASETS;
	DELETE ELS;
	CHANGE ELS2=ELS;
QUIT;
/*CREATE DATASETS FOR R2 R3 R6 R7*/
%MACRO ECAS_DS(EDS,NOT_EDS,CSTRG);
DATA &EDS &NOT_EDS;
	SET ELS;
	LENGTH HELD $6.;
	IF LF_LON_CUR_OWN IN (&UHEAA_LIST) THEN HELD = 'UHEAA';
		ELSE HELD = 'LENDER';
	IF &CSTRG THEN OUTPUT &EDS;
		ELSE OUTPUT &NOT_EDS;
RUN;
%MEND;
%ECAS_DS(E1,NOT_E1,%STR(
	MDY(7,1,2008) <= ACT_MIN_DD <= MDY(7,1,2009)
		AND MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) <= MDY(9,30,2009) 
		AND WC_DW_LON_STA NOT IN ('06','07','08','10','11','12','16','17','18','19','20','21','22') 
		AND LN_BAL > 0 
		AND COALESCE(LD_DLQ_OCC,TODAY()) > DLQ_CUT_OFF)
		); 
%ECAS_DS(E2,NOT_E2,%STR(
	MDY(5,1,2009) <= ACT_MIN_DD <= MDY(6,30,2010)
		AND MDY(7,1,2009) < MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) <= MDY(9,30,2010) 
		AND WC_DW_LON_STA NOT IN ('06','07','08','10','11','12','16','17','18','19','20','21','22') 
		AND LN_BAL > 0 
		AND COALESCE(LD_DLQ_OCC,TODAY()) > DLQ_CUT_OFF)
		);
%MACRO PROC_NOTDS(DS,CSTR1,CSTR2,ECA_YR,OECA_YR);
DATA &DS;
	SET &DS;
	LENGTH IR $3. IR_DESC $50.;
	IF &CSTR1 THEN DO;
			IF WC_DW_LON_STA IN ('20','21','16','17','18','19','07','08','10','11','06','22') 
				OR (WC_DW_LON_STA = '12' AND HAS_03 ^= 1) THEN DO;
					IR = '01A';
					IR_DESC = WC_DW_LON_STA;
			END;
			ELSE IF WC_DW_LON_STA = '12' AND HAS_03 = 1 THEN DO;
				IR = '01B';
				IR_DESC = IR;
				LN_BAL = OL_AMT;
			END;
			ELSE DO;
				IR = '01C';
				IR_DESC = IR;
			END;
	END;
	ELSE IF &CSTR2 THEN DO;
		IR = '02A';
		IR_DESC = IR;
	END;
	ELSE IF COALESCE(LD_DLQ_OCC,TODAY()) < DLQ_CUT_OFF THEN DO;
		IR = '03A';
		IR_DESC = IR;
	END;
	ELSE IF LF_ECA_PGM_YR ^= "&ECA_YR" THEN DO;
		IF LF_ECA_PGM_YR = '' THEN DO;
			IR = '04A';
			IR_DESC = IR;
		END;
		ELSE IF LF_ECA_PGM_YR = "&OECA_YR" THEN DO;
			IR = '04B';
			IR_DESC = IR;
		END;
	END;
	ELSE DO;
		IR = '05A'; 
		IR_DESC = IR;
	END;
RUN;
%MEND PROC_NOTDS;
%PROC_NOTDS(NOT_E1,%STR(MDY(7,1,2008) <= ACT_MIN_DD <= MDY(6,30,2009) AND 
	MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) <= MDY(9,30,2009)),
	%STR(MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) > MDY(9,30,2009)),0809,0910);
%PROC_NOTDS(NOT_E2,%STR(MDY(5,1,2009) <= ACT_MIN_DD <= MDY(6,30,2010) AND 
	MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) <= MDY(9,30,2010)),
	%STR(MAX(ACT_MIN_DD,ACT_MAX_DD,ANT_MIN_DD,ANT_MAX_DD) > MDY(9,30,2010)),0910,0809);
%MACRO EDSC(FDS,RNO,ENUM,TDS); /*MACRO FOR REPORTS 2 AND 4*/
PROC SQL;
CREATE TABLE RDSE AS
SELECT DISTINCT A.OWNER
	,COALESCE(B.ANT_BORS,0) 	AS ANT_BORS
	,COALESCE(B.ANT_LOANS,0) 	AS ANT_LOANS
	,COALESCE(B.ANT_AMT,0) 		AS ANT_AMT
	,COALESCE(C.ACT_BORS,0)		AS ACT_BORS
	,COALESCE(C.ACT_LOANS,0) 	AS ACT_LOANS
	,COALESCE(C.ACT_AMT,0) 		AS ACT_AMT
FROM REP_CAT A
LEFT OUTER JOIN (
	SELECT HELD
		,COUNT(DISTINCT BF_SSN) AS ANT_BORS
		,COUNT(*) 				AS ANT_LOANS
		,SUM(ANT_DIS_AMT) 		AS ANT_AMT
	FROM &FDS 
	WHERE COALESCE(ANT_DIS_AMT,0) > 0
	GROUP BY HELD
	) B
	ON A.OWNER = B.HELD
LEFT OUTER JOIN (
	SELECT HELD
		,COUNT(DISTINCT BF_SSN) AS ACT_BORS
		,COUNT(*) 				AS ACT_LOANS
		,SUM(ACT_DIS_AMT) 		AS ACT_AMT
	FROM &FDS 
	WHERE COALESCE(ACT_DIS_AMT,0) > 0
	GROUP BY HELD
	) C
	ON A.OWNER = C.HELD
;
QUIT;
PROC SQL NOPRINT;
	SELECT PUT(COUNT(DISTINCT BF_SSN),COMMA9.) 
	INTO: SPLT_BORS
	FROM &FDS 
	WHERE IS_SPLIT = 1
	;
QUIT;
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE "ECASLA &ENUM PUT ELIGIBLE";
FOOTNOTE   "JOB = UTLWA32  	 REPORT = ULWA32.LWA32R&RNO";
PROC REPORT DATA=RDSE NOWD HEADSKIP SPLIT= '/';
	COLUMNS OWNER ANT_BORS ANT_LOANS ANT_AMT ACT_BORS ACT_LOANS ACT_AMT;
	DEFINE OWNER / 'OWNER';
	DEFINE ANT_BORS / 'ANTICIPATED/DISBURSEMENTS/# OF BORROWERS' WIDTH=14 FORMAT=COMMA6.;
	DEFINE ANT_LOANS / 'ANTICIPATED/DISBURSEMENTS/# OF LOANS' WIDTH=13 FORMAT=COMMA6.;
	DEFINE ANT_AMT / 'ANTICIPATED/DISBURSEMENTS/$ TOTAL' WIDTH=15 FORMAT=DOLLAR15.2;
	DEFINE ACT_BORS / 'ACTUAL/DISBURSEMENTS/# OF BORROWERS' WIDTH=14 FORMAT=COMMA6.;
	DEFINE ACT_LOANS / 'ACTUAL/DISBURSEMENTS/# OF LOANS' WIDTH=13 FORMAT=COMMA6.;
	DEFINE ACT_AMT / 'ACTUAL/DISBURSEMENTS/$ TOTAL' WIDTH=15 FORMAT=DOLLAR15.2;
	COMPUTE AFTER;
		LINE '';
		LINE '';
		LINE @11 "SPLIT BORROWERS: &SPLT_BORS";
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE FOR REPORT*/
DATA _NULL_;
	SET  &FDS;
	IF IS_SPLIT THEN 
		SPLIT = 'Y';
	ELSE
		SPLIT = 'N';
	FILE REPORT&TDS DELIMITER=',' DSD DROPOVER LRECL=32767;
	    FORMAT LN_SEQ 6. ;
	    FORMAT DF_SPE_ACC_ID $10. ;
	    FORMAT LF_LON_CUR_OWN $8. ;
	    FORMAT WC_DW_LON_STA $2. ;
	    FORMAT LN_BAL BEST12. ;
	    FORMAT ACT_DIS_AMT BEST12. ;
	    FORMAT ANT_DIS_AMT BEST12. ;
	    FORMAT SPLIT $1. ;
	IF _N_ = 1 THEN DO;
	   PUT "ACCOUNT NUMBER,LOAN SEQ,LOAN STATUS,ANTICIPATED DISB AMT,ACTUAL DISB AMT,LENDER,LOAN BALANCE,SPLIT";
	END;
	DO;
	    PUT DF_SPE_ACC_ID $ @;
	    PUT LN_SEQ @;
	    PUT WC_DW_LON_STA $ @;
		PUT ANT_DIS_AMT @;
	    PUT ACT_DIS_AMT @;
		PUT LF_LON_CUR_OWN $ @;
	    PUT LN_BAL @;
	    PUT SPLIT $;
	END;
RUN;


%MEND EDSC;
%EDSC(E1,2,1,6);
%EDSC(E2,4,2,8);
%MACRO IDSC(FDS,RNO,ENUM,FNUM,TDS); /*MACRO FOR REPORTS 3 AND 5*/
PROC SQL;
CREATE TABLE RDSI AS
SELECT DISTINCT A.OWNER
	,A.IR
	,B.IR_DESC 					AS ANT_DESC
	,COALESCE(B.ANT_BORS,0) 	AS ANT_BORS
	,COALESCE(B.ANT_LOANS,0) 	AS ANT_LOANS
	,COALESCE(B.ANT_AMT,0) 		AS ANT_AMT
	,C.IR_DESC 					AS ACT_DESC
	,COALESCE(C.ACT_BORS,0) 	AS ACT_BORS
	,COALESCE(C.ACT_LOANS,0) 	AS ACT_LOANS
	,COALESCE(C.ACT_AMT,0) 		AS ACT_AMT
	,COALESCE(C.AMOUNT,0) 		AS AMOUNT
FROM OWN_DAT A
LEFT OUTER JOIN (
	SELECT HELD
		,IR
		,IR_DESC
		,COUNT(DISTINCT BF_SSN) AS ACT_BORS
		,COUNT(*) 				AS ACT_LOANS
		,SUM(ACT_DIS_AMT) 		AS ACT_AMT
		,SUM(LN_BAL)			AS AMOUNT
	FROM &FDS 
	WHERE COALESCE(ACT_DIS_AMT,0) > 0
	GROUP BY HELD
		,IR
		,IR_DESC
	) C
	ON A.OWNER = C.HELD
	AND A.IR = C.IR
LEFT OUTER JOIN (
	SELECT HELD
		,IR
		,IR_DESC
		,COUNT(DISTINCT BF_SSN) AS ANT_BORS
		,COUNT(*) 				AS ANT_LOANS
		,SUM(ANT_DIS_AMT) 		AS ANT_AMT
	FROM &FDS 
	WHERE COALESCE(ANT_DIS_AMT,0) > 0
	GROUP BY HELD
		,IR
		,IR_DESC
	) B
	ON A.OWNER = B.HELD
	AND A.IR = B.IR
;
QUIT;
DATA RDSI (DROP=ANT_DESC ACT_DESC);
	SET RDSI;
	IF ANT_DESC ^= '' AND ACT_DESC ^= '' THEN DO;
		IF ANT_DESC ^= ACT_DESC THEN 
			DELETE;
	END;
	ELSE IF ANT_DESC = '' AND ACT_DESC = '' THEN
		DELETE;
	IF ANT_DESC = '' THEN
		DESC = ACT_DESC;
	ELSE 
		DESC = ANT_DESC;
RUN;
PROC FORMAT;
	VALUE $IREA	
		'20' = 'BANKRUPTCY'
		'21' = 'BANKRUPTCY'
		'16' = 'DEATH' 
		'17' = 'DEATH'
		'18' = 'DISABILITY'
		'19' = 'DISABILITY'
		'07' = 'CLAIM'
		'08' = 'CLAIM'
		'10' = 'CLAIM'
		'11' = 'CLAIM'
		'12' = 'CLAIM'
		'06' = 'CURE'
		'22' = 'PIF'
		'01B' = 'BANKRUPTCY – GUARANTOR HELD'
		'01C' = 'OTHER'
		'02A' = 'DISB AFTER 9-30'
		'03A' = 'LOAN DELINQUENCY'
		'04A' = 'PRE-ECASLA'
		'04B' = "ECASLA &FNUM ELIGIBLE"
		'05A' = 'OTHER'
	;
QUIT;
PROC PRINTTO PRINT=REPORT&RNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=132 PAGENO=1 CENTER DATE;
TITLE "ECASLA &ENUM NON-PUT ELIGIBLE";
FOOTNOTE   "JOB = UTLWA32  	 REPORT = ULWA32.LWA32R&RNO";
PROC REPORT DATA=RDSI NOWD HEADSKIP SPLIT= '/' SPACING=1;
	COLUMNS OWNER IR DESC ANT_BORS ANT_LOANS ANT_AMT ACT_BORS ACT_LOANS ACT_AMT AMOUNT AMOUNT_SUM;
	DEFINE OWNER / GROUP 'OWNER';
	DEFINE IR / GROUP NOPRINT;
	DEFINE DESC / GROUP 'INELIGIBLE/CATEGORY' FORMAT=$IREA. WIDTH=27;
	DEFINE ANT_BORS / 'ANTICIPATED/DISBURSEMENTS/# OF BORROWERS' WIDTH=14 FORMAT=COMMA6.;
	DEFINE ANT_LOANS / 'ANTICIPATED/DISBURSEMENTS/# OF LOANS' WIDTH=13 FORMAT=COMMA6.;
	DEFINE ANT_AMT / 'ANTICIPATED/DISBURSEMENTS/$ TOTAL' WIDTH=15 FORMAT=DOLLAR15.2;
	DEFINE ACT_BORS / 'ACTUAL/DISBURSEMENTS/# OF BORROWERS' WIDTH=14 FORMAT=COMMA6.;
	DEFINE ACT_LOANS / 'ACTUAL/DISBURSEMENTS/# OF LOANS' WIDTH=13 FORMAT=COMMA6.;
	DEFINE ACT_AMT / 'ACTUAL/DISBURSEMENTS/$ TOTAL' WIDTH=15 FORMAT=DOLLAR15.2;
	DEFINE AMOUNT / NOPRINT;
	DEFINE AMOUNT_SUM / COMPUTED NOPRINT;
	COMPUTE AMOUNT_SUM;
		AMOUNT_SUM = AMOUNT.SUM;
	ENDCOMP;
	COMPUTE AFTER;
		LINE '';
		LINE '';
		LINE @14 "TOTAL AMOUNT: " AMOUNT_SUM DOLLAR18.2 ;
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE FOR REPORT*/
DATA _NULL_;
	SET  &FDS;
	FILE REPORT&TDS DELIMITER=',' DSD DROPOVER LRECL=32767;
	    FORMAT LN_SEQ 6. ;
	    FORMAT DF_SPE_ACC_ID $10. ;
	    FORMAT LF_LON_CUR_OWN $8. ;
	    FORMAT WC_DW_LON_STA $2. ;
	    FORMAT LN_BAL BEST12. ;
	    FORMAT ACT_DIS_AMT BEST12. ;
	    FORMAT ANT_DIS_AMT BEST12. ;
	    FORMAT IR_DESC $IREA. ;
	IF _N_ = 1 THEN DO;
	   PUT "ACCOUNT NUMBER,LOAN SEQ,LOAN STATUS,ANTICIPATED DISB AMT,ACTUAL DISB AMT,LENDER,LOAN BALANCE,INELIGIBLE REASON";
	END;
	DO;
	    PUT DF_SPE_ACC_ID $ @;
	    PUT LN_SEQ @;
	    PUT WC_DW_LON_STA $ @;
		PUT ANT_DIS_AMT @;
	    PUT ACT_DIS_AMT @;
		PUT LF_LON_CUR_OWN $ @;
	    PUT LN_BAL @;
	    PUT IR_DESC $;
	END;
RUN;
%MEND IDSC;
%IDSC(NOT_E1,3,1,2,7);
%IDSC(NOT_E2,5,2,1,9);
