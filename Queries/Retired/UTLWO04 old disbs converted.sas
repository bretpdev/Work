/*DISBURSEMENTS IN THE PRIOR MONTH CONVERTED TO SERVICING IN CURRENT MONTH*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWO04.LWO04R2";
FILENAME REPORT3 "&RPTLIB/ULWO04.LWO04R3";
FILENAME REPORT4 "&RPTLIB/ULWO04.LWO04R4";
FILENAME REPORT5 "&RPTLIB/ULWO04.LWO04R5";
FILENAME REPORT6 "&RPTLIB/ULWO04.LWO04R6";
FILENAME REPORT7 "&RPTLIB/ULWO04.LWO04R7";
FILENAME REPORT8 "&RPTLIB/ULWO04.LWO04R8";
FILENAME REPORT9 "&RPTLIB/ULWO04.LWO04R9";
FILENAME REPORT10 "&RPTLIB/ULWO04.LWO04R10";
FILENAME REPORT11 "&RPTLIB/ULWO04.LWO04R11";
FILENAME REPORT12 "&RPTLIB/ULWO04.LWO04R12";
FILENAME REPORT13 "&RPTLIB/ULWO04.LWO04R13";
FILENAME REPORT14 "&RPTLIB/ULWO04.LWO04R14";
FILENAME REPORT15 "&RPTLIB/ULWO04.LWO04R15";
FILENAME REPORT16 "&RPTLIB/ULWO04.LWO04R16";
FILENAME REPORT17 "&RPTLIB/ULWO04.LWO04R17";
FILENAME REPORT18 "&RPTLIB/ULWO04.LWO04R18";
FILENAME REPORT19 "&RPTLIB/ULWO04.LWO04R19";
*/

OPTIONS SYMBOLGEN;
*FIND THE NEXT-TO-LAST WORKING DAY OF THE MONTH AND FIRST DAY OF CURRENT MONTH;
DATA _NULL_;
*TODAY()+3 IS USED IN CASE EOM HAPPENS PRIOR TO THE LAST CALENDAR DAY;
NTLWD = INTNX('MONTH',TODAY()+3,-3,'end');
IF WEEKDAY(NTLWD) = 1 THEN NTLWD = NTLWD - 3;
ELSE IF WEEKDAY(NTLWD) = 2 THEN NTLWD = NTLWD - 3;
ELSE IF WEEKDAY(NTLWD) = 3 THEN NTLWD = NTLWD - 1;
ELSE IF WEEKDAY(NTLWD) = 4 THEN NTLWD = NTLWD - 1;
ELSE IF WEEKDAY(NTLWD) = 5 THEN NTLWD = NTLWD - 1;
ELSE IF WEEKDAY(NTLWD) = 6 THEN NTLWD = NTLWD - 1;
ELSE IF WEEKDAY(NTLWD) = 7 THEN NTLWD = NTLWD - 2;
CALL SYMPUT('NTLWD',"'"||PUT(NTLWD,MMDDYY10.)||"'");
FOMDT = INTNX('MONTH',TODAY()+3,-2,'BEGINNING');
CALL SYMPUT('FOMDT',"'"||PUT(FOMDT,MMDDYY10.)||"'");
CALL SYMPUT('CUMO',TRIM(LEFT(UPCASE(PUT(FOMDT,MONNAME9.))))||' '||PUT(YEAR(FOMDT),4.));
RUN;
%SYSLPUT NTLWD = &NTLWD;
%SYSLPUT FOMDT = &FOMDT;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE OLDDSB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS BF_SSN
	,B.AN_SEQ
	,COALESCE(A.LA_DSB,0) - COALESCE(A.LA_DSB_CAN,0) AS DISBAMT
	,D.LA_DSB_FEE AS ORIGFEE

	,A.LD_DSB_ROS_PRT
	,A.LD_DSB
	,A.IC_LON_PGM
	,B.AF_DOE_SCL

    ,C.IF_DOE_LDR
	,E.IM_LDR_FUL

	,H.LD_FAT_PST

FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL B 
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.LN10_LON C
    ON C.BF_SSN = A.BF_SSN
    AND C.LN_SEQ = A.LN_SEQ
    AND C.IC_LON_PGM = A.IC_LON_PGM
INNER JOIN OLWHRM1.LN18_DSB_FEE D
    ON D.BF_SSN = A.BF_SSN
    AND D.LN_BR_DSB_SEQ = A.LN_BR_DSB_SEQ
    AND D.LC_DSB_FEE = '02'	/*GTY FEE*/
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON E.IF_DOE_LDR = C.IF_DOE_LDR

INNER JOIN OLWHRM1.LN93_DSB_FIN_TRX G
	ON G.BF_SSN = A.BF_SSN
	AND G.LN_BR_DSB_SEQ = A.LN_BR_DSB_SEQ
INNER JOIN OLWHRM1.LN90_FIN_ATY H
	ON H.BF_SSN = G.BF_SSN
	AND H.LN_SEQ = G.LN_SEQ
	AND H.LN_FAT_SEQ = G.LN_FAT_SEQ
	AND H.LC_STA_LON90 = 'A'

WHERE A.LC_DSB_RLS_STA = 'R'
AND (A.LA_DSB_CAN IS NULL
    OR A.LA_DSB_CAN <> A.LA_DSB)
AND H.PC_FAT_TYP = '01'
AND H.PC_FAT_SUB_TYP = '01'
AND A.LD_DSB_ROS_PRT <= &NTLWD
AND H.LD_FAT_PST > &FOMDT
AND A.LC_LDR_DSB_MDM NOT IN ('M','F','E')
/*EXCLUDE REVERSALS*/
AND H.LN_FAT_SEQ_REV IS NULL
ORDER BY C.IF_DOE_LDR, A.IC_LON_PGM, A.BF_SSN
);
DISCONNECT FROM DB2;

DATA OLDDSB;
SET OLDDSB;
NETDISB = SUM(DISBAMT, -ORIGFEE);
*Key Bank;
IF IF_DOE_LDR = '813760UT' THEN IF_DOE_LDR = '813760';
*Washington Mutual lender merge;
IF IF_DOE_LDR = '820043' THEN IF_DOE_LDR = '829505';
RUN;

ENDRSUBMIT;
DATA OLDDSB; 
SET WORKLOCL.OLDDSB; 
RUN;

%LET TTL = ;
options orientation=landscape;
options ps =39 ls=132;

%MACRO PRINT(LENDER=,REPNO=);
DATA OLDDSBLDR;
SET OLDDSB;
WHERE IF_DOE_LDR = "&LENDER";
RUN;

DATA _NULL_;
SET OLDDSBLDR;
CALL SYMPUT('TTL',TRIM(IM_LDR_FUL));
RUN;

/*PROC PRINTTO PRINT=REPORT&REPNO;
RUN;*/
OPTIONS CENTER NODATE NUMBER PAGENO=1/*132*/;
PROC CONTENTS DATA=OLDDSBLDR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // /*132*/126*'-';
	PUT      ////////
	   @51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
	   @57 '-- END OF REPORT --';
	PUT /////////////
	   @46 "JOB = UTLWO04     REPORT = ULWO04.LWO04R&REPNO";
END;
RETURN;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "DISBURSEMENTS IN PRIOR MONTH";
TITLE3 "CONVERTED TO SERVICING IN &CUMO";
TITLE4 "LENDER ID # &LENDER";
run;

PROC REPORT DATA=OLDDSBLDR NOWD HEADSKIP SPLIT='/';
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "DISBURSEMENTS IN PRIOR MONTH";
TITLE3 "CONVERTED TO SERVICING IN &CUMO";
TITLE4 "LENDER ID # &LENDER - &TTL";
FOOTNOTE "JOB = UTLWO04     REPORT = ULWO04.LWO04R&REPNO";
COLUMN BF_SSN AN_SEQ DISBAMT ORIGFEE NETDISB LD_DSB_ROS_PRT LD_DSB
IC_LON_PGM AF_DOE_SCL LD_FAT_PST N;
DEFINE BF_SSN / DISPLAY "SSN" FORMAT=SSN11. LEFT;
DEFINE AN_SEQ / DISPLAY "APP SEQ" WIDTH=3;
DEFINE DISBAMT / ANALYSIS FORMAT=COMMA12.2 "GROSS DISB/AMOUNT";
DEFINE ORIGFEE / ANALYSIS FORMAT=COMMA8.2 WIDTH=11 "ORIGINATION/FEE";
DEFINE NETDISB / ANALYSIS FORMAT=COMMA12.2 "NET DISB/AMOUNT";
DEFINE LD_DSB_ROS_PRT / DISPLAY FORMAT=MMDDYY10. "ROSTER/DATE";
DEFINE LD_DSB / DISPLAY FORMAT=MMDDYY10. "DISBURSEMENT/DATE" WIDTH=12;
DEFINE IC_LON_PGM / DISPLAY "LOAN/TYPE" WIDTH=6;
DEFINE AF_DOE_SCL / DISPLAY "SCHOOL/CODE";
DEFINE LD_FAT_PST / DISPLAY FORMAT=MMDDYY10. WIDTH=12 "CONVERTED TO/SERVICING";
DEFINE N / NOPRINT;

RBREAK AFTER / SUMMARIZE SKIP DOL SUPPRESS;
RUN;
%MEND PRINT;

%PRINT(LENDER=811698,REPNO=2);
%PRINT(LENDER=813760,REPNO=3);
%PRINT(LENDER=813894,REPNO=4);
%PRINT(LENDER=817440,REPNO=5);
%PRINT(LENDER=817545,REPNO=6);
%PRINT(LENDER=817546,REPNO=7);
%PRINT(LENDER=819628,REPNO=8);
%PRINT(LENDER=829505,REPNO=9);
%PRINT(LENDER=820200,REPNO=10);
%PRINT(LENDER=822373,REPNO=11);
%PRINT(LENDER=829123,REPNO=12);
%PRINT(LENDER=829158,REPNO=13);
%PRINT(LENDER=830132,REPNO=14);
%PRINT(LENDER=830146,REPNO=15);
%PRINT(LENDER=830791,REPNO=16);
%PRINT(LENDER=832241,REPNO=17);
%PRINT(LENDER=833828,REPNO=18);
%PRINT(LENDER=817455,REPNO=19);
