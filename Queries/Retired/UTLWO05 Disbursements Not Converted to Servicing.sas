/*DISBURSEMENTS NOT CONVERTED TO SERVICING*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO05.LWO05R2";
FILENAME REPORT3 "&RPTLIB/ULWO05.LWO05R3";
FILENAME REPORT4 "&RPTLIB/ULWO05.LWO05R4";
FILENAME REPORT5 "&RPTLIB/ULWO05.LWO05R5";
FILENAME REPORT6 "&RPTLIB/ULWO05.LWO05R6";
FILENAME REPORT7 "&RPTLIB/ULWO05.LWO05R7";
FILENAME REPORT8 "&RPTLIB/ULWO05.LWO05R8";
FILENAME REPORT9 "&RPTLIB/ULWO05.LWO05R9";
FILENAME REPORT10 "&RPTLIB/ULWO05.LWO05R10";
FILENAME REPORT11 "&RPTLIB/ULWO05.LWO05R11";
FILENAME REPORT12 "&RPTLIB/ULWO05.LWO05R12";
FILENAME REPORT13 "&RPTLIB/ULWO05.LWO05R13";
FILENAME REPORT14 "&RPTLIB/ULWO05.LWO05R14";
FILENAME REPORT15 "&RPTLIB/ULWO05.LWO05R15";
FILENAME REPORT16 "&RPTLIB/ULWO05.LWO05R16";
FILENAME REPORT17 "&RPTLIB/ULWO05.LWO05R17";
FILENAME REPORT18 "&RPTLIB/ULWO05.LWO05R18";
FILENAME REPORT19 "&RPTLIB/ULWO05.LWO05R19";
FILENAME REPORT20 "&RPTLIB/ULWO05.LWO05R20";
FILENAME REPORT21 "&RPTLIB/ULWO05.LWO05R21";
FILENAME REPORT22 "&RPTLIB/ULWO05.LWO05R22";
FILENAME REPORT23 "&RPTLIB/ULWO05.LWO05R23";
FILENAME REPORT24 "&RPTLIB/ULWO05.LWO05R24";
FILENAME REPORT25 "&RPTLIB/ULWO05.LWO05R25";
FILENAME REPORTZ "&RPTLIB/ULWO05.LWO05RZ";

DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);

CREATE TABLE DNC AS
SELECT BF_SSN  				LENGTH=6
	,AN_SEQ
	,LN_SEQ
	,LN_LON_DSB_SEQ
	,IC_LON_PGM
	,CLUID  				LENGTH=19
	,DISBAMT  				LENGTH=5
	,LA_DSB_ORG_CHK_EFT   	LENGTH=5
	,LD_DSB_ROS_PRT       	LENGTH=4
	,LD_DSB              	LENGTH=4
	,AF_DOE_SCL
	,AF_DOE_LDR
	,IM_LDR_FUL
	,LA_DSB               LENGTH=5
	,LC_LDR_DSB_MDM
	,LA_DSB_FEE 
	,AF_RGL_CAT_LP20
	,DSB_IND
	,GTY_FEE
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS BF_SSN
	,A.AN_SEQ
	,A.LN_SEQ
    ,A.LN_LON_DSB_SEQ
    ,A.IC_LON_PGM
    ,C.AF_CNL||''||C.AF_CNL_SFX AS CLUID 
	,COALESCE(A.LA_DSB,0) - COALESCE(A.LA_DSB_CAN,0) AS DISBAMT
	,A.LA_DSB_ORG_CHK_EFT
	,A.LD_DSB_ROS_PRT
	,A.LD_DSB
    ,C.AF_DOE_SCL 
    ,C.AF_DOE_LDR
	,E.IM_LDR_FUL
	,A.LA_DSB
	,A.LC_LDR_DSB_MDM
	,H.LA_DSB_FEE 
	,I.LA_DSB_FEE AS GTY_FEE
	,C.AF_RGL_CAT_LP20
	,PL_DISB.DSB_IND
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL C 
	ON A.AF_APL_ID = C.AF_APL_ID
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON E.IF_DOE_LDR = C.AF_DOE_LDR
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE H
	ON A.BF_SSN = H.BF_SSN
	AND A.LN_BR_DSB_SEQ = H.LN_BR_DSB_SEQ
	AND H.LC_DSB_FEE = '02'
LEFT OUTER JOIN OLWHRM1.LN18_DSB_FEE I
	ON A.BF_SSN = I.BF_SSN
	AND A.LN_BR_DSB_SEQ = I.LN_BR_DSB_SEQ
	AND I.LC_DSB_FEE = '21'
LEFT OUTER JOIN (
	SELECT BF_SSN 
		,LN_SEQ
		,'X' AS DSB_IND
	FROM OLWHRM1.LN15_DSB
	WHERE DAYS(LD_DSB) BETWEEN DAYS('07/01/2006') AND DAYS('08/07/2006')
	AND IC_LON_PGM IN ('PLUS','PLUSGB')
	AND LC_STA_LON15 IN ('1','3')
	AND LC_DSB_TYP = '2'
	AND LA_DSB <> COALESCE(LA_DSB_CAN,0)
	) PL_DISB
	ON A.BF_SSN = PL_DISB.BF_SSN
	AND A.LN_SEQ = PL_DISB.LN_SEQ
WHERE (
	A.LC_STA_LON15 IN ('1','3')
	AND (
		A.LA_DSB_CAN IS NULL
		OR 
		A.LA_DSB_CAN <> A.LA_DSB
		)
	AND (
			( 
			A.LC_DSB_RLS_STA <> 'R' OR A.LC_DSB_RLS_STA IS NULL
			)
			OR 
			(
				A.LC_DSB_RLS_STA IS NULL AND A.LN_SEQ IS NOT NULL
			)
		)
	AND	A.LC_LDR_DSB_MDM NOT IN ('')		
	AND DAYS(A.LD_DSB_ROS_PRT) <= 
			DAYS((SELECT MAX(Z.LD_DSB_ROS_PRT)
					FROM OLWHRM1.LN15_DSB Z)) - 1
		
	)
AND A.LD_DSB >= '11/01/2001'
/*--------------- TESTING ---------------*/
/*WHERE A.LD_DSB_ROS_PRT =  '04/22/2005'*/
/*---------------------------------------*/
);

CREATE TABLE DUPSRV AS
SELECT CLUID  	length=19
 ,LD_DSB		length=4   
 ,LA_DSB      	length=5
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	CASE WHEN A.LN_LON_ALT_SEQ < 10
		THEN A.LF_LON_ALT||'0'||RTRIM(CHAR(A.LN_LON_ALT_SEQ))
		ELSE A.LF_LON_ALT||RTRIM(CHAR(A.LN_LON_ALT_SEQ))
		END AS CLUID
	,B.LD_DSB
	,B.LA_DSB
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
WHERE B.AN_SEQ IS NULL
);
DISCONNECT FROM DB2;

/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

PROC SORT DATA=DNC;BY CLUID LD_DSB LA_DSB;RUN;
PROC SORT DATA=DUPSRV;BY CLUID LD_DSB LA_DSB;RUN;

DATA DNC;
MERGE DNC (IN=A) DUPSRV (IN=B);
BY CLUID LD_DSB LA_DSB;
IF A AND NOT B;
RUN;

ENDRSUBMIT;
DATA DNC;
SET WORKLOCL.DNC;
RUN;

DATA DNC;
SET DNC;
IF AF_DOE_LDR = '813760UT' THEN AF_DOE_LDR = '813760';/*KEY BANK*/
ELSE IF AF_DOE_LDR = '820043' THEN AF_DOE_LDR = '829505';/*WASHINGTON MUTUAL LENDER MERGE*/
ELSE AF_DOE_LDR = AF_DOE_LDR;
RUN;

DATA DNC;
SET DNC;
LENGTH LOC1 $ 4. LOC2 $ 40.;
IF INPUT(AF_RGL_CAT_LP20,BEST12.) <= 1999030 THEN
	LOC1 = '3%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2006020 AND (
	DSB_IND = 'X' OR IC_LON_PGM IN ('STFFRD','UNSTFD')) THEN 
	LOC1 = '2%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2006020 AND DSB_IND = '' AND IC_LON_PGM IN ('PLUS','PLUSGB') THEN 
	LOC1 = '3%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2007020 AND IC_LON_PGM IN ('STFFRD','UNSTFD') THEN 
	LOC1 = '1.5%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2008020 AND IC_LON_PGM IN ('STFFRD','UNSTFD') THEN 
	LOC1 = '1.%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 AND IC_LON_PGM IN ('STFFRD','UNSTFD') THEN 
	LOC1 = '0.5%';
ELSE IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 AND IC_LON_PGM IN ('PLUS','PLUSGB') THEN 
	LOC1 = '3%';

IF LA_DSB_FEE = 0 THEN DO;
	IF INPUT(AF_RGL_CAT_LP20,BEST12.) = 2008020 THEN
		LOC2 = 'UHEAA PAID ORIGINATION FEE LOANS';
	ELSE
		LOC2 = 'LOAN WITH ZERO ORIGINATION FEE';
END;
ELSE IF LA_DSB_FEE > 0 AND INPUT(AF_RGL_CAT_LP20,BEST12.) = 2009020 THEN DO;
	IF IC_LON_PGM IN ('STFFRD','UNSTFD') THEN DO;
		IF GTY_FEE > 0 THEN
			LOC2 = 'LOANS WITH ORIG / GUAR FEE ED 0.5%/1%';
		ELSE 
			LOC2 = 'LOAN WITH ORIGINATION FEE ED 0.5%';
	END;
	IF IC_LON_PGM IN ('PLUS','PLUSGB') THEN DO;
		IF GTY_FEE > 0 THEN
			LOC2 = 'LOANS WITH ORIG / GUAR FEE ED 3%/1%';
		ELSE 
			LOC2 = 'LOAN WITH ORIGINATION FEE ED 3%';
	END;
END;
ELSE 
	LOC2 = 'LOAN WITH ORIGINATION FEE';
RUN;

%MACRO PRINT(LENDER=,REPNO=);

%LET TTL = ;

DATA DNCLDR;
SET DNC;
WHERE AF_DOE_LDR = "&LENDER";
RUN;

PROC SORT DATA=DNCLDR;
BY LOC2 LOC1 BF_SSN;
RUN;

DATA _NULL_;
SET DNCLDR;
CALL SYMPUT('TTL',TRIM(IM_LDR_FUL));
RUN;

PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
PROC CONTENTS DATA=DNCLDR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT //////////// 
		@46 "JOB = UTLWO05     REPORT = ULWO05.LWO05R&REPNO";
	TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
	TITLE2 'DISBURSEMENTS NOT CONVERTED TO SERVICING';
	TITLE3 "LENDER: &LENDER";
	TITLE4 "RUN DATE: &RUNDATE";
END;
RETURN;
RUN;
PROC REPORT DATA=DNCLDR NOWD HEADSKIP SPLIT='/' SPACING=1;
COLUMN AF_DOE_LDR LOC2 LOC1 BF_SSN AN_SEQ LN_SEQ LN_LON_DSB_SEQ IC_LON_PGM CLUID DISBAMT
LA_DSB_ORG_CHK_EFT LD_DSB_ROS_PRT LD_DSB AF_DOE_SCL N LC_LDR_DSB_MDM;
DEFINE AF_DOE_LDR / ORDER NOPRINT;
DEFINE LOC2 / GROUP "CATEGORY" WIDTH=11 FLOW ;
DEFINE LOC1 / GROUP "%" WIDTH=4;
DEFINE BF_SSN / DISPLAY "SSN" WIDTH=11 FORMAT=SSN11.;
DEFINE AN_SEQ / DISPLAY "APP/SEQ" WIDTH=4;
DEFINE LN_SEQ / DISPLAY "LOAN/SEQ" WIDTH=4;
DEFINE LN_LON_DSB_SEQ / DISPLAY "LOAN/DISB/SEQ" WIDTH=4;
DEFINE IC_LON_PGM / DISPLAY "LOAN/TYPE" WIDTH=6;
DEFINE CLUID / DISPLAY "COMMONLINE UNIQUE ID" WIDTH=19;
DEFINE DISBAMT / ANALYSIS FORMAT=COMMA10.2 "GROSS/DISB AMOUNT";
DEFINE LA_DSB_ORG_CHK_EFT / ANALYSIS FORMAT=COMMA14.2 "NET DISB/AMOUNT";
DEFINE LD_DSB_ROS_PRT / DISPLAY FORMAT=MMDDYY10. "ROSTER/DATE" WIDTH=10;
DEFINE LD_DSB / DISPLAY FORMAT=MMDDYY10. "DISB DATE" WIDTH=10;
DEFINE AF_DOE_SCL / DISPLAY "SCHOOL/CODE" WIDTH=8;
DEFINE LC_LDR_DSB_MDM / DISPLAY "DISB/MED" WIDTH=4;
DEFINE N / NOPRINT;
BREAK AFTER LOC2 / SUMMARIZE SKIP OL SUPPRESS;
/*RBREAK AFTER / SUMMARIZE SKIP DOL;*/
COMPUTE AFTER;
LINE ' ';
LINE @1 'TOTAL NUMBER OF DISBURSEMENTS:' @32 N COMMA7.;
LINE @1 'TOTAL GROSS DISBURSEMENT AMOUNT:' @32 DISBAMT.SUM DOLLAR14.2;
LINE @1 'TOTAL NET DISBURSEMENT AMOUNT:' @32 LA_DSB_ORG_CHK_EFT.SUM DOLLAR14.2;
ENDCOMP;
TITLE "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 'DISBURSEMENTS NOT CONVERTED TO SERVICING';
TITLE3 "LENDER: &LENDER - &TTL";
TITLE4 "RUN DATE: &RUNDATE";
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	"Please take appropriate precautions to safeguard this information.";
FOOTNOTE3	;
FOOTNOTE4 "JOB = UTLWO05     REPORT = ULWO05.LWO05R&REPNO";
RUN;
PROC PRINTTO;
RUN;
%MEND PRINT;

%PRINT(LENDER=811698,REPNO=2);
%PRINT(LENDER=813760,REPNO=3);
%PRINT(LENDER=813894,REPNO=4);
%PRINT(LENDER=817440,REPNO=5);
%PRINT(LENDER=817545,REPNO=6);
%PRINT(LENDER=817546,REPNO=7);
%PRINT(LENDER=819628,REPNO=8);
%PRINT(LENDER=829505,REPNO=9);
%PRINT(LENDER=820200,REPNO=10);
%PRINT(LENDER=822373,REPNO=11);
%PRINT(LENDER=829123,REPNO=12);
%PRINT(LENDER=829158,REPNO=13);
%PRINT(LENDER=830132,REPNO=14);
%PRINT(LENDER=830146,REPNO=15);
%PRINT(LENDER=830791,REPNO=16);
%PRINT(LENDER=832241,REPNO=17);
%PRINT(LENDER=833828,REPNO=18);
%PRINT(LENDER=817455,REPNO=19);
%PRINT(LENDER=817575,REPNO=20);
%PRINT(LENDER=834122,REPNO=21);
%PRINT(LENDER=835577,REPNO=22);
%PRINT(LENDER=834265,REPNO=23);
%PRINT(LENDER=828476,REPNO=24);
%PRINT(LENDER=834370,REPNO=25);
