*-----------------------------------*
| UTLWR16 - Delinquent ECASLA Loans |
*-----------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWR16.LWR16R2";
FILENAME REPORTZ "&RPTLIB/ULWR16.LWR16RZ";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DELECA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.LN_SEQ
	,C.IF_BND_ISS
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.LN35_LON_OWN C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
WHERE A.LA_CUR_PRI > 0
	AND A.LC_STA_LON10 = 'R'
	AND A.LD_LON_1_DSB >= '07/01/2008'
	AND A.LF_LON_CUR_OWN = '828476'
	AND A.LI_CON_PAY_STP_PUR != 'Y'
	AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS','PLUSGB')
	AND B.LC_STA_LON16 = '1'
	AND DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) > 90
	AND C.LC_STA_LON35 = 'A'
	AND C.LD_OWN_EFF_END IS NULL	
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWR16.LWR16RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA DELECA ;*/
/*	SET WORKLOCL.DELECA ;*/
/*RUN;*/
PROC SORT DATA=DELECA NODUPKEY;
	BY IF_BND_ISS BF_SSN LN_SEQ ;
RUN;
DATA DELECA;
	SET DELECA;
	LENGTH BF_SEQ $ 13.;
	BF_SEQ = CATS(BF_SSN,PUT(LN_SEQ,Z4.));
RUN;
DATA _NULL_;
	SET DELECA;
	FORMAT BF_SEQ $13. IF_BND_ISS $8. ;
	FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	DO;
	   PUT BF_SEQ $ @;
	   PUT IF_BND_ISS $ ;
	END;
RUN;
