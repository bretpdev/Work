/*
BORROWERS ELIGIBLE FOR BANK GARNISHMENT

This report lists legal borrowers with no current employer whose last payment
was greater than 60 but less than 180 days ago (not paying but paid recently enough
that a check may still be on file with a bank account number).
Collections uses this report to initiate new bank garnishments.

*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWJD5.LWJD5R2";
*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BANKGARN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID									AS SSN,
	RTRIM(A.DM_PRS_1)||' '||RTRIM(A.DM_PRS_LST)	AS NAME,
	D.ld_lst_br_pay								AS LASTPAYDATE,
	F.bd_trx_pst_hst							AS POSTDATE,
	F.ln_bch_seq								AS BATCHNO
FROM 	OLWHRM1.PD01_PDM_INF A INNER JOIN
		OLWHRM1.LA10_LEG_ACT C ON
			A.DF_PRS_ID = C.DF_PRS_ID_BR INNER JOIN
		OLWHRM1.DC01_LON_CLM_INF D ON
			A.DF_PRS_ID = D.BF_SSN AND
			DAYS(D.ld_lst_br_pay) < DAYS(CURRENT DATE) - 60 AND
			DAYS(D.ld_lst_br_pay) > DAYS(CURRENT DATE) - 180 AND
			D.lc_aux_sta = '' INNER JOIN 
		OLWHRM1.DC02_BAL_INT E ON
			D.AF_APL_ID = E.AF_APL_ID AND
			D.AF_APL_ID_SFX = E.AF_APL_ID_SFX INNER JOIN
		OLWHRM1.DC11_LON_FAT F ON
			A.DF_PRS_ID = F.BF_SSN AND
			D.ld_lst_br_pay = F.ld_trx_eff AND
			D.la_lst_br_pay = F.la_trx AND
			F.lc_trx_typ = 'BR'
WHERE	A.BI_EMP_INF_OVR NOT IN ('Y') AND
		C.BC_WDR_REA = '' AND
		C.BC_INA_REA = '' AND
		C.BC_LEG_ACT_REC_TYP = '2' AND
		E.LA_CLM_BAL > 100 AND
		C.BF_CRT_DTS_LA10 = (SELECT MIN(X.BF_CRT_DTS_LA10)
									FROM OLWHRM1.LA10_LEG_ACT X
									WHERE	A.DF_PRS_ID = X.DF_PRS_ID_BR AND
											X.BC_WDR_REA = '' AND
											X.BC_INA_REA = '' AND
											X.BC_LEG_ACT_REC_TYP = '2'
									GROUP BY X.DF_PRS_ID_BR)
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
PROC SORT DATA = WORKLOCL.BANKGARN;
	BY SSN;
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS PAGENO=1 LS=80;
PROC PRINT SPLIT='/' DATA=WORKLOCL.BANKGARN WIDTH=MIN;
VAR SSN
	NAME
	LASTPAYDATE
	POSTDATE
	BATCHNO;
FORMAT LASTPAYDATE MMDDYY10. POSTDATE MMDDYY10.;
TITLE1 'BORROWERS ELIGIBLE FOR BANK GARNISHMENT';
FOOTNOTE 'JOB = UTLWJD5     REPORT = ULWJD5.LWJD5R2';
/*FOOTNOTE;*/
RUN;
