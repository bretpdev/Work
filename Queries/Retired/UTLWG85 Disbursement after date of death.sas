*--------------------------------------------*
| UTLWG85 DISBURSEMENT AFTER DATE OF DEATH   |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG85.LWG85R2";
FILENAME REPORTZ "&RPTLIB/ULWG85.LWG85RZ";

DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
	CALL SYMPUT('RUNTIME',PUT(TIME(), TIME.));
RUN;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DADOD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,CASE 
		WHEN B.DD_DTH = C.DD_DTH THEN B.DD_DTH
		WHEN B.DD_DTH IS NULL AND C.DD_DTH IS NOT NULL THEN C.DD_DTH
		WHEN C.DD_DTH IS NULL AND B.DD_DTH IS NOT NULL THEN B.DD_DTH
	 END AS DD_DTH
	,B.DF_SPE_ACC_ID

FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.PD01_PDM_INF B
	ON A.BF_SSN = B.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD20_PRS_DTH C
	ON A.BF_SSN = C.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD21_GTR_DTH D
	ON C.DF_PRS_ID = D.DF_PRS_ID
	AND C.DD_DTH_NTF = D.DD_DTH_NTF

WHERE A.LC_STA_LON15 IN ('1','3')
AND A.LC_DSB_TYP = '2'
AND COALESCE(A.LA_DSB_CAN,0) <> COALESCE(A.LA_DSB,0)
AND (
		(
		A.LD_DSB >= B.DD_DTH
		AND NOT EXISTS (
			SELECT *
			FROM OLWHRM1.AY01_BR_ATY Y
			WHERE Y.DF_PRS_ID = A.BF_SSN
			AND Y.PF_ACT = 'GDSRW'
			)
		)
	AND (
		A.LD_DSB >= C.DD_DTH
		AND D.DC_DTH_STA = '02'
		AND NOT EXISTS (
			SELECT *
			FROM OLWHRM1.AY10_BR_LON_ATY X
			WHERE X.BF_SSN = A.BF_SSN
			AND X.PF_REQ_ACT = 'GDSRW'
			)
		)
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWG85.LWG85RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA DADOD;
SET WORKLOCL.DADOD;
RUN;

PROC SORT DATA=DADOD;
BY DF_SPE_ACC_ID;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE 'DISBURSEMENT AFTER DATE OF DEATH';
TITLE2	"&RUNDATE - &RUNTIME";
FOOTNOTE 'JOB = UTLWG85  	 REPORT = ULWG85.LWG85R2';
PROC CONTENTS DATA=DADOD OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWG85  	 REPORT = ULWG85.LWG85R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=DADOD WIDTH=UNIFORM WIDTH=MIN;
FORMAT DD_DTH MMDDYY10.;
VAR DF_SPE_ACC_ID
	DD_DTH;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	DD_DTH = 'DATE OF DEATH';
RUN;
PROC PRINTTO;
RUN;
