*---------------------------------------------------------------*
| UTLWR18 - CUSTODIAN LOANS WITH DISBURSEMENTS NOT PARTICIPATED |
*---------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR18.LWR18R2";
FILENAME REPORTZ "&RPTLIB/ULWR18.LWR18RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :CUSTODIAN_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'CUSTODIAN';
QUIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CLWDNT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT C.DF_SPE_ACC_ID
	,A.LN_SEQ
	,A.LF_LON_CUR_OWN
	,B.LD_DSB
	,B.LA_DSB - COALESCE(B.LA_DSB_CAN,0) AS DISB_AMT
	,D.LF_ECA_PGM_YR
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN15_DSB B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME C
	ON B.BF_SSN = C.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.LNEC_LON_ECA D
	ON B.BF_SSN = D.BF_SSN
	AND B.LN_SEQ = D.LN_SEQ
	AND D.LN_ECA_SEQ = 1
WHERE A.LF_LON_CUR_OWN IN (&CUSTODIAN_LIST)
	AND B.LC_DSB_TYP = '2'
	AND B.LC_STA_LON15 IN ('1','3')
	AND B.LA_DSB != COALESCE(B.LA_DSB_CAN,0) 
	AND B.LD_ECA_DSB_FUD IS NULL 
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR18.LWR18RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA CLWDNT ;
	SET WORKLOCL.CLWDNT;
RUN;
PROC SORT DATA=CLWDNT;
	BY LF_LON_CUR_OWN DF_SPE_ACC_ID LN_SEQ;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'CUSTODIAN LOANS WITH DISBURSEMENTS NOT PARTICIPATED';
FOOTNOTE   'JOB = UTLWR18  	 REPORT = ULWR18.LWR18R2';
PROC CONTENTS DATA=CLWDNT OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR18  	 REPORT = ULWR18.LWR18R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=CLWDNT WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_DSB MMDDYY10. DISB_AMT 14.2;
VAR LF_LON_CUR_OWN DF_SPE_ACC_ID LN_SEQ LD_DSB DISB_AMT LF_ECA_PGM_YR;
LABEL LF_LON_CUR_OWN = 'CURRENT/OWNER' 
	DF_SPE_ACC_ID = 'ACCOUNT/NUMBER'
	LN_SEQ = 'LOAN/SEQ' 
	LD_DSB = 'DISBURSEMENT/DATE' 
	DISB_AMT = 'DISBURSEMENT/AMOUNT' 
	LF_ECA_PGM_YR = 'PROGRAM/YEAR';
RUN;
PROC PRINTTO;
RUN;
