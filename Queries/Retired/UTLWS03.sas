/*=========================*/
/*UTLWS03 - BSV EXIT CALL*/
/*=========================*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = /sas/whse/progrevw;
FILENAME REPORT2 "&RPTLIB/ULWS03.LWS03R2";
FILENAME REPORTZ "&RPTLIB/ULWS03.LWS03RZ";
OPTIONS SYMBOLGEN;
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('DAY',TODAY(),-35,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('END',"'"||PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
/*%SYSLPUT BEGIN = &BEGIN;*/
/*%SYSLPUT END = &END;*/
/*FILENAME REPORT2 "C:\WINDOWS\Temp\ULWS03.LWS03R2";*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO EXTCL(DATA1,DATA2,VALS);
PROC SORT DATA=&DATA1;
	BY &VALS;
RUN;
PROC SORT DATA=&DATA2;
	BY &VALS;
RUN;
DATA &DATA1;
	MERGE &DATA1 (IN=A) &DATA2 (IN=B);
	BY &VALS;
	IF A;
RUN;
%MEND;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
/*INITIAL QUERY PART 1*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE INI1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	A.BF_SSN AS SSN
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LN66_LON_RPS_SPF B
	ON A.BF_SSN = B.BF_SSN 
	AND A.LN_SEQ = B.LN_SEQ 
	AND B.LN_GRD_RPS_SEQ = 1
INNER JOIN OLWHRM1.RS10_BR_RPD C 
	ON B.BF_SSN = C.BF_SSN 
	AND B.LN_RPS_SEQ = C.LN_RPS_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
	AND DW01.LN_SEQ = A.LN_SEQ
INNER JOIN OLWHRM1.PD42_PRS_PHN PD42
	ON A.BF_SSN = PD42.DF_PRS_ID
WHERE C.LC_STA_RPST10 = 'A' 
AND A.LC_STA_LON10 = 'R'
AND A.LA_CUR_PRI > 0
AND PD42.DC_PHN = 'H'
AND PD42.DI_PHN_VLD = 'Y'
AND PD42.DN_DOM_PHN_XCH||PD42.DN_DOM_PHN_LCL||PD42.DN_PHN_XTN <> ' '
AND DW01.WC_DW_LON_STA <> '06'
AND C.LD_SNT_RPD_DIS BETWEEN &BEGIN AND &END
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*INITIAL QUERY PART 2*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE INI2 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	A.BF_SSN AS SSN
FROM OLWHRM1.RS10_BR_RPD A 
INNER JOIN OLWHRM1.LN66_LON_RPS_SPF B
	ON A.BF_SSN = B.BF_SSN 
	AND A.LN_RPS_SEQ = B.LN_RPS_SEQ
	AND B.LN_GRD_RPS_SEQ = 1
INNER JOIN OLWHRM1.LN10_LON C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
	AND C.LA_CUR_PRI > 0
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = C.BF_SSN
	AND DW01.LN_SEQ = C.LN_SEQ
WHERE A.BF_SSN IN 
	(SELECT A1.LF_STU_SSN
	 FROM OLWHRM1.SD10_STU_SPR A1
	 INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B1
	 	ON B1.BF_SSN = A1.LF_STU_SSN
	 WHERE DAYS(B1.WD_LON_RPD_SR) BETWEEN (DAYS(CURRENT DATE) - 60) AND DAYS(CURRENT DATE) + 60
	 AND DAYS(A1.LD_NTF_SCL_SPR) < DAYS(CURRENT DATE))
AND	NOT EXISTS
		(SELECT *
		FROM OLWHRM1.AY10_BR_LON_ATY X
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B1	
			ON B1.BF_SSN = X.BF_SSN	
		INNER JOIN OLWHRM1.RS10_BR_RPD A1
			ON B1.BF_SSN = A1.BF_SSN
			AND B1.LN_SEQ = A1.LN_RPS_SEQ
		WHERE X.BF_SSN = C.BF_SSN															
		AND X.LD_ATY_RSP >= A1.LD_SNT_RPD_DIS 				
		AND X.PF_REQ_ACT = 'XC10R'
		AND X.LC_STA_ACTY10 = 'A'
		AND X.PF_RSP_ACT NOT IN ('CNTCT','COMPL'))
AND	NOT EXISTS
		(SELECT *
		 FROM 
			(SELECT X.BF_SSN, B1.LN_SEQ, COUNT(*) AS CNT
			 FROM OLWHRM1.AY10_BR_LON_ATY X
			 INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B1	
				ON B1.BF_SSN = X.BF_SSN	
			 INNER JOIN OLWHRM1.RS10_BR_RPD A1
				ON B1.BF_SSN = A1.BF_SSN
				AND B1.LN_SEQ = A1.LN_RPS_SEQ
			 WHERE X.LD_ATY_RSP >= A1.LD_SNT_RPD_DIS 					
			 AND X.PF_REQ_ACT = 'XC10R'
			 AND X.LC_STA_ACTY10 = 'A'
			 GROUP BY X.BF_SSN, B1.LN_SEQ
			) AS TEST
		INNER JOIN OLWHRM1.LN10_LON TEST2
		ON TEST.BF_SSN = TEST2.BF_SSN
		AND TEST.LN_SEQ = TEST2.LN_SEQ
		WHERE (CNT > 4)
		AND TEST.BF_SSN = C.BF_SSN)	
AND NOT EXISTS
		(SELECT *
		 FROM OLWHRM1.AY10_BR_LON_ATY X
		 WHERE X.BF_SSN = C.BF_SSN
		 AND X.PF_REQ_ACT = 'XC10R'
		 AND X.LC_STA_ACTY10 = 'A'
		 AND DAYS(X.LD_ATY_RSP) BETWEEN (DAYS(CURRENT DATE) - 8) AND DAYS(CURRENT DATE))
AND NOT EXISTS
		(SELECT *
		FROM OLWHRM1.AY10_BR_LON_ATY X
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B1	
			ON B1.BF_SSN = X.BF_SSN	
		INNER JOIN OLWHRM1.RS10_BR_RPD A1
			ON B1.BF_SSN = A1.BF_SSN
			AND B1.LN_SEQ = A1.LN_RPS_SEQ
		WHERE X.BF_SSN = C.BF_SSN															
		AND X.LD_ATY_RSP >= A1.LD_SNT_RPD_DIS 				
		AND X.PF_REQ_ACT = 'XC10R'
		AND X.LC_STA_ACTY10 = 'A'
		AND X.PF_RSP_ACT = 'CNTCT'
		)
AND NOT EXISTS
		(SELECT *
		 FROM OLWHRM1.WQ20_TSK_QUE X
		 WHERE X.BF_SSN = C.BF_SSN
		 AND X.WF_QUE = 'X1'
		 AND X.WF_SUB_QUE = 'BS')
AND NOT EXISTS 
		(SELECT *
		 FROM OLWHRM1.AY10_BR_LON_ATY X
		 WHERE X.BF_SSN = C.BF_SSN
		 AND X.PF_REQ_ACT = 'XC10R'
		 AND X.LC_STA_ACTY10 = 'A'
		 AND X.LD_ATY_RSP < 
				(SELECT MAX(XX.LD_ATY_RSP)
				 FROM OLWHRM1.AY10_BR_LON_ATY XX
				 WHERE X.BF_SSN = XX.BF_SSN 
				 AND XX.PF_REQ_ACT = 'P200C'))
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

/*SORT AND MERGE DATA INTO XTCL1 DATA SET*/
PROC SORT DATA=INI1;BY SSN;RUN;
PROC SORT DATA=INI2;BY SSN;RUN;
DATA XTCL1;
MERGE INI1 (IN=A) INI2 (IN=B);
BY SSN;
IF A AND B THEN OUTPUT XTCL1;
ELSE DELETE;
RUN;

/*GET EXISTING SSNS IN X1 QUEUE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE X1Q AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	 WQ20.BF_SSN AS SSN
	,MAX(AY10.LD_REQ_RSP_ATY_PRF) AS LD_REQ_RSP_ATY_PRF
	,'X' AS WQ20_IND
FROM OLWHRM1.WQ20_TSK_QUE WQ20 
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON WQ20.BF_SSN = AY10.BF_SSN 
	AND AY10.PF_REQ_ACT = 'XC10R'
	AND AY10.LC_STA_ACTY10 = 'A'
WHERE WQ20.WF_QUE = 'X1'
GROUP BY WQ20.BF_SSN
FOR READ ONLY WITH UR
);

CREATE TABLE WQ10 AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT WN_DAY_PRD_GOA_WRK
	,'Y' AS X1_IND
FROM OLWHRM1.WQ10_TSK_QUE_DFN
WHERE WF_QUE = 'X1'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;

DATA XTCL1;
SET XTCL1;
X1_IND = 'Y';
RUN;

DATA XTCL1 (DROP=X1_IND);
MERGE XTCL1 WQ10;
BY X1_IND;
RUN;

/*CREATE MASTER EXITCALL DATA SET*/
DATA EXITCALL;
SET XTCL1 X1Q;
RUN;

PROC SORT DATA=EXITCALL NODUPKEY;BY SSN;RUN;

/*GET DEMOGRAPHIC INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO_INFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	A.BF_SSN AS SSN
	,CASE 
		WHEN PD10.DM_PRS_MID = ' '
		THEN RTRIM(PD10.DM_PRS_1)||' '||PD10.DM_PRS_LST
		WHEN PD10.DM_PRS_MID <> ' '
		THEN RTRIM(PD10.DM_PRS_1)||' '||RTRIM(PD10.DM_PRS_MID)||' '||PD10.DM_PRS_LST
	 END AS NAME
	,SUBSTR(PD10.DM_PRS_LST,1,2) AS NAME_SORT
	,PD10.DD_BRT
/*ADDRESS INFO*/
	,PD30.DX_STR_ADR_1 	 
	,PD30.DX_STR_ADR_2 	 
	,PD30.DX_STR_ADR_3 	 
	,PD30.DM_CT
	,PD30.DC_DOM_ST
	,PD30.DF_ZIP_CDE
	,PD30.DM_FGN_CNY
	,PD30.DM_FGN_ST
	,PD30.DI_VLD_ADR
/*	PHONE INFO*/
	,PD42.DC_PHN
	,PD42.DN_DOM_PHN_ARA||PD42.DN_DOM_PHN_XCH||PD42.DN_DOM_PHN_LCL AS HME_PHN
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON A.BF_SSN = PD10.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
	ON A.BF_SSN = PD30.DF_PRS_ID
	AND PD30.DC_ADR = 'L'
INNER JOIN OLWHRM1.PD42_PRS_PHN PD42
	ON A.BF_SSN = PD42.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
	AND DW01.LN_SEQ = A.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
AND A.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA <> '06'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,DEMO_INFO,SSN);

/*GET LOAN COUNT AND TOTAL INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LN10_INFO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN AS SSN
	,COUNT(*) AS COUNT
	,SUM(A.LA_CUR_PRI) AS LA_CUR_PRI
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
	AND DW01.LN_SEQ = A.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
AND A.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA <> '06'
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,LN10_INFO,SSN);

/*GET AMOUNT DUE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AMTDU AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT X.BF_SSN AS SSN
	,SUM(COALESCE(X.LA_BIL_PAS_DU,0) +
		 COALESCE(X.LA_BIL_DU_PRT,0) +
		 COALESCE(X.LA_LTE_FEE_OTS_PRT,0)) AS AMTDU
FROM OLWHRM1.LN80_LON_BIL_CRF X
INNER JOIN OLWHRM1.LN10_LON Y
	ON X.BF_SSN = Y.BF_SSN
	AND X.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND X.LC_STA_LON80 = 'A'
AND DW01.WC_DW_LON_STA <> '06'
AND X.LD_BIL_CRT = 
		(SELECT MAX(Y.LD_BIL_CRT) 
		 FROM OLWHRM1.BL10_BR_BIL Y
		 WHERE Y.BF_SSN = X.BF_SSN
		 AND Y.LC_STA_BIL10 = 'A'
		 AND Y.LC_BIL_TYP = 'P')
GROUP BY X.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,AMTDU,SSN);

/*GET MAX EFF DATE FOR LN90 INFO*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TRX_DT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN90.BF_SSN AS SSN
	,MAX(LN90.LD_FAT_EFF) AS MX_EFFDT
FROM OLWHRM1.LN10_LON Y
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
	ON Y.BF_SSN = LN90.BF_SSN
	AND Y.LN_SEQ = LN90.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA <> '06'
AND LN90.PC_FAT_TYP = '10'
AND LN90.PC_FAT_SUB_TYP IN ('10','11','12')
AND LN90.LC_FAT_REV_REA = ' ' 
AND LN90.LC_STA_LON90 = 'A'
GROUP BY LN90.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,TRX_DT,SSN);

/*GET DAYS DELINQUENT*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DLQ_DYS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT LN16.BF_SSN AS SSN 
	,MAX(LN16.LN_DLQ_MAX) AS LN_DLQ_MAX
FROM OLWHRM1.LN10_LON Y
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = Y.BF_SSN
	AND DW01.LN_SEQ = Y.LN_SEQ
INNER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
	ON Y.BF_SSN = LN16.BF_SSN
	AND Y.LN_SEQ = LN16.LN_SEQ
WHERE Y.LA_CUR_PRI > 0
AND DW01.WC_DW_LON_STA <> '06'
AND LN16.LC_STA_LON16 = '1' 
GROUP BY LN16.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,DLQ_DYS,SSN);

/*LAST ATTEMPTED CONTACT DATE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ATY_ATT AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,MAX(AY10.LD_ATY_RSP) AS LD_ATY_RSP_ATT
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON AY10.BF_SSN = A.BF_SSN
/*INNER JOIN OLWHRM1.AC10_ACT_REQ AC10*/
/* 	ON AY10.PF_REQ_ACT = AC10.PF_REQ_ACT*/
WHERE A.LC_STA_LON10 = 'R'
AND D.DC_PHN = 'H'
AND D.DI_PHN_VLD = 'Y'
AND D.DN_DOM_PHN_XCH||D.DN_DOM_PHN_LCL||D.DN_PHN_XTN <> ' '
AND DW01.WC_DW_LON_STA <> '06' 
AND AY10.PF_RSP_ACT IN ('NOCTC','INVPH')
/*AND AC10.PC_DD_ACT_STA = 'D'*/
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,ATY_ATT,SSN);

/*LAST CONTACT DATE*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE ATY_CTC AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,MAX(AY10.LD_ATY_RSP) AS LD_ATY_RSP_CTC
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD42_PRS_PHN D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON DW01.BF_SSN = A.BF_SSN
INNER JOIN OLWHRM1.AY10_BR_LON_ATY AY10
	ON AY10.BF_SSN = A.BF_SSN
/*INNER JOIN OLWHRM1.AC10_ACT_REQ AC10*/
/* 	ON AY10.PF_REQ_ACT = AC10.PF_REQ_ACT*/
WHERE A.LC_STA_LON10 = 'R'
AND D.DC_PHN = 'H'
AND D.DI_PHN_VLD = 'Y'
AND D.DN_DOM_PHN_XCH||D.DN_DOM_PHN_LCL||D.DN_PHN_XTN <> ' '
AND DW01.WC_DW_LON_STA <> '06' 
AND AY10.PF_RSP_ACT = 'CNTCT'
/*AND AC10.PC_DD_ACT_STA = 'D'*/
GROUP BY A.BF_SSN
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%EXTCL(EXITCALL,ATY_CTC,SSN);
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWS03.LWS03RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA EXITCALL;*/
/*SET WORKLOCL.EXITCALL;*/
/*RUN;*/
/*BREAK OUT AND REASSEMBLE PHONE INFO*/
%MACRO PHN(DSET,PHNVAL);
DATA PHN_&DSET (KEEP=SSN DC_PHN_&DSET PHN_&DSET);
SET EXITCALL;
WHERE DC_PHN = &PHNVAL;
DC_PHN_&DSET = DC_PHN;
PHN_&DSET = HME_PHN;
RUN;
PROC SORT DATA = PHN_&DSET;
BY SSN;
RUN;
%MEND;
%PHN(H,'H');
%PHN(A,'A');
%PHN(W,'W');
DATA PHN;
MERGE PHN_H PHN_A PHN_W;
BY SSN;
RUN;

DATA EXITCALL (DROP=DC_PHN HME_PHN);
SET EXITCALL;
RUN;

PROC SORT DATA=PHN;BY SSN;RUN;
PROC SORT DATA=EXITCALL NODUPRECS;BY SSN;RUN;

DATA EXITCALL;
MERGE EXITCALL PHN;
BY SSN;
RUN;
/*VARIABLE INITIALIZATION*/
DATA EXITCALL;
SET EXITCALL;
QUEUE = 'X1BS';
REGION = 'N';
LENDER_CODE = '        ';
CUR_SCL_CODE = '                    ';
PHN_IND = 'D';
REL = 'BORROWER';
REL_CODE = 'B';
GEO_PHN_IND = 'D';
BI_ATY_3_PTY = ' ';
FILLER = '                                                                                                                  ';
RUN;

/*CALCULATE DAYS LEFT TO COMPLETE*/
DATA EXITCALL;
SET EXITCALL;
IF WN_DAY_PRD_GOA_WRK NE . THEN DTC = WN_DAY_PRD_GOA_WRK;
ELSE IF LD_REQ_RSP_ATY_PRF NE . THEN DO;
	IF INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF) =< 0
	THEN DTC = 0;
	ELSE IF INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF) > 0
	THEN DTC = INTCK('DAY',TODAY(),LD_REQ_RSP_ATY_PRF);
END;
RUN;

*WRITE CURRENT RUN DATETIME FOR THIS JOB TO DATASET;
DATA EXITCALL;
SET EXITCALL;
FORMAT RUNDT DATETIME.;
RUNDT = DATETIME();
RUN;

PROC SQL;
CREATE TABLE EXITCALL2 AS 
SELECT DISTINCT SSN
FROM EXITCALL
WHERE WQ20_IND NE 'X';
QUIT;
RUN;

/*ONLY UNCOMMENT THIS IF YOU WANT TO OVERWRITE THE CYPRUS DATA SET FROM A LOCAL RUN!*/
/***********************************************************************************/
/*DATA WORKLOCL.EXITCALL;*/
/*SET EXITCALL;*/
/*RUN;*/
/*RSUBMIT;*/
/***********************************************************************************/
/*THIS MUST BE UNCOMMENTED FOR PRODUCTION*/
/***********************************************************************************/
LIBNAME PROGRVW V8 '/sas/whse/progrevw'; /*CYPRUS DATA SET*/
DATA PROGRVW.ULWS03_LWS03R3;
SET EXITCALL;
RUN;
/***********************************************************************************/
/*ENDRSUBMIT;*/
/***********************************************************************************/
/*FILE TO BUILD QUEUE*/
DATA _NULL_;
SET  WORK.EXITCALL2;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT SSN 9.;
PUT SSN $ ;
RUN;

/*PROC EXPORT DATA=WORK.EXITCALL*/
/*            OUTFILE= "C:\WINDOWS\TEMP\ULWS03_LWS03R3.XLS" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/