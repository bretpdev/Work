/*US BANK SLMA SALE ESTIMATE FOR 11/03*/


OPTIONS SYMBOLGEN;
DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);

CREATE TABLE MAIN AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.IF_DOE_LDR
	,A.BF_SSN
	,A.LF_LON_ALT
	,'0'||CHAR(A.LN_LON_ALT_SEQ) AS LN_LON_ALT_SEQ
	,CASE
		WHEN A.LN_SEQ < 10 THEN '000'||CHAR(A.LN_SEQ)
		ELSE '00'||CHAR(A.LN_SEQ)
	END AS LN_SEQ
	,A.LC_STA_LON10
	,A.LA_CUR_PRI
	,COALESCE(A.LA_NSI_OTS,0) AS LA_NSI_OTS
	,A.IC_LON_PGM
	,A.IF_GTR
	,A.LD_LON_GTR
	,A.LD_TRM_END
	,A.LF_LON_CUR_OWN
	,G.MIN_ACT_DSB
	,G.MAX_ACT_DSB
	,DW01.WC_DW_LON_STA
	,F.FUL_DSB_IND
	,A.LA_LON_AMT_GTR
	,DW01.WA_TOT_BRI_OTS
	,A.LD_PNT_SIG
	,A.LD_TRM_BEG
	,A.LC_ACA_GDE_LEV
	,A.LF_DOE_SCL_ORG
	,A.LF_STU_SSN
FROM  OLWHRM1.LN10_LON A 
/*GET LAST ACTUAL DISBURSEMENT DATE FOR LATE DISB CHECK*/
INNER JOIN
	(SELECT BF_SSN
		,LN_SEQ
		,MIN(LD_DSB) AS MIN_ACT_DSB
		,MAX(LD_DSB) AS MAX_ACT_DSB
	FROM OLWHRM1.LN15_DSB
	WHERE LC_STA_LON15 IN ('1','3') /*ACTIVE ROW*/
	AND LC_DSB_TYP = '2' /*ACTUAL DISB*/
	AND COALESCE(LA_DSB_CAN,0) <> LA_DSB /*NOT FULLY CANCELED*/
	GROUP BY BF_SSN, LN_SEQ
	) G
	ON A.BF_SSN = G.BF_SSN
	AND A.LN_SEQ = G.LN_SEQ
/*GET LOAN STATUS*/
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
	ON A.BF_SSN = DW01.BF_SSN
	AND A.LN_SEQ = DW01.LN_SEQ

/*SET INDICATOR FOR FULLY DISBURSED LOANS*/
LEFT OUTER JOIN 
	(SELECT DISTINCT X.BF_SSN
		,X.LN_SEQ
		,'Y' AS FUL_DSB_IND
	FROM OLWHRM1.LN15_DSB X
	WHERE NOT EXISTS
		(SELECT *
		FROM OLWHRM1.LN15_DSB Y
		WHERE X.BF_SSN = Y.BF_SSN
		AND X.LN_SEQ = Y.LN_SEQ
		AND Y.LC_STA_LON15 IN ('1','3')
		AND Y.LC_DSB_TYP = '1'
		AND (Y.LA_DSB_CAN IS NULL
		    OR Y.LA_DSB_CAN <> Y.LA_DSB)
		)
	) F
	ON A.BF_SSN = F.BF_SSN
	AND A.LN_SEQ = F.LN_SEQ

WHERE A.LC_STA_LON10 = 'R'
AND A.IF_GTR = '000749'
AND A.LA_CUR_PRI > 0
/*SEP DATE CRITERIA*/
AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')
AND DW01.WC_DW_LON_STA IN 
	('01','02','03','04','05','10')
/*owned by us bank*/
AND A.LF_LON_CUR_OWN = '811698'
);


CREATE TABLE ONELINK AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,CASE
		WHEN A.LN_SEQ < 10 THEN '000'||CHAR(A.LN_SEQ)
		ELSE '00'||CHAR(A.LN_SEQ)
	END AS LN_SEQ

	,GA10.AF_CUR_LON_SER_AGY
	,GA10.AF_CUR_LON_OPS_LDR

FROM  OLWHRM1.LN10_LON A 
/*GET ONELINK DATA*/
LEFT OUTER JOIN OLWHRM1.GA10_LON_APP GA10
	ON A.LF_LON_ALT = GA10.AF_APL_ID
	AND '0'||CHAR(A.LN_LON_ALT_SEQ) = GA10.AF_APL_ID_SFX

WHERE A.LC_STA_LON10 = 'R'
AND A.IF_GTR IN ('000749','000800')
AND A.LA_CUR_PRI > 0
/*owned by us bank*/
AND A.LF_LON_CUR_OWN = '811698'
);


/*SET INDICATOR IF BWR HAS AN OPEN LOAN SERVICED INHOUSE OR NELNET OR SLMA*/
/*SET INDICATOR IF BWR HAS OPEN LOANS SERVICED BY NLN OR SLMA WITH ZERO PRIN+INT*/
CREATE TABLE OTHSVC AS
SELECT DF_PRS_ID_BR
,MAX(OTH_NLN_IND) AS OTH_NLN_IND
,MAX(OTH_SLM_IND) AS OTH_SLM_IND
,MAX(OTH_INH_IND) AS OTH_INH_IND
,MAX(ZRO_NLN_IND) AS ZRO_NLN_IND
,MAX(ZRO_SLM_IND) AS ZRO_SLM_IND
FROM CONNECTION TO DB2 (
SELECT X.DF_PRS_ID_BR
	,CASE WHEN Y.AF_CUR_LON_SER_AGY = '700121'
		THEN 'Y'
		END AS OTH_NLN_IND
	,CASE WHEN Y.AF_CUR_LON_SER_AGY IN ('700191','700004','700789','700190','700079')
		THEN 'Y'
		END AS OTH_SLM_IND
	,CASE WHEN Y.AF_CUR_LON_OPS_LDR = '828476'
		THEN 'Y'
		END AS OTH_INH_IND
	,CASE WHEN COALESCE(Y.AA_CUR_PRI,0) + COALESCE(Y.AA_OTS_ACR_INT,0) = 0
		AND Y.AF_CUR_LON_SER_AGY = '700121'
		THEN 'Y'
		END AS ZRO_NLN_IND
	,CASE WHEN COALESCE(Y.AA_CUR_PRI,0) + COALESCE(Y.AA_OTS_ACR_INT,0) = 0
		AND Y.AF_CUR_LON_SER_AGY IN ('700191','700004','700789','700190','700079')
		THEN 'Y'
		END AS ZRO_SLM_IND
FROM  OLWHRM1.GA01_APP X 
INNER JOIN OLWHRM1.GA10_LON_APP Y
	ON X.AF_APL_ID = Y.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA Z
	ON Y.AF_APL_ID = Z.AF_APL_ID
	AND Y.AF_APL_ID_SFX = Z.AF_APL_ID_SFX
	AND Z.AC_STA_GA14 = 'A'
WHERE Z.AC_LON_STA_TYP IN ('CR','DA','FB','IA','ID','IG','IM','RF','RP','UA','UB')
AND (Y.AF_CUR_LON_SER_AGY IN ('700121','700191','700004','700789','700190','700079')
	OR Y.AF_CUR_LON_OPS_LDR = '828476')
)
GROUP BY DF_PRS_ID_BR;


DISCONNECT FROM DB2;

*RSUBMIT;
/*do the merge of the sub selects here*/
PROC SQL;
CREATE TABLE LNSALE AS
SELECT *
FROM MAIN A
LEFT OUTER JOIN ONELINK B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
LEFT OUTER JOIN OTHSVC D
	ON A.BF_SSN = D.DF_PRS_ID_BR
;
QUIT;

endrsubmit  ;
DATA LNSALE; SET WORKLOCL.LNSALE; RUN;

/*****************APPLY LOAN-LEVEL EDITS & CREATE A REJECTS DATASET******************/
DATA RD2SELL;
SET LNSALE;
/*FULLY DISBURSED*/
IF FUL_DSB_IND = 'Y' AND MAX_ACT_DSB <= '09SEP2003'D;
RUN;

/*APPLY BORROWER-LEVEL EDITS:  $50 MINIMUM, USAF, AND SLMA LENDER EDITS */
PROC SQL;
CREATE TABLE RD2SELL_TMP AS
SELECT A.*
	,B.LT50
	,D.ANY_USB_WAMU
	,E.ALL_USB_WAMU
FROM RD2SELL A
/*FLAG BORROWERS BELOW 50 DOLLAR MINIMUM*/
LEFT OUTER JOIN
	(SELECT BF_SSN
		,SUM(LA_CUR_PRI) AS SUM
		,'Y' AS LT50
	FROM RD2SELL
	GROUP BY BF_SSN
	HAVING SUM <= 50 
	) B
	ON A.BF_SSN = B.BF_SSN
LEFT OUTER JOIN
/*FLAG BORROWERS IF ANY LOAN IS US BANK OR WASH MUTUAL*/
	(SELECT DISTINCT BF_SSN
	,'Y' AS ANY_USB_WAMU
	FROM RD2SELL
	WHERE LF_LON_CUR_OWN IN ('811698','829505')
	)D
	ON A.BF_SSN = D.BF_SSN
LEFT OUTER JOIN
/*FLAG BORROWERS IF EVERY LOAN IS US BANK OR WASH MUTUAL*/
	(SELECT DISTINCT BF_SSN
	,'Y' AS ALL_USB_WAMU
	FROM RD2SELL
	WHERE BF_SSN NOT IN
	(SELECT BF_SSN
	FROM RD2SELL
	WHERE LF_LON_CUR_OWN NOT IN ('811698'))
	)E
	ON A.BF_SSN = E.BF_SSN
;
QUIT;

DATA SLGE50 LT50 USAF MULTI_SVC;
SET RD2SELL_TMP;
LENGTH BUYER $ 8.;
/*REMOVE BWRS BELOW $50 MINIMUM*/
IF LT50 = 'Y' THEN OUTPUT LT50;
/*REMOVE BWRS WITH ANY USAF STILL IN THE POOL*/
/*ELSE IF HAS_USF = 'Y' THEN OUTPUT USAF;*/
/*ASSIGN INHOUSE SALES*/
ELSE IF (OTH_INH_IND = ' ' AND OTH_NLN_IND = ' ' AND OTH_SLM_IND = ' ')
OR (OTH_INH_IND = 'Y' AND OTH_NLN_IND = ' ' AND OTH_SLM_IND = ' ')
OR (OTH_INH_IND = 'Y' AND OTH_NLN_IND = ' ' AND OTH_SLM_IND = 'Y' AND ANY_USB_WAMU = ' ')
OR (OTH_INH_IND = ' ' AND OTH_NLN_IND = ' ' AND OTH_SLM_IND = 'Y' AND ANY_USB_WAMU = ' ')
THEN DO; BUYER = 'INHOUSE'; OUTPUT SLGE50; END;
/*ASSIGN NELNET SALES*/
ELSE IF (OTH_INH_IND = ' ' AND OTH_NLN_IND = 'Y' AND OTH_SLM_IND = ' ')
OR (OTH_INH_IND = ' ' AND OTH_NLN_IND = 'Y' AND OTH_SLM_IND = 'Y' AND ANY_USB_WAMU = ' ')
THEN DO; BUYER = 'NELNET'; OUTPUT SLGE50; END;
/*ASSIGN SLMA SALES*/
ELSE IF (OTH_INH_IND = ' ' AND OTH_NLN_IND = ' ' AND OTH_SLM_IND = 'Y' AND ALL_USB_WAMU = 'Y')
THEN DO; BUYER = 'SLMA'; OUTPUT SLGE50; END;
/*REMOVE LOANS WITH MULTIPLE SERVICERS*/
ELSE DO; MULTI_SVC = 'Y'; OUTPUT MULTI_SVC; END;
RUN;

DATA SLGE50;
SET SLGE50;
WHERE BUYER = 'SLMA';
IF_BUY_OWN = 'N/A     ';
IF_LON_SLE = 'N/A     ';
IF_BUY_BND_ISS = 'N/A    ';
IF_SLL_OWN = '811698';
RUN;

/*****************************REPORTING*******************************/

/*REPORT 90 - SUMMARY REPORT*/
/*PROC PRINTTO PRINT=REPORT90;
RUN;*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=126;
DATA RPX;
SET SLGE50;
WHERE BUYER NE ' ';
RUN;
PROC SORT DATA=RPX;
BY BF_SSN;
RUN;
DATA RPX;
FORMAT IF_LON_SLE $9.;
SET RPX;
BY BF_SSN;
IF FIRST.BF_SSN THEN BWR_CT = 1;
ELSE BWR_CT = 0;
RUN;
PROC SORT DATA=RPX;
BY BUYER IF_BUY_OWN;
RUN;

TITLE "LOAN SALE";
TITLE2 "ESTIMATED SALE STATISTICS SUMMARY";
TITLE3 "US BANK SALE TO SLMA - NOV 2003";
TITLE4 "&RUNDATE";
FOOTNOTE  'JOB = ON DEMAND     REPORT = US BANK SLMA SALE ESTIMATE';

PROC REPORT DATA=RPX NOWD /*SPACING=1*/ HEADSKIP SPLIT='/' MISSING;
COLUMN BUYER IF_LON_SLE IF_SLL_OWN IF_BUY_OWN IF_BUY_BND_ISS BWR_CT N
LA_CUR_PRI LA_NSI_OTS TOTAL;
DEFINE BUYER / GROUP NOPRINT;
DEFINE IF_BUY_OWN / GROUP "SERVICER/ID" WIDTH=8;
DEFINE IF_LON_SLE / GROUP "LOAN/SALE ID" WIDTH=8;
DEFINE IF_SLL_OWN / GROUP "SELLER/ID" WIDTH=8;
DEFINE IF_BUY_BND_ISS / GROUP "BOND ID" WIDTH=4;
DEFINE BWR_CT / ANALYSIS SUM NOPRINT "BORROWERS" FORMAT=COMMA6. WIDTH=9;
DEFINE N / "LOANS" FORMAT=COMMA6.;
DEFINE LA_CUR_PRI / ANALYSIS SUM "TOTAL/PRINCIPAL/AMOUNT" FORMAT=DOLLAR18.2;
DEFINE LA_NSI_OTS / ANALYSIS SUM "TOTAL/INTEREST/AMOUNT" FORMAT=DOLLAR18.2;
DEFINE TOTAL / COMPUTED "TOTAL PRINCIPAL AND INTEREST AMOUNT" FORMAT=DOLLAR18.2;

COMPUTE TOTAL;
TOTAL = LA_CUR_PRI.SUM + LA_NSI_OTS.SUM;
ENDCOMP;

COMPUTE BEFORE BUYER;
LINE @1 ' ';
LINE @1 "BUYER = " BUYER $10.;
LINE @1 ' ';
ENDCOMP;

BREAK AFTER BUYER / OL SUMMARIZE SUPPRESS SKIP;

COMPUTE AFTER BUYER;
IF_LON_SLE = 'SUBTOTAL:';
LINE @1 ' ';
LINE @10 "BORROWERS WITH LOANS SOLD TO THIS SERVICER:  " BWR_CT.SUM COMMA6.;
LINE @1 ' ';
ENDCOMP;

COMPUTE AFTER;
IF_LON_SLE = 'TOTAL:';
LINE @1 ' ';
LINE @10 "TOTAL BORROWERS WITH LOANS SOLD:  " BWR_CT.SUM COMMA6.;
LINE @1 ' ';
ENDCOMP;

RBREAK AFTER / DOL DUL SUMMARIZE SUPPRESS SKIP;
RUN;
