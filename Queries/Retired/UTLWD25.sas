/*UTLWD25 SSA TOP OFFSET OBJECTIONS*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWD25.LWD25R2";
DATA _NULL_;
CALL SYMPUT('PRVDT',"'"||PUT(INTNX('DAY',TODAY(),-3653,'BEGINNING'),MMDDYY10.)||"'");
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'),MMDDYY10.));
RUN;
/*%SYSLPUT PRVDT = &PRVDT;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SSATO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_PRS_ID 
	,RTRIM(D.DM_PRS_1)||' '||D.DM_PRS_LST AS NAME
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.DC19_DOE_RIN B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN C
	ON A.BF_SSN = C.DF_PRS_ID
	AND C.DC_ADR = 'L'
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON C.DF_PRS_ID = D.DF_PRS_ID
INNER JOIN OLWHRM1.DC11_LON_FAT E
	ON A.BF_SSN = E.BF_SSN
	AND E.LC_TRX_TYP = 'FO'
INNER JOIN OLWHRM1.DC11_LON_FAT F
	ON A.BF_SSN = F.BF_SSN
	AND F.LC_TRX_TYP = 'FO'
WHERE A.LC_STA_DC10 = '03'
AND A.LD_CLM_ASN_DOE IS NULL
AND B.LD_DOE_REI_AGY <= &PRVDT
AND (
	 C.DC_DOM_ST IN 
				('ND','SD','NE','MN',
				 'IA','MO','AR')
	 OR 
	 C.DC_DOM_ST = 'MI' 
	 AND SUBSTR(C.DF_ZIP,1,3) IN 
					('497','492','487','486','485',
					 '484','483','482','481','480')
	 )
AND (
	 (MONTH(E.LD_TRX_EFF) = MONTH(F.LD_TRX_EFF) - 1
  	  AND YEAR(E.LD_TRX_EFF) = YEAR(F.LD_TRX_EFF))
	 OR 
	 (MONTH(E.LD_TRX_EFF) = 12 AND MONTH(F.LD_TRX_EFF) = 1
  	  AND YEAR(E.LD_TRX_EFF) = YEAR(F.LD_TRX_EFF) - 1 )
	 )
);
DISCONNECT FROM DB2;
/*ENDRSUBMIT;*/
/*DATA SSATO;*/
/*SET WORKLOCL.SSATO;*/
/*RUN;*/
PROC SORT DATA=SSATO;
BY DF_PRS_ID;
RUN;
PROC PRINTTO PRINT=REPORT2;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS NODATE CENTER PAGENO=1 PS=52 LS=96;
TITLE 'SSA TOP OFFSET OBJECTION';
TITLE2 'BORROWERS POTENTIALLY ELIGIBLE';
TITLE3 "RUN DATE: &RUNDATE";
FOOTNOTE 'JOB = UTLWD25	 	 REPORT = ULWD25.LWD25R2'; 
PROC CONTENTS DATA=SSATO OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 95*'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT ////////////////
		@46 "JOB = UTLWD25	 	 REPORT = ULWD25.LWD25R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=SSATO WIDTH=UNIFORM WIDTH=MIN;
VAR DF_PRS_ID NAME;
LABEL DF_PRS_ID = 'SSN';
RUN;

/*USED IN TESTING*/
/*PROC EXPORT DATA=SSATO*/
/*            OUTFILE= "C:\WINDOWS\TEMP\DEMO.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/