/*Application Sent Requiring 2008 Addendum*/
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE OCTOBER AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	PD01.DF_PRS_ID
	,PD01.DF_SPE_ACC_ID
	,PD01.DM_PRS_1
	,PD01.DM_PRS_LST
	,PD01.DC_ADR
	,PD01.DX_STR_ADR_1
	,PD01.DX_STR_ADR_2
	,PD01.DM_CT
	,PD01.DC_DOM_ST
	,PD01.DF_ZIP
	,PD01.DM_FGN_CNY
	,GA10.AC_LON_TYP
	,'MA2327' AS COST_CETNER_CODE
FROM OLWHRM1.PD01_PDM_INF PD01
INNER JOIN OLWHRM1.GA01_APP GA01
	ON PD01.DF_PRS_ID = GA01.DF_PRS_ID_BR
INNER JOIN OLWHRM1.GA10_LON_APP GA10
	ON GA01.AF_APL_ID = GA10.AF_APL_ID
WHERE GA10.AC_PRC_STA = 'A'	
	AND PD01.DC_ADR = 'L'
	AND PD01.DI_VLD_ADR = 'Y'
	AND 
	(
		(
			GA10.AC_LON_TYP IN ('SF', 'SU', 'PL','GB')
			AND DAYS(GA10.AD_PRC) BETWEEN DAYS('10/01/2007') AND DAYS('01/02/2008')
		)
	OR 
		(
			GA10.AC_LON_TYP IN ('CL')
			AND DAYS(GA10.AD_PRC) BETWEEN DAYS('10/01/2007') AND DAYS('01/28/2008')
		)
	)
	
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA OCTOBER; 
	SET WORKLOCL.OCTOBER; 
RUN;
*CALCULATE KEYLINE;
%MACRO KEY_CLC(TBL);
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%MACRO ASR2008_REPS(LTYPES,RNO);
PROC SQL;
CREATE TABLE REPDS AS 
	SELECT DISTINCT ACSKEY 
		,DM_PRS_1
		,DM_PRS_LST 
		,DX_STR_ADR_1 
		,DX_STR_ADR_2 
		,DM_CT 
		,DF_ZIP 
		,DC_DOM_ST 
		,DM_FGN_CNY 
		,COST_CETNER_CODE
	FROM OCTOBER 
	WHERE AC_LON_TYP IN (&LTYPES)
	ORDER BY COST_CETNER_CODE
		,DC_DOM_ST
	;
QUIT;
PROC EXPORT DATA=REPDS
	OUTFILE= "T:\SAS\Application Sent Requiring 2008 Addendum.R&RNO..xls" 
	DBMS=EXCEL2000 REPLACE;
RUN;
%MEND;
%KEY_CLC(OCTOBER);
%ASR2008_REPS(%STR('SF','SU'),2);
%ASR2008_REPS(%STR('PL','GB'),3);
%ASR2008_REPS(%STR('CL'),4);
