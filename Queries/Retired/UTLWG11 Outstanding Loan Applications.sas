/*Loans currently in provisional status.*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWG11.LWG11R2";*/

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PROVAPP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
		integer(A.df_prs_id_br)			as SSN
		,b.AF_APL_ID||b.AF_APL_ID_SFX	as CLUID
		,RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
										AS NAME
		,b.AC_PRC_STA
		,b.AD_PRC
		,a.AF_APL_OPS_SCL
		,C.IM_IST_FUL
FROM  OLWHRM1.GA01_APP A inner join OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
inner join 
	(SELECT DISTINCT 
	IF_IST
	,IM_IST_FUL
	FROM OLWHRM1.SC01_LGS_SCL_INF) C
	ON a.AF_APL_OPS_SCL = C.IF_IST
inner join OLWHRM1.PD01_PDM_INF D
	on A.df_prs_id_br = D.DF_PRS_ID
WHERE b.AC_PRC_STA = 'P'
);
DISCONNECT FROM DB2;

PROC SORT DATA = PROVAPP;
BY IM_IST_FUL AD_PRC SSN CLUID;
RUN;

endrsubmit  ;
DATA PROVAPP; 
SET WORKLOCL.PROVAPP; 
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

/*PROC PRINTTO PRINT=REPORT2;
RUN;*/
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=90;
PROC REPORT DATA=PROVAPP NOWD /*SPACING=1*/ HEADSKIP SPLIT='/';
TITLE 'OUTSTANDING LOAN APPLICATIONS';
TITLE2 "RUN DATE: &RUNDATE";
FOOTNOTE  'JOB = UTLWG11     REPORT = ULWG11.LWG11R2';
COLUMN IM_IST_FUL SSN NAME CLUID AD_PRC N;
DEFINE IM_IST_FUL / GROUP NOPRINT;
DEFINE SSN / DISPLAY LEFT FORMAT=SSN11. "BORROWER/SSN";
DEFINE NAME / DISPLAY WIDTH=30 "BORROWER/NAME";
DEFINE CLUID / DISPLAY "COMMONLINE/UNIQUE ID";
DEFINE AD_PRC / DISPLAY FORMAT=MMDDYY10. "DATE PROCESSED";
DEFINE N / NOPRINT;
COMPUTE BEFORE IM_IST_FUL;
	LINE " ";
	LINE "SCHOOL:   " IM_IST_FUL $40.;
	LINE " ";
ENDCOMP;
COMPUTE AFTER IM_IST_FUL;
	LINE " ";
	LINE "OUTSTANDING LOAN APPLICATIONS FOR ";
	LINE IM_IST_FUL $30. ":  " N COMMA7.;
	LINE " ";
ENDCOMP;
COMPUTE AFTER;
	LINE " ";
	LINE "TOTAL OUTSTANDING LOAN APPLICATIONS:  " N COMMA7.;
	LINE " ";
ENDCOMP;
RUN;