/*EDSB TOTAL ESTIMATED DISBURSEMENTS*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
%LET TABLIB = /sas/whse/progrevw;
/*%LET RPTLIB = T:\SAS;*/
/*%LET TABLIB = Q:\Process Automation\TabSAS;*/
FILENAME REPORT2 "&RPTLIB/ULWG17.LWG17R2";
FILENAME REPORT3 "&RPTLIB/ULWG17.LWG17R3";
FILENAME REPORT4 "&RPTLIB/ULWG17.LWG17R4";
FILENAME REPORT5 "&RPTLIB/ULWG17.LWG17R5";
FILENAME REPORT6 "&RPTLIB/ULWG17.LWG17R6";
FILENAME REPORT7 "&RPTLIB/ULWG17.LWG17R7";
FILENAME REPORT8 "&RPTLIB/ULWG17.LWG17R8";
FILENAME REPORT9 "&RPTLIB/ULWG17.LWG17R9";
FILENAME REPORT10 "&RPTLIB/ULWG17.LWG17R10";
FILENAME REPORT11 "&RPTLIB/ULWG17.LWG17R11";
FILENAME REPORT12 "&RPTLIB/ULWG17.LWG17R12";
FILENAME REPORT13 "&RPTLIB/ULWG17.LWG17R13";
FILENAME REPORT14 "&RPTLIB/ULWG17.LWG17R14";
FILENAME REPORT15 "&RPTLIB/ULWG17.LWG17R15";
FILENAME REPORT16 "&RPTLIB/ULWG17.LWG17R16";
FILENAME REPORT17 "&RPTLIB/ULWG17.LWG17R17";
FILENAME REPORT18 "&RPTLIB/ULWG17.LWG17R18";
FILENAME REPORT19 "&RPTLIB/ULWG17.LWG17R19";
FILENAME REPORT20 "&RPTLIB/ULWG17.LWG17R20";
FILENAME REPORT21 "&RPTLIB/ULWG17.LWG17R21";
FILENAME REPORT22 "&RPTLIB/ULWG17.LWG17R22";
FILENAME REPORT23 "&RPTLIB/ULWG17.LWG17R23";
FILENAME REPORT25 "&RPTLIB/ULWG17.LWG17R25";
FILENAME REPORT90 "&RPTLIB/ULWG17.LWG17R90";
FILENAME REPORT91 "&RPTLIB/ULWG17.LWG17R91";
FILENAME REPORT96 "&RPTLIB/ULWG17.LWG17R96";
FILENAME REPORT97 "&RPTLIB/ULWG17.LWG17R97";
FILENAME REPORT98 "&RPTLIB/ULWG17.LWG17R98";
FILENAME REPORT99 "&RPTLIB/ULWG17.LWG17R99";
DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
/*GET ACTIVE ECASLA CATEGORY*/
DATA _NULL_;
	INFILE "&TABLIB/ACTIVE_ECASLA_CAT" DSD DLM=',' LRECL=67000 ;
	INFORMAT ACTIVE_ECASLA $25.;
	INPUT ACTIVE_ECASLA;
	CALL SYMPUT('ACTIVE_ECASLA',TRIM(ACTIVE_ECASLA));
RUN;
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "&TABLIB/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY "," /*FFEL LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';
QUIT;
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;*/
/*RSUBMIT;*/
OPTION SYMBOLGEN;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE EDSB_A AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT E.IM_LDR_FUL
	,CASE 
		WHEN B.AF_DOE_LDR = '813760UT' THEN '813760'
		ELSE B.AF_DOE_LDR
	END AS AF_DOE_LDR
	,A.BF_SSN
	,F.DF_SPE_ACC_ID
	,CHAR(A.AN_SEQ) AS APP
	,CHAR(A.LN_LON_DSB_SEQ) AS DISB
	,A.LA_DSB - COALESCE(A.LA_DSB_CAN,0) AS NET_DSB
	,CHAR(YEAR(A.LD_DSB)) AS YR
	,CASE 
		WHEN (MONTH(A.LD_DSB)) < 10 
		THEN '0'||CHAR(MONTH(A.LD_DSB))
		ELSE CHAR(MONTH(A.LD_DSB))
		END AS MO
	,UCASE(MONTHNAME(A.LD_DSB)) AS MONAME
	,A.LD_DSB
	,B.AF_APL_ID
	,B.AF_DOE_SCL 
	,B.AF_LDR_BND_ISS
	,D.IM_SCL_FUL
	,B.IC_LON_PGM
	,A.LN_SEQ
	,A.LN_BR_DSB_SEQ
	,A.LD_DSB
	,G.LD_DSB_1
	,B.AD_SCL_LON_TRM_BEG  
	,B.AD_SCL_LON_TRM_END 
	,C.LF_LON_CUR_OWN
	,C.LD_LON_1_DSB
	,C.LD_TRM_BEG
	,C.LD_TRM_END
	,B.AF_CNL
	,B.AF_CNL_SFX
	,F.DM_PRS_1
	,F.DM_PRS_LST
	,H.DX_STR_ADR_1
	,H.DX_STR_ADR_2
	,H.DM_CT
	,CASE 
		WHEN H.DM_FGN_CNY = '' THEN H.DC_DOM_ST 
		ELSE H.DM_FGN_ST
	 END AS STATE 
	,H.DF_ZIP_CDE
	,H.DM_FGN_CNY
	,H.DI_VLD_ADR
	,I.LF_ECA_PGM_YR 
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL B 
	ON A.AF_APL_ID = B.AF_APL_ID
	AND B.AF_CNL NOT LIKE('%REALLO%')
INNER JOIN OLWHRM1.SC10_SCH_DMO D
	ON B.AF_DOE_SCL = D.IF_DOE_SCL 
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON B.AF_DOE_LDR = E.IF_DOE_LDR
LEFT OUTER JOIN OLWHRM1.LN10_LON C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ 
INNER JOIN OLWHRM1.PD10_PRS_NME F
	ON A.BF_SSN = F.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BB.AF_APL_ID
		,MIN(AA.LD_DSB) AS LD_DSB_1
	FROM OLWHRM1.LN15_DSB AA
	INNER JOIN OLWHRM1.AP03_MASTER_APL BB 
		ON AA.AF_APL_ID = BB.AF_APL_ID
	WHERE AA.LC_STA_LON15 IN ('1','3')
		AND AA.LA_DSB != COALESCE(AA.LA_DSB_CAN,0)
	GROUP BY BB.AF_APL_ID
	) G
	ON B.AF_APL_ID = G.AF_APL_ID
LEFT OUTER JOIN OLWHRM1.PD30_PRS_ADR H
	ON A.BF_SSN = H.DF_PRS_ID
	AND H.DC_ADR = 'L'
LEFT OUTER JOIN OLWHRM1.LNEC_LON_ECA I
	ON C.BF_SSN = I.BF_SSN
	AND C.LN_SEQ = I.LN_SEQ
	AND I.LN_ECA_SEQ = 1


WHERE A.LC_DSB_TYP = '1'
AND A.LC_STA_LON15 IN ('1','3')
AND (A.LA_DSB_CAN IS NULL OR A.LA_DSB_CAN <> A.LA_DSB)
AND A.LD_DSB_ROS_PRT IS NULL
AND A.LC_LDR_DSB_MDM = ' '

);
DISCONNECT FROM DB2;

PROC SQL;
CREATE TABLE MDISB AS
SELECT AF_APL_ID
	,MAX(LD_DSB) AS MAX_DISB_DT
FROM EDSB_A
GROUP BY AF_APL_ID
;

CREATE TABLE EDSB_INI AS 
SELECT DISTINCT A.AF_DOE_LDR
	,A.IM_LDR_FUL
	,TRIM(A.YR)||'/'||TRIM(A.MO) AS YRMO
	,TRIM(A.MONAME)||' '||TRIM(A.YR) AS YRMONAME LENGTH=14
	,A.AF_DOE_SCL
	,A.IM_SCL_FUL
	,INPUT(A.YR,BEST12.) AS YR
	,INPUT(A.MO,BEST12.) AS MO
	,WEEK(A.LD_DSB) AS WK
	,A.IC_LON_PGM
	,A.APP
	,A.DISB
	,A.NET_DSB
	,A.DF_SPE_ACC_ID
	,A.BF_SSN
	,A.LN_BR_DSB_SEQ
	,A.LN_SEQ
	,A.LD_DSB
	,A.AF_APL_ID
	,A.LD_DSB_1
	,A.AD_SCL_LON_TRM_BEG  
	,A.AD_SCL_LON_TRM_END
	,A.LF_LON_CUR_OWN
	,A.LD_LON_1_DSB
	,A.LD_TRM_BEG
	,A.LD_TRM_END
	,A.AF_CNL
	,A.AF_CNL_SFX
	,A.DM_PRS_1
	,A.DM_PRS_LST
	,A.DX_STR_ADR_1
	,A.DX_STR_ADR_2
	,A.DM_CT
	,A.STATE 
	,A.DF_ZIP_CDE
	,A.DM_FGN_CNY
	,A.DI_VLD_ADR
	,A.LF_ECA_PGM_YR 
	,A.AF_LDR_BND_ISS
	,B.MAX_DISB_DT
FROM EDSB_A A
LEFT OUTER JOIN MDISB B
	ON A.AF_APL_ID = B.AF_APL_ID
;
QUIT;
/*ENDRSUBMIT;*/
/*DATA EDSB_INI;*/
/*	SET WORKLOCL.EDSB_INI;*/
/*RUN;*/
/***************************************************************************
* ECASLA PROCESSING
****************************************************************************/
DATA ECAS;
	SET EDSB_INI;
RUN;
DATA ECAT;
INFILE CARDS DSD MISSOVER;
INPUT CAT $ 1-10;
CARDS;
ECASLA I
ECASLA II 
NON ECASLA
PRE ECASLA
RUN;
/*DATA DSBECAT;*/
/*	SET ECAS;*/
/*	LENGTH CAT $ 10.;*/
/*	IF IC_LON_PGM NOT IN (&FFELP_LIST) THEN DO;*/
/*		CAT = '';*/
/*	END;*/
/*	ELSE DO;*/
/*		IF LF_ECA_PGM_YR = '0809' THEN */
/*			CAT = 'ECASLA I';*/
/*		ELSE IF LF_ECA_PGM_YR = '0910' THEN*/
/*			CAT = 'ECASLA II';*/
/*		ELSE IF LD_LON_1_DSB < MDY(7,1,2008) AND LD_TRM_END < MDY(7,1,2008) THEN*/
/*			CAT = 'PRE ECASLA';*/
/*		ELSE IF LD_TRM_END >= MDY(7,1,2008) THEN*/
/*			CAT = 'NON ECASLA';*/
/*	END;*/
/*RUN;*/
DATA DSBECAT (DROP=E1_1ST_DSB_LB E1_1ST_DSB_UB E1_LN_TRM_DT E1_MD_DSB_LB E1_MD_DSB_UB E1_MX_DSB_LB E1_MX_DSB_UB 
	E2_1ST_DSB_LB E2_1ST_DSB_UB E2_LN_TRM_DT E2_MD_DSB_LB E2_MD_DSB_UB E2_MX_DSB_LB E2_MX_DSB_UB );

	SET ECAS;
	LENGTH CAT $ 10.;

	/*ECASLA 1 DATE VARIABLE INITIALIZATION*/
	E1_1ST_DSB_LB = MDY(7,1,2008);
	E1_1ST_DSB_UB = MDY(7,1,2009);
	E1_LN_TRM_DT = MDY(7,1,2008);
	E1_MD_DSB_LB = MDY(5,1,2009);
	E1_MD_DSB_UB = MDY(7,1,2009);
	E1_MX_DSB_LB = MDY(7,1,2008);
	E1_MX_DSB_UB = MDY(9,30,2009);

	/*ECASLA 2 DATE VARIABLE INITIALIZATION*/
	E2_1ST_DSB_LB = MDY(5,1,2009);
	E2_1ST_DSB_UB = MDY(6,30,2010);
	E2_LN_TRM_DT = MDY(7,1,2009);
	E2_MD_DSB_LB = MDY(5,1,2009);
	E2_MD_DSB_UB = MDY(7,1,2010);
	E2_MX_DSB_LB = MDY(5,1,2009);
	E2_MX_DSB_UB = MDY(9,30,2010);

	/*IF LS DATA DOESN'T EXIST USE LO DATA*/
	DO;
		IF LD_LON_1_DSB = . THEN DO;
			LD_LON_1_DSB = LD_DSB_1;
		  	LD_TRM_BEG = AD_SCL_LON_TRM_BEG;
			LD_TRM_END = AD_SCL_LON_TRM_END;
		END;
	END;

	/*ASSIGN ECASLA CATEGORY*/
	IF IC_LON_PGM NOT IN (&FFELP_LIST) THEN DO;
		CAT = '';
	END;
	ELSE DO;
		IF LF_ECA_PGM_YR = '0809' OR 
			(
				E1_1ST_DSB_LB <= LD_LON_1_DSB <= E1_1ST_DSB_UB
				AND 
				(
					E1_LN_TRM_DT <= LD_TRM_BEG OR
					LD_TRM_BEG <= E1_LN_TRM_DT <= LD_TRM_END 					
				)
				AND 
				(
					E1_MX_DSB_LB <= MAX_DISB_DT <= E1_MX_DSB_UB OR
					E1_MD_DSB_LB <= LD_LON_1_DSB <= E1_MD_DSB_UB AND MAX_DISB_DT <= E1_MX_DSB_UB 
				)
			) THEN CAT = 'ECASLA I';
		ELSE IF LF_ECA_PGM_YR = '0910' OR 
			(
				E2_1ST_DSB_LB <= LD_LON_1_DSB <= E2_1ST_DSB_UB
				AND 
				(
					E2_LN_TRM_DT <= LD_TRM_BEG OR
					LD_TRM_BEG <= E2_LN_TRM_DT <= LD_TRM_END 					
				)
				AND 
				(
					E2_MX_DSB_LB <= MAX_DISB_DT <= E2_MX_DSB_UB OR
					E2_MD_DSB_LB <= LD_LON_1_DSB <= E2_MD_DSB_UB AND MAX_DISB_DT <= E2_MX_DSB_UB 
				)
			) THEN CAT = 'ECASLA II';
		ELSE IF LD_LON_1_DSB < E1_1ST_DSB_LB AND LD_TRM_END < E1_1ST_DSB_LB
			THEN CAT = 'PRE ECASLA';
		ELSE IF LD_LON_1_DSB >= E1_1ST_DSB_LB AND LD_TRM_END > E1_1ST_DSB_LB
			THEN CAT = 'NON ECASLA';
		ELSE 
			CAT = '';
	END;
RUN;
PROC SQL;
CREATE TABLE ECASDF AS
SELECT YR
	,MO
	,YRMONAME
	,DF_SPE_ACC_ID
	,AF_APL_ID
	,APP
	,LN_SEQ
	,LN_BR_DSB_SEQ
	,AF_DOE_SCL
	,IM_SCL_FUL
	,AF_DOE_LDR
	,IM_LDR_FUL
	,LF_LON_CUR_OWN
	,LD_LON_1_DSB 
	,LD_TRM_BEG 
	,IC_LON_PGM
	,LD_DSB
	,NET_DSB
	,MAX_DISB_DT
	,CAT
	,CATX('-','XXX','XX',SUBSTR(BF_SSN,6,4)) LENGTH=11 AS MASKED_SSN 
	,BF_SSN
	,AF_CNL
	,AF_CNL_SFX
	,DM_PRS_1
	,DM_PRS_LST
	,DX_STR_ADR_1
	,DX_STR_ADR_2
	,DM_CT
	,STATE 
	,DF_ZIP_CDE
	,DM_FGN_CNY
	,DI_VLD_ADR
	,'A' AS AC
FROM DSBECAT B
WHERE NET_DSB ^= 0
ORDER BY YR
	,MO
	,YRMONAME
	,CAT
	,IM_SCL_FUL
	,IM_LDR_FUL
	,DF_SPE_ACC_ID
	,AF_APL_ID
	,LN_BR_DSB_SEQ
;
QUIT;
PROC SQL;
CREATE TABLE ECASRD AS
SELECT YR 
	,MO 
	,YRMONAME 
	,CAT 
	,IM_SCL_FUL 
	,IM_LDR_FUL
	,AC 
	,COALESCE(SUM(NET_DSB),0) AS DISB_AMT
	,COUNT(LN_BR_DSB_SEQ) AS DISBS
FROM ECASDF B
GROUP BY AC
	,YR 
	,MO 
	,YRMONAME 
	,CAT 
	,IM_SCL_FUL 
	,IM_LDR_FUL
ORDER BY AC
	,YR 
	,MO 
	,YRMONAME 
	,CAT 
	,IM_SCL_FUL 
	,IM_LDR_FUL
;
QUIT;
/***************************************************************************
* STANDARD REPORT PROCESSING
****************************************************************************/
DATA EDSB_INI;
	ATTRIB AF_DOE_LDR LENGTH=$12;
	FORMAT AF_DOE_LDR $12.;
	SET EDSB_INI;
	IF INDEX(AF_LDR_BND_ISS,'UBS') > 0 THEN 
		AF_DOE_LDR = CATT(AF_DOE_LDR,'UBS');
	ELSE  
		AF_DOE_LDR = AF_DOE_LDR;
RUN;
DATA EDSB_INI AZRO;
	SET EDSB_INI;
	IF NET_DSB = 0 THEN
		OUTPUT AZRO;
	ELSE 
		OUTPUT EDSB_INI;
RUN;
%MACRO SPLIT_TABLE(TAB,SLCT,GRPBY);
PROC SQL;
CREATE TABLE &TAB AS
SELECT DISTINCT &SLCT
FROM EDSB_INI
GROUP BY &GRPBY ;
QUIT;
%MEND SPLIT_TABLE;
%MACRO PROCIT(TAB,TAB2);
%LET DSID=%SYSFUNC(OPEN(WORK.&TAB));
%LET HASOBS=%SYSFUNC(ATTRN(&DSID,ANY));
%LET RC=%SYSFUNC(CLOSE(&DSID));
%IF &HASOBS=1 %THEN
	%DO;
/***************************************************************************
* CALCUALTE WEEKS FOR DATE RANGE SELECTED AND REPORTING
****************************************************************************/
/*GET THE ABSOLUTE VALUE OF DIFFERENCE IN YEARS BETWEEN THE MIN YEAR AND THE CURRENT YEAR*/
PROC SQL NOPRINT;
SELECT ABS(YEAR(TODAY()) - MIN(INPUT(SUBSTR(YRMO,1,4),BEST12.))) 
INTO : MINYEAR 
FROM &TAB;
QUIT;
/*GET THE ABSOLUTE VALUE OF DIFFERENCE IN YEARS BETWEEN THE MAX YEAR AND THE CURRENT YEAR*/
PROC SQL NOPRINT;
SELECT ABS(YEAR(TODAY()) - MAX(INPUT(SUBSTR(YRMO,1,4),BEST12.)))
INTO : MAXYEAR
FROM &TAB;
QUIT;
/*CREATE A REAL START AND END POINT USING THE VALUES CALCULATED ABOVE*/
DATA _NULL_;
	CALL SYMPUT('YR_FRST_DAY',INTNX('YEAR',TODAY(),-&MINYEAR,'BEGINNING'));
	CALL SYMPUT('YR_LAST_DAY',INTNX('YEAR',TODAY(),+&MAXYEAR,'END'));
RUN;
/*GET A LIST OF ALL THE DAYS IN THE GIVEN DATE RANGE*/
DATA WEEKS;
FORMAT I MMDDYY10.;
DO I = &YR_FRST_DAY TO &YR_LAST_DAY;
	OUTPUT;
END;
RUN;
/*CREATE JOIN VARIABLES*/
DATA WEEKS;
SET WEEKS;
	YR = YEAR(I);
	MO = MONTH(I);
	WK = WEEK(I);
RUN;
/*BRING TO A WEEK LEVEL*/
PROC SORT DATA=WEEKS NODUPKEY;BY YR MO WK;RUN;
/*CALCULATE THE DISPLAY WEEK*/
DATA WEEKS (DROP=I);
SET WEEKS;
BY YR MO;
IF FIRST.MO THEN DISP_WK=1;
	ELSE DISP_WK+1;
RUN;
/*PUT IT ALL TOGETHER*/
PROC SQL;
CREATE TABLE &TAB2 AS 
SELECT DISTINCT A.*
	,B.DISP_WK
FROM &TAB A
LEFT OUTER JOIN WEEKS B
	ON A.YR = B.YR
	AND A.MO = B.MO
	AND A.WK = B.WK
;
QUIT;
	%END;
	%else %do;
proc sql;
create table &tab2 as
select  a.*
	,. as disp_wk
from &tab a;
quit;
	%end;
%MEND PROCIT;
%MACRO PRINT(LENDER=,REPNO=);

DATA EDSBPR;
LENGTH YRMONAME $14.;
FORMAT YRMONAME $14.;
SET EDSB2;
WHERE AF_DOE_LDR = "&LENDER" ;
RUN;

PROC SORT DATA=EDSBPR;
BY YR MO AF_DOE_SCL DISP_WK ;
RUN;

PROC PRINTTO PRINT=REPORT&REPNO NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
OPTIONS CENTER NODATE NONUMBER PAGENO=1 NOBYLINE;

TITLE  "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "ANTICIPATED DISBURSEMENT TOTALS";
TITLE3 "LENDER: &LENDER";
TITLE4 "&RUNDATE";
PROC CONTENTS DATA=EDSBPR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWG17     REPORT = ULWG17.LWG17R&REPNO";
   END;
RETURN;
RUN;
TITLE  "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "ANTICIPATED DISBURSEMENT TOTALS";
TITLE3 "LENDER: #byvaL1 - #byvaL2";
TITLE4 "&RUNDATE";
FOOTNOTE "JOB = UTLWG17     REPORT = ULWG17.LWG17R&REPNO";
PROC REPORT DATA=EDSBPR NOWD SPACING=2 HEADSKIP SPLIT='~';
BY AF_DOE_LDR IM_LDR_FUL;
	COLUMN YRMO YRMONAME DISP_WK IC_LON_PGM AF_DOE_SCL IM_SCL_FUL DSB_CT SUM;
	DEFINE YRMO / ORDER NOPRINT;
	DEFINE YRMONAME / GROUP "MONTH";
	DEFINE DISP_WK / GROUP "WEEK #" WIDTH = 6;
	DEFINE IC_LON_PGM / GROUP "LOAN TYPE" WIDTH=9;
	DEFINE AF_DOE_SCL / ORDER "SCHOOL ID" WIDTH=9; 
	DEFINE IM_SCL_FUL / ORDER "SCHOOL NAME" WIDTH=40;
	DEFINE DSB_CT / ANALYSIS "ANTICIPATED DISBURSEMENTS" FORMAT=COMMA6. WIDTH=14;
	DEFINE SUM / ANALYSIS "TOTAL DISBURSEMENT AMOUNT" FORMAT=DOLLAR14.2;
	COMPUTE AFTER DISP_WK;
		LINE @40 83*'-';
		LINE @ 40 'WEEKLY TOTALS' @101 DSB_CT.SUM COMMA6. @109 SUM.SUM DOLLAR14.2;
		LINE ' ';
	ENDCOMP;
	BREAK AFTER YRMONAME / SUMMARIZE SKIP SKIP DOL ;
	COMPUTE AFTER;
		LINE ' ';
		LINE ' ';
		LINE 'TOTAL ANTICIPATED DISBURSEMENTS       - ' @55 DSB_CT.SUM COMMA6.;
		LINE 'TOTAL ANTICIPATED DISBURSEMENT AMOUNT - ' @47 SUM.SUM DOLLAR14.2;
	ENDCOMP;
RUN;

PROC PRINTTO;
RUN;

%MEND PRINT;
/*CREATE ALL REPORTS EXCEPT UHEAA REPORT (R23)*/
%SPLIT_TABLE(EDSB,
	%STR(AF_DOE_LDR,IM_LDR_FUL,YRMO,YRMONAME,AF_DOE_SCL,IM_SCL_FUL,COUNT((BF_SSN||APP||DISB)) AS DSB_CT
		,SUM(NET_DSB) AS SUM,YR,MO,WK,IC_LON_PGM),
	%STR(AF_DOE_LDR,IM_LDR_FUL,YRMO,YRMONAME,AF_DOE_SCL,IM_SCL_FUL,YR,MO,WK,IC_LON_PGM)
	);
%PROCIT(EDSB,EDSB2);
/*CREATE 0 ANTICIPATED DISB REPORT 99*/
PROC SORT DATA = AZRO; 
	BY AF_DOE_LDR YR MO WK IM_SCL_FUL; 
RUN;
/*CREATE TOTALS DATASET FOR REPORT 98*/
PROC SQL;
CREATE TABLE LNDR_TOTALS AS
SELECT AA.IM_LDR_FUL
	,AA.CAT
	,COALESCE(SUM(NET_DSB),0) AS DISB_AMT
	,COUNT(LN_BR_DSB_SEQ) AS DISBS
FROM (
	SELECT A.CAT
		,B.IM_LDR_FUL
	FROM ECAT A
	FULL OUTER JOIN (
		SELECT DISTINCT IM_LDR_FUL
		FROM DSBECAT
		) B
		ON A.CAT = A.CAT
	) AA
LEFT OUTER JOIN DSBECAT B
	ON AA.CAT = B.CAT
	AND AA.IM_LDR_FUL = B.IM_LDR_FUL
GROUP BY AA.IM_LDR_FUL
	,AA.CAT
ORDER BY AA.IM_LDR_FUL
	,AA.CAT
;
QUIT;
/***************************************************************************
* CREATE ALL JOB REPORTS
****************************************************************************/
%PRINT(LENDER=811698,REPNO=2);
%PRINT(LENDER=813760,REPNO=3);
%PRINT(LENDER=813894,REPNO=4);
%PRINT(LENDER=817440,REPNO=5);
%PRINT(LENDER=817545,REPNO=6);
%PRINT(LENDER=817546,REPNO=7);
%PRINT(LENDER=819628,REPNO=8);
%PRINT(LENDER=829505,REPNO=9);
%PRINT(LENDER=820200,REPNO=10);
%PRINT(LENDER=822373,REPNO=11);
%PRINT(LENDER=829123,REPNO=12);
%PRINT(LENDER=829158,REPNO=13);
%PRINT(LENDER=830132,REPNO=14);
%PRINT(LENDER=830146,REPNO=15);
%PRINT(LENDER=830791,REPNO=16);
%PRINT(LENDER=832241,REPNO=17);
%PRINT(LENDER=833828,REPNO=18);
%PRINT(LENDER=817455,REPNO=19);
%PRINT(LENDER=817575,REPNO=20);
%PRINT(LENDER=834122,REPNO=21);
%PRINT(LENDER=833577,REPNO=22);
%PRINT(LENDER=828476,REPNO=23);
%PRINT(LENDER=828476UBS,REPNO=25);
PROC PRINTTO PRINT=REPORT96 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE 'ECASLA ANTICIPATED DISB SUMMARY REPORT';
TITLE2	"RUNDATE &RUNDATE";
FOOTNOTE   'JOB = UTLWG17  	 REPORT = ULWG17.LWG17R96';
PROC REPORT DATA=ECASRD NOWD HEADSKIP SPLIT='/';
	COLUMN YR MO YRMONAME CAT IM_SCL_FUL IM_LDR_FUL DISBS DISB_AMT;
	DEFINE YR / GROUP NOPRINT;
	DEFINE MO / GROUP NOPRINT;
	DEFINE YRMONAME / GROUP 'MONTH' WIDTH= 15;
	DEFINE CAT / GROUP 'CATEGORY' WIDTH = 10;
	DEFINE IM_SCL_FUL / GROUP 'SCHOOL/NAME' WIDTH=30 FLOW ;
	DEFINE IM_LDR_FUL / GROUP 'LENDER/NAME' WIDTH=30 FLOW ;
	DEFINE DISBS / ANALYSIS SUM 'TOTAL #/DISBURSED' WIDTH=10 FORMAT=COMMA8.;
	DEFINE DISB_AMT / ANALYSIS SUM 'TOTAL $/DISBURSED' WIDTH=18 FORMAT=DOLLAR16.2;
	BREAK AFTER IM_SCL_FUL / SUMMARIZE SUPPRESS SKIP OL;
RUN;
PROC REPORT DATA=ECASRD NOWD HEADSKIP SPLIT='/';
	COLUMN AC CAT DISBS DISB_AMT ;
	DEFINE AC / GROUP NOPRINT;
	DEFINE CAT / GROUP 'CATEGORY' WIDTH = 10;
	DEFINE DISBS / ANALYSIS SUM 'TOTAL #/DISBURSED' WIDTH=10 FORMAT=COMMA8.;
	DEFINE DISB_AMT / ANALYSIS SUM 'TOTAL $/DISBURSED' WIDTH=18 FORMAT=DOLLAR16.2;
	BREAK AFTER AC / SUMMARIZE SUPPRESS SKIP DOL DUL;
RUN;
PROC PRINTTO;
RUN;
DATA _NULL_;
FILE REPORT97 DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
	PUT
		"BORROWER FIRST NAME"
		','
		"BORROWER LAST NAME"
		','
		"ACCT #"
		','
		'SSN' 
		','
		'COMMONLINE UNIQUE ID'
		','
		'COMMONLINE SUFFIX'
		','
		"APP ID"
		','
		"LOAN SEQ #"
		','
		"SCHOOL CODE"
		','
		"SCHOOL NAME"
		','
		"LENDER ID"
		','
		"LENDER NAME"
		','
		"OWNER ID"
		','
		"1ST DISB DATE"
		','
		"LOAN PERIOD BEGIN"
		','
		"LOAN TYPE"
		','
		"DISB DATE"
		','
		"DISB AMT"
		','
		"MAX DISB DATE"
		','
		"ECASLA CATEGORY";
END;
SET WORK.ECASDF;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT AF_APL_ID $9. ;
   FORMAT LN_SEQ 6. ;
   FORMAT AF_DOE_SCL $8. ;
   FORMAT IM_SCL_FUL $40. ;
   FORMAT AF_DOE_LDR $8. ;
   FORMAT IM_LDR_FUL $40. ;
   FORMAT LF_LON_CUR_OWN $8. ;
   FORMAT LD_LON_1_DSB MMDDYY10. ;
   FORMAT LD_TRM_BEG MMDDYY10. ;
   FORMAT IC_LON_PGM $6. ;
   FORMAT LD_DSB MMDDYY10. ;
   FORMAT NET_DSB 16.2 ;
   FORMAT MAX_DISB_DT MMDDYY10. ;
   FORMAT CAT $10. ;
   FORMAT MASKED_SSN $11.;
   FORMAT AF_CNL $17.;
   FORMAT AF_CNL_SFX $2.;
   FORMAT DM_PRS_1 $13.;
   FORMAT DM_PRS_LST $23.;
DO;
   PUT DM_PRS_1 $ @;
   PUT DM_PRS_LST $ @;
   PUT DF_SPE_ACC_ID $ @;
   PUT MASKED_SSN $ @;
   PUT AF_CNL $ @;
   PUT AF_CNL_SFX $ @;
   PUT AF_APL_ID $ @;
   PUT LN_SEQ @;
   PUT AF_DOE_SCL $ @;
   PUT IM_SCL_FUL $ @;
   PUT AF_DOE_LDR $ @;
   PUT IM_LDR_FUL $ @;
   PUT LF_LON_CUR_OWN $ @;
   PUT LD_LON_1_DSB @;
   PUT LD_TRM_BEG @;
   PUT IC_LON_PGM $ @;
   PUT LD_DSB @;
   PUT NET_DSB @;
   PUT MAX_DISB_DT @;
   PUT CAT $ ;
END;
RUN;
PROC PRINTTO PRINT=REPORT98 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
OPTIONS CENTER NODATE NONUMBER PAGENO=1 NOBYLINE;
TITLE  "TOTAL ANTICIPATED DISBURSEMENT SUMMARY REPORT  ";
TITLE2 "&RUNDATE";
FOOTNOTE "JOB = UTLWG17     REPORT = ULWG17.LWG17R98";
PROC CONTENTS DATA=LNDR_TOTALS OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWG17     REPORT = ULWG17.LWG17R98";
   END;
RETURN;
RUN;
PROC REPORT DATA=LNDR_TOTALS NOWD HEADSKIP SPLIT='/';
	COLUMN IM_LDR_FUL CAT DISBS DISB_AMT ;
	DEFINE IM_LDR_FUL / GROUP 'LENDER/NAME' WIDTH=40 ;
	DEFINE CAT / GROUP 'CATEGORY' WIDTH = 10;
	DEFINE DISBS / ANALYSIS 'TOTAL #/DISBURSED' WIDTH=10 FORMAT=COMMA8.;
	DEFINE DISB_AMT / ANALYSIS 'TOTAL $/DISBURSED' WIDTH=18 FORMAT=DOLLAR16.2;
	BREAK AFTER IM_LDR_FUL / SUMMARIZE SUPPRESS SKIP OL;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT99 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
OPTIONS CENTER NODATE NONUMBER PAGENO=1 NOBYLINE;
TITLE  "TOTAL ANTICIPATED DISBURSEMENT REPORT";
TITLE2 "ACCOUNTS WITH A $0.00 ANTICIPATED DISBURSEMENT AMOUNT";
TITLE3 "&RUNDATE";
FOOTNOTE "JOB = UTLWG17     REPORT = ULWG17.LWG17R99";
PROC CONTENTS DATA=AZRO OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWG17     REPORT = ULWG17.LWG17R99";
   END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=AZRO WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_DSB MMDDYY10.;
VAR DF_SPE_ACC_ID
	AF_APL_ID
	LN_SEQ
	LD_DSB
	IC_LON_PGM;
LABEL DF_SPE_ACC_ID = 'ACCOUNT #'
	AF_APL_ID = 'APP ID #'
	LN_SEQ = 'LOAN SEQ'
	LD_DSB = 'DISBURSEMENT DATE'
	IC_LON_PGM = 'LOAN TYPE';
RUN;
PROC PRINTTO;
RUN;
%MACRO R90_AND_R91(RNO,ADR_IND);
DATA R9X;
SET WORK.ECASDF;
WHERE CAT = "&ACTIVE_ECASLA" 
	AND DI_VLD_ADR = "&ADR_IND" 
	AND LD_DSB > MDY(7,15,2009);
RUN;
PROC SORT DATA=R9X;
	BY BF_SSN;
RUN;
DATA _NULL_;
	SET R9X;
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT YR BEST12. ;
	   FORMAT MO BEST12. ;
	   FORMAT YRMONAME $14. ;
	   FORMAT DF_SPE_ACC_ID $10. ;
	   FORMAT AF_APL_ID $9. ;
	   FORMAT APP $6. ;
	   FORMAT LN_SEQ 6. ;
	   FORMAT LN_BR_DSB_SEQ 6. ;
	   FORMAT AF_DOE_SCL $8. ;
	   FORMAT IM_SCL_FUL $40. ;
	   FORMAT AF_DOE_LDR $8. ;
	   FORMAT IM_LDR_FUL $40. ;
	   FORMAT LF_LON_CUR_OWN $8. ;
	   FORMAT LD_LON_1_DSB MMDDYY10. ;
	   FORMAT LD_TRM_BEG MMDDYY10. ;
	   FORMAT IC_LON_PGM $6. ;
	   FORMAT LD_DSB MMDDYY10. ;
	   FORMAT NET_DSB 16.2 ;
	   FORMAT MAX_DISB_DT BEST12. ;
	   FORMAT CAT $10. ;
	   FORMAT MASKED_SSN $11. ;
	   FORMAT AF_CNL $17. ;
	   FORMAT AF_CNL_SFX $2. ;
	   FORMAT DM_PRS_1 $13. ;
	   FORMAT DM_PRS_LST $23. ;
	   FORMAT DX_STR_ADR_1 $30. ;
	   FORMAT DX_STR_ADR_2 $30. ;
	   FORMAT DM_CT $20. ;
	   FORMAT STATE $15. ;
	   FORMAT DF_ZIP_CDE $17. ;
	   FORMAT DM_FGN_CNY $25. ;
	   FORMAT DI_VLD_ADR $1. ;
	   FORMAT AC $1. ;
	   FORMAT BF_SSN $9.;
	IF _N_ = 1 THEN DO;
	   PUT "DM_PRS_1"
			','
			"DM_PRS_LST"
			','
			"DX_STR_ADR_1"
			','
			"DX_STR_ADR_2"
			','
			"DM_CT"
			','
			"STATE"
			','
			"DF_ZIP_CDE"
			','
			"DM_FGN_CNY"
			','
			"DF_SPE_ACC_ID"
			','
			"BF_SSN"
			','
			"AF_CNL"
			','
			"AF_CNL_SFX"
			','
			"AF_APL_ID"
			','
			"APP"
			','
			"LD_DSB"
			','
			"NET_DSB"
		;
	END;
	DO;
		PUT DM_PRS_1 $ @;
		PUT DM_PRS_LST $ @;
		PUT DX_STR_ADR_1 $ @;
		PUT DX_STR_ADR_2 $ @;
		PUT DM_CT $ @;
		PUT STATE $ @;
		PUT DF_ZIP_CDE $ @;
		PUT DM_FGN_CNY $ @;
		PUT DF_SPE_ACC_ID $ @;
		PUT BF_SSN $ @;
		PUT AF_CNL $ @;
		PUT AF_CNL_SFX $ @;
		PUT AF_APL_ID $ @;
		PUT APP $ @;
		PUT LD_DSB @;
		PUT NET_DSB ;
	END;
RUN;
%MEND R90_AND_R91;
%R90_AND_R91(90,Y);
%R90_AND_R91(91,N);
/******************************************************************
* TESTFILE
********************************************************************/
DATA G17_DET;
SET EDSB_INI AZRO;
RUN;
DATA _NULL_;
SET  WORK.G17_DET;
FILE 'T:\SAS\UTLWG17_DETAIL.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT AF_DOE_LDR $12. ;
	FORMAT IM_LDR_FUL $40. ;
	FORMAT YRMO $24. ;
	FORMAT YRMONAME $14. ;
	FORMAT AF_DOE_SCL $8. ;
	FORMAT IM_SCL_FUL $40. ;
	FORMAT YR BEST12. ;
	FORMAT MO BEST12. ;
	FORMAT WK BEST12. ;
	FORMAT IC_LON_PGM $6. ;
	FORMAT APP $6. ;
	FORMAT DISB $6. ;
	FORMAT NET_DSB 16.2 ;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT BF_SSN $9. ;
	FORMAT LN_BR_DSB_SEQ 6. ;
	FORMAT LN_SEQ 6. ;
	FORMAT LD_DSB DATE9. ;
	FORMAT AF_APL_ID $9. ;
	FORMAT LD_DSB_1 DATE9. ;
	FORMAT AD_SCL_LON_TRM_BEG DATE9. ;
	FORMAT AD_SCL_LON_TRM_END DATE9. ;
	FORMAT LF_LON_CUR_OWN $8. ;
	FORMAT LD_LON_1_DSB DATE9. ;
	FORMAT LD_TRM_BEG DATE9. ;
	FORMAT LD_TRM_END DATE9. ;
	FORMAT AF_CNL $17. ;
	FORMAT AF_CNL_SFX $2. ;
	FORMAT DM_PRS_1 $13. ;
	FORMAT DM_PRS_LST $23. ;
	FORMAT DX_STR_ADR_1 $30. ;
	FORMAT DX_STR_ADR_2 $30. ;
	FORMAT DM_CT $20. ;
	FORMAT STATE $15. ;
	FORMAT DF_ZIP_CDE $17. ;
	FORMAT DM_FGN_CNY $25. ;
	FORMAT DI_VLD_ADR $1. ;
	FORMAT LF_ECA_PGM_YR $4. ;
	FORMAT AF_LDR_BND_ISS $8. ;
	FORMAT MAX_DISB_DT BEST12. ;
IF _N_ = 1 THEN DO;
   PUT
      "AF_DOE_LDR"
   ','
      "IM_LDR_FUL"
   ','
      "YRMO"
   ','
      "YRMONAME"
   ','
      "AF_DOE_SCL"
   ','
      "IM_SCL_FUL"
   ','
      "YR"
   ','
      "MO"
   ','
      "WK"
   ','
      "IC_LON_PGM"
   ','
      "APP"
   ','
      "DISB"
   ','
      "NET_DSB"
   ','
      "DF_SPE_ACC_ID"
   ','
      "BF_SSN"
   ','
      "LN_BR_DSB_SEQ"
   ','
      "LN_SEQ"
   ','
      "LD_DSB"
   ','
      "AF_APL_ID"
   ','
      "LD_DSB_1"
   ','
      "AD_SCL_LON_TRM_BEG"
   ','
      "AD_SCL_LON_TRM_END"
   ','
      "LF_LON_CUR_OWN"
   ','
      "LD_LON_1_DSB"
   ','
      "LD_TRM_BEG"
   ','
      "LD_TRM_END"
   ','
      "AF_CNL"
   ','
      "AF_CNL_SFX"
   ','
      "DM_PRS_1"
   ','
      "DM_PRS_LST"
   ','
      "DX_STR_ADR_1"
   ','
      "DX_STR_ADR_2"
   ','
      "DM_CT"
   ','
      "STATE"
   ','
      "DF_ZIP_CDE"
   ','
      "DM_FGN_CNY"
   ','
      "DI_VLD_ADR"
   ','
      "LF_ECA_PGM_YR"
   ','
      "AF_LDR_BND_ISS"
   ','
      "MAX_DISB_DT";
END;
DO;
   PUT AF_DOE_LDR $ @;
   PUT IM_LDR_FUL $ @;
   PUT YRMO $ @;
   PUT YRMONAME $ @;
   PUT AF_DOE_SCL $ @;
   PUT IM_SCL_FUL $ @;
   PUT YR @;
   PUT MO @;
   PUT WK @;
   PUT IC_LON_PGM $ @;
   PUT APP $ @;
   PUT DISB $ @;
   PUT NET_DSB @;
   PUT DF_SPE_ACC_ID $ @;
   PUT BF_SSN $ @;
   PUT LN_BR_DSB_SEQ @;
   PUT LN_SEQ @;
   PUT LD_DSB @;
   PUT AF_APL_ID $ @;
   PUT LD_DSB_1 @;
   PUT AD_SCL_LON_TRM_BEG @;
   PUT AD_SCL_LON_TRM_END @;
   PUT LF_LON_CUR_OWN $ @;
   PUT LD_LON_1_DSB @;
   PUT LD_TRM_BEG @;
   PUT LD_TRM_END @;
   PUT AF_CNL $ @;
   PUT AF_CNL_SFX $ @;
   PUT DM_PRS_1 $ @;
   PUT DM_PRS_LST $ @;
   PUT DX_STR_ADR_1 $ @;
   PUT DX_STR_ADR_2 $ @;
   PUT DM_CT $ @;
   PUT STATE $ @;
   PUT DF_ZIP_CDE $ @;
   PUT DM_FGN_CNY $ @;
   PUT DI_VLD_ADR $ @;
   PUT LF_ECA_PGM_YR $ @;
   PUT AF_LDR_BND_ISS $ @;
   PUT MAX_DISB_DT ;
END;
RUN;
