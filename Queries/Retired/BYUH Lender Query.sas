LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
OPTIONS MPRINT SYMBOLGEN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE GUARS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.AF_APL_ID||A.AF_APL_ID_SFX							AS CLID,
	B.af_cur_apl_ops_ldr									AS LENDER,
	D.im_ist_ful											AS LNAME,
	B.af_apl_ops_scl										AS SCHOOL,
	C.im_ist_ful											AS SNAME,
	A.aa_gte_lon_amt										AS GROSS,
	A.aa_tot_rfd											AS REFUND,
	A.aa_tot_can											AS CANCEL,
	A.ac_lon_typ											AS TYPE
FROM	OLWHRM1.GA10_LON_APP A INNER JOIN
		OLWHRM1.GA01_APP B ON
			A.af_apl_id = B.af_apl_id AND
			A.ac_prc_sta = 'A' INNER JOIN
		OLWHRM1.SC01_LGS_SCL_INF C ON
			B.af_apl_ops_scl = C.if_ist INNER JOIN
		OLWHRM1.LR01_LGS_LDR_INF D ON
			B.af_cur_apl_ops_ldr = D.if_ist

WHERE	A.ac_lon_typ <> 'CL'
		AND A.ad_prc >= '07-01-2001'
		and B.af_apl_ops_scl = '00160600' and
		(A.aa_tot_can IS NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd > 0 OR
		 A.aa_tot_can IS NOT NULL AND A.aa_gte_lon_amt - A.aa_tot_rfd - A.aa_tot_can > 0)
);
DISCONNECT FROM DB2;

ENDRSUBMIT;
DATA GUARS;
	SET WORKLOCL.GUARS;
	IF CANCEL = . THEN CANCEL = 0;
	NETAMT = GROSS - REFUND - CANCEL;
RUN;

PROC SQL;
CREATE TABLE LENDER AS
SELECT	LNAME,
		LENDER,
		SUM(NETAMT)	AS AMOUNT,
		COUNT(CLID)	AS NUMBER
FROM	GUARS
GROUP BY LENDER, LNAME;
QUIT;
PROC SORT DATA = LENDER;
BY LNAME;
RUN;
OPTIONS NODATE CENTER PAGENO=1 LS=80;

PROC PRINT DATA = LENDER NOOBS SPLIT='/';
VAR LNAME
	LENDER
	NUMBER
	AMOUNT;
SUM 	NUMBER
		AMOUNT;
FORMAT	AMOUNT DOLLAR13.
		NUMBER COMMA8.;
LABEL	LNAME = 'LENDER NAME'
		LENDER = 'LENDER ID';
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	"GUARANTEES FOR BYU HAWAII BY LENDER";
TITLE3	'SINCE JULY 01, 2001';
FOOTNOTE	'JOB = ON DEMAND	REPORT = BYUH LENDER QUERY';
RUN;
