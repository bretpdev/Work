/*Loans currently in hold status.*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG10.LWG10R2";
libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	integer(A.df_prs_id_br)			as SSN,
		b.AF_APL_ID||b.AF_APL_ID_SFX	as CLUID,
		RTRIM(D.DM_PRS_LST)||', '||RTRIM(D.DM_PRS_1)||' '||RTRIM(D.DM_PRS_MID)
										AS NAME,
		b.AC_PRC_STA
		,d.DC_PRS_HLD_REA_1
		,d.DC_PRS_HLD_REA_2
		,d.DC_PRS_HLD_REA_3
		,d.DC_PRS_HLD_REA_4
		,d.DC_PRS_HLD_REA_5
		,b.AC_APL_SPS_REA_1
		,b.AC_APL_SPS_REA_2
		,b.AC_APL_SPS_REA_3
		,b.AC_APL_SPS_REA_4
		,b.AC_APL_SPS_REA_5
		,D.DF_SPE_ACC_ID
FROM  OLWHRM1.GA01_APP A inner join OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
left outer join OLWHRM1.PD01_PDM_INF D
	on A.df_prs_id_br = D.DF_PRS_ID
WHERE b.AC_PRC_STA in ('H','M')
ORDER BY A.df_prs_id_br, b.AF_APL_ID||b.AF_APL_ID_SFX
);
DISCONNECT FROM DB2;
endrsubmit  ;

DATA DEMO; 
SET WORKLOCL.DEMO; 
RUN;

/*PROCESS ACCOUNT HOLDS*/
DATA DEMOA (DROP=AC_APL_SPS_REA_1-AC_APL_SPS_REA_5);
SET DEMO;
RUN;

PROC TRANSPOSE DATA=DEMOA OUT=DEMOA (DROP = _NAME_ _LABEL_ RENAME=(COL1=DC_PRS_HLD_REA));
VAR DC_PRS_HLD_REA_1-DC_PRS_HLD_REA_5;
BY SSN CLUID NAME AC_PRC_STA DF_SPE_ACC_ID;
RUN;

DATA DEMOA;
SET DEMOA;
WHERE DC_PRS_HLD_REA NE ' ';
RUN;

DATA DEMOA (DROP = AC_PRC_STA DC_PRS_HLD_REA);
SET DEMOA;
ATTRIB PRIN_STA LENGTH=$16 HLD_REA LENGTH=$50;
IF AC_PRC_STA = 'M'	THEN PRIN_STA = 'ACCOUNT HOLD';
IF DC_PRS_HLD_REA = "A" THEN HLD_REA = "MISCELLANEOUS";
ELSE IF DC_PRS_HLD_REA = "B" THEN HLD_REA = "POSSIBLE FRAUD";
ELSE IF DC_PRS_HLD_REA = "D" THEN HLD_REA = "NEVER ENROLLED";
ELSE IF DC_PRS_HLD_REA = "F" THEN HLD_REA = "INQUIRY";
ELSE IF DC_PRS_HLD_REA = "G" THEN HLD_REA = "GRANT REVIEW";
ELSE IF DC_PRS_HLD_REA = "H" THEN HLD_REA = "DEFAULT REVIEW";
ELSE IF DC_PRS_HLD_REA = "I" THEN HLD_REA = "ENROLLMENT STATUS DISCREPANCY";
ELSE IF DC_PRS_HLD_REA = "J" THEN HLD_REA = "DEATH";
ELSE IF DC_PRS_HLD_REA = "L" THEN HLD_REA = "SSN INVESTIGATION";
ELSE IF DC_PRS_HLD_REA = "O" THEN HLD_REA = "INELIGIBLE BORROWER";
ELSE IF DC_PRS_HLD_REA = "Q" THEN HLD_REA = "INQUIRY COMPLETED NO LEGAL ACTION";
ELSE IF DC_PRS_HLD_REA = "R" THEN HLD_REA = "INQUIRY COMPLETED LEGAL ACTION TAKEN";
ELSE IF DC_PRS_HLD_REA = "S" THEN HLD_REA = "INQUIRY COMPLETED PURSUE ACCOUNT";
ELSE IF DC_PRS_HLD_REA = "T" THEN HLD_REA = "GRANT MISREPRESENTATION";
ELSE IF DC_PRS_HLD_REA = "X" THEN HLD_REA = "NAME DISCREPANCY";
ELSE IF DC_PRS_HLD_REA = "Y" THEN HLD_REA = "BIRTHDATE DISCREPANCY";
RUN;

/*PROCESS APPLICATION HOLDS*/
DATA DEMOB (DROP=DC_PRS_HLD_REA_1-DC_PRS_HLD_REA_5);
SET DEMO;
RUN;

PROC TRANSPOSE DATA=DEMOB OUT=DEMOB (DROP = _NAME_ _LABEL_ RENAME=(COL1=AC_APL_SPS_REA));
VAR AC_APL_SPS_REA_1-AC_APL_SPS_REA_5;
BY SSN CLUID NAME AC_PRC_STA DF_SPE_ACC_ID;
RUN;

DATA DEMOB;
SET DEMOB;
WHERE AC_APL_SPS_REA NE ' ';
RUN;

DATA DEMOB (DROP = AC_PRC_STA AC_APL_SPS_REA);
SET DEMOB;
ATTRIB PRIN_STA LENGTH=$16 HLD_REA LENGTH=$50;
IF AC_PRC_STA = 'H'	THEN PRIN_STA = 'APPLICATION HOLD';
IF AC_APL_SPS_REA = "A" THEN HLD_REA = "MISCELLANEOUS";
ELSE IF AC_APL_SPS_REA = "C" THEN HLD_REA = "CORRESPONDENCE ATTACHED";
ELSE IF AC_APL_SPS_REA = "E" THEN HLD_REA = "TERM BEGIN DATE";
ELSE IF AC_APL_SPS_REA = "F" THEN HLD_REA = "FAFSAS";
ELSE IF AC_APL_SPS_REA = "G" THEN HLD_REA = "GUARANTY AND PRINT DISCREPANCY";
ELSE IF AC_APL_SPS_REA = "H" THEN HLD_REA = "PRECLAIM/DEFAULT";
ELSE IF AC_APL_SPS_REA = "K" THEN HLD_REA = "INSTITUTION PENDING";
ELSE IF AC_APL_SPS_REA = "L" THEN HLD_REA = "SCHOOL CODE DISCREPANCY";
ELSE IF AC_APL_SPS_REA = "M" THEN HLD_REA = "MISCELLANEOUS COMMONLINE";
ELSE IF AC_APL_SPS_REA = "S" THEN HLD_REA = "SYSTEMLINK FOR ALL LENGTHS";
ELSE IF AC_APL_SPS_REA = "Y" THEN HLD_REA = "GRADE LEVEL DISCREPANCY";
ELSE IF AC_APL_SPS_REA = "Z" THEN HLD_REA = "FEDERAL LEGISLATION";
RUN;

DATA DEMOP;
SET DEMOA DEMOB;
RUN;

PROC SORT DATA=DEMOP;
BY DF_SPE_ACC_ID CLUID;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=126;
PROC PRINT NOOBS SPLIT='/' DATA=DEMOP WIDTH=UNIFORM WIDTH=MIN;
VAR DF_SPE_ACC_ID NAME CLUID PRIN_STA HLD_REA;
LABEL NAME = 'NAME' CLUID = 'COMMONLINE UNIQUE ID'
PRIN_STA = "HOLD TYPE" HLD_REA = "HOLD REASON" DF_SPE_ACC_ID='ACCT #';
TITLE 'HOLD STATUS REPORT';
TITLE2 "RUN DATE: &RUNDATE";
FOOTNOTE  'JOB = UTLWG10     REPORT = ULWG10.LWG10R2';
RUN;
PROC PRINTTO;
RUN;
