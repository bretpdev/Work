/*EDSB TOTAL ESTIMATED DISBURSEMENTS*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*FILENAME REPORT2 "&RPTLIB/ULWQ15.LWQ15R2";*/


*EOM PROCESSING;
/*SET DATE LIMIT TO CURRENT EOM + 6 MONTHS*/
DATA _NULL_;
CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY()-3,+6,'end'), MMDDYYD10.)||"'");
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT END = &END;
%SYSLPUT RUNDATE = &RUNDATE;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;
RSUBMIT;

OPTION SYMBOLGEN;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE EDSB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT 
RTRIM(X.YR)||'/'||RTRIM(X.MO) AS YRMO
,RTRIM(X.MONAME)||' '||RTRIM(X.YR) AS MONTH
,X.AF_DOE_SCL AS SCHOOLID
,X.IM_SCL_FUL AS FULLNAME
,COUNT( (X.BF_SSN||X.APP||X.DISB)) AS DSB_CT
,SUM(X.NET_DSB) AS SUM
/*,X.BF_SSN AS SSN*/
,X.MO AS MON
,X.YR AS YER
FROM
	(SELECT B.AF_DOE_LDR
		,E.IM_LDR_FUL
		,A.BF_SSN
		,CHAR(A.AN_SEQ) AS APP
		,CHAR(A.LN_LON_DSB_SEQ) AS DISB
		,A.LA_DSB - COALESCE(A.LA_DSB_CAN,0) AS NET_DSB
		,CHAR(YEAR(A.LD_DSB)) AS YR
		,CASE 
			WHEN (MONTH(A.LD_DSB)) < 10 
			THEN '0'||CHAR(MONTH(A.LD_DSB))
			ELSE CHAR(MONTH(A.LD_DSB))
			END AS MO
		,UCASE(MONTHNAME(A.LD_DSB)) AS MONAME
		,B.AF_DOE_SCL 
		,D.IM_SCL_FUL
	FROM OLWHRM1.LN15_DSB A
	INNER JOIN OLWHRM1.AP03_MASTER_APL B 
		ON A.AF_APL_ID = B.AF_APL_ID
	INNER JOIN OLWHRM1.SC10_SCH_DMO D
		ON B.AF_DOE_SCL = D.IF_DOE_SCL 
	INNER JOIN OLWHRM1.LR10_LDR_DMO E
		ON B.AF_DOE_LDR = E.IF_DOE_LDR 
	WHERE A.LC_DSB_TYP = '1'
	AND A.LC_STA_LON15 IN ('1','3')
	AND (A.LA_DSB_CAN IS NULL OR A.LA_DSB_CAN <> A.LA_DSB)
	AND A.LD_DSB_ROS_PRT IS NULL
	AND A.LC_LDR_DSB_MDM = ' '
	AND A.LD_DSB <= &END
	)X
GROUP BY X.YR, X.MO, X.MONAME, X.IM_SCL_FUL, X.AF_DOE_SCL/*, X.BF_SSN */
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA EDSB;
	SET WORKLOCL.EDSB;
RUN;

OPTIONS PAGENO=1 ORIENTATION=PORTRAIT;
OPTIONS PS=52 LS=96;


%MACRO PRINT1(REPNO=);

DATA EDSBPR;
LENGTH MONTH $14.;
FORMAT MONTH $14.;
SET EDSB;
RUN;

PROC SORT DATA=EDSBPR;
BY YER MON/*FULLNAME*/ SCHOOLID ;
RUN;

TITLE  "UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY";
TITLE2 "ANTICIPATED DISBURSEMENT TOTALS";
/*TITLE3 "LENDER: #byvaL1 - #byvaL2";*/
TITLE4 "&RUNDATE";
FOOTNOTE "JOB = UTLWQ15     REPORT = ULWQ15.LWQ15R&REPNO";

/*PROC PRINTTO PRINT=REPORT&REPNO;
RUN;*/
OPTIONS CENTER NODATE NONUMBER PAGENO=1 LS=90 NOBYLINE;
PROC CONTENTS DATA=EDSBPR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
   PUT // 90*'-';
   PUT      ////////
       @31 '**** NO OBSERVATIONS FOUND ****';
   PUT ////////
       @37 '-- END OF REPORT --';
   PUT ///////////////
   		@26 "JOB = UTLWQ15     REPORT = ULWQ15.LWQ15R2";
   END;
RETURN;
RUN;

PROC REPORT DATA=EDSBPR NOWD SPACING=2 HEADSKIP SPLIT='/';
/*BY YER MON ; */
COLUMN YRMO MONTH SCHOOLID FULLNAME DSB_CT SUM;/**/
DEFINE YRMO / ORDER NOPRINT;
DEFINE MONTH / ORDER NOPRINT;
/*DEFINE SSN / DISPLAY "SSN" WIDTH=10;*/
DEFINE SCHOOLID / DISPLAY "SCHOOL ID" WIDTH=9; 
DEFINE FULLNAME / DISPLAY "SCHOOL NAME" WIDTH=40;
DEFINE DSB_CT / ANALYSIS "ANTICIPATED DISBURSEMENTS" FORMAT=COMMA6. WIDTH=14;
DEFINE SUM / ANALYSIS "TOTAL DISBURSEMENT AMOUNT" FORMAT=DOLLAR14.2;

COMPUTE BEFORE MONTH;
LINE @1 'MONTH = ' MONTH $20.;
LINE ' ';
ENDCOMP;

BREAK AFTER MONTH / SUMMARIZE SKIP OL SUPPRESS; 

COMPUTE AFTER;
LINE ' ';
LINE ' ';
LINE 'TOTAL ANTICIPATED DISBURSEMENTS       - ' @55 DSB_CT.SUM COMMA6.;
LINE 'TOTAL ANTICIPATED DISBURSEMENT AMOUNT - ' @47 SUM.SUM DOLLAR14.2;
ENDCOMP;
RUN;

%MEND PRINT1;

%PRINT1(REPNO=2);

/*PROC EXPORT DATA=EDSBPR*/
/*            OUTFILE= "T:\SAS\DEMO.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/





