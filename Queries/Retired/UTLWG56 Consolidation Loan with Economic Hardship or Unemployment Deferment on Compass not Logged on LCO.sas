/*UTLWG56 - Consolidation Loan with Economic Hardship or Unemployment Deferment on Compass not Logged on LCO*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWG56.LWG56R2";
FILENAME REPORTZ "&RPTLIB/ULWG56.LWG56RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LCO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT AP1A.DF_LCO_PRS_SSN_BR
	,AP1A.AD_LCO_DSB_APV_STA
	,AP1A.AD_LCO_APL_RCV
	,AP1A.AD_LCO_APL_DSB
	,INTEGER(SUBSTR(LC10.LF_UND_LN_ACC_NUM,8,2)) AS LNSEQ 
	,AP1A.AN_LCO_APL_SEQ AS SEQ
	,C.DF_SPE_ACC_ID
FROM OLWHRM1.AP1A_LCO_APL AP1A
INNER JOIN OLWHRM1.LC10_UND_LN_INF LC10
	ON AP1A.DF_LCO_PRS_SSN_BR = LC10.DF_LCO_PRS_SSN_BR
	AND AP1A.AN_LCO_APL_SEQ = LC10.AN_LCO_APL_SEQ
	AND SUBSTR(LF_UND_LN_ACC_NUM,1,4) = 'L0UT'
INNER JOIN OLWHRM1.PD6A_LCO_PRS_DMO C
	ON AP1A.DF_LCO_PRS_SSN_BR = C.DF_LCO_PRS_SSN
WHERE AP1A.AD_LCO_APL_DSB IS NULL
AND AP1A.AD_LCO_APL_RCV IS NOT NULL
FOR READ ONLY WITH UR
);

CREATE TABLE HARDSHIP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	DF10.BF_SSN
	,DF10.LC_DFR_STA
	,CASE WHEN DF10.LC_DFR_TYP = '29'
		THEN 'Economic Hardship'
		WHEN DF10.LC_DFR_TYP = '13'
		THEN 'Unemployment'
	END AS LC_DFR_TYP
	,LN50.LN_SEQ
	,LN50.LN_DFR_OCC_SEQ
	,RTRIM(PD10.DM_PRS_LST) AS NAME
	,LN50.LD_DFR_BEG
	,LN50.LD_DFR_END
FROM	OLWHRM1.DF10_BR_DFR_REQ DF10
LEFT OUTER JOIN OLWHRM1.LN50_BR_DFR_APV LN50
	ON DF10.BF_SSN = LN50.BF_SSN
	AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM 
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON PD10.DF_PRS_ID = DF10.BF_SSN
WHERE DF10.LC_DFR_STA = 'A'
AND DF10.LC_DFR_TYP IN ('29','13')
AND LN50.LC_STA_LON50 = 'A'
FOR READ ONLY WITH UR
);

CREATE TABLE ARC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT AY10.BF_SSN
	,LN85.LN_SEQ
	,MAX(AY10.LD_ATY_REQ_RCV)  AS LD_ATY_REQ_RCV
FROM OLWHRM1.AY10_BR_LON_ATY AY10
INNER JOIN OLWHRM1.LN85_LON_ATY LN85
	ON LN85.BF_SSN = AY10.BF_SSN
	AND LN85.LN_ATY_SEQ = AY10.LN_ATY_SEQ
WHERE  AY10.PF_REQ_ACT = 'OC320'
GROUP BY AY10.BF_SSN,LN85.LN_SEQ
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
ENDRSUBMIT;

DATA LCO; SET WORKLOCL.LCO; RUN;
DATA HARDSHIP; SET WORKLOCL.HARDSHIP; RUN;
DATA ARC; SET WORKLOCL.ARC; RUN;

PROC SQL;
CREATE TABLE ARC2 AS
SELECT *
FROM LCO
WHERE  AD_LCO_DSB_APV_STA < (SELECT ARC.LD_ATY_REQ_RCV FROM ARC
						WHERE ARC.BF_SSN = LCO.DF_LCO_PRS_SSN_BR
						AND ARC.LN_SEQ = LCO.LNSEQ
						)
;
QUIT;

PROC SQL;
CREATE TABLE T1 AS
SELECT DISTINCT LCO.DF_LCO_PRS_SSN_BR
	,LCO.DF_SPE_ACC_ID
	,LCO.SEQ
	,NAME
	,LD_DFR_BEG
	,LD_DFR_END
	,LC_DFR_TYP
FROM LCO
INNER JOIN HARDSHIP
	ON HARDSHIP.BF_SSN = LCO.DF_LCO_PRS_SSN_BR
	AND HARDSHIP.LN_SEQ = LCO.LNSEQ
WHERE  (LCO.LNSEQ NOT IN (SELECT ARC2.LNSEQ FROM ARC2
						WHERE ARC2.DF_LCO_PRS_SSN_BR = LCO.DF_LCO_PRS_SSN_BR
						)
		OR AD_LCO_DSB_APV_STA IS NULL)
;
QUIT;

PROC SORT DATA=T1;
BY DF_SPE_ACC_ID;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS CENTER NODATE NUMBER PAGENO=1 PS=39 LS=127;
PROC PRINT NOOBS SPLIT='/' DATA=T1;
FORMAT LD_DFR_BEG MMDDYY10. LD_DFR_END MMDDYY10.;
VAR DF_SPE_ACC_ID SEQ NAME LC_DFR_TYP LD_DFR_BEG LD_DFR_END;
LABEL	DF_SPE_ACC_ID = 'ACCT #'
		SEQ = 'TOMD SEQ #'
		NAME = 'LAST NAME'
		LC_DFR_TYP = 'DERFERMENT TYPE'
		LD_DFR_BEG = 'DERFERMENT BEGIN DATE'
		LD_DFR_END = 'DEFERMENT END DATE'
;
TITLE 'Consol Loan with Econ Hardship/Unemp Def on Compass not Logged on LCO';
FOOTNOTE  'JOB = UTLWG56     REPORT = UTLWG56.LWG56R2';
RUN;
