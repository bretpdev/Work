*--------------------------------------------*
| UTLWO99 Imaged Paper MPNs with a Cert Loan |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO99.LWO99R2";
FILENAME REPORTZ "&RPTLIB/ULWO99.LWO99RZ";
DATA _NULL_;
	CALL SYMPUT('CUR_DT',"'"||PUT(INTNX('DAY',TODAY(),-1,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('YEARS_AGO_1',"'"||PUT(INTNX('YEAR',TODAY(),-1,'S'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT CUR_DT= &CUR_DT;
%SYSLPUT YEARS_AGO_1 = &YEARS_AGO_1;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE IPMWCL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID_BR
	,C.MAX_GMBSE_DT
	,D.MAX_GDMST_DT
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN (
	SELECT DF_PRS_ID
		,MAX(BD_ATY_PRF) AS MAX_GMBSE_DT
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'GMBSE'
	GROUP BY DF_PRS_ID
	) C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT DF_PRS_ID
		,MAX(BD_ATY_PRF) AS MAX_GDMST_DT
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'GDMST'
	GROUP BY DF_PRS_ID
	) D
	ON A.DF_PRS_ID_BR = D.DF_PRS_ID
WHERE B.AC_LON_TYP IN ('SF','SU')
	AND B.AC_PRC_STA NOT IN ('A','R','X')
	AND A.AF_APL_OPS_SCL != ''
	AND A.AI_SCL_SIG = 'Y'
	AND A.AX_SCL_SIG_IAD > &YEARS_AGO_1
	AND A.AD_IST_TRM_END > &CUR_DT
	AND A.AD_BR_SIG IS NULL
	AND 
	(
		B.AC_APL_SPS_REA_1 != 'A'  
		OR B.AC_APL_SPS_REA_2 != 'A'  
		OR B.AC_APL_SPS_REA_3 != 'A'  
		OR B.AC_APL_SPS_REA_4 != 'A'  
		OR B.AC_APL_SPS_REA_5 != 'A' 
	) 
	AND C.MAX_GMBSE_DT >= &YEARS_AGO_1
	AND DAYS(C.MAX_GMBSE_DT) > COALESCE(DAYS(D.MAX_GDMST_DT),DAYS(C.MAX_GMBSE_DT)-1)
	AND A.DF_PRS_ID_BR NOT IN (SELECT DF_PRS_ID_BR
								FROM OLWHRM1.CT30_CALL_QUE
								WHERE IF_WRK_GRP = 'MARRYAPP')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO99.LWO99RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA IPMWCL ;
	SET WORKLOCL.IPMWCL;
RUN;
DATA _NULL_;
SET WORK.IPMWCL;
QN = 'MARRYAPP';
V = '';
COMMENTS = 'SCHOOL CERT AND POSSIBLE PAPER MPN';
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DF_PRS_ID_BR $9. ;
FORMAT MAX_GMBSE_DT DATE9. ;
FORMAT MAX_GDMST_DT DATE9. ;
DO;
	PUT DF_PRS_ID_BR $ @;
	PUT QN $ @;
	PUT V $ @;
	PUT V $ @;
	PUT V $ @;
	PUT V $ @;
	PUT COMMENTS $;
END;
RUN;
