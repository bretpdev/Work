/*UTLWD24 QC CONFIRM IMAGED FBS WITH PAYMENT/INTEREST LESS THAN $50*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD24.LWD24R2";

DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
	 CALL SYMPUT('NODYS',DAY(INTNX('MONTH',TODAY(),0,'end')));
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE COIMPILT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,RTRIM(A.AF_APL_ID)||A.AF_APL_ID_SFX AS CLUID
	,SUM(A.LA_PAY_XPC) AS LA_PAY_XPC
	,SUM(A.LA_CLM_PRI) +
	 SUM(A.LA_CLM_INT) -
	 SUM(A.LA_PRI_COL) AS INT_CALC	
	,B.LR_CUR_INT	
	,C.DF_SPE_ACC_ID
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.DC02_BAL_INT B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.BF_SSN = C.DF_PRS_ID
WHERE A.LC_STA_DC10 = '03'
AND A.LD_CLM_ASN_DOE IS NULL
AND EXISTS
	(SELECT * 
	 FROM OLWHRM1.AY01_BR_ATY X
	 WHERE X.DF_PRS_ID = A.BF_SSN
	 AND X.PF_ACT = 'DD136'
	 AND X.BD_ATY_PRF BETWEEN &BEGIN AND &END)
GROUP BY A.BF_SSN
	,RTRIM(A.AF_APL_ID)||A.AF_APL_ID_SFX 
	,B.LR_CUR_INT,C.DF_SPE_ACC_ID
);

CREATE TABLE PDAY AS 
SELECT * 
FROM CONNECTION TO DB2(
SELECT DISTINCT A.DF_PRS_ID AS BF_SSN
	,A.BD_ATY_PRF
	,A.BF_LST_USR_AY01
	,B.DM_PRS_1
	,B.DM_PRS_LST
FROM OLWHRM1.AY01_BR_ATY A
INNER JOIN OLWHRM1.PD01_PDM_INF B
	ON A.DF_PRS_ID = B.DF_PRS_ID 	
WHERE A.PF_ACT = 'DD136'
AND A.BD_ATY_PRF BETWEEN &BEGIN AND &END
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA COIMPILT;
SET WORKLOCL.COIMPILT;
RUN;

DATA PDAY;
SET WORKLOCL.PDAY;
RUN;

PROC SORT DATA=COIMPILT;
BY BF_SSN CLUID;
RUN;

PROC SORT DATA=PDAY;
BY BF_SSN;
RUN;

/*CALCULATE AGENCY MO INTEREST*/
DATA COIMPILT;
SET COIMPILT;
INT1=ROUND((((INT_CALC*(LR_CUR_INT/100))/365)*&NODYS),.01);
RUN;

PROC SORT DATA=COIMPILT;
BY BF_SSN CLUID;
RUN;

/*SUM AGENCY MO INT AND MONTHLY PAYMENT OBLIGATION BY BORROWER*/
DATA COIMPILT ;
SET COIMPILT;
BY BF_SSN;
IF FIRST.BF_SSN THEN ICAL=0;
ICAL+INT1;
IF FIRST.BF_SSN THEN XCAL=0;
XCAL+LA_PAY_XPC;
IF LAST.BF_SSN THEN OUTPUT;
RUN;

/*IF THE AGENCY MONTHLY INTEREST IS > 50 THEN USE IT AS THE CALC VARIABLE*/
DATA COIMPILT (KEEP=ICAL XCAL BF_SSN CALVAR DF_SPE_ACC_ID) ;
SET COIMPILT;
IF ICAL > 50.00 THEN CALVAR = ROUND(ICAL,.01);
ELSE CALVAR = 50.00;
XCAL = ROUND(XCAL,.01);
RUN;

DATA COIMPILT;
SET COIMPILT;
/*IF EXPECTED PAYMENT < SUM OF AGENCY INT OR 50 THEN OUTPUT INFO TO REPORT */
IF XCAL LT CALVAR THEN OUTPUT;
RUN;

/*MERGE ALL INFO */
DATA COIMPILT;
MERGE PDAY (IN=A) COIMPILT (IN=B);
BY BF_SSN;
IF A AND B;
RUN;

PROC SORT DATA=COIMPILT;
	BY BF_LST_USR_AY01;
RUN;

DATA _NULL_;
	SET COIMPILT;
	USER = BF_LST_USR_AY01;
	ACT_DT = BD_ATY_PRF;
	DESCRIPTION = CATX(',',
		'BORROWER FIRST NAME = '||DM_PRS_1,
		'BORROWER LAST NAME = '||DM_PRS_LST,
		'EXPECTED PAYMENT AMOUNT = '||CATS(PUT(XCAL,12.2)),   
		'TOTAL SUM AGENCY MONTHLY INTEREST = '||CATS(PUT(ICAL,12.2)) 
	);
	FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT USER $10. ;
	FORMAT ACT_DT MMDDYY10. ;
	FORMAT DESCRIPTION $600. ;
	IF _N_ = 1 THEN DO;
		PUT "USER,ACT_DT,DESCRIPTION";
	END;
	DO;
	   PUT USER $ @;
	   PUT ACT_DT @;
	   PUT DESCRIPTION $ ;
	END;
RUN;
