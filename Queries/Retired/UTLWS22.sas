*--------------------------------------------------------------*
|UTLWS22 IN-SCHOOL CONSOL BORROWERS NEEDING EXPLANATION LETTER |
*--------------------------------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = C:\WINDOWS\TEMP;*/
FILENAME REPORT2 "&RPTLIB/ULWS22.LWS22R2";
FILENAME REPORTZ "&RPTLIB/ULWS22.LWS22RZ";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LCO_BASE AS
SELECT *
FROM CONNECTION TO DB2 ( 
SELECT DISTINCT A.DF_LCO_PRS_SSN_BR AS SSN
	,B.DF_SPE_ACC_ID
	,B.DM_LCO_PRS_1
	,B.DM_LCO_PRS_MI
	,B.DM_LCO_PRS_LST
	,B.DX_LCO_PRS_STR_1
	,B.DX_LCO_PRS_STR_2
	,B.DM_LCO_PRS_CT
	,B.DC_LCO_PRS_ST
	,B.DM_LCO_PRS_FGN_ST
	,B.DM_LCO_PRS_FGN_CNY
	,B.DF_LCO_PRS_ZIP
	,'MA2325' AS COST_CENTER_CODE
	,'L' AS DC_ADR
FROM OLWHRM1.AP1A_LCO_APL A
INNER JOIN OLWHRM1.PD6A_LCO_PRS_DMO B
	ON A.DF_LCO_PRS_SSN_BR = B.DF_LCO_PRS_SSN
INNER JOIN OLWHRM1.LN10_LON C
	ON B.DF_LCO_PRS_SSN = C.BF_SSN
WHERE A.AD_LCO_APL_RCV >= '03/20/2006' 
AND A.IC_LCO_DSB_APV_STA <> 'A'
AND A.IC_LCO_GTE_STA_OVR <> 'A'
AND C.LA_CUR_PRI > 0
FOR READ ONLY WITH UR
);

CREATE TABLE WVGRC AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT DISTINCT DF_PRS_ID AS SSN
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'WVGRC' 
	AND BD_ATY_PRF >= '03/20/2006'
UNION 
	SELECT DISTINCT BF_SSN AS SSN
	FROM OLWHRM1.AY10_BR_LON_ATY
	WHERE PF_REQ_ACT = 'WVGRC' 
	AND LD_ATY_REQ_RCV >= '03/20/2006'
FOR READ ONLY WITH UR
);

CREATE TABLE EXCLD AS
SELECT *
FROM CONNECTION TO DB2 (
	SELECT DISTINCT DF_PRS_ID AS SSN
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'MLXLC' 
UNION 
	SELECT DISTINCT BF_SSN AS SSN
	FROM OLWHRM1.AY10_BR_LON_ATY
	WHERE PF_REQ_ACT = 'MLXLC' 
FOR READ ONLY WITH UR
);

DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWS22.LWS22RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA LCO_BASE;SET WORKLOCL.LCO_BASE;RUN;*/
/*DATA WVGRC;SET WORKLOCL.WVGRC;RUN;*/
/*DATA EXCLD;SET WORKLOCL.EXCLD;RUN;*/
PROC SQL;
CREATE TABLE ISCBNEL AS
SELECT DISTINCT A.*
FROM LCO_BASE A
INNER JOIN WVGRC B
	ON A.SSN = B.SSN
WHERE NOT EXISTS (
	SELECT *
	FROM EXCLD X
	WHERE X.SSN = A.SSN
	)
;
QUIT;

DATA ISCBNEL;
SET ISCBNEL;
IF DC_LCO_PRS_ST = 'ZZ' THEN DO;
	DC_LCO_PRS_ST = '';
	STATE_IND = DM_LCO_PRS_FGN_ST;
	SVAR = 1;
	END;
ELSE DO;
	DM_LCO_PRS_FGN_ST = '';
	DM_LCO_PRS_FGN_CNY = '';
	STATE_IND = DC_LCO_PRS_ST;
	SVAR = 2;
	END;
RUN;

*CALCULATE KEYLINE;
DATA ISCBNEL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET ISCBNEL;
KEYSSN = TRANSLATE(SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

PROC SORT DATA=ISCBNEL;
BY SVAR STATE_IND;
RUN;
     
DATA _NULL_;
SET  WORK.ISCBNEL;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT SSN $9. ;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT DM_LCO_PRS_1 $13. ;
   FORMAT DM_LCO_PRS_MI $1. ;
   FORMAT DM_LCO_PRS_LST $23. ;
   FORMAT DX_LCO_PRS_STR_1 $40. ;
   FORMAT DX_LCO_PRS_STR_2 $40. ;
   FORMAT DM_LCO_PRS_CT $30. ;
   FORMAT DC_LCO_PRS_ST $3. ;
   FORMAT DM_LCO_PRS_FGN_ST $30. ;
   FORMAT DM_LCO_PRS_FGN_CNY $25. ;
   FORMAT DF_LCO_PRS_ZIP $17. ;
   FORMAT ACSKEY $18. ;
   FORMAT STATE_IND $30. ;
   FORMAT COST_CENTER_CODE $6. ;
IF _N_ = 1 THEN        
DO;
   PUT
   'SSN'
   ','
   'DF_SPE_ACC_ID'
   ','
   'DM_LCO_PRS_1'
   ','
   'DM_LCO_PRS_MI'
   ','
   'DM_LCO_PRS_LST'
   ','
   'DX_LCO_PRS_STR_1'
   ','
   'DX_LCO_PRS_STR_2'
   ','
   'DM_LCO_PRS_CT'
   ','
   'DC_LCO_PRS_ST'
   ','
   'DM_LCO_PRS_FGN_ST'
   ','
   'DM_LCO_PRS_FGN_CNY'
   ','
   'DF_LCO_PRS_ZIP'
   ','
   'ACSKEY'
   ','
   'STATE_IND'
   ','
   'COST_CENTER_CODE'
   ;
END;
DO;
   PUT SSN $ @;
   PUT DF_SPE_ACC_ID $ @;
   PUT DM_LCO_PRS_1 $ @;
   PUT DM_LCO_PRS_MI $ @;
   PUT DM_LCO_PRS_LST $ @;
   PUT DX_LCO_PRS_STR_1 $ @;
   PUT DX_LCO_PRS_STR_2 $ @;
   PUT DM_LCO_PRS_CT $ @;
   PUT DC_LCO_PRS_ST $ @;
   PUT DM_LCO_PRS_FGN_ST $ @;
   PUT DM_LCO_PRS_FGN_CNY $ @;
   PUT DF_LCO_PRS_ZIP $ @;
   PUT ACSKEY $ @;
   PUT STATE_IND $ @;
   PUT COST_CENTER_CODE $ ;
   ;
END;
RUN;
