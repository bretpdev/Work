*------------------------------------------------------------------------------------------*
| UTLWO63 - LOANS GUARANTEED WITH SCHOOL CERTIFICATION DATE AFTER THE LOAN PERIOD END DATE |
*------------------------------------------------------------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWO63.LWO63R2";
FILENAME REPORTZ "&RPTLIB/ULWO63.LWO63RZ";
DATA _NULL_;
     CALL SYMPUT('DAYS_AGO_1',"'"||PUT(INTNX('DAY',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
/*%SYSLPUT DAYS_AGO_1 = &DAYS_AGO_1;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE LGSCDA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT C.DF_SPE_ACC_ID
	,C.DM_PRS_1
	,C.DM_PRS_LST
	,A.AF_APL_OPS_SCL
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,B.AD_PRC
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID_BR = C.DF_PRS_ID
INNER JOIN OLWHRM1.GA14_LON_STA E
	ON B.AF_APL_ID = E.AF_APL_ID
	AND B.AF_APL_ID_SFX = E.AF_APL_ID_SFX
WHERE B.AD_PRC = &DAYS_AGO_1
AND B.AC_PRC_STA = 'A'
AND A.AD_IST_TRM_END < A.AD_SCL_SIG
AND E.AC_STA_GA14 = 'A'
AND E.AC_LON_STA_TYP NOT IN ('CA','PC','PF','PN')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWO63.LWO63RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA LGSCDA;*/
/*SET WORKLOCL.LGSCDA;*/
/*RUN;*/
PROC SORT DATA=LGSCDA;
BY AF_APL_OPS_SCL DF_SPE_ACC_ID;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 DATE PAGENO=1 CENTER;
TITLE 'SCHOOL CERT AFTER LOAN PERIOD END DATE';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWO63  	 REPORT = ULWO63.LWO63R2';
PROC CONTENTS DATA=LGSCDA OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      /////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT /////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWO63  	 REPORT = ULWO63.LWO63R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=LGSCDA WIDTH=UNIFORM WIDTH=MIN;
FORMAT AD_PRC MMDDYY10.;
VAR DF_SPE_ACC_ID DM_PRS_1 DM_PRS_LST AF_APL_OPS_SCL CLUID AD_PRC;
LABEL DF_SPE_ACC_ID = 'ACCOUNT NUMBER'
	DM_PRS_1 = 'BORROWER FIRST NAME'
	DM_PRS_LST = 'BORROWER LAST NAME'
	AF_APL_OPS_SCL = 'SCHOOL CODE'
	CLUID = 'UNIQUE ID'
	AD_PRC = 'GUARANTY DATE';
RUN;
PROC PRINTTO;
RUN;
