/*--------------------------------------*
| UTLWG88 CONSOL APPS WITH H8 HOLD CODE |
*--------------------------------------*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = C:\WINDOWS\TEMP;*/
FILENAME REPORT2 "&RPTLIB/ULWG88.LWG88R2";
FILENAME REPORT3 "&RPTLIB/ULWG88.LWG88R3";
FILENAME REPORT4 "&RPTLIB/ULWG88.LWG88R4";
FILENAME REPORTZ "&RPTLIB/ULWG88.LWG88RZ";
DATA _NULL_;
     CALL SYMPUT('DCRT',INTNX('DAY',TODAY(),-30,'BEGINNING'));
RUN;
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	FILENAME REPORTZ "&RPTLIB/&SQLRPT";
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CAWHC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_LCO_PRS_SSN_BR
	,D.DM_LCO_PRS_1
	,D.DM_LCO_PRS_MI
	,D.DM_LCO_PRS_LST
	,D.DX_LCO_PRS_STR_1
	,D.DX_LCO_PRS_STR_2
	,D.DX_LCO_PRS_STR_3
	,D.DM_LCO_PRS_CT
	,D.DC_LCO_PRS_ST
	,D.DF_LCO_PRS_ZIP
	,D.DM_LCO_PRS_FGN_ST
	,D.DM_LCO_PRS_FGN_CNY
	,D.DF_SPE_ACC_ID
	,B.BD_ATY_PRF 
	,C.LD_ATY_RSP 
/*----- FOR ACS KEYLINE -----*/
	,'L' AS DC_ADR
/*----- COST CENTER INFO -----*/
	,D.DC_LCO_PRS_ST AS STATE
	,'MA2324' AS COST_CENTER_CODE
	,CASE 
		WHEN D.DC_LCO_PRS_ST IN ('ZZ','') THEN 1
		ELSE 2
	 END AS SVAR
FROM OLWHRM1.AP1A_LCO_APL A
INNER JOIN OLWHRM1.PD6A_LCO_PRS_DMO D
	ON A.DF_LCO_PRS_SSN_BR = D.DF_LCO_PRS_SSN
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY B
	ON A.DF_LCO_PRS_SSN_BR = B.DF_PRS_ID
	AND B.PF_ACT = 'H8WVR'
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY C
	ON A.DF_LCO_PRS_SSN_BR = C.BF_SSN
	AND C.PF_REQ_ACT = 'H8WVR'
WHERE A.AC_LCO_ACC_STA = 'H8'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=UTLWG88.LWG88RZ);
QUIT;
/*ENDRSUBMIT;*/
/*DATA CAWHC;SET WORKLOCL.CAWHC;RUN;*/
/*----------------------- CALCULATE KEYLINE -----------------------*/
DATA CAWHC (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET CAWHC;
KEYSSN = TRANSLATE(DF_LCO_PRS_SSN_BR,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

PROC SORT DATA=CAWHC;BY SVAR DF_LCO_PRS_SSN_BR;RUN;

%MACRO FILE_CREATE(CRIT,REPNO);
DATA _NULL_;
SET  WORK.CAWHC ;
WHERE &CRIT ;
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_LCO_PRS_SSN_BR $9. ;
   FORMAT DM_LCO_PRS_1 $13. ;
   FORMAT DM_LCO_PRS_MI $1. ;
   FORMAT DM_LCO_PRS_LST $23. ;
   FORMAT DX_LCO_PRS_STR_1 $40. ;
   FORMAT DX_LCO_PRS_STR_2 $40. ;
   FORMAT DX_LCO_PRS_STR_3 $40. ;
   FORMAT DM_LCO_PRS_CT $30. ;
   FORMAT DC_LCO_PRS_ST $3. ;
   FORMAT DF_LCO_PRS_ZIP $17. ;
   FORMAT DF_SPE_ACC_ID $10.;
   FORMAT COST_CENTER_CODE $6.;
   FORMAT STATE $3.;
   FORMAT DM_LCO_PRS_FGN_ST $30.;
   FORMAT DM_LCO_PRS_FGN_CNY $25.;
   FORMAT ACSKEY $18.;
IF _N_ = 1 THEN
DO;
   PUT
   'DF_LCO_PRS_SSN_BR'
   ','
   'DF_SPE_ACC_ID'
   ','
   'DM_LCO_PRS_1'
   ','
   'DM_LCO_PRS_MI'
   ','
   'DM_LCO_PRS_LST'
   ','
   'DX_LCO_PRS_STR_1'
   ','
   'DX_LCO_PRS_STR_2'
   ','
   'DX_LCO_PRS_STR_3'
   ','
   'DM_LCO_PRS_CT'
   ','
   'DC_LCO_PRS_ST'
   ','
   'DM_LCO_PRS_FGN_ST '
   ','
   'DM_LCO_PRS_FGN_CNY'
   ','
   'DF_LCO_PRS_ZIP'
   ','
   'ACSKEY'
   ','
   'STATE'
   ','
   'COST_CENTER_CODE'
   ;
END;
DO;
	PUT DF_LCO_PRS_SSN_BR $ @;
	PUT DF_SPE_ACC_ID $ @; 
	PUT DM_LCO_PRS_1 $ @; 
	PUT DM_LCO_PRS_MI $ @; 
	PUT DM_LCO_PRS_LST $ @; 
	PUT DX_LCO_PRS_STR_1 $ @; 
	PUT DX_LCO_PRS_STR_2 $ @; 
	PUT DX_LCO_PRS_STR_3 $ @; 
	PUT DM_LCO_PRS_CT $ @; 
	PUT DC_LCO_PRS_ST $ @; 
	PUT DM_LCO_PRS_FGN_ST $ @;  
	PUT DM_LCO_PRS_FGN_CNY $ @; 
	PUT DF_LCO_PRS_ZIP $ @; 
	PUT ACSKEY $ @; 
	PUT STATE $ @; 
	PUT COST_CENTER_CODE $ ; 
 END;
RUN;
%MEND FILE_CREATE;
%FILE_CREATE(
	(BD_ATY_PRF GT &DCRT) OR (LD_ATY_RSP GT &DCRT),2	
	);
%FILE_CREATE(
	(BD_ATY_PRF NE . AND BD_ATY_PRF LE &DCRT) OR (LD_ATY_RSP NE . AND LD_ATY_RSP LE &DCRT),3	
	);
%FILE_CREATE(
	(BD_ATY_PRF EQ .) OR (LD_ATY_RSP EQ .),4	
	);
