*---------------------------------------------*
| UTLWR23 Conduit Financial - Loan Information|
*---------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR23.LWR23R2";
FILENAME REPORT3 "&RPTLIB/ULWR23.LWR23R3";
FILENAME REPORT4 "&RPTLIB/ULWR23.LWR23R4";
FILENAME REPORT5 "&RPTLIB/ULWR23.LWR23R5";
FILENAME REPORT6 "&RPTLIB/ULWR23.LWR23R6";
FILENAME REPORT7 "&RPTLIB/ULWR23.LWR23R7";
FILENAME REPORTZ "&RPTLIB/ULWR23.LWR23RZ";
PROC FORMAT;
	VALUE $CSTAT 	
		'CE' = 'CONDUIT ELIGIBLE'
		'CP' = 'CONDUIT PENDING'
		'CF' = 'CONDUIT FUNDED'
		'CR' = 'CONDUIT REDEEMED';
QUIT;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CFLI AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN 
	,A.LN_SEQ
/*ADDED ATTRIBUTE TO IDENTIFY UNIQUE TRANSACTIONS SO*/
		,C.LN_FAT_SEQ 
/*DUPLICATES COULD BE ELIMINATED */
	,A.IC_LON_PGM
	,A.LF_LON_CUR_OWN
	,A.LA_CUR_PRI
	,B.LC_LON_CDU_STA
	,CASE    
	 	WHEN B.LC_LON_CDU_STA = 'CF' AND C.IF_BND_ISS = '' THEN E.LF_CUR_POR
		ELSE C.IF_BND_ISS
	 END AS IF_BND_ISS
	,C.IF_CUS_ACC
	,C.LD_FAT_EFF
	,C.LD_FAT_APL
	,C.LA_FAT_CUR_PRI
	,C.LA_FAT_ILG_PRI
	,C.LA_FAT_NSI
	,C.LA_FAT_LTE_FEE
	,C.LC_CSH_ADV
	,C.PC_FAT_TYP||C.PC_FAT_SUB_TYP AS TRX_TYP
	,RTRIM(D.DM_PRS_1)||' '||D.DM_PRS_LST AS NAME
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.LNEC_LON_ECA B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.MR90_DLY_APL_FAT C
	ON A.BF_SSN = C.BF_SSN 
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.LN35_LON_OWN E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
	AND A.LF_LON_CUR_OWN = E.IF_OWN
WHERE B.LN_ECA_SEQ = 1
	AND B.LC_LON_CDU_STA IN ('CE','CP','CF','CR')
	AND DAYS(C.LD_FAT_APL) = DAYS(CURRENT DATE) - 1
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR23.LWR23RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA CFLI ;
	SET WORKLOCL.CFLI;
RUN;

/*CREATE FILES*/
%MACRO CFLIES(RNO,CUST_STA);
PROC SORT DATA=CFLI OUT=RDS(WHERE=(LC_LON_CDU_STA = "&CUST_STA"));
	BY IF_BND_ISS BF_SSN LN_SEQ;
RUN;
DATA _NULL_;
SET RDS;
FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
	FORMAT LN_SEQ 6. ;
	FORMAT IC_LON_PGM $6. ;
	FORMAT LF_LON_CUR_OWN $8. ;
	FORMAT LC_LON_CDU_STA $2. ;
	FORMAT IF_BND_ISS $20. ;
	FORMAT IF_CUS_ACC $4. ;
	FORMAT LD_FAT_EFF MMDDYY10. ;
	FORMAT LD_FAT_APL MMDDYY10. ;
	FORMAT LA_FAT_CUR_PRI 10.2 ;
	FORMAT LA_FAT_ILG_PRI 10.2 ;
	FORMAT LA_FAT_NSI 9.2 ;
	FORMAT LA_FAT_LTE_FEE 9.2 ;
	FORMAT LC_CSH_ADV $1. ;
	FORMAT TRX_TYP $4. ;
	FORMAT NAME $37. ;
IF _N_ = 1 THEN DO;
	PUT "Owner" 
		','
		"Customer Account"
		','
		"Bond ID" 
		','
		"Cash/Advice Payment Type" 
		','
		"Trans Type" 
		','
		"Loan Type" 
		','
		"Effective Date" 
		','
		"Applied Date" 
		','
		"Borrower Name" 
		','
		"SSN" 
		','
		"Loan Seq" 
		','
		"Principal Amount" 
		','
		"Ineligible Principal Amount" 
		','
		"Interest Amount" 
		','
		"Late Fees Amount";
	END;
	DO;
		PUT LF_LON_CUR_OWN $ @;
		PUT IF_CUS_ACC $ @;
		PUT IF_BND_ISS $ @;
		PUT LC_CSH_ADV $ @;
		PUT TRX_TYP $ @;
		PUT IC_LON_PGM $ @;
		PUT LD_FAT_EFF @;
		PUT LD_FAT_APL @;
		PUT NAME $ @;
		PUT BF_SSN $ @;
		PUT LN_SEQ @;
		PUT LA_FAT_CUR_PRI @;
		PUT LA_FAT_ILG_PRI @;
		PUT LA_FAT_NSI @;
		PUT LA_FAT_LTE_FEE ;
	END;
RUN;
%MEND CFLIES;
%CFLIES(2,CP);
%CFLIES(3,CF);
%CFLIES(4,CR);
%CFLIES(5,CE);
/*SUMMARY REPORTS*/
PROC SORT DATA=CFLI;
	BY LC_LON_CDU_STA IF_BND_ISS LC_CSH_ADV TRX_TYP ;
RUN;
PROC PRINTTO PRINT=REPORT6 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'TRANSACTION SUMMARY REPORT';
FOOTNOTE   'JOB = UTLWR23  	 REPORT = ULWR23.LWR23R6';
PROC CONTENTS DATA=CFLI OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR23  	 REPORT = ULWR23.LWR23R6";
	END;
RETURN;
RUN;
PROC REPORT DATA=CFLI NOWD SPLIT='/' HEADSKIP;
	COLUMN LC_LON_CDU_STA LF_LON_CUR_OWN IF_BND_ISS LC_CSH_ADV TRX_TYP LA_FAT_CUR_PRI LA_FAT_ILG_PRI LA_FAT_NSI LA_FAT_LTE_FEE;
	COMPUTE BEFORE LC_LON_CDU_STA;
		LINE @14 LC_LON_CDU_STA $2. ' STATUS';
		LINE @1 '';
	ENDCOMP;
	DEFINE LC_LON_CDU_STA / GROUP NOPRINT;
	DEFINE LF_LON_CUR_OWN / GROUP 'OWNER' WIDTH=8;
	DEFINE IF_BND_ISS / GROUP 'BOND ID' WIDTH=8;
	DEFINE LC_CSH_ADV / GROUP 'PAYMENT/TYPE' WIDTH=7;
	DEFINE TRX_TYP / GROUP 'TRAN/TYPE' WIDTH=4;
	DEFINE LA_FAT_CUR_PRI / ANALYSIS SUM'PRINCIPAL/AMOUNT' FORMAT=COMMA18.2;
	DEFINE LA_FAT_ILG_PRI / ANALYSIS SUM'INELIGIBLE/PRINCIPAL/AMOUNT' FORMAT=COMMA14.2 ;
	DEFINE LA_FAT_NSI / ANALYSIS SUM'INTEREST/AMOUNT' FORMAT=COMMA14.2;
	DEFINE LA_FAT_LTE_FEE / ANALYSIS SUM'LATE FEES/AMOUNT' FORMAT=COMMA14.2;
	BREAK AFTER LC_CSH_ADV / SUMMARIZE SUPPRESS SKIP OL;
	COMPUTE AFTER LC_LON_CDU_STA;
		LINE @14 101*'-' ;
		LINE @14 LC_LON_CDU_STA $2. ' TOTALS' 
			@49 LA_FAT_CUR_PRI.SUM COMMA18.2
			@69 LA_FAT_ILG_PRI.SUM COMMA14.2 
			@85 LA_FAT_NSI.SUM COMMA14.2
			@101 LA_FAT_LTE_FEE.SUM COMMA14.2;
		LINE @14 101*'-' ;
	LINE ' ';
	ENDCOMP;
RUN;
PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT7 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'CONDUIT LOANS SUMMARY REPORT';
FOOTNOTE   'JOB = UTLWR23  	 REPORT = ULWR23.LWR23R7';
PROC CONTENTS DATA=CFLI OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR23  	 REPORT = ULWR23.LWR23R7";
	END;
RETURN;
RUN;
PROC REPORT DATA=CFLI NOWD SPLIT='/' HEADSKIP;
	COLUMN LC_LON_CDU_STA LF_LON_CUR_OWN IF_BND_ISS LA_CUR_PRI LA_FAT_NSI;
	DEFINE LC_LON_CDU_STA / GROUP 'CONDUIT/STATUS' WIDTH=20 FORMAT=$CSTAT.;
	DEFINE LF_LON_CUR_OWN / GROUP 'OWNER' WIDTH=8;
	DEFINE IF_BND_ISS / GROUP 'BOND ID' WIDTH=8;
	DEFINE LA_CUR_PRI / ANALYSIS SUM'CURRENT/PRINCIPAL' FORMAT=COMMA18.2;
	DEFINE LA_FAT_NSI / ANALYSIS SUM'OUTSTANDING/INTEREST' FORMAT=COMMA14.2;
	BREAK AFTER LC_LON_CDU_STA / SKIP;
	RBREAK AFTER / SUMMARIZE SKIP OL;
RUN;
PROC PRINTTO;
RUN;
