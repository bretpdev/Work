/*UTLWM03 - SPECIAL CAMPAIGN - DROPOUTS*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*FILENAME REPORT2 "&RPTLIB/ULWM03.LWM03R2";*/
/*FILENAME REPORT3 "&RPTLIB/ULWM03.LWM03R3";*/
/*FILENAME REPORT4 "&RPTLIB/ULWM03.LWM03R4";*/
/*FILENAME REPORT5 "&RPTLIB/ULWM03.LWM03R5";*/
/*FILENAME REPORT6 "&RPTLIB/ULWM03.LWM03R6";*/

FILENAME REPORT2 "C:\WINDOWS\TEMP\ULWM03.LWM03R2";
FILENAME REPORT3 "C:\WINDOWS\TEMP\ULWM03.LWM03R3";
FILENAME REPORT4 "C:\WINDOWS\TEMP\ULWM03.LWM03R4";
FILENAME REPORT5 "C:\WINDOWS\TEMP\ULWM03.LWM03R5";
FILENAME REPORT6 "C:\WINDOWS\TEMP\ULWM03.LWM03R6";

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*DATA FOR REPORT 1*/
CREATE TABLE SCDO1A AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID_BR AS DF_PRS_ID
	,RTRIM(PD01.DM_PRS_1) AS DM_PRS_1
	,RTRIM(PD01.DM_PRS_LST) AS DM_PRS_LST
	,PD03.DX_STR_ADR_1
	,PD03.DX_STR_ADR_2
	,PD03.DM_CT
	,PD03.DC_DOM_ST
	,PD03.DF_ZIP
	,PD03.DC_ADR
	,PD03.DM_FGN_CNY
	,A.AF_APL_OPS_SCL
	,SC01.IM_IST_SHO
	,A.DF_PRS_ID_STU
	,U.TOB
	,PD03.DI_PHN_VLD
	,PD03.DI_VLD_ADR
FROM  OLWHRM1.GA01_APP A 
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA GA14
	ON B.AF_APL_ID = GA14.AF_APL_ID
	AND B.AF_APL_ID_SFX = GA14.AF_APL_ID_SFX
INNER JOIN (SELECT Q.DF_PRS_ID_BR
				  ,SUM(J.AA_GTE_LON_AMT - COALESCE(J.AA_TOT_CAN,0) - J.AA_TOT_RFD) AS TOB	
			FROM  OLWHRM1.GA14_LON_STA K, OLWHRM1.GA10_LON_APP J, OLWHRM1.GA01_APP Q
		 	WHERE K.AF_APL_ID = J.AF_APL_ID
		 	AND K.AF_APL_ID_SFX = J.AF_APL_ID_SFX
			AND J.AF_APL_ID = Q.AF_APL_ID
			AND K.AC_STA_GA14 = 'A'
			AND K.AC_LON_STA_TYP IN ('ID','IG','IA','DA','FB','RP','CR','CP')
			GROUP BY Q.DF_PRS_ID_BR ) U
	ON A.DF_PRS_ID_BR = U.DF_PRS_ID_BR
INNER JOIN OLWHRM1.PD01_PDM_INF PD01
	ON A.DF_PRS_ID_BR = PD01.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN PD03
	ON A.DF_PRS_ID_BR = PD03.DF_PRS_ID
INNER JOIN
	(SELECT DISTINCT 
		IF_IST
		,IM_IST_SHO
	FROM OLWHRM1.SC01_LGS_SCL_INF
	)SC01
	ON A.AF_APL_OPS_SCL = SC01.IF_IST
WHERE B.AC_LON_TYP IN ('SF','SU')
AND GA14.AC_STA_GA14 = 'A'			/*ACTIVE LOAN STATUS ROW*/
AND PD03.DC_ADR = 'L'
AND GA14.AC_LON_STA_TYP IN (/*'IA',*/'IG')
AND A.AF_APL_OPS_SCL IN 
	('02298500','02178500','02360800','00367300','00367400',
	 '00367405','00367404','00367401','00367403','03003000',
	 '03030601','03030602','03030606','03030603','03030605',
	 '03030604','03030600','00367400','00367405','00367404',
	 '00367403','00367401')
AND NOT EXISTS (SELECT *
				FROM OLWHRM1.AY01_BR_ATY X
				WHERE X.DF_PRS_ID = A.DF_PRS_ID_BR
				AND X.PF_ACT = 'ADROP')
);

/*ENROLLMENT RECORDS TABLE FOR REPORTS 1 2 3 4*/
CREATE TABLE SD02 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT X.DF_PRS_ID_STU
,X.LF_STU_ENR_RPT_SEQ
,X.LC_STU_ENR_TYP
,X.LD_XPC_GRD
,X.LD_ENR_CER
,X.LD_STU_ENR_STA
,X.IF_OPS_SCL_RPT
FROM OLWHRM1.SD02_STU_ENR X
WHERE X.IF_OPS_SCL_RPT IN 
	('02298500','02178500','02360800','00367300','00367400',
	 '00367405','00367404','00367401','00367403','03003000',
	 '03030601','03030602','03030606','03030603','03030605',
	 '03030604','03030600','00367400','00367405','00367404',
	 '00367403','00367401')
AND X.LC_STA_SD02 <> 'I'
);

/*DATA FOR REPORT2*/
CREATE TABLE SCDO2A AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID
	,RTRIM(C.DM_PRS_1) AS DM_PRS_1
	,RTRIM(C.DM_PRS_LST) AS DM_PRS_LST
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP
	,D.DC_ADR
	,D.DM_FGN_CNY
	,E.AF_APL_OPS_SCL
	,D.DI_PHN_VLD
	,D.DI_VLD_ADR

FROM OLWHRM1.AY01_BR_ATY A
INNER JOIN 
	(SELECT DF_PRS_ID
		,COUNT(*)
	FROM OLWHRM1.AY01_BR_ATY 
	WHERE PF_ACT = 'AUCON'
	GROUP BY DF_PRS_ID
	HAVING COUNT(*) >= 6)B
	ON A.DF_PRS_ID = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN D
	ON A.DF_PRS_ID = D.DF_PRS_ID
INNER JOIN OLWHRM1.GA01_APP E
	ON C.DF_PRS_ID = E.DF_PRS_ID_STU 
INNER JOIN OLWHRM1.GA10_LON_APP F
	ON E.AF_APL_ID = F.AF_APL_ID

WHERE A.PF_ACT = 'ADROP'
AND D.DC_ADR = 'L'
AND E.AF_APL_OPS_SCL IN 
	('02298500','02178500','02360800','00367300','00367400',
	 '00367405','00367404','00367401','00367403','03003000',
	 '03030601','03030602','03030606','03030603','03030605',
	 '03030604','03030600','00367400','00367405','00367404',
	 '00367403','00367401')
AND NOT EXISTS 
	(SELECT *
	 FROM OLWHRM1.AY01_BR_ATY X
	 WHERE X.DF_PRS_ID = A.DF_PRS_ID
	 AND X.PF_ACT = 'ALBNC')
AND NOT EXISTS
	(SELECT *
	 FROM OLWHRM1.AY01_BR_ATY Y
	 WHERE Y.DF_PRS_ID = A.DF_PRS_ID
	 AND Y.PF_ACT = 'ASCON'
	 AND Y.BD_ATY_PRF >= 
	 		(SELECT MIN(Z.BD_ATY_PRF)
			 FROM OLWHRM1.AY01_BR_ATY Z
			 WHERE Z.DF_PRS_ID = Y.DF_PRS_ID 
			 AND Z.PF_ACT = 'ADROP')
	 )
);

/*DATA FOR REPORTS 3 AND 4*/
CREATE TABLE SCDO34A AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID
	,RTRIM(C.DM_PRS_1) AS DM_PRS_1
	,RTRIM(C.DM_PRS_LST) AS DM_PRS_LST
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP
	,D.DC_ADR
	,D.DM_FGN_CNY
	,SUBSTR(B.BX_CMT,1,1) AS BX_CMT_1ST
	,E.AF_APL_OPS_SCL
	,D.DI_PHN_VLD
	,D.DI_VLD_ADR
FROM OLWHRM1.AY01_BR_ATY A
INNER JOIN OLWHRM1.AY01_BR_ATY B
	ON A.DF_PRS_ID = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN D
	ON A.DF_PRS_ID = D.DF_PRS_ID
INNER JOIN OLWHRM1.GA01_APP E
	ON C.DF_PRS_ID = E.DF_PRS_ID_STU 
INNER JOIN OLWHRM1.GA10_LON_APP F
	ON E.AF_APL_ID = F.AF_APL_ID
WHERE E.AF_APL_OPS_SCL IN 
	('02298500','02178500','02360800','00367300','00367400',
	 '00367405','00367404','00367401','00367403','03003000',
	 '03030601','03030602','03030606','03030603','03030605',
	 '03030604','03030600','00367400','00367405','00367404',
	 '00367403','00367401')
AND D.DC_ADR = 'L'
AND A.PF_ACT = 'ADROP'
AND B.PF_ACT = 'ASCON'
AND NOT EXISTS 
	(SELECT *
	 FROM OLWHRM1.AY01_BR_ATY X
	 WHERE X.DF_PRS_ID = A.DF_PRS_ID
	 AND (X.PF_ACT = 'ALSCL' 
		  OR X.PF_ACT = 'ALNCH')
	)
);

/*IDENTIFY BORROWERS WITH OPEN SLMA LOANS FOR EXCLUSION*/
CREATE TABLE SLMA AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT W.DF_PRS_ID_BR
FROM OLWHRM1.GA01_APP W
INNER JOIN OLWHRM1.GA10_LON_APP X
	ON W.AF_APL_ID = X.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA Y
	ON X.AF_APL_ID = Y.AF_APL_ID
	AND X.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
WHERE Y.AC_STA_GA14 = 'A'
AND Y.AC_LON_STA_TYP IN ('IA','ID','IG','IM','RP','CR','FB','DA')
AND (X.AF_CUR_LON_SER_AGY = '700191'
	 OR X.AF_CUR_LON_OPS_LDR IN 
						('829587','830084','830151','831053',
						 '831474','833253','888885','899993'))

);
DISCONNECT FROM DB2;

/*REMOVE RECORDS FROM CAMPAIGN IF THEY WERE IDENTIFIED IN SLMA TABLE ABOVE*/
CREATE TABLE SCDO1 AS
SELECT A.*
FROM SCDO1A A
WHERE A.DF_PRS_ID NOT IN
(SELECT DF_PRS_ID_BR
 FROM SLMA)
;

/*REMOVE RECORDS FROM CAMPAIGN IF THEY WERE IDENTIFIED IN SLMA TABLE ABOVE*/
CREATE TABLE SCDO2 AS
SELECT B.*
FROM SCDO2A B
WHERE B.DF_PRS_ID NOT IN
(SELECT DF_PRS_ID_BR
 FROM SLMA)
;

/*REMOVE RECORDS FROM CAMPAIGN IF THEY WERE IDENTIFIED IN SLMA TABLE ABOVE*/
CREATE TABLE SCDO34 AS
SELECT C.*
FROM SCDO34A C
WHERE C.DF_PRS_ID NOT IN
(SELECT DF_PRS_ID_BR
 FROM SLMA)
;

QUIT;

*CALCULATE KEYLINE;
%MACRO KEY_CLC(TBL);
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;

/******************************CREATE FILES 1 2 3 4********************************/

/*ORDER AND NUMBER ENROLLMENT ROWS FOR ALL REPORTS*/
PROC SORT DATA=SCDO1;BY DF_PRS_ID_STU AF_APL_OPS_SCL;RUN;
PROC SORT DATA=SCDO2;BY DF_PRS_ID AF_APL_OPS_SCL;RUN;
PROC SORT DATA=SCDO34;BY DF_PRS_ID AF_APL_OPS_SCL;RUN;
PROC SORT DATA=SD02;
BY DF_PRS_ID_STU IF_OPS_SCL_RPT DESCENDING LD_ENR_CER DESCENDING LF_STU_ENR_RPT_SEQ;
RUN;
DATA SD02;
SET SD02;
BY DF_PRS_ID_STU IF_OPS_SCL_RPT DESCENDING LD_ENR_CER DESCENDING LF_STU_ENR_RPT_SEQ;
IF FIRST.IF_OPS_SCL_RPT THEN ORDER = 1;
ELSE ORDER + 1;
RUN;

/*IDENTIFY MOST RECENT CERT ROW*/
DATA SD021;
SET SD02;
WHERE ORDER = 1;
RUN;

/*IDENTIFY NEXT PREVIOUSLY CERTIFIED A, F, OR H ROW*/
DATA SD022;
SET SD02;
WHERE ORDER NE 1
AND LC_STU_ENR_TYP IN ('A','F','H');
RUN;
DATA SD022;
SET SD022;
BY DF_PRS_ID_STU IF_OPS_SCL_RPT DESCENDING LD_ENR_CER DESCENDING LF_STU_ENR_RPT_SEQ;
IF FIRST.IF_OPS_SCL_RPT;
RUN;

/*PUT FILE 1 TOGETHER*/
PROC SQL;
CREATE TABLE SCDO1B AS
SELECT A.*
	,B.LD_STU_ENR_STA
FROM SCDO1 A
LEFT OUTER JOIN SD021 B
	ON A.DF_PRS_ID_STU = B.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = B.IF_OPS_SCL_RPT
LEFT OUTER JOIN SD022 C
	ON A.DF_PRS_ID_STU = C.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = C.IF_OPS_SCL_RPT
WHERE B.LC_STU_ENR_TYP = 'W'
AND B.LD_STU_ENR_STA > '31MAR2003'D
AND B.LD_STU_ENR_STA < TODAY() - 60
AND B.LD_STU_ENR_STA <> C.LD_XPC_GRD
;
QUIT;

/*PUT FILE 2 TOGETHER*/
PROC SQL;
CREATE TABLE SCDO2B AS
SELECT A.*
	,B.LD_STU_ENR_STA
FROM SCDO2 A
LEFT OUTER JOIN SD021 B
	ON A.DF_PRS_ID = B.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = B.IF_OPS_SCL_RPT
LEFT OUTER JOIN SD022 C
	ON A.DF_PRS_ID = C.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = C.IF_OPS_SCL_RPT
WHERE B.LC_STU_ENR_TYP = 'W'
AND B.LD_STU_ENR_STA > '31MAR2003'D
AND B.LD_STU_ENR_STA < TODAY() - 60
AND B.LD_STU_ENR_STA <> C.LD_XPC_GRD
;
QUIT;

/*PUT FILES 3 & 4 TOGETHER*/
PROC SQL;
CREATE TABLE SCDO34B AS
SELECT A.*
	,B.LD_STU_ENR_STA
FROM SCDO34 A
LEFT OUTER JOIN SD021 B
	ON A.DF_PRS_ID = B.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = B.IF_OPS_SCL_RPT
LEFT OUTER JOIN SD022 C
	ON A.DF_PRS_ID = C.DF_PRS_ID_STU
	AND A.AF_APL_OPS_SCL = C.IF_OPS_SCL_RPT
WHERE B.LC_STU_ENR_TYP = 'W'
AND B.LD_STU_ENR_STA > '31MAR2003'D
AND B.LD_STU_ENR_STA < TODAY() - 60
AND B.LD_STU_ENR_STA <> C.LD_XPC_GRD
;
QUIT;

/*CREATE FILES 3 & 4*/
DATA SCDO3B SCDO4B;
SET SCDO34B;
IF BX_CMT_1ST = '1' THEN OUTPUT SCDO3B;
ELSE IF BX_CMT_1ST = '2' THEN OUTPUT SCDO4B;
RUN;

/*ADD KEYLINES*/
%KEY_CLC(SCDO1B);
%KEY_CLC(SCDO2B);
%KEY_CLC(SCDO3B);
%KEY_CLC(SCDO4B);

/*SORT*/
PROC SORT DATA=SCDO1B NODUPKEY;BY DF_PRS_ID;RUN;
PROC SORT DATA=SCDO2B NODUPKEY;BY DF_PRS_ID;RUN;
PROC SORT DATA=SCDO3B NODUPKEY;BY DF_PRS_ID;RUN;
PROC SORT DATA=SCDO4B NODUPKEY;BY DF_PRS_ID;RUN;

ENDRSUBMIT;
DATA SCDO1B;SET WORKLOCL.SCDO1B;RUN;
DATA SCDO2B;SET WORKLOCL.SCDO2B;RUN;
DATA SCDO3B;SET WORKLOCL.SCDO3B;RUN;
DATA SCDO4B;SET WORKLOCL.SCDO4B;RUN;

%MACRO ADD_EVAL(DS,NUM);
DATA &DS IVAD&NUM;
SET &DS;
IF DI_PHN_VLD = 'N' THEN OUTPUT IVAD&NUM;
ELSE IF DI_VLD_ADR = 'N' THEN OUTPUT IVAD&NUM;
ELSE OUTPUT &DS;
RUN;
%MEND ADD_EVAL;
%ADD_EVAL(SCDO1B,1);
%ADD_EVAL(SCDO2B,2);
%ADD_EVAL(SCDO3B,3);
%ADD_EVAL(SCDO4B,4);
DATA IVAD;
SET IVAD1 IVAD2 IVAD3 IVAD4;
RUN;
%MACRO CCC(DS);
DATA &DS ;
SET &DS;
COST_CENTER_CODE = 'MA2328';
RUN;
%MEND CCC;

%CCC(SCDO1B);
%CCC(SCDO2B);
%CCC(SCDO3B);
%CCC(SCDO4B);

/*CREATE FILES*/
DATA _NULL_;
SET  WORK.SCDO1B;
FILE REPORT2 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT LD_STU_ENR_STA MMDDYY10. ;
FORMAT AF_APL_OPS_SCL $8. ;
FORMAT IM_IST_SHO $20. ;
FORMAT ACSKEY $18. ;
FORMAT TOB 12.2 ;
FORMAT DM_FGN_CNY $15.;
FORMAT COST_CENTER_CODE $6.;
DO;
PUT DF_PRS_ID $ @;
PUT DM_PRS_1 $ @;
PUT DM_PRS_LST $ @;
PUT DX_STR_ADR_1 $ @;
PUT DX_STR_ADR_2 $ @;
PUT DM_CT $ @;
PUT DC_DOM_ST $ @;
PUT DF_ZIP $ @;
PUT LD_STU_ENR_STA @;
PUT AF_APL_OPS_SCL $ @;
PUT IM_IST_SHO $ @;
PUT ACSKEY $ @;
PUT TOB $ @;
PUT DM_FGN_CNY $ @;
PUT DC_DOM_ST $ @;
PUT COST_CENTER_CODE $;
;
END;
RUN;

DATA _NULL_;
SET  WORK.SCDO2B;
FILE REPORT3 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT ACSKEY $18. ;
FORMAT LD_STU_ENR_STA MMDDYY10.;
FORMAT DM_FGN_CNY $15.;
FORMAT COST_CENTER_CODE $6.;
DO;
PUT DF_PRS_ID $ @;
PUT DM_PRS_1 $ @;
PUT DM_PRS_LST $ @;
PUT DX_STR_ADR_1 $ @;
PUT DX_STR_ADR_2 $ @;
PUT DM_CT $ @;
PUT DC_DOM_ST $ @;
PUT DF_ZIP $ @;
PUT ACSKEY $ @;
PUT LD_STU_ENR_STA $ @;
PUT DM_FGN_CNY $ @;
PUT DC_DOM_ST $ @;
PUT COST_CENTER_CODE $;
;
END;
RUN;

DATA _NULL_;
SET  WORK.SCDO3B;
FILE REPORT4 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT ACSKEY $18. ;
FORMAT LD_STU_ENR_STA MMDDYY10.;
FORMAT DM_FGN_CNY $15.;
FORMAT COST_CENTER_CODE $6.;
DO;
PUT DF_PRS_ID $ @;
PUT DM_PRS_1 $ @;
PUT DM_PRS_LST $ @;
PUT DX_STR_ADR_1 $ @;
PUT DX_STR_ADR_2 $ @;
PUT DM_CT $ @;
PUT DC_DOM_ST $ @;
PUT DF_ZIP $ @;
PUT ACSKEY $ @;
PUT LD_STU_ENR_STA $ @;
PUT DM_FGN_CNY $ @;
PUT DC_DOM_ST $ @;
PUT COST_CENTER_CODE $;
;
END;
RUN;

DATA _NULL_;
SET  WORK.SCDO4B;
FILE REPORT5 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT ACSKEY $18. ;
FORMAT LD_STU_ENR_STA MMDDYY10.;
FORMAT DM_FGN_CNY $15.;
FORMAT COST_CENTER_CODE $6.;
DO;
PUT DF_PRS_ID $ @;
PUT DM_PRS_1 $ @;
PUT DM_PRS_LST $ @;
PUT DX_STR_ADR_1 $ @;
PUT DX_STR_ADR_2 $ @;
PUT DM_CT $ @;
PUT DC_DOM_ST $ @;
PUT DF_ZIP $ @;
PUT ACSKEY $ @;
PUT LD_STU_ENR_STA $ @;
PUT DM_FGN_CNY $ @;
PUT DC_DOM_ST $ @;
PUT COST_CENTER_CODE $;
;
END;
RUN;

DATA _NULL_;                                      
CURRENT=TODAY();                                  
CALL SYMPUT('CURDATE',PUT(CURRENT,MMDDYY10.));       
RUN;  

PROC PRINTTO PRINT=REPORT6 NEW;
RUN;
OPTIONS PAGENO=1 LS=100 NODATE CENTER;
TITLE 'SC--DROP OUTS INVALID ADDRESS OR PHONE';
TITLE2 "RUN DATE: &CURDATE";
PROC CONTENTS DATA=IVAD OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 100*'-';
	PUT      ////////
		@45 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@49 '-- END OF REPORT --';
	PUT /////////////////////////
		@38 'JOB = UTLWM03  	 REPORT = ULWM03.LWM03R6';
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=IVAD N="TOTAL: ";
VAR DF_PRS_ID
	DI_VLD_ADR
	DI_PHN_VLD;
LABEL DI_PHN_VLD = 'VALID PHONE'
	DI_VLD_ADR = 'VALID ADDRESS';
FOOTNOTE  'JOB = UTLWM03  	 REPORT = ULWM03.LWM03R6';
RUN;

PROC PRINTTO;
RUN;
