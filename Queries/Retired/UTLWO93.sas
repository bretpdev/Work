LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORTZ "&RPTLIB/ULWO93.LWO93RZ";
FILENAME REPORT2 "&RPTLIB/ULWO93.LWO93R2";
FILENAME REPORT3 "&RPTLIB/ULWO93.LWO93R3";
FILENAME REPORT4 "&RPTLIB/ULWO93.LWO93R4";
FILENAME REPORT5 "&RPTLIB/ULWO93.LWO93R5";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);

CREATE TABLE CUMFU AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	DISTINCT
		A.DF_SPE_ACC_ID
		,A.DF_PRS_ID
		,TRIM(A.DM_PRS_1)||' '||TRIM(A.DM_PRS_LST)		AS NAME
		,A1.DI_PHN_VLD
		,A1.DI_ALT_PHN_VLD
		,A1.DX_EML_ADR
		,A1.DI_EML_ADR_VAL
		,D.IF_CRT_DTS_CT30


FROM	OLWHRM1.PD01_PDM_INF A
		INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN A1 ON
			A.DF_PRS_ID = A1.DF_PRS_ID
		INNER JOIN OLWHRM1.GA01_APP B ON
			A.DF_PRS_ID = B.DF_PRS_ID_BR
		INNER JOIN OLWHRM1.GA10_LON_APP C ON
			B.AF_APL_ID = C.AF_APL_ID
			AND (C.AC_APL_SPS_REA_1 = 'A' 
                 OR C.AC_APL_SPS_REA_2 = 'A' 
                 OR C.AC_APL_SPS_REA_3 = 'A' 
                 OR C.AC_APL_SPS_REA_4 = 'A' 
                 OR C.AC_APL_SPS_REA_5 = 'A')
		INNER JOIN OLWHRM1.CT30_CALL_QUE D ON
			A.DF_PRS_ID = D.DF_PRS_ID_BR
			AND D.IF_WRK_GRP IN ('PAPHOLDA','SAPHOLDA')
	
FOR READ ONLY WITH UR
);

CREATE TABLE R3_PHON AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	DISTINCT
		A.DF_PRS_ID
FROM	OLWHRM1.AY01_BR_ATY A
WHERE	A.PF_ACT = 'GHLDA'


		AND (
			 DAYS(A.BF_CRT_DTS_AY01) BETWEEN DAYS(CURRENT DATE) - 17 AND DAYS(CURRENT DATE) - 12 
			 OR
			 DAYS(A.BF_CRT_DTS_AY01) BETWEEN DAYS(CURRENT DATE) - 47 AND DAYS(CURRENT DATE) - 42
			)
		AND NOT EXISTS (SELECT 	B.DF_PRS_ID
						FROM	OLWHRM1.AY01_BR_ATY B
						WHERE	B.PF_ACT IN ('GSCAM','GUCAM')
								AND (
									 DAYS(B.BF_CRT_DTS_AY01) BETWEEN DAYS(CURRENT DATE) - 17 AND DAYS(CURRENT DATE) - 12
									 OR
									 DAYS(B.BF_CRT_DTS_AY01) BETWEEN DAYS(CURRENT DATE) - 47 AND DAYS(CURRENT DATE) - 42
									)
								AND A.DF_PRS_ID = B.DF_PRS_ID )
		AND NOT EXISTS (SELECT 	C.DF_PRS_ID_BR
						FROM 	OLWHRM1.CT30_CALL_QUE C
						WHERE	C.IF_WRK_GRP = 'CUMEMPHN'
								AND A.DF_PRS_ID = C.DF_PRS_ID_BR )

FOR READ ONLY WITH UR
);


CREATE TABLE R5_30DAY AS
SELECT  DISTINCT
		A.DF_SPE_ACC_ID
		,A.NAME
		,A.DX_EML_ADR
FROM	CUMFU A
WHERE	A.DI_EML_ADR_VAL = 'Y'
		AND DATEPART(A.IF_CRT_DTS_CT30) BETWEEN TODAY() - 30 AND TODAY() - 35
		AND NOT EXISTS (SELECT * 
						FROM 	CONNECTION TO DB2 (
								SELECT 	*
								FROM	OLWHRM1.AY01_BR_ATY
								WHERE	PF_ACT = 'GSLAM'			
								FOR READ ONLY WITH UR) B
						WHERE	DATEPART(B.BD_ATY_PRF) > DATEPART(A.IF_CRT_DTS_CT30) + 30 
								AND	B.DF_PRS_ID = A.DF_PRS_ID
						)
;

CREATE TABLE R2_15DAY AS
SELECT	DISTINCT
		A.DF_SPE_ACC_ID
		,A.NAME
		,A.DX_EML_ADR
FROM	CUMFU A
WHERE	A.DI_EML_ADR_VAL = 'Y'
		AND DATEPART(A.IF_CRT_DTS_CT30) BETWEEN TODAY() - 2 AND TODAY() - 5
		AND NOT EXISTS (SELECT * 
						FROM 	CONNECTION TO DB2 (
								SELECT 	*
								FROM	OLWHRM1.AY01_BR_ATY
								WHERE	PF_ACT = 'GLCAM'			
								FOR READ ONLY WITH UR) B
						WHERE	DATEPART(B.BD_ATY_PRF) > DATEPART(A.IF_CRT_DTS_CT30)
								AND	B.DF_PRS_ID = A.DF_PRS_ID
						)
;

CREATE TABLE R3_PHONE AS
SELECT 	DISTINCT
		A.DF_PRS_ID AS TARGET_ID
		,'CUMEMPHN' AS QUEUE_NAME
		,'' AS INSTITUTION_ID
		,'' AS INSTITUTION_TYPE
		,'' AS DATE_DUE
		,'' AS TIME_DUE
		,'CREDIT UNION MEMBERSHIP HOLD FOLLOW UP CALL' AS COMMENTS
FROM	CUMFU A
		INNER JOIN R3_PHON B
			ON A.DF_PRS_ID = B.DF_PRS_ID
WHERE	A.DI_PHN_VLD = 'Y'
		OR A.DI_ALT_PHN_VLD = 'Y'
;

CREATE TABLE R4_NOEMAIL AS
SELECT 	DISTINCT
		A.DF_PRS_ID AS TARGET_ID
		,'CUPHNEML' AS QUEUE_NAME
		,'' AS INSTITUTION_ID
		,'' AS INSTITUTION_TYPE
		,'' AS DATE_DUE
		,'' AS TIME_DUE
		,'CREDIT UNION MEMBERSHIP HOLD FOLLOW UP CALL NO VALID E-MAIL' AS COMMENTS
FROM	CUMFU A
WHERE	A.DI_EML_ADR_VAL = 'N'
		AND (A.DI_PHN_VLD = 'Y' OR A.DI_ALT_PHN_VLD = 'Y')
		AND DATEPART(A.IF_CRT_DTS_CT30) BETWEEN TODAY() - 2 AND TODAY() - 5
		AND NOT EXISTS (SELECT * 
						FROM 	CONNECTION TO DB2 (
								SELECT 	*
								FROM	OLWHRM1.AY01_BR_ATY
								WHERE	PF_ACT IN ('GSCAM','GUCAM')			
								FOR READ ONLY WITH UR) C
						WHERE	DATEPART(C.BD_ATY_PRF) > DATEPART(A.IF_CRT_DTS_CT30) 
								AND	C.DF_PRS_ID = A.DF_PRS_ID
						)
		AND NOT EXISTS (SELECT * 
						FROM 	CONNECTION TO DB2 (
								SELECT 	*
								FROM	OLWHRM1.CT30_CALL_QUE
								WHERE	IF_WRK_GRP = 'CUPHNEML'			
								FOR READ ONLY WITH UR) C
						WHERE	DATEPART(C.IF_CRT_DTS_CT30) > DATEPART(A.IF_CRT_DTS_CT30) 
								AND	C.DF_PRS_ID_BR = A.DF_PRS_ID
						)
;

DISCONNECT FROM DB2;

%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;
%sqlcheck;
quit;

/*ENDRSUBMIT;*/
/*DATA R2_15DAY;SET WORKLOCL.R2_15DAY; RUN;*/
/*DATA R3_PHONE;SET WORKLOCL.R3_PHONE; RUN;*/
/*DATA R4_NOEMAIL;SET WORKLOCL.R4_NOEMAIL; RUN;*/
/*DATA R5_30DAY;SET WORKLOCL.R5_30DAY; RUN;*/

%MACRO EMAILER(FILE=,REPNO=);
DATA _NULL_;
	SET  &FILE;
	FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	IF _N_ = 1 THEN DO;
	PUT "DF_SPE_ACC_ID"
		','
		"NAME"
		','
		"DX_EML_ADR";
	END;
	   FORMAT DF_SPE_ACC_ID $10. ;
	   FORMAT NAME $48. ;
	   FORMAT DX_EML_ADR $56. ;
	DO;
	   PUT DF_SPE_ACC_ID $ @;
	   PUT NAME $ @;
	   PUT DX_EML_ADR $ ;
	END;
RUN;
%MEND EMAILER;

%MACRO QBUILDER(FILE=,REPNO=);
DATA _NULL_;
SET  &FILE; 
FILE REPORT&REPNO DELIMITER=',' DSD DROPOVER LRECL=32767;
    FORMAT TARGET_ID $9. ;	
    FORMAT QUEUE_NAME $8. ;
    FORMAT INSTITUTION_ID $1. ;
    FORMAT INSTITUTION_TYPE $1. ;
    FORMAT DATE_DUE $1. ;
    FORMAT TIME_DUE $1. ;
    FORMAT COMMENTS $600. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
%MEND QBUILDER;

%EMAILER(FILE=R2_15DAY,REPNO=2)
%QBUILDER(FILE=R3_PHONE,REPNO= 3)
%QBUILDER(FILE=R4_NOEMAIL,REPNO= 4)
%EMAILER(FILE=R5_30DAY,REPNO=5)
