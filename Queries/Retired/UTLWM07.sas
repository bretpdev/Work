/*UTLWM08 - FIRST PAYMENT MISSED SPECIAL CAMPAIGN*/

LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWM07.LWM07R2";
FILENAME REPORT3 "&RPTLIB/ULWM07.LWM07R3";

/*FILENAME REPORT2 "T:\SAS\ULWM07.LWM07R2";*/
/*FILENAME REPORT3 "T:\SAS\ULWM07.LWM07R3";*/
/*libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;*/
/*RSUBMIT;*/

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SCFPM AS
SELECT * 
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.DF_PRS_ID
	,F.AF_APL_ID
	,F.AF_APL_ID_SFX
	,F.AF_CUR_LON_SER_AGY 
	,RS10.LD_RPS_1_PAY_DU
	,LN16.LD_DLQ_OCC

	,LN10.LN_SEQ
	,RTRIM(C.DM_PRS_1) AS DM_PRS_1
	,RTRIM(C.DM_PRS_LST) AS DM_PRS_LST
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP
	,D.DC_ADR
FROM 
	/*ACONS, ADROP, OR APLUS WITHIN LAST 6 MONTHS*/
	(SELECT DF_PRS_ID
	,PF_ACT
	,BD_ATY_PRF
	FROM OLWHRM1.AY01_BR_ATY
	WHERE DAYS(BD_ATY_PRF) > DAYS(CURRENT DATE) - 183
	AND PF_ACT IN ('ACONS','ADROP','APLUS')
	)A
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.DF_PRS_ID = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN D
	ON A.DF_PRS_ID = D.DF_PRS_ID
INNER JOIN OLWHRM1.GA01_APP E
	ON A.DF_PRS_ID = E.DF_PRS_ID_BR 
INNER JOIN OLWHRM1.GA10_LON_APP F
	ON E.AF_APL_ID = F.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA G
	ON F.AF_APL_ID = G.AF_APL_ID
	AND F.AF_APL_ID_SFX = G.AF_APL_ID_SFX
/*JOIN COMPASS*/
INNER JOIN OLWHRM1.LN10_LON LN10
	ON F.AF_APL_ID = LN10.LF_LON_ALT
	AND INTEGER(F.AF_APL_ID_SFX) = LN10.LN_LON_ALT_SEQ
/*COMPASS ORIGINAL REPAY SCHED INFO*/
INNER JOIN OLWHRM1.LN65_LON_RPS LN65
	ON LN10.BF_SSN = LN65.BF_SSN
	AND LN10.LN_SEQ = LN65.LN_SEQ
INNER JOIN OLWHRM1.RS10_BR_RPD RS10
	ON LN65.BF_SSN = RS10.BF_SSN
	AND LN65.LN_RPS_SEQ = RS10.LN_RPS_SEQ
/*COMPASS ACTIVE DELINQUENCY INFO*/
LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
	ON LN10.BF_SSN = LN16.BF_SSN
	AND LN10.LN_SEQ = LN16.LN_SEQ
	AND LN16.LC_STA_LON16 = 'A'

WHERE E.AF_APL_OPS_SCL IN ('02178500','02298500','02360800','00367300',
	'00367400','00367401','00367403','00367404','00367405','03003000')
AND G.AC_LON_STA_TYP = 'RP'						/*REPAYMENT STATUS*/
AND NOT EXISTS
	(SELECT *
	FROM OLWHRM1.AY01_BR_ATY X
	WHERE X.DF_PRS_ID = E.DF_PRS_ID_BR
	AND X.PF_ACT = 'ALMFP'
	AND X.BD_ATY_PRF > A.BD_ATY_PRF)
AND G.AC_STA_GA14 = 'A' 						/*ACTIVE LOAN STATUS*/
AND D.DC_ADR = 'L'								/*LEGAL ADDRESS*/
AND RS10.LN_RPS_SEQ = 1							/*ORIGINAL REPAYMENT SCHED ONLY*/

ORDER BY A.DF_PRS_ID
);
DISCONNECT FROM DB2;

/*ENDRSUBMIT;*/
/*DATA SCFPM; */
/*SET WORKLOCL.SCFPM; */
/*RUN;*/

*CALCULATE KEYLINE;
%MACRO KEY_CLC(TBL);
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%KEY_CLC(SCFPM);

/*NELNET FILE*/
DATA NLN (KEEP=DF_PRS_ID);
SET SCFPM;
WHERE AF_CUR_LON_SER_AGY = '700121';
RUN;

PROC SORT DATA=NLN NODUPKEY;
BY DF_PRS_ID;
RUN;

DATA _NULL_;
SET  WORK.NLN;
FILE REPORT2 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
PUT DF_PRS_ID $;
RUN;

/*UHEAA FILE*/
DATA UHE;
SET SCFPM;
WHERE AF_CUR_LON_SER_AGY = '700126'
AND LD_RPS_1_PAY_DU BETWEEN (DATE()-15) AND (DATE()-20)
AND LD_DLQ_OCC <= (DATE()-15)
AND LD_DLQ_OCC NE .;
RUN;

PROC SORT DATA=UHE NODUPKEY ;
BY DF_PRS_ID;
RUN;

DATA _NULL_;
SET  WORK.UHE;
FILE REPORT3 DELIMITER=',' DSD DROPOVER ;
FORMAT DF_PRS_ID $9. ;
FORMAT DM_PRS_1 $12. ;
FORMAT DM_PRS_LST $35. ;
FORMAT DX_STR_ADR_1 $35. ;
FORMAT DX_STR_ADR_2 $35. ;
FORMAT DM_CT $30. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP $14. ;
FORMAT ACSKEY $18. ;
DO;
PUT DF_PRS_ID $ @;
PUT DM_PRS_1 $ @;
PUT DM_PRS_LST $ @;
PUT DX_STR_ADR_1 $ @;
PUT DX_STR_ADR_2 $ @;
PUT DM_CT $ @;
PUT DC_DOM_ST $ @;
PUT DF_ZIP $ @;
PUT ACSKEY $ ;
;
END;
RUN;