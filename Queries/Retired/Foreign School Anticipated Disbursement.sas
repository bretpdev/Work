/*QSASQ02 FOREIGN SCHOOL ANTICIPATED DISBURSEMENTS*/

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FCANT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT INTEGER(A.BF_SSN) AS SSN
	,RTRIM(PD10.DM_PRS_LST)||', '||RTRIM(PD10.DM_PRS_1)||' '||(PD10.DM_PRS_MID) AS NAME
	,A.AN_SEQ
	,A.LN_LON_DSB_SEQ
	,B.IC_LON_PGM
	,A.LD_DSB
	,A.LC_CMN_LN_HLD_RLS
	,B.LF_DOE_SCL_ORG
	,E.IM_SCL_FUL
	,G.II_COP
	,B.IF_DOE_LDR

FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.LN10_LON B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ 
INNER JOIN OLWHRM1.SC10_SCH_DMO E
	ON B.LF_DOE_SCL_ORG = E.IF_DOE_SCL
INNER JOIN OLWHRM1.SC20_SCH_GTR G
	ON B.LF_DOE_SCL_ORG = G.IF_DOE_SCL
	AND B.IC_LON_PGM = G.IC_LON_PGM
	AND B.IF_GTR = G.IF_GTR
INNER JOIN OLWHRM1.SC25_SCH_DPT F
	ON B.LF_DOE_SCL_ORG = F.IF_DOE_SCL
	AND F.IC_SCL_DPT = '000'
INNER JOIN OLWHRM1.PD10_PRS_NME PD10 
	ON A.BF_SSN = PD10.DF_PRS_ID

WHERE F.IC_SCL_DOM_ST = 'FC'
AND A.LC_DSB_TYP = '1'
AND A.LC_STA_LON15 IN ('1', '3')
AND (A.LA_DSB_CAN IS NULL
	OR A.LA_DSB <> A.LA_DSB_CAN) 
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA FCANT; 
SET WORKLOCL.FCANT; 
RUN;

PROC SORT DATA=FCANT;
BY LD_DSB SSN AN_SEQ LN_LON_DSB_SEQ;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

OPTIONS CENTER NODATE NUMBER PAGENO=1 LS=132;
PROC PRINT NOOBS SPLIT='*' DATA=FCANT WIDTH=UNIFORM WIDTH=MIN;
VAR SSN NAME AN_SEQ LN_LON_DSB_SEQ IC_LON_PGM LD_DSB LC_CMN_LN_HLD_RLS LF_DOE_SCL_ORG IM_SCL_FUL
II_COP IF_DOE_LDR;
LABEL SSN='BORROWER SSN' NAME='BORROWER NAME' AN_SEQ='APP SEQ #'
LN_LON_DSB_SEQ='DISB #' IC_LON_PGM='LOAN TYPE' LD_DSB='DISB DATE'
LC_CMN_LN_HLD_RLS='DISB HOLD/RELEASE INDICATOR' LF_DOE_SCL_ORG='SCHOOL CODE'
IM_SCL_FUL='SCHOOL NAME' II_COP='SCHOOL COPAYABLE INDICATOR' IF_DOE_LDR='LENDER ID';
FORMAT SSN SSN11. LD_DSB MMDDYY10.;
TITLE 'COMPASS ANTICIPATED DISBURSEMENTS AT FOREIGN SCHOOLS';
TITLE2 "RUN DATE:  &RUNDATE";
FOOTNOTE  'JOB = QSASQ02     REPORT = QSASQ02.R2';
RUN;