OPTIONS NOCENTER;
/*UTLWG5H APPROVAL AND PENDING ACCOUNTS NOT IN STAFF REVIEW*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2  "&RPTLIB/ULWG5H.LWG5HR2"  ;
FILENAME REPORT3  "&RPTLIB/ULWG5H.LWG5HR3"  ;

DATA NULLRPT ;  STRING= "***  END OF OBS OR NO OBSERVATIONS FOUND  ***" ;  RUN ;


PROC FORMAT ;
  VALUE $STATUS   'A' = 'APPROVED'
                  'P' = 'PENDING APPROVALS'
                      ;
RUN;
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/

PROC SQL ;
 CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE A1 AS
 SELECT *
FROM CONNECTION TO DB2
(
SELECT DISTINCT
      A.IF_IST                      AS SCODE ,
      A.II_GNR_NTC                  AS GENIND,
      A.II_ETR_MND                  AS MONDY ,
      A.II_ETR_TSD                  AS TUEDY ,
      A.II_ETR_WDD                  AS WEDDY ,
      A.II_ETR_TRD                  AS THUDY ,
      A.II_ETR_FRD                  AS FRIDY ,
  DEC(C.DF_PRS_ID_BR,9)             AS SSNB  ,
  DEC(C.DF_PRS_ID_STU,9)            AS SSNS  ,
      C.AF_CUR_APL_OPS_LDR          AS LNDR  ,
      D.AF_APL_ID||D.AF_APL_ID_SFX  AS UID   ,
      D.AC_PRC_STA                  AS STAT  ,
      D.AD_PRC                      AS DATE  ,
	  E.DF_SPE_ACC_ID				AS ACCTNUMBR,
	  F.DF_SPE_ACC_ID				AS ACCTNUMST

FROM

    OLWHRM1.IN04_IST_NTC_PFL A INNER JOIN olwhrm1.SC01_LGS_SCL_INF B
         ON A.IF_IST = B.IF_IST
    INNER JOIN olwhrm1.GA01_APP C
         ON A.IF_IST = C.AF_APL_OPS_SCL
    INNER JOIN olwhrm1.GA10_LON_APP D
         ON C.AF_APL_ID = D.AF_APL_ID
	INNER JOIN OLWHRM1.PD01_PDM_INF E
		 ON C.DF_PRS_ID_BR = E.DF_PRS_ID
	INNER JOIN OLWHRM1.PD01_PDM_INF F
		 ON C.DF_PRS_ID_STU = F.DF_PRS_ID


WHERE A.II_GNR_NTC = 'Y'
  AND B.IN_SDL_PTC_YR = ' '
  AND D.AC_PRC_STA IN ('A','P')
  AND D.AC_LON_TYP <> 'CL'
  AND D.AD_PRC > '04/01/2000'
  AND D.AD_PRC <> CURRENT DATE
  AND D.AD_LDR_NTF_PRC_RSL IS NULL
  AND D.AD_SCL_NTF_PRC_RSL IS NULL
  AND D.AD_BR_NTF_PRC_RSL IS NULL

  ) ;

DISCONNECT FROM DB2 ;
%PUT &SQLXRC& &SQLXMSG& ;

/*ENDRSUBMIT;*/

/*DATA A1; SET WORKLOCL.A1;*/
/*RUN;*/

DATA TEST ;
 SET A1 ;

IF  (MONDY = 'Y' AND (WEEKDAY(TODAY())=2)
  OR TUEDY = 'Y' AND (WEEKDAY(TODAY())=3)
  OR WEDDY = 'Y' AND (WEEKDAY(TODAY())=4)
  OR THUDY = 'Y' AND (WEEKDAY(TODAY())=5)
  OR FRIDY = 'Y' AND (WEEKDAY(TODAY())=6)) THEN OUTPUT;

PROC SORT DATA=TEST ;
 BY STAT ;
RUN ;

PROC MEANS
  DATA=TEST
  NOPRINT;
  BY STAT;
  VAR SSNB;
  OUTPUT OUT=ALLINFO N(SSNB)=NUMBER ;
RUN;

PROC SORT DATA=TEST ;
 BY LNDR STAT ;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

PROC PRINT NOOBS U N DATA=TEST SPLIT='/' ;
 VAR LNDR ACCTNUMST ACCTNUMBR UID DATE STAT  ;
  FORMAT ACCTNUMBR $10. ACCTNUMST $10. DATE MMDDYY10. STAT $STATUS. ;
 LABEL ACCTNUMST = "STUDENT'S/ACCT#"
       ACCTNUMBR = "BORROWER'S/ACCT#"
       UID  = "UNIQUE ID"
       STAT = "PROCESSING/STATUS"-
       DATE = "DATE/PROCESSED"
       LNDR = "LENDER"
       ;
TITLE1 "APPROVAL AND PENDING ACCOUNTS NOT IN STAFF REVIEW OR ON APPLICATION" ;
TITLE2 "THAT WERE NOT PROCESSED TODAY AND THE NOTIFIED FIELDS ARE BLANK" ;
FOOTNOTE 'RLW, JOB=LWG5H, REPORT=LWG5HR2';
RUN ;


PROC PRINT DATA = NULLRPT ; RUN ;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;

PROC TABULATE
  DATA=ALLINFO
  FORMAT=COMMA9.
  ORDER=DATA;
  CLASS STAT ;
  VAR NUMBER ;
  TABLE STAT=' ' ALL='TOTAL',
    NUMBER='NUMBER PER STATUS' / RTS=63;
  FORMAT STAT $STATUS.;
  TITLE 'BREAK DOWN OF NUMBERS' ;
FOOTNOTE 'RLW, JOB=LWG5H, REPORT=LWG5HR3';
RUN;

PROC PRINT DATA = NULLRPT ; RUN ;

PROC PRINTTO;
RUN;
