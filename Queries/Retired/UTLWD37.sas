*-----------------------------------------*
| UTLWD37 Verbal Rehabilitation Selection |
*-----------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = T:\SAS;*/
FILENAME REPORT2 "&RPTLIB/ULWD37.LWD37R2";
FILENAME REPORT3 "&RPTLIB/ULWD37.LWD37R3";
FILENAME REPORTZ "&RPTLIB/ULWD37.LWD37RZ";
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE BORI AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.AF_APL_ID||A.AF_APL_ID_SFX AS CLUID
	,A.LC_RHB_CON 
	,B.BN_RHB_PAY_CTR
	,(A.LA_CLM_PRI 
		+A.LA_CLM_INT
		-A.LA_PRI_COL
		+A.LA_INT_ACR
		+COALESCE(D.LA_CLM_INT_ACR,0)
		-A.LA_INT_COL)					
		 + (A.LA_LEG_CST_ACR
		-A.LA_LEG_CST_COL
		+A.LA_OTH_CHR_ACR
		-A.LA_OTH_CHR_COL
		+A.LA_COL_CST_ACR
		-A.LA_COL_CST_COL)
		+(COALESCE(D.LA_CLM_PRJ_COL_CST,0)) AS OB
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.BR01_BR_CRF B
	ON A.BF_SSN = B.BF_SSN
LEFT OUTER JOIN OLWHRM1.DC02_BAL_INT D
		ON A.AF_APL_ID = D.AF_APL_ID
		AND A.AF_APL_ID_SFX = D.AF_APL_ID_SFX
		AND A.LF_CRT_DTS_DC10 = D.LF_CRT_DTS_DC10
LEFT OUTER JOIN OLWHRM1.LA11_LEG_ACT_ATY C
	ON B.BF_SSN = C.DF_PRS_ID_BR
WHERE A.LC_STA_DC10 = '03'
	AND A.LC_AUX_STA = ''
	AND A.LD_CLM_ASN_DOE IS NULL
	AND A.LC_PCL_REA IN ('DB','DQ','DF')
	AND B.BC_RHB_NOT_ELG = ''
	AND 
	(
		C.BC_LEG_ACT_ATY_TYP = 'GG' OR
		C.DF_PRS_ID_BR IS NULL
	)
FOR READ ONLY WITH UR
);

CREATE TABLE AY01 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DF_PRS_ID
	,'X' AS PF_ACT
FROM OLWHRM1.AY01_BR_ATY
WHERE PF_ACT IN ('DRTRE','DRBWR')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWD37.LWD37RZ);*/
/*QUIT;*/
PROC SQL;
CREATE TABLE BRFR AS
SELECT DISTINCT A.BF_SSN
	,A.LC_RHB_CON 
	,A.BN_RHB_PAY_CTR
	,B.PF_ACT
	,C.OB
FROM BORI A
LEFT OUTER JOIN AY01 B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN (
	SELECT BF_SSN
		,SUM(OB) AS OB
	FROM BORI
	GROUP BY BF_SSN
	) C
	ON A.BF_SSN = C.BF_SSN
WHERE C.OB > 300
;
QUIT;
/*ENDRSUBMIT;*/
/*DATA BRFR ;*/
/*	SET WORKLOCL.BRFR;*/
/*RUN;*/
PROC SORT DATA=BRFR OUT=BRFR_R2
	(
		WHERE=(PF_ACT = 'X' & LC_RHB_CON = '')
	) 
	NODUPKEY;
	BY BF_SSN;
RUN;
PROC SORT DATA=BRFR OUT=BRFR_R3
	(
		WHERE=(BN_RHB_PAY_CTR = 9 & LC_RHB_CON = '08')
	) 
	NODUPKEY;
	BY BF_SSN;
RUN;
%MACRO UTLWD37_REPS(DS,RNO);
DATA _NULL_;
	SET  &DS;
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
	DO;
		PUT BF_SSN $ ;
	END;
RUN;
%MEND UTLWD37_REPS;
%UTLWD37_REPS(BRFR_R2,2);
%UTLWD37_REPS(BRFR_R3,3);
