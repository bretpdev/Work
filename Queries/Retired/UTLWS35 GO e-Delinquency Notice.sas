/*UTLWS35 GO e-Delinquency Notice*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET TABLIB = /sas/whse/progrevw;*/

%LET RPTLIB = T:\SAS;
%LET TABLIB = Q:\Process Automation\TabSAS;
FILENAME REPORT2 "&RPTLIB/ULWS35.LWS35R2";
FILENAME REPORT3 "&RPTLIB/ULWS35.LWS35R3";
FILENAME REPORT4 "&RPTLIB/ULWS35.LWS35R4";
FILENAME REPORT5 "&RPTLIB/ULWS35.LWS35R5";
FILENAME REPORT6 "&RPTLIB/ULWS35.LWS35R6";
FILENAME REPORT7 "&RPTLIB/ULWS35.LWS35R7";


/************************************************************************
* INPUT LOAN TYPES FOR FFEL AND NON FFEL LOANS
*************************************************************************/
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "&TABLIB/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
/************************************************************************
* CREATE MACRO VARIALBE LISTS OF LOAN PROGRAMS(FFEL AND PRIVATE LOANS)
*************************************************************************/
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :PRIVATE_LIST SEPARATED BY "," /*NON FFEL LOAN LIST*/
	FROM LOAN_TYPES
	WHERE LN_PGM = 'PRIVATE';
QUIT;

%SYSLPUT PRIVATE_LIST = &PRIVATE_LIST;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	FILENAME REPORTZ "&RPTLIB/&SQLRPT";
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*GODEL - BORROWERS*/
CREATE TABLE GODEL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	DISTINCT
		LN10.BF_SSN
		,PD10.DF_SPE_ACC_ID 									
		,TRIM(PD10.DM_PRS_1)||' '||TRIM(PD10.DM_PRS_LST) AS DM_PRS_NM
		,UPPER(PD32.DX_ADR_EML)	AS DX_ADR_EML
		,DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC) AS DAYS_DELQ
		,PD32.DC_STA_PD32

FROM	OLWHRM1.LN10_LON LN10
		INNER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
			ON LN10.BF_SSN = LN16.BF_SSN
			AND LN10.LN_SEQ = LN16.LN_SEQ
			AND DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC) >= 5
			AND LN16.LC_STA_LON16 = '1'
			AND LN10.IC_LON_PGM IN (&PRIVATE_LIST)
			AND	LN10.LA_CUR_PRI > 0
		INNER JOIN OLWHRM1.PD32_PRS_ADR_EML PD32
			ON LN10.BF_SSN = PD32.DF_PRS_ID
			AND PD32.DI_VLD_ADR_EML = 'Y'
			AND PD32.DC_ADR_EML <> ''
			AND NOT PD32.DC_ADR_EML IS NULL 
			AND PD32.DC_STA_PD32 = 'A'
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON LN10.BF_SSN =  PD10.DF_PRS_ID

WHERE	NOT EXISTS (SELECT	*
					FROM	OLWHRM1.AY10_BR_LON_ATY	AY10
					WHERE	AY10.PF_REQ_ACT IN ('GOEN1', 'GOEN2', 'GOEN3', 'GOEN4', 'GOEN5', 'GOEN6')
							AND DAYS(CURRENT DATE) - DAYS(AY10.LD_ATY_REQ_RCV) <= 10
							AND AY10.BF_SSN = LN10.BF_SSN)
ORDER BY DF_SPE_ACC_ID
	);

/*GODELEND - ENDORSERS*/
CREATE TABLE GODELEND AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	DISTINCT
		LN20.LF_EDS
		,PD10.DF_SPE_ACC_ID 									
		,TRIM(PD10.DM_PRS_1)||' '||TRIM(PD10.DM_PRS_LST) AS DM_PRS_NM
		,UPPER(PD32.DX_ADR_EML)	AS DX_ADR_EML
		,DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC) AS DAYS_DELQ
		,PD32.DC_STA_PD32

FROM	OLWHRM1.LN10_LON LN10
		INNER JOIN OLWHRM1.LN16_LON_DLQ_HST LN16
			ON LN10.BF_SSN = LN16.BF_SSN
			AND LN10.LN_SEQ = LN16.LN_SEQ
			AND DAYS(CURRENT DATE) - DAYS(LN16.LD_DLQ_OCC) >= 5
			AND LN16.LC_STA_LON16 = '1'
			AND LN10.IC_LON_PGM IN (&PRIVATE_LIST)
			AND	LN10.LA_CUR_PRI > 0
		INNER JOIN OLWHRM1.LN20_EDS LN20
			ON LN20.BF_SSN = LN10.BF_SSN
			AND LN20.LN_SEQ = LN10.LN_SEQ
			AND LN20.LC_STA_LON20 = 'A'
		INNER JOIN OLWHRM1.PD32_PRS_ADR_EML PD32
			ON LN20.LF_EDS = PD32.DF_PRS_ID
			AND PD32.DI_VLD_ADR_EML = 'Y'
			AND PD32.DC_ADR_EML <> ''
			AND NOT PD32.DC_ADR_EML IS NULL 
			AND PD32.DC_STA_PD32 = 'A'
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON LN20.LF_EDS =  PD10.DF_PRS_ID

WHERE	NOT EXISTS (SELECT	*
					FROM	OLWHRM1.AY10_BR_LON_ATY	AY10
					WHERE	AY10.PF_REQ_ACT IN ('GOEN1', 'GOEN2', 'GOEN3', 'GOEN4', 'GOEN5', 'GOEN6')
							AND DAYS(CURRENT DATE) - DAYS(AY10.LD_ATY_REQ_RCV) <= 10
							AND AY10.BF_SSN = LN20.LF_EDS)
ORDER BY DF_SPE_ACC_ID
	);

/*GODELNOTICE - BORROWER & ENDORSERS*/
CREATE TABLE GODELNOTICE AS
SELECT *
FROM	GODEL
UNION
SELECT *
FROM	GODELEND
ORDER BY DF_SPE_ACC_ID;

DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWS35.LWS35RZ);*/
/*QUIT;*/
ENDRSUBMIT;

/*DATA GODEL;	SET WORKLOCL.GODEL; RUN;*/
/*DATA GODELEND;	SET WORKLOCL.GODELEND; RUN;*/
DATA GODELNOTICE;	SET WORKLOCL.GODELNOTICE; RUN;

%MACRO DELQ_FILES(RNO,LB,UB);
DATA _NULL_;
	SET  WORK.GODELNOTICE;
	WHERE &LB <= DAYS_DELQ <= &UB;
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
		FORMAT DF_SPE_ACC_ID $10. ;
		FORMAT DM_PRS_NM $34. ;
		FORMAT DX_ADR_EML $254. ;

	IF _N_ = 1 THEN DO;
		PUT "AccountNumber,DM_PRS_NM,Recipient" ;
	END;

	DO;
	   PUT DF_SPE_ACC_ID $ @;
	   PUT DM_PRS_NM $ @;
	   PUT DX_ADR_EML $ ;
	END;
RUN;
%MEND DELQ_FILES;

%DELQ_FILES(2,5,30);
%DELQ_FILES(3,31,60);
%DELQ_FILES(4,61,90);
%DELQ_FILES(5,91,120);
%DELQ_FILES(6,121,150);
%DELQ_FILES(7,151,180);

/*DETAIL FILE*/
PROC EXPORT DATA=GODELNOTICE
	OUTFILE= "T:\SAS\S35Detail.xls" 
	DBMS=EXCEL2000 REPLACE;
RUN;
