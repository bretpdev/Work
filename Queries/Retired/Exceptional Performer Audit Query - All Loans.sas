*--------------------------------------------------------------*
| EXCEPTIONAL PERFORMER AUDIT QUERY - LOANS ENTERING REPAYMENT |
*--------------------------------------------------------------*;
%LET BEGIN = '01/01/2006';
%LET END = '06/30/2006';

%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE EPAQALR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT B.DF_SPE_ACC_ID
	,B.DM_PRS_LST
	,B.DM_PRS_1
	,A.LN_SEQ
	,RTRIM(A.LF_LON_ALT)||'0'||CHAR(A.LN_LON_ALT_SEQ) AS CLUID
	,C.WC_DW_LON_STA
	,C.WD_LON_RPD_SR
	,D.LD_DLQ_OCC
	,SEP_DT.LD_SCL_SPR
	,DIS_PRT.LD_SNT_RPD_DIS
	,LO.WD_ORG_RPD
	,A.IC_LON_PGM
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C	
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.AY10_BR_LON_ATY E
	ON A.BF_SSN = E.BF_SSN
LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST D
	ON C.BF_SSN = D.BF_SSN
	AND C.LN_SEQ = D.LN_SEQ
	AND D.LC_STA_LON16 = '1'
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LN_SEQ
		,WD_ORG_RPD
	FROM OLWHRM1.GR10_RPT_LON_APL 
/*	WHERE WC_STA_GR10 = 'A'*/
	) LO
	ON A.BF_SSN = LO.BF_SSN
	AND A.LN_SEQ = LO.LN_SEQ
LEFT OUTER JOIN (
	SELECT A.BF_SSN
		,A.LN_SEQ
		,MAX(B.LD_SNT_RPD_DIS) AS LD_SNT_RPD_DIS
	FROM OLWHRM1.LN65_LON_RPS A
	INNER JOIN OLWHRM1.RS10_BR_RPD B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_RPS_SEQ = B.LN_RPS_SEQ
	GROUP BY A.BF_SSN
		,A.LN_SEQ
	) DIS_PRT
	ON A.BF_SSN = DIS_PRT.BF_SSN
	AND A.LN_SEQ = DIS_PRT.LN_SEQ
LEFT OUTER JOIN (
	SELECT DISTINCT B.BF_SSN
		,B.LN_SEQ
		,A.LD_SCL_SPR
	FROM OLWHRM1.SD10_STU_SPR A
	INNER JOIN OLWHRM1.LN13_LON_STU_OSD B
		ON A.LF_STU_SSN = B.LF_STU_SSN
		AND A.LN_STU_SPR_SEQ = B.LN_STU_SPR_SEQ
	WHERE A.LC_STA_STU10 = 'A'
	AND B.LC_STA_LON13 = 'A'
	) SEP_DT
	ON A.BF_SSN = SEP_DT.BF_SSN
	AND A.LN_SEQ = SEP_DT.LN_SEQ
WHERE E.LD_ATY_REQ_RCV BETWEEN &BEGIN AND &END
AND E.PF_REQ_ACT IN (
	'DL200','DL400','DL500','DL800','DL910','DL140', 
	'DL160','DL170','DL202','DL203','DL230','DLFDL'
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWAB1.LWAB1RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA EPAQALR;
SET WORKLOCL.EPAQALR;
RUN;

PROC FORMAT;
VALUE $STAT '01'='GRACE' 
	'02'='SCHOOL'
	'03'='REPAYMENT' 
	'04'='DEFERMENT' 
	'05'='FORBEARANCE' 
	'06'='CURE' 
	'07'='CLAIM PENDING' 
	'08'='CLAIM SUBMITTED' 
	'09'='CLAIM CANCLED' 
	'10'='CLAIM REJECT' 
	'11'='CLAIM RETURNED' 
	'12'='CLAIM PAID' 
	'13'='PRE-CLAIM PENDING' 
	'14'='PRE-CLAIM SUBMITTED'
	'15'='PRE-CLAIM CANCELLED' 
	'16'='DEATH ALLEGED' 
	'17'='DEATH VERIFIED' 
	'18'='DISABILITY ALLEGED' 
	'19'='DISABILITY VERIFIED' 
	'20'='BANKRUPTCY ALLEGED' 
	'21'='BANKRUPTCY VERIFIED' 
	'22'='PAID IN FULL' 
	'23'='NOT FULLY ORIGINATED' 
	'88'='PROCESSING ERROR';
RUN; 

DATA _NULL_;
SET  WORK.EPAQALR;
FILE 'C:\WINDOWS\TEMP\ExceptionalPerformerAQ.ALIR.R2.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT DM_PRS_LST $23. ;
   FORMAT DM_PRS_1 $13. ;
   FORMAT LN_SEQ 6. ;
   FORMAT CLUID $24. ;
   FORMAT WC_DW_LON_STA $STAT. ;
   FORMAT WD_LON_RPD_SR MMDDYY10. ;
   FORMAT LD_DLQ_OCC MMDDYY10. ;
   FORMAT LD_SCL_SPR MMDDYY10. ;
   FORMAT LD_SNT_RPD_DIS MMDDYY10. ;
   FORMAT WD_ORG_RPD MMDDYY10. ;
   FORMAT IC_LON_PGM $6. ;
IF _N_ = 1 THEN   
 DO;
   PUT
	'DF_SPE_ACC_ID' 
	','
	'DM_PRS_LST'  
	','
	'DM_PRS_1' 
	','
	'LN_SEQ' 
	','
	'IC_LON_PGM' 
	','
	'WC_DW_LON_STA' 
	','
	'LD_SCL_SPR' 
	','
	'WD_LON_RPD_SR' 
	','
	'WD_ORG_RPD' 
	','
	'LD_SNT_RPD_DIS' 
	','
	'LD_DLQ_OCC' 
	','
	'CLUID' ;
END;
DO;
   	PUT DF_SPE_ACC_ID $ @;
   	PUT DM_PRS_LST $ @;
   	PUT DM_PRS_1 $ @;
   	PUT LN_SEQ @;
	PUT IC_LON_PGM $ @;
	PUT WC_DW_LON_STA $ @;
	PUT LD_SCL_SPR @;
	PUT WD_LON_RPD_SR @;
	PUT WD_ORG_RPD @;
	PUT LD_SNT_RPD_DIS @;
	PUT LD_DLQ_OCC @;
	PUT CLUID $ ;
END;
RUN;
