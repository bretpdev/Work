/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = C:\WINDOWS\TEMP;
FILENAME REPORT2 "&RPTLIB/ULWS05.LWS05R2";
FILENAME REPORT3 "&RPTLIB/ULWS05.LWS05R3";
FILENAME REPORT4 "&RPTLIB/ULWS05.LWS05R4";
FILENAME REPORT5 "&RPTLIB/ULWS05.LWS05R5";
%LET LNUM = 36; /*SET MAX# OF LOANS TO WRITE TO ERROR REPORT*/
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE SLXFER AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,C.DF_SPE_ACC_ID
	,C.DM_PRS_LST
	,RTRIM(C.DM_PRS_1)||' '||RTRIM(C.DM_PRS_MID)||' '||RTRIM(C.DM_PRS_LST) AS NAME
	,D.DX_STR_ADR_1
	,D.DX_STR_ADR_2
	,D.DX_STR_ADR_3
	,D.DM_CT
	,D.DC_DOM_ST
	,D.DF_ZIP_CDE
	,D.DM_FGN_ST
	,D.DM_FGN_CNY
	,D.DI_VLD_ADR
	,D.DC_ADR
	,A.IC_LON_PGM
	,A.LD_LON_1_DSB
	,A.LA_CUR_PRI
	,RS.LA_RPS_ISL
	,RS.LD_RPS_1_PAY_DU
	,A.LN_SEQ
	,B.LD_OWN_EFF_SR
	,A.IF_DOE_LDR
	,E.IF_SLL_OWN_SLD
	,E.IF_BUY_OWN_SLD
FROM  OLWHRM1.LN10_LON A 
INNER JOIN OLWHRM1.LN35_LON_OWN B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN
	(SELECT LN65.BF_SSN
		,LN65.LN_SEQ
		,LN66.LA_RPS_ISL
		,RS10.LD_RPS_1_PAY_DU
	FROM OLWHRM1.LN65_LON_RPS LN65
	INNER JOIN OLWHRM1.LN66_LON_RPS_SPF LN66
		ON LN65.BF_SSN = LN66.BF_SSN
		AND LN65.LN_SEQ = LN66.LN_SEQ
		AND LN65.LN_RPS_SEQ = LN66.LN_RPS_SEQ
		AND LN66.LN_GRD_RPS_SEQ = 1
		AND LN65.LC_STA_LON65 = 'A'
	INNER JOIN OLWHRM1.RS10_BR_RPD RS10
		ON LN65.BF_SSN = RS10.BF_SSN
		AND LN65.LN_RPS_SEQ = RS10.LN_RPS_SEQ
		AND RS10.LC_STA_RPST10 = 'A'
	)RS
	ON A.BF_SSN = RS.BF_SSN
	AND A.LN_SEQ = RS.LN_SEQ
LEFT OUTER JOIN OLWHRM1.LN99_LON_SLE_FAT E
	ON B.BF_SSN = E.BF_SSN
	AND B.LN_SEQ = E.LN_SEQ
	AND B.IF_LON_SLE = E.IF_LON_SLE
WHERE A.LF_LON_CUR_OWN in (&UHEAA_LIST)
	AND A.LF_LON_CUR_OWN != '834437'
	AND DAYS(B.LD_OWN_EFF_SR) = DAYS(CURRENT DATE) - 1	
	AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')
	AND D.DC_ADR = 'L'
	AND 
	(
		E.IF_LON_SLE IS NULL
	OR 
		(
			NOT 
			(
				A.LF_LON_CUR_OWN = E.IF_SLL_OWN_SLD AND 
				E.IF_SLL_OWN_SLD = E.IF_BUY_OWN_SLD
			)
		)	
	)		
/******************************************************
* SPECIFY A SPECIFIC LENDER AND SALE DATE IF NECESSARY
*******************************************************
* AND B.LD_OWN_EFF_SR = '06/07/2005' 
* AND A.IF_DOE_LDR = '817455'
* B.LD_OWN_EFF_SR = '09/23/2004' 
* AND A.LF_LON_CUR_OWN = '828476'
*******************************************************/
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA SLXFER; 
SET WORKLOCL.SLXFER; 
RUN;

PROC SORT DATA=SLXFER;
BY BF_SSN LN_SEQ;
RUN;

PROC SQL;
CREATE TABLE SLXFER2 AS 
SELECT BF_SSN
FROM SLXFER
GROUP BY BF_SSN
HAVING COUNT(*) > &LNUM; 
QUIT;
RUN;

DATA SLXFER ERR(KEEP=BF_SSN NAME);
MERGE SLXFER (IN=A) SLXFER2 (IN=B);
BY BF_SSN;
IF A AND B THEN OUTPUT ERR;
ELSE OUTPUT SLXFER;
RUN;

PROC SORT DATA=ERR NODUPRECS;
BY BF_SSN;
RUN;

DATA SLXFER;
SET SLXFER;
IF DC_DOM_ST = '' THEN STATE_CODE = 'A';
ELSE STATE_CODE = 'Z';
COST_CENTER_CODE = 'MA2324';
RUN;

%MACRO KEY_CLC(TBL);
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;

%KEY_CLC(SLXFER);

PROC SORT DATA=SLXFER;
BY STATE_CODE DM_PRS_LST;
RUN;

DATA SLX1 SLX2 SLX3;
SET SLXFER;
IF DI_VLD_ADR = 'N' THEN OUTPUT SLX1;
ELSE IF DI_VLD_ADR = 'Y' AND LA_RPS_ISL = . THEN OUTPUT SLX2;
ELSE IF DI_VLD_ADR = 'Y' AND LA_RPS_ISL NE . THEN OUTPUT SLX3;
RUN;

%MACRO PRINTEM(SET,RP);
DATA _NULL_;
SET &SET;
FILE REPORT&RP DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT BF_SSN $9.;
FORMAT NAME $51. ;
FORMAT DX_STR_ADR_1 $30. ;
FORMAT DX_STR_ADR_2 $30. ;
FORMAT DX_STR_ADR_3 $30. ;
FORMAT DM_CT $20. ;
FORMAT DC_DOM_ST $2. ;
FORMAT DF_ZIP_CDE $17. ;
FORMAT DM_FGN_ST $15. ;
FORMAT DM_FGN_CNY $25. ;
FORMAT IC_LON_PGM $6. ;
FORMAT LD_LON_1_DSB MMDDYY10. ;
FORMAT LA_CUR_PRI 10.2 ;
FORMAT LA_RPS_ISL 9.2 ;
FORMAT LD_RPS_1_PAY_DU MMDDYY10. ;
FORMAT LN_SEQ 6. ;
FORMAT LD_OWN_EFF_SR MMDDYY10. ;
FORMAT IF_DOE_LDR $8. ;
FORMAT DF_SPE_ACC_ID $10. ;
FORMAT COST_CENTER_CODE $6. ;
FORMAT ACSKEY $18. ;

DO;
	PUT BF_SSN $ @;
	PUT NAME $ @;
	PUT DX_STR_ADR_1 $ @;
	PUT DX_STR_ADR_2 $ @;
	PUT DX_STR_ADR_3 $ @;
	PUT DM_CT $ @;
	PUT DC_DOM_ST $ @;
	PUT DF_ZIP_CDE $ @;
	PUT DM_FGN_ST $ @;
	PUT DM_FGN_CNY $ @;
	PUT IC_LON_PGM $ @;
	PUT LD_LON_1_DSB @;
	PUT LA_CUR_PRI @;
	PUT LA_RPS_ISL @;
	PUT LD_RPS_1_PAY_DU @;
	PUT LN_SEQ @;
	PUT LD_OWN_EFF_SR @;
	PUT IF_DOE_LDR $ @;
	PUT DF_SPE_ACC_ID $ @;
	PUT ACSKEY $ @ ;
	PUT DC_DOM_ST $ @;
	PUT COST_CENTER_CODE $;
END;
RUN;
%MEND PRINTEM;

%PRINTEM(SLX1,2);
%PRINTEM(SLX2,3);
%PRINTEM(SLX3,4);

DATA _NULL_;                                      
CURRENT=TODAY();                                  
CALL SYMPUT('CURDATE',PUT(CURRENT,MMDDYY10.));       
RUN; 

PROC PRINTTO PRINT=REPORT5 NEW;
RUN;
OPTIONS ORIENTATION = PORTRAIT PAGENO=1;
OPTIONS PS=52 LS=96;
TITLE	'LOAN SALE TRANFER LETTER';
TITLE2 	"BORROWERS WITH MORE THAN &LNUM LOANS";
TITLE3	"RUNDATE: &CURDATE";
FOOTNOTE  'JOB = UTLWS05  	 REPORT = ULWS05.LWS05R5';
PROC CONTENTS DATA=ERR OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 96*'-';
	PUT      ////////
		@35 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@39 '-- END OF REPORT --';
	PUT ////////////////
		@29 "JOB = UTLWS05   	 REPORT = ULWS05.LWS05R5";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=ERR WIDTH=MIN;
VAR BF_SSN NAME;LABEL BF_SSN = 'SSN';
RUN;
PROC PRINTTO;
RUN;
