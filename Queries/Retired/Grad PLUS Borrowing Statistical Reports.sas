*-----------------------------------------*
| GRAD PLUS BORROWING STATISTICAL REPORTS |
*-----------------------------------------*;
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/GradPLStats.R2.txt";
FILENAME REPORT3 "&RPTLIB/GradPLStats.R3.txt";
FILENAME REPORT4 "&RPTLIB/GradPLStats.R4.txt";
FILENAME REPORT5 "&RPTLIB/GradPLStats.R5.txt";
FILENAME REPORT6 "&RPTLIB/GradPLStats.R6.txt";
FILENAME REPORT7 "&RPTLIB/GradPLStats.R7.txt";
FILENAME REPORT8 "&RPTLIB/GradPLStats.R8.txt";
FILENAME REPORT9 "&RPTLIB/GradPLStats.R9.txt";
DATA _NULL_;
YEARS_AGO = 1; /*SET THIS TO 0 FOR THE CURRENT FY*/
	CALL SYMPUT('FYBEG',"'"||PUT(INTNX('YEAR.7',TODAY(),- YEARS_AGO,'BEGINNING'), MMDDYYS10.)||"'");
	CALL SYMPUT('FYEND',"'"||PUT(INTNX('YEAR.7',TODAY(),- YEARS_AGO,'END'), MMDDYYS10.)||"'");
	CALL SYMPUT('FY',TRIM(PUT(INTNX('YEAR.7',TODAY(),- YEARS_AGO,'END'),YEAR4.)));
RUN;
%SYSLPUT FYBEG = &FYBEG;
%SYSLPUT FYEND = &FYEND;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE GPLSSR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID_BR
	,B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,B.AC_LON_TYP
	,B.AC_PRC_STA 
	,B.AD_PRC 
	,B.AA_GTE_LON_AMT
	,A.AD_APL_CRT 
	,A.AC_CRD_CHK_PRF 
	,A.AC_EDS_TYP
	,STFRD.SIND 
	,C.IM_IST_FUL
	,A.AF_APL_OPS_SCL 
	,C.IC_SCL_TYP
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
LEFT OUTER JOIN OLWHRM1.SC01_LGS_SCL_INF C
	ON A.AF_APL_OPS_SCL = C.IF_IST
LEFT OUTER JOIN (
	SELECT DISTINCT LJ1.DF_PRS_ID_BR
		,'X' AS SIND
	FROM OLWHRM1.GA01_APP LJ1
	INNER JOIN OLWHRM1.GA10_LON_APP LJ2
		ON LJ1.AF_APL_ID = LJ2.AF_APL_ID
	WHERE LJ2.AC_LON_TYP IN ('SF','SU')
	AND LJ2.AC_PRC_STA = 'A' 
	AND LJ2.AD_PRC BETWEEN &FYBEG AND &FYEND
	) STFRD
	ON A.DF_PRS_ID_BR = STFRD.DF_PRS_ID_BR
WHERE B.AC_LON_TYP IN ('GB','PL')
AND B.AD_PRC BETWEEN &FYBEG AND &FYEND
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWAB1.LWAB1RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA GPLSSR;
SET WORKLOCL.GPLSSR;
RUN;
PROC SORT DATA=GPLSSR;BY DF_PRS_ID_BR CLUID;RUN;
/****************************************************************************
* FILE PROCESSING
*****************************************************************************/
PROC SQL;
/***********************************
* R2
************************************/
CREATE TABLE R2 AS 
SELECT AC_LON_TYP
	,SUM(COALESCE(AA_GTE_LON_AMT,0)) AS LN_SUM
	,COUNT(DISTINCT CLUID) AS LN_COUNT
	,SUM(COALESCE(AA_GTE_LON_AMT,0)) / COUNT(DISTINCT CLUID) AS LN_AVG_GUAR
FROM GPLSSR
WHERE AC_PRC_STA = 'A'
AND AC_LON_TYP = 'GB'
GROUP BY AC_LON_TYP;
/***********************************
* R3
************************************/
CREATE TABLE R3 AS 
SELECT BR_CT
	,BR_NLNS
	,COALESCE(B.BR_NLNS,0)/A.BR_CT AS PERCENT
FROM (
	SELECT AC_LON_TYP
		,COUNT(DISTINCT DF_PRS_ID_BR) AS BR_CT 
	FROM GPLSSR
	WHERE AC_PRC_STA = 'A'
	AND AC_LON_TYP = 'GB'
	GROUP BY AC_LON_TYP
	) A
LEFT OUTER JOIN (
	SELECT AC_LON_TYP
		,COUNT(DISTINCT DF_PRS_ID_BR) AS BR_NLNS 
	FROM GPLSSR
	WHERE AC_PRC_STA = 'A'
	AND AC_LON_TYP = 'GB'
	AND SIND ^= 'X'
	GROUP BY AC_LON_TYP
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP;
/***********************************
* R4
************************************/
CREATE TABLE R4 AS 
SELECT COALESCE(COUNT(DISTINCT DF_PRS_ID_BR),0) AS BR_WEDS
FROM GPLSSR 
WHERE AC_PRC_STA = 'A'
AND AC_LON_TYP = 'GB'
AND AC_EDS_TYP = 'E';
/***********************************
* R5
************************************/
CREATE TABLE R5 AS 
	SELECT DISTINCT 'GRAD PLUS APPROVED CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,1) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,1 AS AC_CRD_CHK_PRF 
			,COUNT(DISTINCT CLUID) AS CRDCK
		FROM GPLSSR
		WHERE AC_LON_TYP = 'GB'
		AND AC_CRD_CHK_PRF IN ('Y')
		GROUP BY AC_LON_TYP 
			,AC_CRD_CHK_PRF 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'GB'
UNION
	SELECT DISTINCT 'GRAD PLUS DENIED CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,2) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,AC_CRD_CHK_PRF 
			,SUM(COALESCE(CRDCK,0)) AS CRDCK
		FROM (
			SELECT AC_LON_TYP 
				,2 AS AC_CRD_CHK_PRF 
				,COUNT(DISTINCT CLUID) AS CRDCK
			FROM GPLSSR
			WHERE AC_LON_TYP = 'GB'
			AND AC_CRD_CHK_PRF IN ('A','D')
			GROUP BY AC_LON_TYP 
				,AC_CRD_CHK_PRF 
			)
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'GB'
UNION
	SELECT DISTINCT 'GRAD PLUS PENDING CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,3) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,3 AS AC_CRD_CHK_PRF 
			,COUNT(DISTINCT CLUID) AS CRDCK
		FROM GPLSSR
		WHERE AC_LON_TYP = 'GB'
		AND AC_CRD_CHK_PRF = 'C'
		GROUP BY AC_LON_TYP 
			,AC_CRD_CHK_PRF 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'GB';
/***********************************
* R6
************************************/
CREATE TABLE R6 AS 
SELECT DSCR
	,SRTBY
	,PLNS
FROM (
	SELECT DISTINCT 'GRAD PLUS LOANS AT PUBLIC INSTITUTIONS' AS DSCR
		,COALESCE(B.TYP,1) AS SRTBY
		,COALESCE(B.PLNS,0) AS PLNS
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,1 AS TYP
			,COUNT(DISTINCT CLUID) AS PLNS
		FROM GPLSSR
		WHERE AC_LON_TYP = 'GB'
		AND AC_PRC_STA = 'A'
		AND IC_SCL_TYP <> '00' 
		GROUP BY AC_LON_TYP 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'GB'
UNION
	SELECT DISTINCT 'GRAD PLUS LOANS AT PRIVATE INSTITUTIONS' AS DSCR
		,COALESCE(B.TYP,2) AS SRTBY
		,COALESCE(B.PLNS,0) AS PLNS
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,2 AS TYP
			,COUNT(DISTINCT CLUID) AS PLNS
		FROM GPLSSR
		WHERE AC_LON_TYP = 'GB'
		AND AC_PRC_STA = 'A'
		AND IC_SCL_TYP = '00' 
		GROUP BY AC_LON_TYP 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'GB'
)
ORDER BY SRTBY;
/***********************************
* R7
************************************/
CREATE TABLE R7 AS 
SELECT COALESCE(COUNT(DISTINCT DF_PRS_ID_BR),0) AS BR_WEDS
FROM GPLSSR 
WHERE AC_PRC_STA = 'A'
AND AC_LON_TYP = 'PL'
AND AC_EDS_TYP = 'E';
/***********************************
* R8
************************************/
CREATE TABLE R8 AS 
	SELECT DISTINCT 'PLUS APPROVED CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,1) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,1 AS AC_CRD_CHK_PRF 
			,COUNT(DISTINCT CLUID) AS CRDCK
		FROM GPLSSR
		WHERE AC_LON_TYP = 'PL'
		AND AC_CRD_CHK_PRF IN ('Y')
		GROUP BY AC_LON_TYP 
			,AC_CRD_CHK_PRF 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'PL'
UNION
	SELECT DISTINCT 'PLUS DENIED CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,2) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,AC_CRD_CHK_PRF 
			,SUM(COALESCE(CRDCK,0)) AS CRDCK
		FROM (
			SELECT AC_LON_TYP 
				,2 AS AC_CRD_CHK_PRF 
				,COUNT(DISTINCT CLUID) AS CRDCK
			FROM GPLSSR
			WHERE AC_LON_TYP = 'PL'
			AND AC_CRD_CHK_PRF IN ('A','D')
			GROUP BY AC_LON_TYP 
				,AC_CRD_CHK_PRF 
			)
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'PL'
UNION
	SELECT DISTINCT 'PLUS PENDING CREDIT' AS DSCR
		,COALESCE(B.AC_CRD_CHK_PRF,3) AS AC_CRD_CHK_PRF
		,COALESCE(B.CRDCK,0) AS CRDCK
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,3 AS AC_CRD_CHK_PRF 
			,COUNT(DISTINCT CLUID) AS CRDCK
		FROM GPLSSR
		WHERE AC_LON_TYP = 'PL'
		AND AC_CRD_CHK_PRF = 'C'
		GROUP BY AC_LON_TYP 
			,AC_CRD_CHK_PRF 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'PL';
/***********************************
* R9
************************************/
CREATE TABLE R9 AS 
SELECT DSCR
	,SRTBY
	,PLNS
FROM (
	SELECT DISTINCT 'PLUS LOANS AT PUBLIC INSTITUTIONS' AS DSCR
		,COALESCE(B.TYP,1) AS SRTBY
		,COALESCE(B.PLNS,0) AS PLNS
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,1 AS TYP
			,COUNT(DISTINCT CLUID) AS PLNS
		FROM GPLSSR
		WHERE AC_LON_TYP = 'PL'
		AND AC_PRC_STA = 'A'
		AND IC_SCL_TYP <> '00' 
		GROUP BY AC_LON_TYP 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'PL'
UNION
	SELECT DISTINCT 'PLUS LOANS AT PRIVATE INSTITUTIONS' AS DSCR
		,COALESCE(B.TYP,2) AS SRTBY
		,COALESCE(B.PLNS,0) AS PLNS
	FROM GPLSSR A
	LEFT OUTER JOIN (
		SELECT AC_LON_TYP 
			,2 AS TYP
			,COUNT(DISTINCT CLUID) AS PLNS
		FROM GPLSSR
		WHERE AC_LON_TYP = 'PL'
		AND AC_PRC_STA = 'A'
		AND IC_SCL_TYP = '00' 
		GROUP BY AC_LON_TYP 
	) B
	ON A.AC_LON_TYP = B.AC_LON_TYP
	WHERE A.AC_LON_TYP = 'PL'
)
ORDER BY SRTBY;
QUIT;
/****************************************************************************
* REPORT CREATION
*****************************************************************************/
%MACRO GBPRINT1(DS,NUM,T1,FRMT,VARS,LABS);
PROC PRINTTO PRINT=REPORT&NUM NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE "&T1";
TITLE2 "FISCAL YEAR &FY";
FOOTNOTE   "JOB = GRAD PLUS STATISTICS  	 REPORT = R&NUM";
PROC PRINT NOOBS SPLIT='~' DATA=&DS WIDTH=UNIFORM WIDTH=MIN;
FORMAT &FRMT ;
VAR &VARS ;
LABEL &LABS;
RUN;
PROC PRINTTO;
RUN;
%MEND GBPRINT1;
%MACRO GBPRINT2(DS,NUM,T1,T2,FRMT,VARS,LABS,SVAR);
PROC PRINTTO PRINT=REPORT&NUM NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE "&T1";
TITLE2 "&T2";
TITLE3 "FISCAL YEAR &FY";
FOOTNOTE   "JOB = GRAD PLUS STATISTICS  	 REPORT = R&NUM";
PROC PRINT NOOBS SPLIT='~' DATA=&DS WIDTH=UNIFORM WIDTH=MIN;
FORMAT &FRMT ;
VAR &VARS ;
LABEL &LABS;
SUM &SVAR;
RUN;
PROC PRINTTO;
RUN;
%MEND GBPRINT2;

%GBPRINT1(R2,2,ORIGINAL AVERAGE GRAD PLUS AMOUNT GUARANTEED,
	%STR(LN_SUM DOLLAR18.2 LN_COUNT COMMA6. LN_AVG_GUAR DOLLAR18.2),
	%STR(LN_SUM LN_COUNT LN_AVG_GUAR),
	%STR(LN_SUM = 'SUM OF GUARANTEES' LN_COUNT = 'TOTAL # OF LOANS' LN_AVG_GUAR = 'AVERAGE ORIGINAL ~ AMOUNT GUARANTEED')
	);
%GBPRINT1(R3,3,PERCENT OF GRAD PLUS BORROWERS WITH NO STAFFORD LOANS,
	%STR(BR_NLNS COMMA6. PERCENT PERCENT10.),
	%STR(BR_NLNS PERCENT),
	%STR(BR_NLNS = 'TOTAL # OF GRAD PLUS BORROWERS ~ W/O STAFFORD LOANS' PERCENT = 'TOTAL # OF GRAD PLUS BORROWERS ~ % W/O STAFFORD LOANS')
	);
%GBPRINT1(R4,4,GRAD PLUS BORROWERS WITH ENDORSERS,
	%STR(BR_WEDS COMMA6.),
	%STR(BR_WEDS),
	%STR(BR_WEDS = 'GRAD PLUS BORROWERS ~ WITH ENDORSER')
	);
%GBPRINT2(R5,5,GRAD PLUS LOANS ADDED TO SYSTEM,
	SUBTOTALED BY APPROVED AND DENIED CREDIT,
	%STR(CRDCK COMMA6.),
	%STR(DSCR CRDCK),
	%STR(DSCR = 'CATEGORY' CRDCK = 'SUBTOTALS'),
	%STR(CRDCK)
	);
%GBPRINT2(R6,6,TOTAL GRAD PLUS LOANS GUARANTEED,
	PUBLIC vs PRIVATE INSTITUTIONS,
	%STR(PLNS COMMA6.),
	%STR(DSCR PLNS),
	%STR(DSCR = 'CATEGORY' PLNS = 'SUBTOTALS'),
	%STR(PLNS)
	);
%GBPRINT1(R7,7,PLUS BORROWERS WITH ENDORSERS,
	%STR(BR_WEDS COMMA6.),
	%STR(BR_WEDS),
	%STR(BR_WEDS = 'PLUS BORROWERS ~ WITH ENDORSER')
	);
%GBPRINT2(R8,8,PLUS LOANS ADDED TO SYSTEM,
	SUBTOTALED BY APPROVED AND DENIED CREDIT,
	%STR(CRDCK COMMA6.),
	%STR(DSCR CRDCK),
	%STR(DSCR = 'CATEGORY' CRDCK = 'SUBTOTALS'),
	%STR(CRDCK)
	);
%GBPRINT2(R9,9,TOTAL PLUS LOANS GUARANTEED,
	PUBLIC vs PRIVATE INSTITUTIONS,
	%STR(PLNS COMMA6.),
	%STR(DSCR PLNS),
	%STR(DSCR = 'CATEGORY' PLNS = 'SUBTOTALS'),
	%STR(PLNS)
	);
