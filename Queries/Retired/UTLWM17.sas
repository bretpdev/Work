*-------------------------------------------------------*
| UTLWM17 - AWG AMNESTY PROGRAM AMENDED OOW TO EMPLOYER |
*-------------------------------------------------------*;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
/*%LET RPTLIB = C:\WINDOWS\TEMP;*/
FILENAME REPORTZ "&RPTLIB/ULWM17.LWM17RZ";
FILENAME REPORT2 "&RPTLIB/ULWM17.LWM17R2";
DATA _NULL_;
     CALL SYMPUT('DAYS_30',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
/*%SYSLPUT DAYS_30 = &DAYS_30;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
%MACRO SQLCHECK ;
  %IF  &SQLXRC NE 0  %THEN  %DO  ;
    DATA _NULL_  ;
            FILE REPORTZ NOTITLES  ;
            PUT @01 " ********************************************************************* "
              / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
              / @01 " ****  THE SAS SHOULD BE REVIEWED.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
              / @01 " ****  &SQLXMSG   **** "
              / @01 " ********************************************************************* "
            ;
         RUN  ;
  %END  ;
%MEND  ;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AAPAOTE AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT DC01.BF_SSN
	,PD01.DF_SPE_ACC_ID
	,PD01.DM_PRS_1
	,PD01.DM_PRS_LST
	,PD03.DX_STR_ADR_1
	,PD03.DX_STR_ADR_2
	,PD03.DM_CT
	,PD03.DC_DOM_ST
	,PD03.DF_ZIP
	,PD03.DC_ADR
	,PD03.DM_FGN_CNY
	,SUM(
		COALESCE(DC01.LA_CLM_PRI,0) + 
		COALESCE(DC01.LA_CLM_INT,0) -
		COALESCE(DC01.LA_PRI_COL,0) +
		COALESCE(DC01.LA_INT_ACR,0) +
		COALESCE(DC02.LA_CLM_INT_ACR,0) -
		COALESCE(DC01.LA_INT_COL,0)
		) + SUM(
				COALESCE(DC01.LA_LEG_CST_ACR,0) -
				COALESCE(DC01.LA_LEG_CST_COL,0) +
				COALESCE(DC01.LA_OTH_CHR_ACR,0) -
				COALESCE(DC01.LA_OTH_CHR_COL,0) +
				COALESCE(DC01.LA_COL_CST_ACR,0) -
				COALESCE(DC01.LA_COL_CST_COL,0) +
				COALESCE(DC02.LA_CLM_PRJ_COL_CST,0)
				) AS ACO 
	,IN01.IM_IST_FUL
	,IN01.IX_GEN_STR_ADR_1
	,IN01.IX_GEN_STR_ADR_2
	,IN01.IX_GEN_STR_ADR_3
	,IN01.IM_GEN_CT
	,IN01.IC_GEN_ST
	,IN01.IF_GEN_ZIP
FROM OLWHRM1.DC01_LON_CLM_INF DC01
INNER JOIN (
		SELECT BF_SSN
		FROM OLWHRM1.DC01_LON_CLM_INF X
		INNER JOIN OLWHRM1.LA10_LEG_ACT Y
			ON X.BF_SSN = Y.DF_PRS_ID_BR
		INNER JOIN OLWHRM1.LA11_LEG_ACT_ATY Z
			ON Y.DF_PRS_ID_BR = Z.DF_PRS_ID_BR
			AND Y.BF_CRT_DTS_LA10 = Z.BF_CRT_DTS_LA10		
		WHERE X.LC_GRN = '07' 
		AND Z.BC_EXE_TYP = '05'
	UNION
		SELECT BF_SSN
		FROM OLWHRM1.DC01_LON_CLM_INF X
		INNER JOIN OLWHRM1.LA10_LEG_ACT Y
			ON X.BF_SSN = Y.DF_PRS_ID_BR
		WHERE X.LC_GRN = '02' 
		AND Y.BD_WDR IS NULL		
	 ) GRN_TYP
	ON DC01.BF_SSN = GRN_TYP.BF_SSN
INNER JOIN OLWHRM1.PD01_PDM_INF PD01
	ON DC01.BF_SSN = PD01.DF_PRS_ID
INNER JOIN OLWHRM1.PD03_PRS_ADR_PHN PD03
	ON DC01.BF_SSN = PD03.DF_PRS_ID
INNER JOIN OLWHRM1.IN01_LGS_IDM_MST IN01
	ON PD01.BF_EMP_ID_1 = IN01.IF_IST
INNER JOIN OLWHRM1.DC02_BAL_INT DC02
	ON DC01.AF_APL_ID= DC02.AF_APL_ID
	AND DC01.AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
WHERE DC01.LC_STA_DC10 = '03'
AND DC01.LD_CLM_ASN_DOE IS NULL
AND DC01.LC_AUX_STA = ''
AND PD03.DC_ADR = 'L'
AND EXISTS (
	SELECT *
	FROM (
		SELECT MAX(BD_ATY_PRF) AS BD_ATY_PRF
			,DF_PRS_ID
		FROM OLWHRM1.AY01_BR_ATY 
		WHERE PF_ACT = 'DL1NI'
		GROUP BY DF_PRS_ID
		) X
	WHERE X.DF_PRS_ID = DC01.BF_SSN 
	AND X.BD_ATY_PRF <= &DAYS_30
	) 
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.AY01_BR_ATY Y
	WHERE Y.DF_PRS_ID = DC01.BF_SSN
	AND Y.PF_ACT = 'DDOOW'
	) 

GROUP BY DC01.BF_SSN
	,PD01.DF_SPE_ACC_ID
	,PD01.DM_PRS_1
	,PD01.DM_PRS_LST
	,PD03.DX_STR_ADR_1
	,PD03.DX_STR_ADR_2
	,PD03.DM_CT
	,PD03.DC_DOM_ST
	,PD03.DF_ZIP
	,PD03.DC_ADR
	,PD03.DM_FGN_CNY
	,IN01.IM_IST_FUL
	,IN01.IX_GEN_STR_ADR_1
	,IN01.IX_GEN_STR_ADR_2
	,IN01.IX_GEN_STR_ADR_3
	,IN01.IM_GEN_CT
	,IN01.IC_GEN_ST
	,IN01.IF_GEN_ZIP

FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>> ;  ** INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK;
QUIT;
/*ENDRSUBMIT;*/
/*DATA AAPAOTE;*/
/*SET WORKLOCL.AAPAOTE;*/
/*RUN;*/
DATA AAPAOTE (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET AAPAOTE;
/***************************
* COST CETNER VARIABLES
***************************/
COST_CENTER_CODE = 'MA2329';
IF DM_FGN_CNY ^= '' 
	THEN SVAR = 1;
ELSE SVAR = 2;
/***************************
* KEYLINE VARIABLES
***************************/
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;

PROC SORT DATA=AAPAOTE;
BY SVAR DC_DOM_ST BF_SSN;
RUN;

DATA _NULL_;
SET  WORK.AAPAOTE;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
	FORMAT BF_SSN $9. ;
	FORMAT DF_SPE_ACC_ID $10. ;
	FORMAT DM_PRS_1 $12. ;
	FORMAT DM_PRS_LST $35. ;
	FORMAT DX_STR_ADR_1 $35. ;
	FORMAT DX_STR_ADR_2 $35. ;
	FORMAT DM_CT $30. ;
	FORMAT DC_DOM_ST $2. ;
	FORMAT DF_ZIP $14. ;
	FORMAT DC_ADR $1. ;
	FORMAT DM_FGN_CNY $25. ;
	FORMAT ACO dollar12.2 ;
	FORMAT IM_IST_FUL $40. ;
	FORMAT IX_GEN_STR_ADR_1 $40. ;
	FORMAT IX_GEN_STR_ADR_2 $40. ;
	FORMAT IX_GEN_STR_ADR_3 $40. ;
	FORMAT IM_GEN_CT $30. ;
	FORMAT IC_GEN_ST $2. ;
	FORMAT IF_GEN_ZIP $14. ;
	FORMAT COST_CENTER_CODE $6. ;
	FORMAT SVAR BEST12. ;
	FORMAT ACSKEY $18. ;
IF _N_ = 1 THEN DO;
PUT 'BF_SSN'
	','
	'DF_SPE_ACC_ID'
	','
	'DM_PRS_1' 
	','
	'DM_PRS_LST'
	','
	'DX_STR_ADR_1' 
	','
	'DX_STR_ADR_2' 
	','
	'DM_CT' 
	','
	'DC_DOM_ST' 
	','
	'DF_ZIP' 
	','
	'ACSKEY' 
	','
	'ACO' 
	','
	'IM_IST_FUL' 
	','
	'IX_GEN_STR_ADR_1' 
	','
	'IX_GEN_STR_ADR_2' 
	','
	'IX_GEN_STR_ADR_3'
	','
	'IM_GEN_CT' 
	','
	'IC_GEN_ST' 
	','
	'IF_GEN_ZIP' 
	','
	'STATE_IND' 
	','
	'COST_CENTER_CODE';	
END;
DO;
	PUT BF_SSN $ @;
	PUT DF_SPE_ACC_ID $ @;
	PUT DM_PRS_1 $ @;
	PUT DM_PRS_LST $ @;
	PUT DX_STR_ADR_1 $ @;
	PUT DX_STR_ADR_2 $ @;
	PUT DM_CT $ @;
	PUT DC_DOM_ST $ @;
	PUT DF_ZIP $ @;
	PUT ACSKEY $ @;
	PUT ACO @;
	PUT IM_IST_FUL $ @;
	PUT IX_GEN_STR_ADR_1 $ @;
	PUT IX_GEN_STR_ADR_2 $ @;
	PUT IX_GEN_STR_ADR_3 $ @;
	PUT IM_GEN_CT $ @;
	PUT IC_GEN_ST $ @;
	PUT IF_GEN_ZIP $ @;
	PUT DC_DOM_ST $ @;
	PUT COST_CENTER_CODE $ ;	
END;
RUN;
