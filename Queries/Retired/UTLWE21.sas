/*UTLWE21 CONSOLIDATION MARKETING FLYER*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWE21.LWE21R2";
FILENAME REPORT3 "&RPTLIB/ULWE21.LWE21R3";
DATA _NULL_;
  CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
  CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
/*%SYSLPUT BEGIN = &BEGIN;*/
/*%SYSLPUT END = &END;*/
/*LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;*/
/*RSUBMIT;*/
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CMARK AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,A.LA_CUR_PRI
	,B.DM_PRS_1
	,B.DM_PRS_MID
	,B.DM_PRS_LST
	,B.DM_PRS_LST_SFX
	,C.DX_STR_ADR_1
	,C.DX_STR_ADR_2
	,C.DX_STR_ADR_3
	,C.DM_CT
	,CASE 
	 WHEN DM_FGN_ST <> ' ' AND DM_FGN_CNY <> ' '
	 THEN DM_FGN_ST
	 ELSE DC_DOM_ST 
	 END AS STATE
	,CASE 
	 WHEN DM_FGN_ST <> ' ' AND DM_FGN_CNY <> ' '
	 THEN DM_FGN_CNY
	 ELSE ' ' 
	 END AS FGN_CNY
	,C.DF_ZIP_CDE
	,C.DC_ADR	
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD30_PRS_ADR C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.LN35_LON_OWN D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.LN99_LON_SLE_FAT E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
WHERE A.LC_STA_LON10 = 'R'
AND D.IF_OWN = '828476'
AND C.DC_ADR = 'L'
AND C.DI_VLD_ADR = 'Y'
AND D.LD_OWN_EFF_SR BETWEEN &BEGIN AND &END
AND E.IF_SLL_OWN_SLD <> E.IF_BUY_OWN_SLD 
AND A.IC_LON_PGM IN ('STFFRD','UNSTFD','PLUS')
AND NOT EXISTS
	(SELECT *
	 FROM OLWHRM1.DW01_DW_CLC_CLU X
	 WHERE X.BF_SSN = A.BF_SSN
	 AND X.WC_DW_LON_STA IN ('07','08','10','11','16','17','20','21'))
AND NOT EXISTS
	(SELECT *
	 FROM OLWHRM1.LN10_LON Z
	 WHERE Z.BF_SSN = A.BF_SSN
	 AND Z.IC_LON_PGM IN ('SUBCNS','UNCNS')
	 AND Z.LA_CUR_PRI > 0)
AND NOT EXISTS
	(SELECT *
	 FROM OLWHRM1.AY10_BR_LON_ATY Y
	 WHERE Y.BF_SSN = A.BF_SSN
	 AND Y.LC_STA_ACTY10 = 'A'
	 AND Y.PF_REQ_ACT IN ('OC101','OC302'))
);
DISCONNECT FROM DB2;
/*ENDRSUBMIT;*/
/*DATA CMARK;*/
/*SET WORKLOCL.CMARK;*/
/*RUN;*/
PROC SORT DATA=CMARK;
BY BF_SSN LN_SEQ;
RUN;
DATA CMARK2;
SET CMARK;
BY BF_SSN;
IF FIRST.BF_SSN THEN LTOT = 0;
LTOT+LA_CUR_PRI;
IF LAST.BF_SSN THEN OUTPUT CMARK2;
RUN;
DATA CMARK2 (KEEP=BF_SSN LTOT);
SET CMARK2;
WHERE LTOT >= 4900;
RUN;
PROC SORT DATA=CMARK2;
BY BF_SSN;
RUN;
DATA CMARK (DROP=LA_CUR_PRI);
MERGE CMARK (IN=A) CMARK2 (IN=B);
BY BF_SSN;
IF B;
RUN;
%MACRO KEY_CLC(TBL);
*CALCULATE KEYLINE;
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(BF_SSN,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||DC_ADR;
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%KEY_CLC(CMARK);
DATA CMARK2 (KEEP=BF_SSN LN_SEQ);
SET CMARK;
RUN;
DATA CMARK (DROP=LN_SEQ DC_ADR LTOT);
SET CMARK;
RUN;
PROC SORT DATA=CMARK2 NODUPKEY;
BY BF_SSN LN_SEQ;
RUN;
PROC SORT DATA=CMARK NODUPKEY;
BY BF_SSN;
RUN;
DATA _NULL_;
SET CMARK;
*FILE 'C:\WINDOWS\TEMP\ULWE21.LWE21R2' DELIMITER=',' DSD DROPOVER LRECL=32767;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DM_PRS_1 $13. ;
FORMAT DM_PRS_MID $13. ;
FORMAT DM_PRS_LST $23. ;
FORMAT DM_PRS_LST_SFX $4. ;
FORMAT DX_STR_ADR_1 $30. ;
FORMAT DX_STR_ADR_2 $30. ;
FORMAT DX_STR_ADR_3 $30. ;
FORMAT DM_CT $20. ;
FORMAT STATE $15. ;
FORMAT FGN_CNY $15. ;
FORMAT DF_ZIP_CDE $9. ;
FORMAT ACSKEY $18. ;
DO;
   PUT ACSKEY $ @;	
   PUT DM_PRS_1 $ @;
   PUT DM_PRS_MID $ @;
   PUT DM_PRS_LST $ @;
   PUT DM_PRS_LST_SFX $ @;
   PUT DX_STR_ADR_1 $ @;
   PUT DX_STR_ADR_2 $ @;
   PUT DX_STR_ADR_3 $ @;
   PUT DM_CT $ @;
   PUT STATE $ @;
   PUT FGN_CNY $ @;
   PUT DF_ZIP_CDE $ ;
END;
RUN;
DATA _NULL_;
SET WORK.CMARK2;
*FILE 'C:\WINDOWS\TEMP\ULWE21.LWE21R3' DELIMITER=',' DSD DROPOVER LRECL=32767;
FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT BF_SSN $9. ;
FORMAT LN_SEQ 6. ;
DO;
   PUT BF_SSN $ @;
   PUT LN_SEQ ;
 END;
RUN;