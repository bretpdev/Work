LIBNAME REGENTS ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\REGENTS.dsn;" ;
%LET rptlib = T:\SAS;
%let REPORT = &RPTLIB\R01;


DATA TEST;
I = QTR(TODAY());
ARRAY TERM{4} $ ("'Winter'", "'Spring'", "'Summer'", "'Fall'");
CALL SYMPUT('TERM',TERM(I));
RUN;
%PUT &TERM;

PROC SQL;
CREATE TABLE R4 AS
SELECT O.STATESTUDENTID	AS SSID
	,TRIM(pROPCASE(C.FIRSTNAME)) ||' ' || pROPCASE(C.LASTNAME) AS NAME
	,O.APPLICATIONYEAR AS COHORT
	,B.DESCRIPTION AS ACCOUNT_STATUS LENGTH=20
	,datepart(A.ENDDATE) AS END_DATE FORMAT= MMDDYY10.
FROM REGENTS.SCHOLARSHIPAPPLICATION O
INNER JOIN REGENTS.LEAVEDEFERRAL A
	ON O.STATESTUDENTID = A.STATESTUDENTID
inner JOIN REGENTS.AWARDSTATUSLOOKUP B
	ON O.AWARDSTATUSCODE = B.CODE
INNER JOIN REGENTS.STUDENT C
	ON O.STATESTUDENTID = C.STATESTUDENTID
WHERE O.AWARDSTATUSCODE in (4,5)
	AND datepart(A.ENDDATE) < TODAY()
;

CREATE TABLE R3 AS
SELECT O.STATESTUDENTID	AS SSID
	,TRIM(pROPCASE(C.FIRSTNAME)) ||' ' || pROPCASE(C.LASTNAME) AS NAME
	,O.APPLICATIONYEAR AS COHORT
	,'Pmt Not Recvd' AS COMMENT
FROM REGENTS.SCHOLARSHIPAPPLICATION O
INNER JOIN REGENTS.STUDENT C
	ON O.STATESTUDENTID = C.STATESTUDENTID
LEFT OUTER JOIN REGENTS.PAYMENT B
	ON O.STATESTUDENTID = B.STATESTUDENTID
	AND B.TERM = &TERM
	AND B.TERMYEAR = YEAR(TODAY())
WHERE O.AWARDSTATUSCODE in (1)
	AND B.STATESTUDENTID IS NULL
;

CREATE TABLE R2 AS
SELECT O.STATESTUDENTID	AS SSID
	,TRIM(PROPCASE(C.FIRSTNAME)) ||' ' || PROPCASE(C.LASTNAME) AS NAME
	,O.APPLICATIONYEAR AS COHORT
	,SUM(CREDITSENROLLED) AS CREDITS
	,COUNT(*) AS PAYMENTS
FROM REGENTS.SCHOLARSHIPAPPLICATION O
INNER JOIN REGENTS.STUDENT C
	ON O.STATESTUDENTID = C.STATESTUDENTID
INNER JOIN REGENTS.PAYMENT B
	ON O.STATESTUDENTID = B.STATESTUDENTID
WHERE O.AWARDSTATUSCODE in (1)
	AND B.PAYMENTTYPE = 'Exemplary'
	AND O.EXEMPLARYAWARDISEARNED = 1
GROUP BY 1,2
HAVING COUNT(*) >= 4
;

QUIT;

%MACRO OUT(R,TIT);
title &tit;
PROC SORT DATA=R&R; BY SSID; RUN;

PROC PRINTTO PRINT="&REPORT..R&R" NEW;
RUN;
PROC PRINT DATA=R&R;
RUN;
PROC PRINTTO;
RUN;
%MEND;
%OUT(2,"Students in an Approved Status with 4 Exemplary Award Payments");
%OUT(3,"Approved students who have not yet received payment");
%OUT(4,"Students in active LOA/Defer status, but LOA/Defer end date has past");



