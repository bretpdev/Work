/*%LET RPTLIB = %SYSGET(reportdir);*/
/*LIBNAME SAS_TAB V8 '/sas/whse/progrevw';*/
LIBNAME SAS_TAB V8 'Y:\Development\SAS Test Files\progrevw';
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS79.NWS79RZ";
FILENAME REPORT2 "&RPTLIB/UNWS79.NWS79R2";
FILENAME REPORT3 "&RPTLIB/UNWS79.NWS79R3";
LIBNAME  LEGEND  REMOTE  SERVER=LEGEND  SLIBREF=WORK;

PROC SQL;
	CREATE TABLE CURRENT_CAMPAIGN_BORROWERS AS
		SELECT
			BORR.DF_SPE_ACC_ID
		FROM
			SAS_TAB.CAMPAIGNS CAMP
			JOIN SAS_TAB.CAMPAIGN_BORROWERS BORR
				ON CAMP.CAMPAIGN_ID = BORR.CAMPAIGN_ID
		WHERE
			TODAY() BETWEEN BEGIN_DATE AND END_DATE
			AND CAMP.CAMPAIGN_NAME = 'CR 2651'
	;
QUIT;

DATA LEGEND.CURRENT_CAMPAIGN_BORROWERS; SET CURRENT_CAMPAIGN_BORROWERS; RUN;

RSUBMIT;
LIBNAME PKUB DB2 DATABASE=DNFPUTDL OWNER=PKUB;
PROC SQL;
	CREATE TABLE BDS0 AS
		SELECT DISTINCT 
			LN10.BF_SSN
			,LN10.LA_CUR_PRI
			,LN10.LF_LON_CUR_OWN
			,LN16.LD_DLQ_ITL_MIN
			,LN16.LN_DLQ_MAX + 1 AS LN_DLQ_MAX
			,TRIM(PD10.DM_PRS_1) || ', ' || PD10.DM_PRS_LST AS BOR_NAME
			,PD30.DX_STR_ADR_1
			,PD30.DX_STR_ADR_2
			,PD30.DX_STR_ADR_3
			,PD30.DM_CT
			,PD30.DC_DOM_ST
			,PD30.DF_ZIP_CDE
		FROM
			CURRENT_CAMPAIGN_BORROWERS CAMP
			JOIN PKUB.PD10_PRS_NME PD10
				ON CAMP.DF_SPE_ACC_ID = PD10.DF_SPE_ACC_ID
			LEFT JOIN PKUB.PD30_PRS_ADR PD30
				ON PD10.DF_PRS_ID = PD30.DF_PRS_ID
				AND PD30.DC_ADR = 'L'
			JOIN
				(
					SELECT 
						Y.BF_SSN
						,Y.LF_LON_CUR_OWN
						,SUM(Y.LA_CUR_PRI + COALESCE(Z.WA_TOT_BRI_OTS,0)) * 100 AS LA_CUR_PRI
					FROM
						PKUB.LN10_LON Y
						LEFT JOIN PKUB.DW01_DW_CLC_CLU Z
							ON Y.BF_SSN = Z.BF_SSN
							AND Y.LN_SEQ = Z.LN_SEQ
					WHERE 
						Y.LC_STA_LON10 = 'R'
						AND Y.LA_CUR_PRI > 0
					GROUP BY 
						Y.BF_SSN
						,Y.LF_LON_CUR_OWN
				) LN10
				ON PD10.DF_PRS_ID = LN10.BF_SSN
			JOIN
				(
					SELECT 
						Q.BF_SSN
						,MIN(LD_DLQ_OCC) AS LD_DLQ_ITL_MIN
						,LN_DLQ_MAX
					FROM 
						PKUB.LN16_LON_DLQ_HST Q
						JOIN PKUB.LN10_LON U
							ON Q.BF_SSN = U.BF_SSN
							AND Q.LN_SEQ = U.LN_SEQ
					WHERE
						Q.LC_STA_LON16 = '1'
						AND U.LC_STA_LON10 = 'R'
						AND U.LA_CUR_PRI > 0
						AND Q.LD_DLQ_MAX = TODAY() - 1
					GROUP BY 
						Q.BF_SSN
				) LN16
				ON LN10.BF_SSN = LN16.BF_SSN
			LEFT JOIN PKUB.DW01_DW_CLC_CLU DW01
				ON PD10.DF_PRS_ID = DW01.BF_SSN
				AND DW01.WC_DW_LON_STA IN ('21','19','16','17','04','05','18')
		WHERE 
			DW01.BF_SSN IS NULL
	;

/*	select higher delinquency in cases where borrower has split delinquency*/
	CREATE TABLE BDS AS
		SELECT
			BDS.*
		FROM
			BDS0 BDS
			JOIN
				(
					SELECT
						BF_SSN,
						MAX(LN_DLQ_MAX) AS LN_DLQ_MAX
					FROM
						BDS0
					GROUP BY
						BF_SSN
				) DLQ
				ON BDS.BF_SSN = DLQ.BF_SSN
				AND BDS.LN_DLQ_MAX = DLQ.LN_DLQ_MAX
	;

CREATE TABLE PHN AS
SELECT DISTINCT A.BF_SSN
	,E.DN_DOM_PHN_ARA || E.DN_DOM_PHN_XCH || E.DN_DOM_PHN_LCL AS PHN
	,E.DC_PHN
	,CASE
		WHEN E.DC_ALW_ADL_PHN IN ('L','Q','P') THEN 'Y'
		ELSE 'N'
	 END AS DC_ALW_ADL_PHN
FROM BDS A
INNER JOIN PKUB.PD40_PRS_PHN E
	ON A.BF_SSN = E.DF_PRS_ID
WHERE di_phn_vld = 'Y';

CREATE TABLE DELQ AS
SELECT DISTINCT A.*
	,COALESCE(B.AMTDU,0) * 100 AS AMTDU
FROM PHN A
LEFT OUTER JOIN (
	SELECT B.BF_SSN
		,SUM(COALESCE(B.LA_BIL_PAS_DU,0) +
			COALESCE(B.LA_BIL_DU_PRT,0) +
			COALESCE(B.LA_LTE_FEE_OTS_PRT,0)) AS AMTDU
	FROM PKUB.LN80_LON_BIL_CRF B
	INNER JOIN (
		SELECT BIL_SEQ.BF_SSN
			,BIL_SEQ.LD_BIL_CRT
			,MAX(LN_SEQ_BIL_WI_DTE) AS LN_SEQ_BIL_WI_DTE
		FROM PKUB.LN80_LON_BIL_CRF BIL_SEQ
		INNER JOIN (
			SELECT BF_SSN
				,MAX(LD_BIL_CRT) AS LD_BIL_CRT
			FROM PKUB.LN80_LON_BIL_CRF 
			WHERE LC_STA_LON80 = 'A'
				AND LC_BIL_TYP_LON = 'P'
			GROUP BY BF_SSN
			) BIL_DT
			ON BIL_DT.BF_SSN = BIL_SEQ.BF_SSN
			AND BIL_DT.LD_BIL_CRT = BIL_SEQ.LD_BIL_CRT
		WHERE BIL_SEQ.LC_STA_LON80 = 'A'
			AND BIL_SEQ.LC_BIL_TYP_LON = 'P'
		GROUP BY BIL_SEQ.BF_SSN
			,BIL_SEQ.LD_BIL_CRT
		) THE_BILL
		ON B.BF_SSN = THE_BILL.BF_SSN
		AND B.LD_BIL_CRT = THE_BILL.LD_BIL_CRT
		AND B.LN_SEQ_BIL_WI_DTE = THE_BILL.LN_SEQ_BIL_WI_DTE
	LEFT join PKUB.PD10_PRS_NME c
		on b.bf_ssn = c.df_prs_id
	LEFT join pkub.LN10_LON d
		on b.bf_ssn = d.bf_ssn
		and b.ln_seq = d.ln_seq
	WHERE B.LC_STA_LON80 = 'A'
		and d.la_cur_pri > 0
		and d.LC_STA_LON10 = 'R'
	GROUP BY B.BF_SSN
	) B
	ON A.BF_SSN = B.BF_SSN
;
quit;

PROC SORT DATA=PHN NODUPRECS; BY BF_SSN; RUN;
PROC SORT DATA=BDS; BY BF_SSN; RUN;
PROC SORT DATA=DELQ; BY BF_SSN; RUN;

DATA PHN(KEEP=BF_SSN PHN_H PHN_A PHN_W PHN_H_CON PHN_A_CON PHN_W_CON);
	SET PHN;
	BY BF_SSN;
	LENGTH PHN_H PHN_A PHN_W $10. PHN_H_CON PHN_A_CON PHN_W_CON $1.;
	RETAIN PHN_H PHN_A PHN_W PHN_H_CON PHN_A_CON PHN_W_CON;
	IF FIRST.BF_SSN THEN DO;
		PHN_A = '';
		PHN_W = '';
		PHN_H = '';
		PHN_H_CON = '';
		PHN_W_CON = '';
		PHN_A_CON = '';
	END;
	IF DC_PHN = 'H' THEN DO;
		PHN_H = PHN;
		PHN_H_CON = DC_ALW_ADL_PHN;
	END;
	ELSE IF DC_PHN = 'A' THEN DO;
		PHN_A = PHN;
		PHN_A_CON = DC_ALW_ADL_PHN;
	END;
	ELSE IF DC_PHN = 'W' THEN DO;
		PHN_W = PHN;
		PHN_W_CON = DC_ALW_ADL_PHN;
	END;
	IF LAST.BF_SSN THEN OUTPUT;
RUN;

data BDS(drop=dc_phn PHN DC_ALW_ADL_PHN);
merge BDS phn(in=a) DELQ;
by bf_ssn;
if a;
run;

PROC SORT DATA=BDS OUT=BDS NODUPKEY; BY BF_SSN; RUN;  

DATA BDS;
	SET BDS;
	IF LN_DLQ_MAX > 10;
RUN;

ENDRSUBMIT;

DATA BDS; 
	SET LEGEND.BDS;  
RUN;

DATA _NULL_;
SET BDS;
FILE REPORT2 DROPOVER LRECL=32767;
DO;
	PUT @1 BF_SSN   			
		@15 bor_name
		@70 LN_DLQ_MAX
		@159 PHN_H_CON
		@160 PHN_H
		@175 PHN_A_CON
		@176 PHN_A
		@191 PHN_W_CON
		@192 PHN_W
		@260 dx_str_adr_1
		@290 dx_str_adr_2
		@320 dx_str_adr_3
		@350 dm_ct
		@370 dc_dom_st
		@372 df_zip_cde
		@414 LF_LON_CUR_OWN
		@422 AMTDU
		@429 LA_CUR_PRI;	
	END;
RUN;


DATA _NULL_;
	SET BDS;

	FILE
		REPORT3
		DELIMITER = ','
		DSD
		DROPOVER
		LRECL = 32767
	;

	FORMAT
		AMTDU DOLLAR15.2
		LA_CUR_PRI DOLLAR15.2
	;

	IF _N_ = 1 THEN 
		DO;
			PUT 'SSN,Name,Days Delinquent,Consent for Home Phone,Home Phone,Consent for Alternate Phone,Alternate Phone,Consent for Work Phone,' @;
			PUT 'Work Phone,Street 1,Street 2,Street 3,City,State,Zip Code,Owner Code,Amount Due,Total balance of all loans';
		END;

	AMTDU = AMTDU/100;
	LA_CUR_PRI = LA_CUR_PRI/100;

	PUT BF_SSN @;			
	PUT bor_name @;
	PUT LN_DLQ_MAX @;
	PUT PHN_H_CON @;
	PUT PHN_H @;
	PUT PHN_A_CON @;
	PUT PHN_A @;
	PUT PHN_W_CON @;
	PUT PHN_W @;
	PUT dx_str_adr_1 @;
	PUT dx_str_adr_2 @;
	PUT dx_str_adr_3 @;
	PUT dm_ct @;
	PUT dc_dom_st @;
	PUT df_zip_cde @;
	PUT LF_LON_CUR_OWN @;
	PUT AMTDU @;
	PUT LA_CUR_PRI;	
RUN;
