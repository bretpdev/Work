*--------------------------------------------*
| UTLWR22 Compass Suspense  				 |
*--------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:/SAS;
FILENAME REPORT2 "&RPTLIB/ULWR22.LWR22R2";
FILENAME REPORT3 "&RPTLIB/ULWR22.LWR22R3";
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CMPSUS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.PC_FAT_TYP||A.PC_FAT_SUB_TYP AS TRX_TYP
	,A.LX_SPS_REA_PST
	,RTRIM(B.DM_PRS_LST)||','||RTRIM(B.DM_PRS_1)||','||B.DM_PRS_MID AS NAME
	,C.IC_LON_PGM
	,C.LF_LON_CUR_OWN
	,A.LD_RMT_PAY_EFF_PST
	,A.LA_BR_RMT_PST
	,A.LN_RMT_SEQ_PST
	,F.LF_RMT_BCH_USR_ASN
	,E.IF_FLS_DEA
	,A.LD_RMT_BCH_INI
	,A.LC_RMT_BCH_SRC_IPT
	,A.LN_RMT_BCH_SEQ
	,A.LN_RMT_SEQ_PST
	,A.LN_RMT_ITM_PST
	,A.LN_RMT_ITM_SEQ_PST
	,A.LX_SPS_REA_PST
	,G.LN_SEQ
	,G.LA_TGT
FROM OLWHRM1.RM31_BR_RMT_PST A
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.LN10_LON C
	ON B.DF_PRS_ID = C.BF_SSN
INNER JOIN OLWHRM1.LN35_LON_OWN D
	ON C.BF_SSN = D.BF_SSN
	AND C.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.OW30_LON_SLE_CTL E
	ON D.IF_LON_SLE = E.IF_LON_SLE
INNER JOIN OLWHRM1.RM10_RMT_BCH F
	ON A.LD_RMT_BCH_INI = F.LD_RMT_BCH_INI
	AND A.LC_RMT_BCH_SRC_IPT = F.LC_RMT_BCH_SRC_IPT
	AND A.LN_RMT_BCH_SEQ = F.LN_RMT_BCH_SEQ
LEFT OUTER JOIN OLWHRM1.RM40_RMT_LON_TGT G
	ON C.BF_SSN = G.BF_SSN
	AND C.LN_SEQ = G.LN_SEQ
	AND F.LD_RMT_BCH_INI = G.LD_RMT_BCH_INI
	AND F.LC_RMT_BCH_SRC_IPT = G.LC_RMT_BCH_SRC_IPT
	AND F.LN_RMT_BCH_SEQ = G.LN_RMT_BCH_SEQ
WHERE A.LC_RMT_STA_PST = 'S'
	AND A.LX_SPS_REA_PST IN ('06193','06159')
	AND D.LD_OWN_EFF_END IS NULL
	AND E.IF_FLS_DEA != ''
	AND C.LC_STA_LON10 = 'R'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR22.LWR22RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA CMPSUS ;
	SET WORKLOCL.CMPSUS;
RUN;
DATA CMPSUS(DROP=LD_RMT_BCH_INI LC_RMT_BCH_SRC_IPT LN_RMT_BCH_SEQ LN_SEQ LA_TGT);
	SET CMPSUS;
	BATCH = CATS(PUT(LD_RMT_BCH_INI,MMDDYY10.),LC_RMT_BCH_SRC_IPT,PUT(LN_RMT_BCH_SEQ,Z3.));
	IF LN_SEQ ^= . AND LA_TGT ^= . THEN 
		LNSEQ_TGTAMT = CATX('-',PUT(LN_SEQ,BEST12.),PUT(LA_TGT,BEST12.));
	ELSE 
		LNSEQ_TGTAMT = '';
RUN;
/*CREATE LIST VARIABLES */
%MACRO CLV(LINE_VAR,NEW_VAR);
PROC SQL;
CREATE TABLE TEMP_ONE_LINE AS 
SELECT DISTINCT BF_SSN 
	,NAME 
	,LD_RMT_PAY_EFF_PST 
	,LA_BR_RMT_PST 
	,BATCH 
	,TRX_TYP 
	,LN_RMT_SEQ_PST 
	,LF_RMT_BCH_USR_ASN
	,LN_RMT_ITM_PST
	,&LINE_VAR
FROM CMPSUS
ORDER BY BF_SSN 
	,NAME 
	,LD_RMT_PAY_EFF_PST 
	,LA_BR_RMT_PST 
	,BATCH 
	,TRX_TYP 
	,LN_RMT_SEQ_PST 
	,LF_RMT_BCH_USR_ASN
	,LN_RMT_ITM_PST
;
QUIT;
DATA TEMP_ONE_LINE (DROP=&LINE_VAR VLIST VLIST2);
	SET TEMP_ONE_LINE;
	LENGTH VLIST VLIST2 $200. ;
	BY BF_SSN NAME LD_RMT_PAY_EFF_PST LA_BR_RMT_PST BATCH TRX_TYP LN_RMT_SEQ_PST LF_RMT_BCH_USR_ASN LN_RMT_ITM_PST;
	RETAIN VLIST VLIST2;
	IF FIRST.LN_RMT_ITM_PST THEN DO;
			VLIST = &LINE_VAR;
			VLIST2 = VLIST;
	END;
	ELSE DO;
		VLIST2 = TRIM(VLIST2);
		VLIST = &LINE_VAR;
		VLIST2 = TRIM(VLIST2)||' '||TRIM(VLIST);
	END;
	IF LAST.LN_RMT_ITM_PST THEN DO;
			&NEW_VAR = LEFT(TRIM(VLIST2));
			OUTPUT ;
	END;
RUN;
PROC SQL;
	CREATE TABLE TEMP_COMBINED(DROP=&LINE_VAR) AS 
	SELECT DISTINCT A.*
		,B.&NEW_VAR
	FROM CMPSUS A
	INNER JOIN TEMP_ONE_LINE B
		ON A.BF_SSN = B.BF_SSN
		AND A.NAME = B.NAME
		AND A.LD_RMT_PAY_EFF_PST = B.LD_RMT_PAY_EFF_PST
		AND A.LA_BR_RMT_PST = B.LA_BR_RMT_PST 
		AND A.BATCH = B.BATCH
		AND A.TRX_TYP = B.TRX_TYP
		AND A.LN_RMT_SEQ_PST = B.LN_RMT_SEQ_PST 
		AND A.LF_RMT_BCH_USR_ASN = B.LF_RMT_BCH_USR_ASN
		AND A.LN_RMT_ITM_PST = B.LN_RMT_ITM_PST
	;
QUIT;
PROC DATASETS;
	DELETE CMPSUS;
	CHANGE TEMP_COMBINED = CMPSUS;
QUIT;
%MEND CLV;
%CLV(IC_LON_PGM,LON_PGM_LIST);
%CLV(IF_FLS_DEA,DEAL_NUM_LIST);
%CLV(LF_LON_CUR_OWN,LON_OWN_LIST);
%CLV(LNSEQ_TGTAMT,LNSEQ_TGTAMT_LIST);
PROC SORT DATA=CMPSUS (DROP= LN_RMT_SEQ_PST0 LN_RMT_ITM_SEQ_PST)NODUPKEY;
	BY BF_SSN NAME LD_RMT_PAY_EFF_PST LA_BR_RMT_PST BATCH TRX_TYP LN_RMT_SEQ_PST LF_RMT_BCH_USR_ASN LN_RMT_ITM_PST;
RUN;
	
%MACRO CREATE_SUSPENSE_FILES(RNO,ERR_CD,SVARS);
PROC SORT DATA=CMPSUS OUT=RDS(WHERE=(LX_SPS_REA_PST = "&ERR_CD"));
	BY &SVARS;
RUN;
DATA _NULL_;
	SET RDS;
	ACC_ID = '';
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
	   FORMAT BF_SSN $9. ;
	   FORMAT TRX_TYP $4. ;
	   FORMAT NAME $51. ;
	   FORMAT LD_RMT_PAY_EFF_PST MMDDYY10. ;
	   FORMAT LA_BR_RMT_PST 10.2 ;
	   FORMAT LN_RMT_SEQ_PST 7. ;
	   FORMAT LF_RMT_BCH_USR_ASN $8. ;
	   FORMAT BATCH $200. ;
	   FORMAT LON_PGM_LIST $200. ;
	   FORMAT DEAL_NUM_LIST $200. ;
	   FORMAT LON_OWN_LIST $200. ;
	   FORMAT LNSEQ_TGTAMT_LIST $200. ;
	   FORMAT ACC_ID $1.;
	IF _N_ = 1 THEN DO;
	PUT "SUSPENSE ACCT #"
		','
		"ACCOUNT ID"
		','
		"BORROWER NAME"
		','
		"LOAN TYPE"
		','
		"EFFECTIVE DATE"
		','
		"AMOUNT"
		','
		"DEAL #"
		','
		"BATCH #"
		','
		"TRANSACTION TYPE"
		','
		"SEQ #"
		','
		"ASSIGNED TO"
		','
		"OWNER"
		','
		"LOAN SEQ-PMT TARGETED";
	END;
	DO;
		PUT BF_SSN $ @;
		PUT ACC_ID $ @;
		PUT NAME $ @;
		PUT LON_PGM_LIST $ @;	
		PUT LD_RMT_PAY_EFF_PST @;
		PUT LA_BR_RMT_PST @;
		PUT DEAL_NUM_LIST $ @;
		PUT BATCH $ @;
		PUT TRX_TYP $ @;
		PUT LN_RMT_SEQ_PST @;
		PUT LF_RMT_BCH_USR_ASN $ @;
		PUT LON_OWN_LIST $ @;
		PUT LNSEQ_TGTAMT_LIST $;
	END;
RUN;
%MEND CREATE_SUSPENSE_FILES;
%CREATE_SUSPENSE_FILES(2,06193,BF_SSN LD_RMT_PAY_EFF_PST LA_BR_RMT_PST);
%CREATE_SUSPENSE_FILES(3,06159,BF_SSN TRX_TYP LD_RMT_PAY_EFF_PST LA_BR_RMT_PST);
