/*UTLWR20 - Delinquent Loans with Loans Put to ED*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
/*%LET TBLLIB = /sas/whse/progrevw;*/
%LET RPTLIB = T:\SAS;
%LET TBLLIB = Q:\Process Automation\TabSAS;
FILENAME REPORTZ "&RPTLIB/ULWR20.LWR20RZ";
FILENAME REPORT2 "&RPTLIB/ULWR20.LWR20R2";
/*INPUT LOAN TYPES FOR PRIVATE AND FFEL LOANS*/
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "&TBLLIB/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
/*CREATE A MACRO VARIALBE LISTS OF FFEL LOAN PROGRAMS*/
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY ","
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';
QUIT;
%SYSLPUT FFELP_LIST = &FFELP_LIST;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DSTAT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT B.BF_SSN
	,B.LN_SEQ
	,B.LC_STA_LON10
	,A.DF_SPE_ACC_ID
	,RTRIM(A.DM_PRS_LST)||','||A.DM_PRS_1 AS NAME
	,B.LC_STA_LON10 
	,B.LA_CUR_PRI
	,'DCON' AS TYP
FROM OLWHRM1.PD10_PRS_NME A
INNER JOIN OLWHRM1.LN10_LON B
	ON A.DF_PRS_ID = B.BF_SSN
INNER JOIN OLWHRM1.LNEC_LON_ECA C
	ON B.BF_SSN = C.BF_SSN
	AND B.LN_SEQ = C.LN_SEQ
WHERE B.IC_LON_PGM IN (&FFELP_LIST)
	AND B.LC_STA_LON10 = 'D'
	AND C.LD_ECA_LON_PUT_DOE = '9/23/2009'
	AND C.LN_ECA_SEQ = 1
FOR READ ONLY WITH UR
);

CREATE TABLE DELQ AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT LN10.BF_SSN
	,LN10.LN_SEQ
	,LN10.LC_STA_LON10 
	,LN10.LA_CUR_PRI
	,LN80.LA_BIL_PAS_DU  
	,BL10.LC_STA_BIL10 
	,BL10.LC_IND_BIL_SNT 
	,BL10.LD_BIL_CRT
	,BL10.LC_BIL_TYP
	,B.LD_DLQ_OCC 
	,DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) AS NUM_DAYS_DELQ 
	,B.LC_STA_LON16 
FROM OLWHRM1.BL10_BR_BIL BL10
INNER JOIN OLWHRM1.LN80_LON_BIL_CRF LN80
	ON BL10.BF_SSN = LN80.BF_SSN
	AND BL10.LD_BIL_CRT	= LN80.LD_BIL_CRT
	AND BL10.LN_SEQ_BIL_WI_DTE = LN80.LN_SEQ_BIL_WI_DTE
INNER JOIN OLWHRM1.LN10_LON LN10
	ON LN10.BF_SSN = LN80.BF_SSN
	AND LN10.LN_SEQ = LN80.LN_SEQ
LEFT OUTER JOIN OLWHRM1.LN16_LON_DLQ_HST B
	ON LN10.BF_SSN = B.BF_SSN
	AND LN10.LN_SEQ = B.LN_SEQ
	AND DAYS(CURRENT DATE) - DAYS(B.LD_DLQ_OCC) > 10
	AND B.LC_STA_LON16 = '1'
WHERE LN10.IC_LON_PGM IN (&FFELP_LIST)
	AND LN10.LA_CUR_PRI > 0
	
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/
PROC SORT DATA=DELQ; 
	BY BF_SSN LN_SEQ DESCENDING LD_BIL_CRT;
RUN;
DATA DELQ2;
SET DELQ;
BY BF_SSN LN_SEQ;
IF FIRST.LN_SEQ THEN 
	OUTPUT;
RUN;
PROC SQL;
	CREATE TABLE BD AS 
	SELECT DISTINCT A.DF_SPE_ACC_ID
		,A.NAME
	FROM DSTAT A
	INNER JOIN DELQ2 DELQ
		ON A.BF_SSN = DELQ.BF_SSN
	INNER JOIN DELQ2 PDAHD
		ON DELQ.BF_SSN = PDAHD.BF_SSN
	WHERE DELQ.NUM_DAYS_DELQ > 10
		AND DELQ.LC_STA_LON10 = 'R'
		AND PDAHD.LA_BIL_PAS_DU > 5
		AND PDAHD.LC_STA_LON10 = 'R'
		AND PDAHD.LC_IND_BIL_SNT IN ('5','N')
		AND PDAHD.LC_BIL_TYP = 'P'
	;
QUIT;
PROC DATASETS;
	DELETE DSTAT DELQ2 ;
QUIT;
ENDRSUBMIT;
DATA BD;
	SET WORKLOCL.BD;
RUN;
PROC SORT DATA=BD;
	BY NAME;
RUN;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 DATE CENTER PAGENO=1;
TITLE 'DELINQUENT LOANS WITH LOANS PUT TO ED';
FOOTNOTE   'JOB = UTLWR20  	 REPORT = ULWR20.LWR20R2';
PROC CONTENTS DATA=BD OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWR20  	 REPORT = ULWR20.LWR20R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=BD WIDTH=UNIFORM WIDTH=MIN LABEL;
VAR DF_SPE_ACC_ID NAME;
LABEL DF_SPE_ACC_ID = 'ACCOUNT #';
RUN;
PROC PRINTTO;
RUN;
