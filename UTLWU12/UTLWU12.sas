/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU12.LWU12R2";
FILENAME REPORTZ "&RPTLIB/ULWU12.LWU12RZ";
OPTIONS SYMBOLGEN NOCENTER NODATE NONUMBER ;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DUPTB AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT PD10.DM_PRS_LST
	,PD30.DX_STR_ADR_1
	,PD30.DX_STR_ADR_2
	,PD30.DM_CT
	,PD30.DC_DOM_ST
	,PD30.DF_ZIP_CDE
	,PD30.DM_FGN_ST
	,PD30.DM_FGN_CNY
	,PD30.DI_VLD_ADR
	,BR30_2.BF_EFT_ABA 
	,BR30_2.BF_EFT_ACC
	,BR30.BD_EFT_STA 
	,PD10.DF_SPE_ACC_ID
	,PD10.DM_PRS_1
	,PD30.DX_STR_ADR_1
	,PD30.DX_STR_ADR_2
FROM OLWHRM1.BR30_BR_EFT BR30
/*GET DUPLICATE ROUTING # AND BANK ACCOUNT*/
INNER JOIN (SELECT 
		A.BF_EFT_ABA 
		,A.BF_EFT_ACC
		FROM OLWHRM1.BR30_BR_EFT A
		WHERE A.BC_EFT_STA = 'A'
		GROUP BY A.BF_EFT_ABA, A.BF_EFT_ACC
		HAVING COUNT(*) > 1) BR30_2
	ON BR30.BF_EFT_ABA = BR30_2.BF_EFT_ABA
	AND BR30.BF_EFT_ACC = BR30_2.BF_EFT_ACC
INNER JOIN OLWHRM1.PD30_PRS_ADR PD30
	ON PD30.DF_PRS_ID = BR30.BF_SSN
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON PD10.DF_PRS_ID = BR30.BF_SSN
LEFT OUTER JOIN (
	SELECT DISTINCT A.BF_SSN
		,'X' AS BYE 
	FROM OLWHRM1.AY10_BR_LON_ATY A
	INNER JOIN OLWHRM1.LN83_EFT_TO_LON B
		ON A.BF_SSN = B.BF_SSN
	WHERE A.PF_REQ_ACT = 'QCACH'
		AND B.LC_STA_LN83 = 'A'
		AND A.LD_ATY_REQ_RCV > B.LD_EFT_EFF_BEG
	) A
	ON BR30.BF_SSN = A.BF_SSN
WHERE BR30.BC_EFT_STA = 'A'
	AND PD30.DC_ADR = 'L'
	AND A.BYE IS NULL
ORDER BY BR30_2.BF_EFT_ABA ,BR30_2.BF_EFT_ACC
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA DUPTB; SET WORKLOCL.DUPTB; RUN;

/*GET ACCOUNTS WITH DUPLICATE LAST NAME*/
PROC SQL;
CREATE TABLE DUPLN AS
SELECT DISTINCT 
	DM_PRS_LST
	,BF_EFT_ABA
	,BF_EFT_ACC
FROM DUPTB
;
QUIT;
PROC SQL;
CREATE TABLE DUPLN2 AS
SELECT  
	BF_EFT_ABA
	,BF_EFT_ACC
	,COUNT(*) AS CNT
FROM DUPLN
GROUP BY BF_EFT_ABA, BF_EFT_ACC
HAVING COUNT(*) > 1

;
QUIT;

/*GET ACCOUNTS WITH DUPLICATE ADDRESSES*/
PROC SQL;
CREATE TABLE DUPADD AS
SELECT DISTINCT 
	DX_STR_ADR_1
	,DX_STR_ADR_2
	,DM_CT
	,DC_DOM_ST
	,DF_ZIP_CDE
	,DM_FGN_ST
	,DM_FGN_CNY
	,DI_VLD_ADR
	,BF_EFT_ABA
	,BF_EFT_ACC
FROM DUPTB
;
QUIT;
PROC SQL;
CREATE TABLE DUPADD2 AS
SELECT  
	BF_EFT_ABA
	,BF_EFT_ACC
	,COUNT(*) AS CNT
FROM DUPADD
GROUP BY BF_EFT_ABA, BF_EFT_ACC
HAVING COUNT(*) > 1

;
QUIT;

/*GET ACCOUNTS WITH DIFFERING ADDRESSES AND DIFFERING LAST NAMES*/
PROC SQL;
CREATE TABLE DUPFIN AS
SELECT  
	A.*
FROM DUPTB A
INNER JOIN DUPADD2 B
	ON A.BF_EFT_ABA = B.BF_EFT_ABA
	AND A.BF_EFT_ACC = B.BF_EFT_ACC
INNER JOIN DUPLN2 C
	ON A.BF_EFT_ABA = C.BF_EFT_ABA
	AND A.BF_EFT_ACC = C.BF_EFT_ACC
;
QUIT;

PROC SORT DATA=DUPFIN;
BY BF_EFT_ACC;
RUN;
DATA _NULL_;
	SET DUPFIN ;
	BY BF_EFT_ACC;
LENGTH DESCRIPTION $600.;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
USER = ' ';
ACT_DT = BD_EFT_STA;
IF FIRST.BF_EFT_ACC THEN DO;
	DESCRIPTION = CATX(',',
			DF_SPE_ACC_ID
			,DM_PRS_1
			,DM_PRS_LST
			,DX_STR_ADR_1
			,DX_STR_ADR_2
			,DM_CT
			,DC_DOM_ST
			,DF_ZIP_CDE
			,DM_FGN_ST
			,DM_FGN_CNY
			,DI_VLD_ADR
	);
END;
IF FIRST.BF_EFT_ACC = 0 THEN DO;
	DESC2 = CATX(',',
			USER
			,PUT(ACT_DT,MMDDYY10.)
			,DF_SPE_ACC_ID
			,DM_PRS_1
			,DM_PRS_LST
			,DX_STR_ADR_1
			,DX_STR_ADR_2
			,DM_CT
			,DC_DOM_ST
			,DF_ZIP_CDE
			,DM_FGN_ST
			,DM_FGN_CNY
			,DI_VLD_ADR
	);
DESCRIPTION = CATX(',',DESCRIPTION,DESC2);
RETAIN DESCRIPTION ;
END;
IF LAST.BF_EFT_ACC THEN DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;

RUN;
