USE ULS
GO

CREATE SCHEMA [utlwq10] AUTHORIZATION [dbo]
GO

GRANT EXECUTE ON SCHEMA ::[utlwq10] TO [db_executor]
GO

CREATE PROCEDURE [utlwq10].[R2_SSNChanges]
	@AesUserId_R2 VARCHAR(MAX)
AS

DECLARE @DAY1 DATE = 
(--if run on Monday then do 3 days ago, else do yesterday
	SELECT
		CASE
			WHEN DATENAME(WEEKDAY,GETDATE()) = 'Monday'
			THEN DATEADD(DAY,-3,GETDATE())
			ELSE DATEADD(DAY,-1,GETDATE())
		END
);

DECLARE @DAY2 DATE = DATEADD(DAY,-1,GETDATE());

/*****************************************
	R2 - SSN Changes
*****************************************/

;WITH COMPASS_BORROWERS AS
(
	SELECT DISTINCT
		PI10.DF_USR_ID_CHG_REQ AS [User ID],
		PD10.DF_SPE_ACC_ID AS [Account Number],
		PI10.DF_LST_DTS_PI10 AS [Activity Date],
		PI10.DF_PRS_ID_NEW AS [New SSN],
		PI10.DF_PRS_ID_OLD AS [Old SSN],
		CASE 
			WHEN PI10.DF_USR_ID_CHG_REQ IS NOT NULL
			THEN 'COMPASS'
			ELSE NULL
		END AS [System]
	FROM 
		UDW..PI10_PID_CHG_LOG PI10
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = PI10.DF_PRS_ID_NEW
	WHERE
		PI10.DF_PRS_ID_NEW != PI10.DF_PRS_ID_OLD --SSN change
		AND PI10.DC_STA_PI10 = 'C' --record status complete
		AND CAST(PI10.DF_LST_DTS_PI10 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PI10.DF_USR_ID_CHG_REQ IN (SELECT * FROM STRING_SPLIT(@AesUserId_R2,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PI10.DF_USR_ID_CHG_REQ IN (SELECT * FROM @AesUserId_R2) --TEST/SQLSERVER
)
,COMPASS_COBORROWERS AS
(
	SELECT DISTINCT /**COMPASS**/
		PI10.DF_USR_ID_CHG_REQ AS [User ID],
		PD10.DF_SPE_ACC_ID AS [Account Number],
		PI10.DF_LST_DTS_PI10 AS [Activity Date],
		PI10.DF_PRS_ID_NEW AS [New SSN],
		PI10.DF_PRS_ID_OLD AS [Old SSN],
		CASE 
			WHEN PI10.DF_USR_ID_CHG_REQ IS NOT NULL
			THEN 'COMPASS'
			ELSE NULL
		END AS [System]
	FROM
		UDW..LN20_EDS LN20
		INNER JOIN UDW..PI10_PID_CHG_LOG PI10
			ON PI10.DF_PRS_ID_NEW = LN20.LF_EDS
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = LN20.LF_EDS
	WHERE
		LN20.LC_EDS_TYP IN ('M','S') --co-borrower
		AND PI10.DF_PRS_ID_NEW != PI10.DF_PRS_ID_OLD --SSN change
		AND PI10.DC_STA_PI10 = 'C' --record status complete
		AND CAST(PI10.DF_LST_DTS_PI10 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PI10.DF_USR_ID_CHG_REQ IN (SELECT * FROM STRING_SPLIT(@AesUserId_R2,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PI10.DF_USR_ID_CHG_REQ IN (SELECT * FROM @AesUserId_R2) --TEST/SQLSERVER
)
,ONELINK_BOTH AS
(
	SELECT DISTINCT
		PI01.DF_USR_REQ_CHG_DEL AS [User ID],
		PD01.DF_SPE_ACC_ID AS [Account Number],
		PI01.DF_CRT_DTS_PI01 AS [Activity Date],
		PI01.DF_CRR_PRS AS [New SSN],
		PI01.DF_IRR_PRS AS [Old SSN],
		CASE 
			WHEN PI01.DF_USR_REQ_CHG_DEL IS NOT NULL
			THEN 'ONELINK'
			ELSE NULL
		END AS [System]
	FROM
		ODW..GA01_APP GA01
		INNER JOIN ODW..PI01_PID_CHG_INF PI01
			ON PI01.AF_APL_ID = GA01.AF_APL_ID
		INNER JOIN ODW..PD01_PDM_INF PD01
			ON PD01.DF_PRS_ID = PI01.DF_CRR_PRS
		INNER JOIN ODW..DC01_LON_CLM_INF DC01
			ON DC01.AF_APL_ID = GA01.AF_APL_ID
	WHERE
		DC01.LC_STA_DC10 = '03' --loan defaulted
		AND DC01.LD_CLM_ASN_DOE IS NULL --not re-assigned to DOE
		AND GA01.AC_EDS_TYP IN (' ','C','S') --borrower, Comaker, Spousal
		AND PI01.DF_CRR_PRS != PI01.DF_IRR_PRS --SSN change
		AND CAST(PI01.DF_CRT_DTS_PI01 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PI01.DF_USR_REQ_CHG_DEL IN (SELECT * FROM STRING_SPLIT(@AesUserId_R2,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PI01.DF_USR_REQ_CHG_DEL IN (SELECT * FROM @AesUserId_R2) --TEST/SQLSERVER
)
SELECT * FROM COMPASS_BORROWERS
UNION
SELECT * FROM COMPASS_COBORROWERS
UNION
SELECT * FROM ONELINK_BOTH
;

GO

CREATE PROCEDURE [utlwq10].[R3_NameChanges]
	@AesUserId VARCHAR(MAX)
AS

DECLARE @DAY1 DATE = 
(--if run on Monday then do 3 days ago, else do yesterday
	SELECT
		CASE
			WHEN DATENAME(WEEKDAY,GETDATE()) = 'Monday'
			THEN DATEADD(DAY,-3,GETDATE())
			ELSE DATEADD(DAY,-1,GETDATE())
		END
);

DECLARE @DAY2 DATE = DATEADD(DAY,-1,GETDATE());

/*****************************************
	R3 - Name Changes
*****************************************/

;WITH COMPASS_BORROWERS AS
(
	SELECT DISTINCT
		PD11.DF_LST_USR_PD11 AS [User ID],
		PD10.DF_SPE_ACC_ID AS [Account Number],
		PD11.DF_LST_DTS_PD11 AS [Activity Date],
		RTRIM(LTRIM(RTRIM(PD10.DM_PRS_1)) + ' ' + LTRIM(RTRIM(PD10.DM_PRS_MID)) + ' ' + LTRIM(RTRIM(PD10.DM_PRS_LST)) + ' ' + LTRIM(RTRIM(ISNULL(PD10.DM_PRS_LST_SFX, '')))) AS [New Name],
		RTRIM(LTRIM(RTRIM(PD11.DM_PRS_1_HST)) + ' ' + LTRIM(RTRIM(PD11.DM_PRS_MID_HST)) + ' ' + LTRIM(RTRIM(PD11.DM_PRS_LST_HST)) + ' ' + LTRIM(RTRIM(ISNULL(PD11.DM_PRS_LST_SFX_HST,'')))) AS [Old Name],
		CASE 
			WHEN PD11.DF_LST_USR_PD11 IS NOT NULL
			THEN 'COMPASS'
			ELSE NULL
		END AS [System],
		PD10.DF_PRS_ID AS BF_SSN
	FROM 
		UDW..PD10_PRS_NME PD10
		INNER JOIN UDW..PD11_PRS_NME_HST PD11
			ON PD11.DF_PRS_ID = PD10.DF_PRS_ID
	WHERE
		PD10.DM_PRS_1 + ' ' + PD10.DM_PRS_MID + ' ' + PD10.DM_PRS_LST + ' ' + ISNULL(PD10.DM_PRS_LST_SFX, '') != PD11.DM_PRS_1_HST + ' ' + PD11.DM_PRS_MID_HST + ' ' + PD11.DM_PRS_LST_HST + ' ' + ISNULL(PD11.DM_PRS_LST_SFX_HST,'')
		AND CAST(PD11.DD_CRT_PD11 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PD11.DF_LST_USR_PD11 IN (SELECT * FROM STRING_SPLIT(@AesUserId,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PD11.DF_LST_USR_PD11 IN (SELECT * FROM @AesUserId) --TEST/SQLSERVER
)
,COMPASS_COBORROWERS AS
(
	SELECT DISTINCT
		PD11.DF_LST_USR_PD11 AS [User ID],
		PD10.DF_SPE_ACC_ID AS [Account Number],
		PD11.DF_LST_DTS_PD11 AS [Activity Date],
		RTRIM(LTRIM(RTRIM(PD10.DM_PRS_1)) + ' ' + LTRIM(RTRIM(PD10.DM_PRS_MID)) + ' ' + LTRIM(RTRIM(PD10.DM_PRS_LST)) + ' ' + LTRIM(RTRIM(ISNULL(PD10.DM_PRS_LST_SFX, '')))) AS [New Name],
		RTRIM(LTRIM(RTRIM(PD11.DM_PRS_1_HST)) + ' ' + LTRIM(RTRIM(PD11.DM_PRS_MID_HST)) + ' ' + LTRIM(RTRIM(PD11.DM_PRS_LST_HST)) + ' ' + LTRIM(RTRIM(ISNULL(PD11.DM_PRS_LST_SFX_HST,'')))) AS [Old Name],
		CASE 
			WHEN PD11.DF_LST_USR_PD11  IS NOT NULL
			THEN 'COMPASS'
			ELSE NULL
		END AS [System],
		LN20.LF_EDS AS BF_SSN
	FROM
		UDW..LN20_EDS LN20
		INNER JOIN UDW..PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = LN20.LF_EDS --coborrower info
		INNER JOIN UDW..PD11_PRS_NME_HST PD11
			ON PD11.DF_PRS_ID = LN20.LF_EDS --coborrower info
	WHERE
		LN20.LC_EDS_TYP IN ('M','S') --co-borrower
		AND PD10.DM_PRS_1 + ' ' + PD10.DM_PRS_MID + ' ' + PD10.DM_PRS_LST + ' ' + ISNULL(PD10.DM_PRS_LST_SFX, '') != PD11.DM_PRS_1_HST + ' ' + PD11.DM_PRS_MID_HST + ' ' + PD11.DM_PRS_LST_HST + ' ' + ISNULL(PD11.DM_PRS_LST_SFX_HST,'')
		AND CAST(PD11.DD_CRT_PD11 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PD11.DF_LST_USR_PD11 IN (SELECT * FROM STRING_SPLIT(@AesUserId,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PD11.DF_LST_USR_PD11 IN (SELECT * FROM @AesUserId) --TEST/SQLSERVER
)
,ONELINK_BOTH AS
(
	SELECT DISTINCT
		PD05.DF_LST_USR_PD05 AS [User ID],
		PD01.DF_SPE_ACC_ID AS [Account Number],
		PD05.DD_LST_UPD_PD05 AS [Activity Date],
		LTRIM(RTRIM(PD01.DM_PRS_1)) + ' ' + LTRIM(RTRIM(PD01.DM_PRS_MID)) + ' ' + LTRIM(RTRIM(PD01.DM_PRS_LST)) AS [New Name],
		LTRIM(RTRIM(PD05.DM_PRS_1_HST)) + ' ' + LTRIM(RTRIM(PD05.DM_PRS_MID_HST)) + ' ' + LTRIM(RTRIM(PD05.DM_PRS_LST_HST)) AS [Old Name],
		CASE 
			WHEN PD05.DF_LST_USR_PD05 IS NOT NULL
			THEN 'ONELINK'
			ELSE NULL
		END AS [System],
		PD01.DF_PRS_ID AS BF_SSN
	FROM
		ODW..GA01_APP GA01
		INNER JOIN ODW..PD01_PDM_INF PD01
			ON PD01.DF_PRS_ID = GA01.DF_PRS_ID_BR
		INNER JOIN ODW..PD05_PRS_NME_HST PD05
			ON PD05.DF_PRS_ID = PD01.DF_PRS_ID
		INNER JOIN ODW..DC01_LON_CLM_INF DC01
			ON DC01.AF_APL_ID = GA01.AF_APL_ID
	WHERE
		DC01.LC_STA_DC10 = '03' --loan defaulted
		AND DC01.LD_CLM_ASN_DOE IS NULL --not re-assigned to DOE
		AND	PD01.DM_PRS_1 + ' ' + PD01.DM_PRS_MID + ' ' + PD01.DM_PRS_LST != PD05.DM_PRS_1_HST + ' ' + PD05.DM_PRS_MID_HST + ' ' + PD05.DM_PRS_LST_HST
		AND GA01.AC_EDS_TYP IN (' ','C','S') --borrower, Comaker, Spousal
		AND CAST(PD05.DF_CRT_DTS_PD05 AS DATE) BETWEEN @DAY1 AND @DAY2
		AND PD05.DF_LST_USR_PD05 IN (SELECT * FROM STRING_SPLIT(@AesUserId,',')) --value from NOCHOUSE --LIVE/SSRS
		--AND PD05.DF_LST_USR_PD05 IN (SELECT * FROM @AesUserId) --TEST/SQLSERVER
)
SELECT * FROM COMPASS_BORROWERS
UNION
SELECT * FROM COMPASS_COBORROWERS
UNION
SELECT * FROM ONELINK_BOTH
;

GO

CREATE PROCEDURE [utlwq10].[R4_DOBChanges]
	@AesUserId VARCHAR(MAX)
AS

DECLARE @DAY1 DATE = 
(--if run on Monday then do 3 days ago, else do yesterday
	SELECT
		CASE
			WHEN DATENAME(WEEKDAY,GETDATE()) = 'Monday'
			THEN DATEADD(DAY,-3,GETDATE())
			ELSE DATEADD(DAY,-1,GETDATE())
		END
);

DECLARE @DAY2 DATE = DATEADD(DAY,-1,GETDATE());

/*****************************************
	R4 - DOB changes
*****************************************/

--note: only ONELINK tracks DOB changes; COMPASS does not
SELECT DISTINCT
	PD05.DF_LST_USR_PD05 AS [User ID],
	PD01.DF_SPE_ACC_ID AS [Account Number],
	PD05.DD_LST_UPD_PD05 AS [Activity Date],
	PD01.DD_BRT AS [New DOB],
	PD05.DD_BRT_HST AS [Old DOB],
	CASE 
		WHEN PD05.DF_LST_USR_PD05 IS NOT NULL
		THEN 'ONELINK'
		ELSE NULL
	END AS [System]
FROM
	ODW..GA01_APP GA01
	INNER JOIN ODW..PD01_PDM_INF PD01
		ON PD01.DF_PRS_ID = GA01.DF_PRS_ID_BR
	INNER JOIN ODW..PD05_PRS_NME_HST PD05
		ON PD05.DF_PRS_ID = PD01.DF_PRS_ID
	INNER JOIN ODW..DC01_LON_CLM_INF DC01
		ON DC01.AF_APL_ID = GA01.AF_APL_ID
WHERE
	DC01.LC_STA_DC10 = '03' --loan defaulted
	AND DC01.LD_CLM_ASN_DOE IS NULL --not re-assigned to DOE
	AND PD01.DD_BRT != PD05.DD_BRT_HST --DOB change
	AND GA01.AC_EDS_TYP IN (' ','C','S') --borrower, Comaker, Spousal
	AND CAST(PD05.DF_CRT_DTS_PD05 AS DATE) BETWEEN @DAY1 AND @DAY2
	AND PD05.DF_LST_USR_PD05 IN (SELECT * FROM STRING_SPLIT(@AesUserId,',')) --value from NOCHOUSE --LIVE/SSRS
	--AND PD05.DF_LST_USR_PD05 IN (SELECT * FROM @AesUserId) --TEST/SQLSERVER
;