USE CDW
GO

MERGE
	dbo.BORR_Hardship ExistingData
USING
	(
		SELECT DISTINCT
			PD10.DF_SPE_ACC_ID,
			CASE WHEN SUM(DATEDIFF(MONTH, LN60.LD_FOR_BEG, LN60.LD_FOR_END)) OVER(PARTITION BY PD10.DF_PRS_ID) >= 36 THEN 'Y' ELSE 'N' END FORB_36
		FROM
			PD10_PRS_NME PD10
			INNER JOIN 
			(
				SELECT DISTINCT
					LN60.BF_SSN,
					LN60.LD_FOR_BEG,
					CASE WHEN CAST(LN60.LD_FOR_END AS DATE) > CAST(GETDATE() AS DATE) 
						THEN CAST(GETDATE() AS DATE)
						ELSE CAST(LN60.LD_FOR_END AS DATE)
					END AS LD_FOR_END
				FROM
					FB10_BR_FOR_REQ FB10
					INNER JOIN LN60_BR_FOR_APV LN60
						ON LN60.BF_SSN = FB10.BF_SSN
						AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
				WHERE
					FB10.LC_FOR_STA = 'A'
					AND FB10.LC_STA_FOR10 = 'A'
					AND FB10.LC_FOR_TYP = '05'
					AND LN60.LC_STA_LON60 = 'A'
					AND LN60.LC_FOR_RSP != '003' --003 = FORBEARANCE REQUEST DENIED
					AND CAST(LN60.LD_FOR_BEG AS DATE) <= CAST(GETDATE() AS DATE)
			) LN60
				ON LN60.BF_SSN = PD10.DF_PRS_ID
	) NewData
		ON NewData.DF_SPE_ACC_ID = ExistingData.DF_SPE_ACC_ID
WHEN MATCHED THEN UPDATE SET
	ExistingData.FORB_36 = NewData.FORB_36
WHEN NOT MATCHED THEN
	INSERT
	(
		DF_SPE_ACC_ID,
		FORB_36
	)
	VALUES
	(
		NewData.DF_SPE_ACC_ID,
		NewData.FORB_36
	)
WHEN NOT MATCHED BY SOURCE THEN
	DELETE;