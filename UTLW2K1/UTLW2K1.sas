/* 	UTLW2K1 looks for claims that were paid but did not bill for reinsurance 
	during the previous month.  This report is used in reconciling Forms 2000.*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULW2K1.LW2K1R2";
libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS MPRINT SYMBOLGEN;
DATA _NULL_;
     BEGDATE = PUT(INTNX('MONTH',TODAY(),-1), YYMMDD10.);	/*RESOLVES TO 1ST OF PRIOR MONTH*/
     ENDDATE = PUT(INTNX('MONTH',TODAY(),0)-1 , YYMMDD10.);	/*RESOLVES TO END OF PRIOR MONTH*/
     CALL SYMPUT('BEGIN',"'"||BEGDATE||"'");	            /*CREATES MACRO VARIABLE WITH IN FORMAT  'YYYY/MM/DD'*/
     CALL SYMPUT('END',"'"||ENDDATE||"'");              	/* WILL BE USED AS REPLACEMENTS IN CODE*/
RUN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CLMRPT AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.BF_SSN						AS SSN,
	A.LD_LDR_POF					AS CLMPD,
	A.AF_APL_ID||A.AF_APL_ID_SFX	AS CLUID,
	C.DF_SPE_ACC_ID
FROM  OLWHRM1.DC01_LON_CLM_INF A left outer join OLWHRM1.DC19_DOE_RIN B
on A.AF_APL_ID=B.AF_APL_ID
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.BF_SSN = C.DF_PRS_ID
and A.AF_APL_ID_SFX=B.AF_APL_ID_SFX
WHERE A.LD_LDR_POF BETWEEN &BEGIN AND &END
and a.lc_sta_dc10 in ('03','04')
/*There is no case where the greatest bill date for this loan is
more recent than the greatest preclaim received date for this loan.*/
AND not exists 
			(SELECT *
			FROM  OLWHRM1.DC01_LON_CLM_INF x
			WHERE x.AF_APL_ID=a.AF_APL_ID
			AND x.AF_APL_ID_SFX=a.AF_APL_ID_SFX
			AND 
				(select max(y.ld_agy_bil_doe)
				from OLWHRM1.DC19_DOE_RIN y
				where x.AF_APL_ID=y.AF_APL_ID 
				and x.AF_APL_ID_SFX=y.AF_APL_ID_SFX
				)
				>
				(select max(z.ld_pcl_rcv) 
				from OLWHRM1.DC01_LON_CLM_INF z 
				where x.AF_APL_ID=z.AF_APL_ID 
				and x.AF_APL_ID_SFX=z.AF_APL_ID_SFX
				)
			) 
);
DISCONNECT FROM DB2;

PROC SORT data=CLMRPT;
BY SSN CLUID;
RUN;
endrsubmit  ;

data clmrpt;
set worklocl.clmrpt;
run;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER noDATE PAGENO=1 LS=80;
PROC PRINT NOOBS SPLIT='/' DATA=CLMRPT N="Total: ";
VAR DF_SPE_ACC_ID
	CLMPD
	CLUID;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	CLMPD = 'CLAIM PAID DATE'
	CLUID = 'COMMONLINE UNIQUE ID';
FORMAT CLMPD MMDDYY10.;
TITLE 'PAID CLAIMS WHICH DID NOT BILL FOR REINSURANCE';
TITLE2 "FOR THE MONTH OF &EFFDATE";
FOOTNOTE  'JOB = UTLW2K1     REPORT = ULW2K1.LW2K1R2';
RUN;
PROC PRINTTO;
RUN;
