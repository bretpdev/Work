%LET RPTLIB = T:\SAS;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;

PROC SQL;
CREATE TABLE DEMO AS
SELECT DISTINCT A.DF_SPE_ACC_ID
	,TRIM(A.DM_PRS_1) || ' ' || TRIM(A.DM_PRS_LST) AS NAME
	,C.DX_ADR_EML
FROM DLGSUTWH.PD10_PRS_NME A
INNER JOIN DLGSUTWH.LN10_LON B
	ON A.DF_PRS_ID = B.bf_ssn
INNER JOIN DLGSUTWH.PD32_PRS_ADR_EML C
	ON A.DF_PRS_ID = C.DF_PRS_ID
WHERE A.DD_BRT >= '01JAN1976'D
	AND B.LA_CUR_PRI > 0
	AND C.DI_VLD_ADR_EML = 'Y'
	AND C.DC_STA_PD32 = 'A';
QUIT;
PROC SORT DATA=DEMO;
	BY DF_SPE_ACC_ID;
RUN;
ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;

/*So the script does not crash, each */
/*report should be limited to 10000 rows*/
PROC SQL NOPRINT;
SELECT CEIL(COUNT(*)/10000) INTO :NUM_RPTS
FROM DEMO;
QUIT;
%PUT &NUM_RPTS;

%MACRO REPORT(R,START,FINISH);
DATA _NULL_;
SET WORK.DEMO(FIRSTOBS=&START OBS=&FINISH);
FILE "&RPTLIB/Service Mark Survey Email.R&R" DELIMITER=',' DSD DROPOVER LRECL=32767;
IF _N_ = 1 THEN DO;
	PUT "ACCOUNT_NUMBER,NAME,DX_ADR_EML";
END;
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT NAME $ @;
		PUT DX_ADR_EML $;
	END;
RUN;
%MEND;

%MACRO LETITGO;
%LET R = 2;
%LET START = 1;
%LET FINISH = 10000;
%DO %UNTIL (%SYSEVALF(&R - 1) > &NUM_RPTS);
	%REPORT(&R,&START,&FINISH)
	%LET R = %SYSEVALF(&R + 1);
	%LET START = %SYSEVALF(&START + 10000);
	%LET FINISH = %SYSEVALF(&FINISH + 10000);
%END;
%MEND;
%LETITGO;
