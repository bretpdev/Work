/*Management report for Default Prevention to show indicated*/
/*DAAR requests received during the previous month for which */
/*we have already billed at least once prior to that month */
/*and cannot bill for again.*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB=T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWDPF.LWDPFR2";
DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
     CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;
OPTIONS NOCENTER NODATE NONUMBER LS=132;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT	A.af_apl_id||A.af_apl_id_sfx	as CLUID,
		integer(A.bf_ssn)				as SSN,
		a.ld_pcl_rcv					AS PCLRCV,
		A.LA_CLM_PRI					AS PRIN,
		A.LA_CLM_INT					AS INT,
		(A.LA_CLM_PRI+A.LA_CLM_INT)*0.01 AS FEE,
		C.DF_SPE_ACC_ID

FROM  OLWHRM1.DC01_LON_CLM_INF A inner join OLWHRM1.DC20_FEE_TRF B
	on a.af_apl_id = b.af_apl_id
	and a.af_apl_id_sfx = b.af_apl_id_sfx
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.BF_SSN = C.DF_PRS_ID

WHERE A.LC_PCL_REA IN ('DF','DQ')
AND A.lf_crt_dts_dc10 = (SELECT MAX(G.lf_crt_dts_dc10)
					FROM OLWHRM1.DC01_LON_CLM_INF G
					WHERE G.AF_APL_ID = A.AF_APL_ID
					AND G.AF_APL_ID_SFX = A.AF_APL_ID_SFX
					AND G.LC_PCL_REA IN ('DF','DQ')
					AND G.LD_PCL_RCV BETWEEN &BEGIN AND &END)

AND B.ld_trf < &BEGIN
AND B.lc_rec_sta = 'I'
AND (
	NOT EXISTS (SELECT *
			FROM OLWHRM1.DC20_FEE_TRF X
			WHERE X.AF_APL_ID = B.AF_APL_ID
			AND X.AF_APL_ID_SFX = B.AF_APL_ID_SFX
			AND X.lc_rec_sta = 'R')
OR
	EXISTS (SELECT *
			FROM  OLWHRM1.DC01_LON_CLM_INF Y
			WHERE Y.AF_APL_ID = A.AF_APL_ID
			AND Y.AF_APL_ID_SFX = A.AF_APL_ID_SFX
			AND Y.LD_PCL_RCV > 
				(SELECT Z.LD_TRF
				FROM OLWHRM1.DC20_FEE_TRF Z
				WHERE Z.AF_APL_ID = Y.AF_APL_ID
				AND Z.AF_APL_ID_SFX = Y.AF_APL_ID_SFX
				AND Z.lc_rec_sta = 'I'
				AND Z.LD_TRF = 
					(SELECT MAX(F.LD_TRF)
					FROM OLWHRM1.DC20_FEE_TRF F
					WHERE F.AF_APL_ID = Z.AF_APL_ID
					AND F.AF_APL_ID_SFX = Z.AF_APL_ID_SFX
					)
				)
			)
	)
);
DISCONNECT FROM DB2;

PROC SORT data=DEMO;
BY DF_SPE_ACC_ID CLUID;
RUN;
endrsubmit  ;

DATA DEMO; 
SET WORKLOCL.DEMO; 
RUN;

DATA _NULL_;
     EFFMO = PUT(INTNX('MONTH',TODAY(),-1), MONNAME9.);
	 EFFYR = PUT(INTNX('MONTH',TODAY(),-1), YEAR4.);
     CALL SYMPUT('EFFDATE',EFFMO||' '||EFFYR);
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS NOCENTER NODATE NONUMBER ps=52 ls=88 ;
PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN N='# of Loans:  ';
VAR DF_SPE_ACC_ID CLUID PCLRCV PRIN INT FEE;
SUM PRIN INT FEE;
LABEL	DF_SPE_ACC_ID = 'ACCT # ' CLUID = 'Commonline Unique ID'	PRIN='Principal'
INT='Interest' FEE='1% Fee' PCLRCV = 'Preclaim Received Date';
FORMAT SSN SSN11. PCLRCV MMDDYY10. PRIN DOLLAR15.2 INT DOLLAR15.2 FEE DOLLAR15.2;
TITLE "DAF Previously Billed Report - &EFFDATE";
FOOTNOTE  'JOB = UTLWDPF     REPORT = ULWDPF.LWDPFR2';
RUN;
PROC PRINTTO;
RUN;
