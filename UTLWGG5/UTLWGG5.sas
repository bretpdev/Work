/*UTLWGG5 POST CLAIM QC - AWG RELEASE FOR VOLUNTARY PAY*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWGG5.LWGG5R2";
FILENAME REPORT3 "&RPTLIB/ULWGG5.LWGG5R3";
DATA _NULL_;
     CALL SYMPUT('THEDATE',"'"||PUT(INTNX('DAY',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
	 CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'beginning'), MMDDYYD10.)||"'");
	 CALL SYMPUT('MONTHS_AGO_9',"'"||PUT(INTNX('MONTH',TODAY(),-9,'S'), MMDDYYD10.)||"'");
	 CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'end'), MMDDYYD10.)||"'");
	 CALL SYMPUT('CURDATE',"'"||PUT(TODAY(), MMDDYYD10.)||"'");
RUN;
%SYSLPUT MONTHS_AGO_9 = &MONTHS_AGO_9;
%SYSLPUT THEDATE = &THEDATE;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
%SYSLPUT CURDATE = &CURDATE;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE INITSEL AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN AS SSN
	,B.DM_PRS_LST
	,B.DF_SPE_ACC_ID
	,C.BF_LST_USR_LA10
	,C.BD_WDR
	,D.BD_ATY_PRF AS LRVWA_DT
	,SUBSTR(D.BX_CMT,1,16) AS LRVWA_CMT
	,E.BD_ATY_PRF AS LRDPA_DT
	,SUBSTR(E.BX_CMT,1,16) AS LRDPA_CMT
	,F.LA_INT_AGY_BIL_DOE
	,F.LA_PAY_XPC
	,COALESCE(BD_WDR.BD_WDR_CRIT,0) AS BD_WDR_CRIT
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.PD01_PDM_INF B
	ON A.BF_SSN = B.DF_PRS_ID
INNER JOIN OLWHRM1.LA10_LEG_ACT C
	ON A.BF_SSN = C.DF_PRS_ID_BR 
LEFT JOIN (
			SELECT DF_PRS_ID, BD_ATY_PRF, BX_CMT
			FROM OLWHRM1.AY01_BR_ATY
			WHERE BD_ATY_PRF = &THEDATE
				AND PF_ACT = 'LRVWA'
			) D ON A.BF_SSN = D.DF_PRS_ID
LEFT JOIN (
			SELECT DF_PRS_ID, BD_ATY_PRF, BX_CMT
			FROM OLWHRM1.AY01_BR_ATY
			WHERE BD_ATY_PRF = &THEDATE
				AND PF_ACT = 'LRDPA'
			) E ON A.BF_SSN = E.DF_PRS_ID
INNER JOIN (
			SELECT AA.BF_SSN
				,SUM(BB.LA_INT_AGY_BIL_DOE) AS LA_INT_AGY_BIL_DOE
				,SUM(AA.LA_PAY_XPC) AS LA_PAY_XPC
			FROM OLWHRM1.DC01_LON_CLM_INF AA
			INNER JOIN OLWHRM1.DC19_DOE_RIN BB
				ON AA.AF_APL_ID = BB.AF_APL_ID
				AND AA.AF_APL_ID_SFX = BB.AF_APL_ID_SFX
				AND AA.LF_CRT_DTS_DC10 = BB.LF_CRT_DTS_DC10
			WHERE AA.LC_STA_DC10 = '03'
				AND AA.LC_REA_CLM_ASN_DOE = ''
			GROUP BY AA.BF_SSN
			) F ON A.BF_SSN = F.BF_SSN
LEFT OUTER JOIN (
	SELECT DISTINCT PAST9.DF_PRS_ID_BR
		,1 AS BD_WDR_CRIT
	FROM OLWHRM1.LA10_LEG_ACT PAST9
	INNER JOIN ( 
		SELECT DF_PRS_ID_BR
			,COUNT(*) AS BD_WDR_CT
		FROM OLWHRM1.LA10_LEG_ACT
		WHERE BD_WDR IS NOT NULL
			AND BC_WDR_REA = '07'
		GROUP BY DF_PRS_ID_BR
		HAVING COUNT(*) > 2
		) GT2
		ON 
		PAST9.DF_PRS_ID_BR = GT2.DF_PRS_ID_BR
	WHERE PAST9.BD_WDR > &MONTHS_AGO_9
		AND PAST9.BC_WDR_REA = '07'
	) BD_WDR
	ON A.BF_SSN = BD_WDR.DF_PRS_ID_BR
WHERE A.LC_STA_DC10 = '03'
	AND A.LC_REA_CLM_ASN_DOE = ''
	AND A.LD_CLM_ASN_DOE IS NULL
	AND C.BD_WDR = &THEDATE
	AND C.BC_WDR_REA = '07'

);

DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA INITSEL;SET WORKLOCL.INITSEL;RUN;

PROC SORT DATA=INITSEL;BY SSN;RUN;

/*PULL OUT OBS FOR R2 RPT*/
DATA R2;
SET INITSEL;
IF MAX(LRVWA_DT,LRDPA_DT) NE . AND BD_WDR_CRIT THEN 
	OUTPUT;
RUN;

/*CALCULATE WHICH IS GREATER 50.00 OR LA_INT_AGY_BIL_DOE*/
DATA R3;
SET INITSEL;
IF LA_INT_AGY_BIL_DOE > 50 THEN MAXAMT = LA_INT_AGY_BIL_DOE; ELSE MAXAMT = 50;  
RUN;

/*PULL OUT OBS FOR R3 RPT*/
DATA R3;
SET R3;
IF ((LRVWA_DT EQ NULL AND LRDPA_DT EQ NULL AND LA_PAY_XPC < MAXAMT)) OR (LRVWA_CMT NE "OWW RELEASED PER" AND LRVWA_DT NE NULL) OR (LRDPA_CMT NE "OWW RELEASED PER" AND LRDPA_DT NE NULL) THEN OUTPUT;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW; 
RUN;
OPTIONS PAGENO=1 LS=126 NODATE CENTER;
PROC PRINT NOOBS SPLIT='/' DATA=R2 WIDTH=UNIFORM;
FORMAT BD_WDR MMDDYY10.	LRVWA_DT MMDDYY10. LRDPA_DT MMDDYY10.;
VAR DF_SPE_ACC_ID
	DM_PRS_LST
	BD_WDR
	LRVWA_DT
	LRDPA_DT
	BF_LST_USR_LA10;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	DM_PRS_LST = 'LAST NAME'
	BD_WDR = 'DATE WITHDRAWN'
	LRVWA_DT = 'LRVWA DATE'
	LRDPA_DT = 'LRDPA DATE'
	BF_LST_USR_LA10 = 'USER';
TITLE	'AWG RELEASE FOR VOL PAY: LOWER PMTS';
TITLE2 "RUN DATE: &CURDATE";
FOOTNOTE  'JOB = UTLWGG5  	 REPORT = ULWGG5.LWGG5R2';
RUN;

PROC PRINTTO PRINT=REPORT3 NEW; 
RUN;
OPTIONS PAGENO=1 LS=126 NODATE CENTER;
PROC PRINT NOOBS SPLIT='/' DATA=R3 WIDTH=UNIFORM;
FORMAT BD_WDR MMDDYY10.;
VAR DF_SPE_ACC_ID
	DM_PRS_LST
	BD_WDR
	BF_LST_USR_LA10;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	DM_PRS_LST = 'LAST NAME'
	BD_WDR = 'DATE WITHDRAWN'
	BF_LST_USR_LA10 = 'USER';
TITLE	'UNAUTHORIZED AWG RELEASE FOR VOL PAY';
TITLE2 "RUN DATE: &CURDATE";
FOOTNOTE  'JOB = UTLWGG5  	 REPORT = ULWGG5.LWGG5R3';
RUN;

PROC PRINTTO;
RUN;
