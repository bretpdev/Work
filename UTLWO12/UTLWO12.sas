/*UTLWO12 IN-HOUSE BORROWERS WITH REFUND OR CANCELLATIONS*/
/*RECEIVED AFTER REPAYMENT SCHEDULE ESTABLISHED*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO12.LWO12R2";

libname  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=work  ;
RSUBMIT;

PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE RSREF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	A.LF_LON_CUR_OWN
	,INTEGER(A.BF_SSN) AS BF_SSN
	,RTRIM(E.DM_PRS_LST)||', '||RTRIM(E.DM_PRS_1)||' '||DM_PRS_MID AS NAME
	,A.LN_SEQ
	,A.LA_CUR_PRI
	,A.IC_LON_PGM
	,LN66.LA_RPS_ISL
	,G.PF_REQ_ACT
	,G.LD_REQ_RSP_ATY_PRF
	,X.LD_FAT_PST
	,X.LD_FAT_APL	
	,X.LD_FAT_EFF
	,G.LN_ATY_SEQ
	,E.DF_SPE_ACC_ID AS ACCTNUM
FROM  OLWHRM1.LN10_LON A 
INNER JOIN OLWHRM1.LN65_LON_RPS B
	ON A.BF_SSN = B.BF_SSN
	AND A.LN_SEQ = B.LN_SEQ
	AND B.LC_STA_LON65 = 'A'
INNER JOIN OLWHRM1.LN66_LON_RPS_SPF LN66
	ON B.BF_SSN = LN66.BF_SSN
	AND B.LN_SEQ = LN66.LN_SEQ
	AND B.LN_RPS_SEQ = LN66.LN_RPS_SEQ
	AND LN66.LN_GRD_RPS_SEQ = 1
INNER JOIN OLWHRM1.RS10_BR_RPD C
	ON A.BF_SSN = C.BF_SSN
	AND B.LN_RPS_SEQ = C.LN_RPS_SEQ
	AND C.LC_STA_RPST10 = 'A'
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU D
	ON A.BF_SSN = D.BF_SSN
	AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME E
	ON A.BF_SSN = E.DF_PRS_ID
INNER JOIN OLWHRM1.LN90_FIN_ATY X
	ON X.BF_SSN = A.BF_SSN
	AND X.LN_SEQ = A.LN_SEQ
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY G
	ON A.BF_SSN = G.BF_SSN
	AND G.PF_REQ_ACT = 'RVWRS'
WHERE A.LC_STA_LON10 = 'R'
AND D.WC_DW_LON_STA NOT IN 
	('06','07','08','11','12','16','17','18','19','20''21','22')
AND C.LC_STA_RPST10 = 'A'
AND X.PC_FAT_TYP = '10'
AND X.PC_FAT_SUB_TYP IN ('40','45')
AND X.LC_STA_LON90 = 'A'
AND X.LC_FAT_REV_REA <> '0'
AND X.LD_FAT_PST >= C.LD_STA_RPST10
);

DISCONNECT FROM DB2;
ENDRSUBMIT  ;
DATA RSREF; SET WORKLOCL.RSREF; RUN;

PROC SQL;	
CREATE TABLE RSREF2 AS
SELECT DISTINCT R.* 
FROM RSREF R
WHERE NOT EXISTS (
	SELECT * 
	FROM RSREF HH
	WHERE HH.BF_SSN = R.BF_SSN
	AND HH.LD_REQ_RSP_ATY_PRF > R.LD_FAT_EFF
	)
; 
QUIT;

PROC SORT DATA=RSREF2 NODUPREC;
BY NAME LN_SEQ;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

OPTIONS CENTER NODATE NUMBER PAGENO=1 ORIENTATION=LANDSCAPE;
OPTIONS PS=39 LS=127;

PROC PRINT DATA=RSREF2 NOOBS SPLIT='/' WIDTH=UNIFORM WIDTH=MIN;  
VAR LF_LON_CUR_OWN ACCTNUM NAME LN_SEQ LA_CUR_PRI IC_LON_PGM LA_RPS_ISL;
LABEL LF_LON_CUR_OWN='OWNER' NAME='NAME' LN_SEQ='LOAN SEQ #'
LA_CUR_PRI='PRINCIPAL BALANCE' IC_LON_PGM='LOAN TYPE' LA_RPS_ISL='INSTALLMENT AMOUNT';
FORMAT LA_CUR_PRI LA_RPS_ISL COMMA10.2;
TITLE 'IN-HOUSE BORROWERS WITH REFUNDS OR CANCELLATIONS';
TITLE2 'RECEIVED AFTER REPAYMENT SCHEDULE ESTABLISHED';
TITLE3 "&RUNDATE";
FOOTNOTE  'JOB = UTLWO12     REPORT = ULWO12.LWO12R2';
RUN;

PROC PRINTTO;
RUN;
/**/
/*PROC EXPORT DATA=WORK.RSREF*/
/*            OUTFILE= "T:\SAS\DEMO.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/


