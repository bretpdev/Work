*----------------------------------*
| SASR 2763 - VWA SUCCESS TRACKING |
*----------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD38.LWD38R2";
FILENAME REPORTZ "&RPTLIB/ULWD38.LWD38RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
DATA _NULL_;
	CALL SYMPUT('BEGIN',"'"||PUT(INTNX('MONTH',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',TODAY(),-1,'END'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE VWAST AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.AF_APL_ID||A.AF_APL_ID_SFX AS CLUID 
	,DC11.LF_CRT_DTS_DC10 
	,DC11.LF_CRT_DTS_DC11 
	,DC11.LD_TRX_EFF 
	,DC11.LC_TRX_TYP
	,(A.LA_CLM_PRI + 
		A.LA_CLM_INT - 
		A.LA_PRI_COL + 
		A.LA_INT_ACR +  
		COALESCE(E.LA_CLM_INT_ACR,0) - 
		A.LA_INT_COL) + 
	  (A.LA_LEG_CST_ACR - 
		A.LA_LEG_CST_COL + 
		A.LA_OTH_CHR_ACR - 
		A.LA_OTH_CHR_COL + 
		A.LA_COL_CST_ACR - 
		A.LA_COL_CST_COL) +
     	COALESCE(E.LA_CLM_PRJ_COL_CST,0) AS TOB
FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.LA10_LEG_ACT B
	ON A.BF_SSN = B.DF_PRS_ID_BR
LEFT OUTER JOIN OLWHRM1.DC11_LON_FAT DC11
	ON A.AF_APL_ID = DC11.AF_APL_ID
	AND A.AF_APL_ID_SFX = DC11.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = DC11.LF_CRT_DTS_DC10
LEFT OUTER JOIN OLWHRM1.DC02_BAL_INT E
	ON A.AF_APL_ID = E.AF_APL_ID
	AND A.AF_APL_ID_SFX = E.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = E.LF_CRT_DTS_DC10
WHERE A.LC_AUX_STA = '10'
	AND A.LD_AUX_STA_UPD BETWEEN &BEGIN AND &END
	AND B.BC_WDR_REA= '07'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWD38.LWD38RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA VWAST ;
	SET WORKLOCL.VWAST;
RUN;
PROC SORT DATA=VWAST NODUPKEY;
	BY CLUID DESCENDING LD_TRX_EFF LF_CRT_DTS_DC10 LF_CRT_DTS_DC11 ;
RUN;
DATA VWAST2(DROP=TV2);
SET VWAST;
BY CLUID;
RETAIN TV TV2;
IF FIRST.CLUID THEN DO;
	IF LC_TRX_TYP = 'RH' THEN DO;
		TV = 1;
		TV2 = 1;
	END;
	ELSE DO;
		TV = 0;	
		TV2 = 0;
	END;
END;
ELSE DO;
	IF TV > 0 THEN DO;
		TV2 = TV+1;
		TV = TV2;
	END;
	ELSE DO;
		TV = 0;	
		TV2 = 0;
	END;
END;
IF TV <= 10 THEN 
	OUTPUT;
RUN;
PROC SQL;
CREATE TABLE TVWAST AS 
SELECT DISTINCT A.BF_SSN
	,A.CLUID
	,A.TOB
	,RH.MIN_RH_DT FORMAT=MMDDYY10.
	,RH.NUM_RH_TRX
	,EP.MAX_EP_DT FORMAT=MMDDYY10.
	,EP.NUM_EP_TRX
	,CASE 
		WHEN RH.NUM_RH_TRX = 1 AND NUM_EP_TRX = 9 THEN 1
		ELSE 0
	 END AS COL_GRP
FROM VWAST A
LEFT OUTER JOIN (
	SELECT BF_SSN
		,CLUID
		,MIN(LD_TRX_EFF) AS MIN_RH_DT
		,COUNT(*) AS NUM_RH_TRX
	FROM VWAST2
	WHERE LC_TRX_TYP = 'RH'
	GROUP BY BF_SSN
		,CLUID
	) RH
	ON A.CLUID = RH.CLUID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,CLUID
		,MAX(LD_TRX_EFF) AS MAX_EP_DT
		,COUNT(*) AS NUM_EP_TRX
	FROM VWAST2
	WHERE LC_TRX_TYP = 'EP'
	GROUP BY BF_SSN
		,CLUID
	) EP
	ON A.CLUID = EP.CLUID
;
QUIT;
PROC SQL;
	CREATE TABLE X AS
	SELECT COUNT(DISTINCT BF_SSN) AS COL1_BOR
	FROM TVWAST A
;
	CREATE TABLE Y AS
	SELECT COUNT(DISTINCT BF_SSN) AS COL2_BOR
		,SUM(TOB) AS COL2_TOT
	FROM TVWAST A
	WHERE COL_GRP=1;
;
QUIT;
DATA Z ;
	MERGE X Y;
	PORB = ROUND(COL2_BOR/COL1_BOR,.01);
RUN;
PROC DATASETS;
	DELETE X Y;
	CHANGE Z=SUMMARY_VWAST;
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'VWA SUCCESS TRACKING';
FOOTNOTE   'JOB = UTLWD38  	 REPORT = ULWD38.LWD38R2';
PROC CONTENTS DATA=SUMMARY_VWAST OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWD38  	 REPORT = ULWD38.LWD38R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=SUMMARY_VWAST WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT COL1_BOR COL2_BOR COMMA7. COL2_TOT DOLLAR12.2 PORB PERCENT10.;
VAR COL1_BOR COL2_BOR COL2_TOT PORB;
LABEL COL1_BOR = 'TOTAL/NUMBER/OF/BORROWERS'
	COL2_BOR = 'TOTAL/NUMBER/OF/BORROWERS/WITH/EP PAYMENT/TYPE'
	COL2_TOT = 'TOTAL/OUTSTANDING/BALANCE/OF/BORROWERS/WITH/EP PAYMENTS'
	PORB = 'PERCENT OF/REHABILITATED/BORROWERS';
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE*/
PROC EXPORT DATA=VWAST
	OUTFILE= "T:\SAS\D38.Detail.xls" 
	DBMS=EXCEL2000 REPLACE;
RUN;

