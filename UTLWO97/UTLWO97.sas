*-----------------------------------------*
| UTLWO97 - Automated Email Communication |
*-----------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWO97.LWO97R2";
FILENAME REPORT3 "&RPTLIB/ULWO97.LWO97R3";
FILENAME REPORT4 "&RPTLIB/ULWO97.LWO97R4";
FILENAME REPORT5 "&RPTLIB/ULWO97.LWO97R5";
FILENAME REPORT6 "&RPTLIB/ULWO97.LWO97R6";
FILENAME REPORTZ "&RPTLIB/ULWO97.LWO97RZ";
DATA _NULL_;
	CALL SYMPUT('PLUS_1_DAY',"'"||PUT(INTNX('DAY',TODAY(),+1,'BEGINNING'), MMDDYYD10.)||"'");	
	CALL SYMPUT('PLUS_12_DAYS',"'"||PUT(INTNX('DAY',TODAY(),+12,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('DAYS_AGO_45',"'"||PUT(INTNX('DAY',TODAY(),-45,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
%SYSLPUT PLUS_1_DAY = &PLUS_1_DAY;
%SYSLPUT PLUS_12_DAYS = &PLUS_12_DAYS ;
%SYSLPUT DAYS_AGO_45 = &DAYS_AGO_45 ;
RSUBMIT;
DATA LOAN_TYPES;
	FORMAT LN_TYP LN_PGM $50.;
	INFILE "/sas/whse/progrevw/GENR_REF_LoanTypes.txt" DLM=',' MISSOVER DSD;
	INFORMAT LN_TYP LN_PGM $50.;
	INPUT LN_TYP LN_PGM ;
	LN_PGM = UPCASE(LN_PGM);
RUN;
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LN_TYP)||"'" 
		INTO :FFELP_LIST SEPARATED BY "," 
	FROM LOAN_TYPES
	WHERE LN_PGM = 'FFEL';
QUIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE AECD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT D.DF_SPE_ACC_ID
	,D.DM_PRS_1
	,C.DX_ADR_EML
	,A.LD_DSB
	,A.IC_LON_PGM 
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.PD32_PRS_ADR_EML C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON C.DF_PRS_ID = D.DF_PRS_ID
WHERE A.LD_DSB BETWEEN &PLUS_1_DAY AND &PLUS_12_DAYS 
	AND A.LC_STA_LON15 IN ('1','3')
	AND A.LC_DSB_TYP = '1'
	AND A.LA_DSB != COALESCE(A.LA_DSB_CAN,0) 
	AND A.IC_LON_PGM IN (&FFELP_LIST)
	AND SUBSTR(A.IC_LON_PGM,1,2) != 'PL' 
	AND B.AF_DOE_SCL IN 
	(
		'00367500','00402700','00367700','00367800',
		'00522000','00368000','00367600','00367100',
		'00367900','00368100','00367200','00367000',
		'00162500','00160600' 
	)
	AND C.DC_ADR_EML = 'H'
	AND C.DI_VLD_ADR_EML = 'Y'
	AND NOT EXISTS 
	(
		SELECT 1
		FROM OLWHRM1.AY10_BR_LON_ATY X
		WHERE X.BF_SSN = A.BF_SSN
			AND X.LC_STA_ACTY10 = 'A'
			AND X.PF_REQ_ACT = 'DISBE'
			AND X.LD_ATY_REQ_RCV >= &DAYS_AGO_45
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWO97.LWO97RZ);*/
/*QUIT;*/
PROC SORT DATA=AECD NODUPKEY;
	BY DF_SPE_ACC_ID;
RUN;
ENDRSUBMIT;
DATA AECD ;
	SET WORKLOCL.AECD;
RUN;
PROC SORT DATA=AECD ;
	BY LD_DSB;
RUN;
DATA AECD;
	SET AECD;
	N = _N_;
RUN;
%MACRO O97_FILES(RNO,LB,UB);
DATA _NULL_;
	SET  WORK.AECD;
	WHERE &LB <= N <= &UB;
	FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
		FORMAT DF_SPE_ACC_ID $10. ;
		FORMAT DM_PRS_1 $13. ;
		FORMAT DX_ADR_EML $254. ;
		FORMAT LD_DSB DATE9. ;
		FORMAT N BEST12. ;
	IF _N_ = 1 THEN DO;
		PUT "DF_SPE_ACC_ID,DM_PRS_1,DX_ADR_EML" ;
	END;
	DO;
	   PUT DF_SPE_ACC_ID $ @;
	   PUT DM_PRS_1 $ @;
	   PUT DX_ADR_EML $ ;
	END;
RUN;
%MEND O97_FILES;
%O97_FILES(2,1,10000);
%O97_FILES(3,10001,20000);
%O97_FILES(4,20001,30000);
%O97_FILES(5,30001,40000);
%O97_FILES(6,40001,50000);
