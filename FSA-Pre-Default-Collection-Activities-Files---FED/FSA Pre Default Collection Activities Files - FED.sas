/*set the sample size for each group*/
%LET SAMPLE_SIZE_61 = 100;
%LET SAMPLE_SIZE_240 = 10;

LIBNAME LEGEND REMOTE SERVER=LEGEND SLIBREF=WORK;
RSUBMIT;
LIBNAME PKUB DB2 DATABASE=DNFPUTDL OWNER=PKUB;
PROC SQL;
	CREATE TABLE FSA AS
		SELECT DISTINCT
			DF_SPE_ACC_ID,
			CATX(' ',PD10.DM_PRS_1,PD10.DM_PRS_LST) AS NAME,
			LOWCASE(PD32H.DX_ADR_EML) AS EMAIL,
			CASE
				WHEN LN16.LN_DLQ_MAX BETWEEN 61 AND 90 THEN '61-90'
				ELSE '240-270'
			END AS BUCKET
		FROM
			PKUB.PD10_PRS_NME PD10
			inner JOIN PKUB.PD32_PRS_ADR_EML PD32H
				ON PD10.DF_PRS_ID = PD32H.DF_PRS_ID
				AND PD32H.DC_STA_PD32 = 'A'
				AND PD32H.DI_VLD_ADR_EML = 'Y'
				AND PD32H.DC_ADR_EML = 'H'
			JOIN PKUB.LN10_LON LN10
				ON PD10.DF_PRS_ID = LN10.BF_SSN
			JOIN PKUB.LN16_LON_DLQ_HST LN16
				ON LN10.BF_SSN = LN16.BF_SSN
				AND LN10.LN_SEQ = LN16.LN_SEQ
				AND LN16.LC_STA_LON16 = '1'
			INNER JOIN PKUB.DW01_DW_CLC_CLU DW01
				ON DW01.BF_SSN = LN10.BF_SSN
				AND DW01.LN_SEQ = LN10.LN_SEQ
				AND DW01.WC_DW_LON_STA = '03'
			LEFT JOIN PKUB.AY10_BR_LON_ATY FIR
				ON LN10.BF_SSN = FIR.BF_SSN
				AND FIR.PF_REQ_ACT IN ('FIRCO','FIRNO')
			LEFT JOIN PKUB.AY10_BR_LON_ATY SEC
				ON LN10.BF_SSN = FIR.BF_SSN
				AND FIR.PF_REQ_ACT IN ('SECCO','SECNO')
		WHERE
			LN10.LA_CUR_PRI > 0
			AND LN10.LC_STA_LON10 = 'R'
			AND 
				(
					(LN16.LN_DLQ_MAX BETWEEN 61 AND 90 AND FIR.BF_SSN IS NULL)
					OR (LN16.LN_DLQ_MAX BETWEEN 240 AND 270  AND SEC.BF_SSN IS NULL)
				)
	;
QUIT;
ENDRSUBMIT;

DATA FSA; SET LEGEND.FSA; RUN;

/*split population into sets by delinquency bucket*/
DATA ALL_61 ALL_240;
	SET FSA;
	IF BUCKET = '61-90' THEN OUTPUT ALL_61;
	ELSE OUTPUT ALL_240;
RUN;

/*get random sample of 61-90 bucket for control group*/
DATA CTRL_61 (KEEP=DF_SPE_ACC_ID NAME EMAIL);
	SAMPSIZE=&SAMPLE_SIZE_61;
	OBSLEFT=TOTOBS;
	DO WHILE(SAMPSIZE>0);
		PICKIT+1;
		IF RANUNI(0)<SAMPSIZE/OBSLEFT THEN DO;
			SET ALL_61 POINT=PICKIT
				NOBS=TOTOBS;
			OUTPUT CTRL_61;
			SAMPSIZE=SAMPSIZE-1;
		END;
		OBSLEFT=OBSLEFT-1;
	END;
	STOP;
RUN;

/*get random sample of 240-270 bucket for control group*/
DATA CTRL_240 (KEEP=DF_SPE_ACC_ID NAME EMAIL);
	SAMPSIZE=&SAMPLE_SIZE_240;
	OBSLEFT=TOTOBS;
	DO WHILE(SAMPSIZE>0);
		PICKIT+1;
		IF RANUNI(0)<SAMPSIZE/OBSLEFT THEN DO;
			SET ALL_240 POINT=PICKIT
				NOBS=TOTOBS;
			OUTPUT CTRL_240;
			SAMPSIZE=SAMPSIZE-1;
		END;
		OBSLEFT=OBSLEFT-1;
	END;
	STOP;
RUN;

/*get population not in control groups*/
PROC SQL;
	CREATE TABLE EML_61 AS
		SELECT
			A.DF_SPE_ACC_ID,
			A.NAME,
			A.EMAIL
		FROM
			ALL_61 A
/*			LEFT JOIN CTRL_61 C*/
/*				ON A.DF_SPE_ACC_ID = C.DF_SPE_ACC_ID*/
/*				AND A.NAME = C.NAME*/
/*				AND A.EMAIL = C.EMAIL*/
		WHERE
			A.DF_SPE_ACC_ID NOT IN (SELECT DF_SPE_ACC_ID FROM CTRL_61)
	;

	CREATE TABLE EML_240 AS
		SELECT
			A.DF_SPE_ACC_ID,
			A.NAME,
			A.EMAIL
		FROM
			ALL_240 A
/*			LEFT JOIN CTRL_240 C*/
/*				ON A.DF_SPE_ACC_ID = C.DF_SPE_ACC_ID*/
/*				AND A.NAME = C.NAME*/
/*				AND A.EMAIL = C.EMAIL*/
		WHERE
			A.DF_SPE_ACC_ID NOT IN (SELECT DF_SPE_ACC_ID FROM CTRL_240)
	;
QUIT;

/*generate reports*/
PROC EXPORT
		DATA=EML_61 (DROP=DF_SPE_ACC_ID)
		OUTFILE='T:\SAS\61-90 Days Delinquent File for FSA.R2.XLSX'
		REPLACE;
RUN;

PROC EXPORT
		DATA=EML_240 (DROP=DF_SPE_ACC_ID)
		OUTFILE='T:\SAS\240-270 Days Delinquent File for FSA.R3.XLSX'
		REPLACE;
RUN;

DATA _NULL_;
	SET EML_61 ;
	FILE 'T:\SAS\FSAPRECONV.R4' DELIMITER=',' DSD DROPOVER LRECL=32767;
	PUT DF_SPE_ACC_ID 'FIRNO,,,,,,,ALL,' ;
RUN;

DATA _NULL_;
	SET EML_240 ;
	FILE 'T:\SAS\FSAPRECONV.R5' DELIMITER=',' DSD DROPOVER LRECL=32767;
	PUT DF_SPE_ACC_ID 'SECNO,,,,,,,ALL,' ;
RUN;

DATA _NULL_;
	SET CTRL_61 ;
	FILE 'T:\SAS\FSAPRECONV.R6' DELIMITER=',' DSD DROPOVER LRECL=32767;
	PUT DF_SPE_ACC_ID 'FIRCO,,,,,,,ALL,' ;
RUN;

DATA _NULL_;
	SET CTRL_240 ;
	FILE 'T:\SAS\FSAPRECONV.R7' DELIMITER=',' DSD DROPOVER LRECL=32767;
	PUT DF_SPE_ACC_ID 'SECCO,,,,,,,ALL,' ;
RUN;
