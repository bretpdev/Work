*-----------------------------------------------------*
| UTLWQ24 Anticipated Disbursements for Funding Needs |
*-----------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWQ24.LWQ24RZ";
FILENAME REPORT2 "&RPTLIB/ULWQ24.LWQ24R2";
FILENAME REPORT3 "&RPTLIB/ULWQ24.LWQ24R3";
DATA _NULL_;
	CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK  ;
RSUBMIT;
OPTION SYMBOLGEN;
PROC SQL STIMER;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DISBI AS
SELECT DISTINCT AF_DOE_LDR
	,IM_LDR_FUL
	,AF_DOE_SCL
	,IM_SCL_FUL
	,IC_LON_PGM
	,APP
	,DISB
	,NET_DSB
	,BF_SSN
	,LD_DSB
	,GDE_LEV
FROM CONNECTION TO DB2 (
SELECT E.IM_LDR_FUL
	,CASE 
		WHEN B.AF_DOE_LDR = '813760UT' THEN '813760'
		ELSE B.AF_DOE_LDR
	END AS AF_DOE_LDR
	,A.BF_SSN
	,CHAR(A.AN_SEQ) AS APP
	,CHAR(A.LN_LON_DSB_SEQ) AS DISB
	,A.LA_DSB - COALESCE(A.LA_DSB_CAN,0) AS NET_DSB
	,A.LD_DSB
	,B.AF_DOE_SCL 
	,D.IM_SCL_FUL
	,B.IC_LON_PGM
	,B.AC_SCL_ACA_GDE_LEV AS GDE_LEV
FROM OLWHRM1.LN15_DSB A
INNER JOIN OLWHRM1.AP03_MASTER_APL B 
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.SC10_SCH_DMO D
	ON B.AF_DOE_SCL = D.IF_DOE_SCL 
INNER JOIN OLWHRM1.LR10_LDR_DMO E
	ON B.AF_DOE_LDR = E.IF_DOE_LDR 
WHERE A.LC_DSB_TYP = '1'
	AND A.LC_STA_LON15 IN ('1','3')
	AND (A.LA_DSB_CAN IS NULL OR A.LA_DSB_CAN <> A.LA_DSB)
	AND A.LD_DSB_ROS_PRT IS NULL
	AND A.LC_LDR_DSB_MDM = ' '
	AND B.AF_CNL NOT LIKE '%REALLO%'
);
DISCONNECT FROM DB2;
ENDRSUBMIT;
DATA DISBI;
	SET WORKLOCL.DISBI;
RUN;
DATA DISBI;
	SET DISBI;
	FORMAT REP_DATE MMDDYY10.;
	LENGTH REP_PGM $ 25.;
	IF LD_DSB <= TODAY() THEN 
		REP_DATE = TODAY();
	ELSE 
		REP_DATE = LD_DSB;
	SELECT;
		WHEN
		(
			IC_LON_PGM IN ('STFFRD') 
			AND GDE_LEV IN ('1','2','3','4','5')
		) REP_PGM = 'STFRDU';
		WHEN
		(
			IC_LON_PGM IN ('STFFRD') 
			AND GDE_LEV IN ('A','B','C','D')
		) REP_PGM = 'STFRDG';
		WHEN
		(
			IC_LON_PGM IN ('UNSTFD') 
		) REP_PGM = 'UNSTFD';
		WHEN
		(
			IC_LON_PGM IN ('PLUS') 
		) REP_PGM = 'PLUS';
		WHEN
		(
			IC_LON_PGM IN ('PLUSGB') 
		) REP_PGM = 'PLUSGB';
		OTHERWISE REP_PGM = '';
	END;
RUN;
/*CREATE THE DATA SETS FOR R2 & R3*/
%MACRO REPDSES(TBL,EID,ENAME);
	PROC SQL;
		CREATE TABLE &TBL AS
			SELECT DISTINCT A.&EID
				,A.&ENAME
				,A.REP_DATE
				,COALESCE(B.TSTFRDU,0) AS TSTFRDU
				,COALESCE(C.TSTFRDG,0) AS TSTFRDG
				,COALESCE(D.TUNSTFD,0) AS TUNSTFD
				,COALESCE(E.TPLUS,0) AS TPLUS
				,COALESCE(F.TPLUSGB,0) AS TPLUSGB
				,G.TALL 
			FROM DISBI A
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TSTFRDU
				FROM DISBI
				WHERE REP_PGM = 'STFRDU'
				GROUP BY &EID
					,REP_DATE
				) B
				ON A.&EID = B.&EID
				AND A.REP_DATE = B.REP_DATE
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TSTFRDG
				FROM DISBI
				WHERE REP_PGM = 'STFRDG'
				GROUP BY &EID
					,REP_DATE
				) C
				ON A.&EID = C.&EID
				AND A.REP_DATE = C.REP_DATE
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TUNSTFD
				FROM DISBI
				WHERE REP_PGM = 'UNSTFD'
				GROUP BY &EID
					,REP_DATE
				) D
				ON A.&EID = D.&EID
				AND A.REP_DATE = D.REP_DATE
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TPLUS
				FROM DISBI
				WHERE REP_PGM = 'PLUS'
				GROUP BY &EID
					,REP_DATE
				) E
				ON A.&EID = E.&EID
				AND A.REP_DATE = E.REP_DATE
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TPLUSGB
				FROM DISBI
				WHERE REP_PGM = 'PLUSGB'
				GROUP BY &EID
					,REP_DATE
				) F
				ON A.&EID = F.&EID
				AND A.REP_DATE = F.REP_DATE
			LEFT OUTER JOIN (
				SELECT &EID
					,REP_DATE
					,SUM(NET_DSB) AS TALL
				FROM DISBI
				GROUP BY &EID
					,REP_DATE
				) G
				ON A.&EID = G.&EID
				AND A.REP_DATE = G.REP_DATE
			ORDER BY A.&EID
				,A.REP_DATE
	;
	QUIT;
%MEND REPDSES;
%REPDSES(LENDER,AF_DOE_LDR,IM_LDR_FUL);
%REPDSES(SCHOOLS,AF_DOE_SCL,IM_SCL_FUL);
%MACRO UTLWQ24_REPORTS(RNO,DS,EIDR,ENAMER);
DATA _NULL_;
SET  &DS;
FILE REPORT&RNO DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT &EIDR $8. ;
   FORMAT &ENAMER $40. ;
   FORMAT REP_DATE MMDDYY10. ;
   FORMAT TSTFRDU BEST12. ;
   FORMAT TSTFRDG BEST12. ;
   FORMAT TUNSTFD BEST12. ;
   FORMAT TPLUS BEST12. ;
   FORMAT TPLUSGB BEST12. ;
   FORMAT TALL BEST12. ;
IF _N_ = 1 THEN DO;
   PUT "&EIDR"
	','
	"&ENAMER"
	','
	'DATE'
	','
	'SUB STAFFORD UNDERGRAD TOTAL'
	','
	'SUB STAFFORD GRAD TOTAL'
	','
	'UNSUB STAFFOARD TOTAL'
	','
	'PLUS TOTAL'
	','
	'PLUSGB TOTAL'
	','
	'TOTAL DISBURSEMENTS';
END;
DO;
	PUT &EIDR $ @;
	PUT &ENAMER $ @;
	PUT REP_DATE @;
	PUT TSTFRDU @;
	PUT TSTFRDG @;
	PUT TUNSTFD @;
	PUT TPLUS @;
	PUT TPLUSGB @;
	PUT TALL ;
END;
RUN;
%MEND UTLWQ24_REPORTS;
%UTLWQ24_REPORTS(2,LENDER,AF_DOE_LDR,IM_LDR_FUL);
%UTLWQ24_REPORTS(3,SCHOOLS,AF_DOE_SCL,IM_SCL_FUL);

/*DETAIL FILE*/
PROC EXPORT DATA=DISBI
	OUTFILE= "T:\SAS\UTLWQ24_Detail.xls" 
	DBMS=EXCEL2000 REPLACE;
RUN;

