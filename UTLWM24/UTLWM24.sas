*------------------------------------*
| UTLWM24 - COLLECTION TERMINATED QC |
*------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWM24.LWM24R2";
FILENAME REPORTZ "&RPTLIB/ULWM24.LWM24RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE COLTERM AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.BF_SSN
	,A.AF_APL_ID||A.AF_APL_ID_SFX AS CLUID
	,A.LD_LST_PAY
	,A.LA_LST_PAY
	,D.DF_SPE_ACC_ID
	,A.LF_CLM_ID
	,COALESCE(A.LA_CLM_INT,0) - 
		COALESCE(A.LA_PRI_COL,0) +
		COALESCE(A.LA_INT_ACR,0) +
		COALESCE(B.LA_CLM_INT_ACR,0) - 
		COALESCE(A.LA_INT_COL,0) + 
		COALESCE(A.LA_LEG_CST_ACR,0) -
		COALESCE(A.LA_LEG_CST_COL,0) +
		COALESCE(A.LA_OTH_CHR_ACR,0) -
		COALESCE(A.LA_OTH_CHR_COL,0) +
		COALESCE(A.LA_COL_CST_ACR,0) -
		COALESCE(A.LA_COL_CST_COL,0) + 
		COALESCE(B.LA_CLM_PRJ_COL_CST,0) AS AMT_O
	,C.LA_TRX
	,C.LD_TRX_EFF
	,C.LC_TRX_TYP
FROM OLWHRM1.DC01_LON_CLM_INF A
LEFT OUTER JOIN OLWHRM1.DC02_BAL_INT B
	ON A.AF_APL_ID||A.AF_APL_ID_SFX = B.AF_APL_ID||B.AF_APL_ID_SFX
INNER JOIN OLWHRM1.PD01_PDM_INF D
	ON A.BF_SSN = D.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.DC11_LON_FAT C
	ON A.AF_APL_ID = C.AF_APL_ID
	AND A.AF_APL_ID_SFX = C.AF_APL_ID_SFX
	AND A.LD_LST_PAY = C.LD_TRX_EFF
WHERE A.LC_AUX_STA = '06'
AND DAYS(A.LD_LST_PAY) = DAYS(CURRENT DATE) - 1
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWM24.LWM24RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA COLTERM;
SET WORKLOCL.COLTERM;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 DATE CENTER PAGENO=1;
TITLE 'COLLECTION TERMINATED & NEW PAYMENT RECEIVED';
FOOTNOTE1  	"This document may contain borrowers' sensitive information that UHEAA has pledged to protect.";
FOOTNOTE2	'Please take appropriate precautions to safeguard this information.';
FOOTNOTE3	;
FOOTNOTE4   'JOB = UTLWM24  	 REPORT = ULWM24.LWM24R2';
PROC CONTENTS DATA=COLTERM OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
	FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWM24  	 REPORT = ULWM24.LWM24R2";
	END;
RETURN;
RUN;

PROC PRINT NOOBS SPLIT='/' DATA=COLTERM WIDTH=UNIFORM WIDTH=MIN;
FORMAT LA_TRX  AMT_O DOLLAR10.2 LD_TRX_EFF MMDDYY10.;
VAR CLUID LF_CLM_ID DF_SPE_ACC_ID AMT_O LD_TRX_EFF LA_TRX LC_TRX_TYP;
LABEL CLUID = 'COMMONLINE ID'
	LF_CLM_ID = 'CLAIM ID' 
	DF_SPE_ACC_ID = 'ACCT #'
	AMT_O = 'AMOUNT OWING'
	LD_TRX_EFF = 'DATE OF LAST PAYMENT'
	LA_TRX  = 'AMOUNT OF TRANSACTION'
	LC_TRX_TYP = 'FINANCIAL TRANSACTION TYPE';
RUN;

PROC PRINTTO;
RUN;
