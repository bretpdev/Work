/*UTLWS09 -- BORROWERS WITH MULTIPLE FIRST DUE DATES*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS09.LWS09R2";

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE DEMO AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
A.BF_SSN,
RTRIM(B.DM_PRS_LST)||', '||RTRIM(B.DM_PRS_1)||' '||RTRIM(B.DM_PRS_MID)	AS DM_PRS_NAME,
A.LN_SEQ,
A.IC_LON_PGM,
D.LD_CRT_LON65,
C.LD_RPS_1_PAY_DU,
E.DF_SPE_ACC_ID

FROM	OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.MR01_MGT_RPT_LON B
ON A.BF_SSN = B.BF_SSN
AND A.LN_SEQ = B.LN_SEQ
AND A.IC_LON_PGM IN ('STFFRD','UNSTFD')
AND A.LC_STA_LON10 = 'R'
AND A.LA_CUR_PRI > 0
INNER JOIN OLWHRM1.LN65_LON_RPS D
ON A.BF_SSN = D.BF_SSN
AND A.LN_SEQ = D.LN_SEQ
INNER JOIN OLWHRM1.RS10_BR_RPD C
ON A.BF_SSN = C.BF_SSN
AND C.LC_STA_RPST10 = 'A'
AND C.LN_RPS_SEQ = D.LN_RPS_SEQ
LEFT OUTER JOIN OLWHRM1.PD10_PRS_NME E
	ON A.BF_SSN = E.DF_PRS_ID

WHERE EXISTS (SELECT *
			  FROM	OLWHRM1.LN10_LON L
				INNER JOIN OLWHRM1.LN65_LON_RPS M
				ON L.BF_SSN = M.BF_SSN
				AND L.LN_SEQ = M.LN_SEQ
				AND L.IC_LON_PGM IN ('STFFRD','UNSTFD')
				AND L.LC_STA_LON10 = 'R'
				AND L.LA_CUR_PRI > 0
				INNER JOIN OLWHRM1.RS10_BR_RPD O
				ON L.BF_SSN = O.BF_SSN
				AND O.LC_STA_RPST10 = 'A'
				AND M.LN_RPS_SEQ = O.LN_RPS_SEQ
			  WHERE A.BF_SSN = L.BF_SSN
				AND DAY(C.LD_RPS_1_PAY_DU) <> DAY(O.LD_RPS_1_PAY_DU)
				AND A.LD_END_GRC_PRD = L.LD_END_GRC_PRD
				)
ORDER BY DM_PRS_NAME
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA DEMO;
SET WORKLOCL.DEMO;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS CENTER PAGENO=1 ORIENTATION=PORTRAIT ;
OPTIONS LS=96 PS=52;
PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN;
VAR 
	DF_SPE_ACC_ID
	DM_PRS_NAME
	LN_SEQ
	IC_LON_PGM
	LD_CRT_LON65
	LD_RPS_1_PAY_DU;
LABEL	DF_SPE_ACC_ID = 'ACCT #'
		DM_PRS_NAME = 'NAME'
		LN_SEQ = 'LOAN'
		IC_LON_PGM = 'PROGRAM'
		LD_CRT_LON65 = 'DISCLOSURE CREATE DATE'
		LD_RPS_1_PAY_DU = 'FIRST DUE DATE';

FORMAT 	LD_CRT_LON65 MMDDYY10.
		LD_RPS_1_PAY_DU MMDDYY10.;

TITLE1	'BORROWERS WITH MULTIPLE FIRST DUE DATES';
FOOTNOTE  'JOB = UTLWS09     REPORT = ULWS09.LWS09R2';
RUN;
PROC PRINTTO;
RUN;
