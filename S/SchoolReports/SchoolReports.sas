/*Clears out the Y:\Development\SAS Test Files\School Reports folder*/
OPTIONS XSYNC XMIN NOXWAIT;
%LET PATH=Y:\Development\SAS Test Files\School Reports;
%SYSEXEC Del /f/q "&PATH.";

%LET RPTLIB = T:\SAS;
LIBNAME LEGEND REMOTE SERVER=LEGEND SLIBREF=WORK;
RSUBMIT LEGEND;
PROC SQL;
	CONNECT TO DB2 (DATABASE=DNFPUTDL);
	CREATE TABLE GEN AS
		SELECT 
			*
		FROM 
			CONNECTION TO DB2 
			(
				SELECT 
					 LN10.BF_SSN
					,LN10.LN_SEQ
					,LN10.IC_LON_PGM
					,SUBSTR(LN10.LF_DOE_SCL_ORG,1,6) AS LF_DOE_SCL_ORG_PRE
					,SUBSTR(LN10.LF_DOE_SCL_ORG,7,2) AS LF_DOE_SCL_ORG_BNCH
					,LN10.LF_DOE_SCL_ORG
					,LN10.LA_CUR_PRI
					,DW01.WC_DW_LON_STA
					,DW01.WD_LON_RPD_SR
					,DW01.WA_TOT_BRI_OTS
					,PD10.DM_PRS_1
					,PD10.DM_PRS_LST
					,PD30.DX_STR_ADR_1
					,PD30.DX_STR_ADR_2
					,PD30.DM_CT
					,COALESCE(PD30.DC_DOM_ST,PD30.DM_FGN_ST) AS DC_DOM_ST
					,PD30.DF_ZIP_CDE
					,PHN_H.DN_DOM_PHN_ARA||PHN_H.DN_DOM_PHN_XCH||PHN_H.DN_DOM_PHN_LCL AS HPHN
					,PHN_W.DN_DOM_PHN_ARA||PHN_W.DN_DOM_PHN_XCH||PHN_W.DN_DOM_PHN_LCL AS WPHN
					,PHN_A.DN_DOM_PHN_ARA||PHN_A.DN_DOM_PHN_XCH||PHN_A.DN_DOM_PHN_LCL AS APHN
					,PHN_M.DN_DOM_PHN_ARA||PHN_M.DN_DOM_PHN_XCH||PHN_M.DN_DOM_PHN_LCL AS MPHN
					,PHN_H.DI_PHN_VLD 	AS DI_PHN_VLD_H
					,PHN_W.DI_PHN_VLD 	AS DI_PHN_VLD_W
					,PHN_A.DI_PHN_VLD 	AS DI_PHN_VLD_A
					,PHN_M.DI_PHN_VLD 	AS DI_PHN_VLD_M
					,EML.DX_ADR_EML
					,EML.DI_VLD_ADR_EML
					,COALESCE(LN16.LN_DLQ_MAX,0) AS DYS_DLQ
					,LN16.LD_DLQ_OCC 			 AS DLQ_DT
					,SD10.LD_NTF_SCL_SPR
					,LN65.LC_TYP_SCH_DIS 
					,DAY(RS10.LD_RPS_1_PAY_DU) 	 AS LD_RPS_1_PAY_DU
					,SC10.IM_SCL_FUL
				FROM 
					PKUB.LN10_LON LN10
					INNER JOIN PKUB.DW01_DW_CLC_CLU DW01
						ON LN10.BF_SSN = DW01.BF_SSN
						AND LN10.LN_SEQ = DW01.LN_SEQ
					/*DEMOS*/
					INNER JOIN PKUB.PD10_PRS_NME PD10
						ON DW01.BF_SSN = PD10.DF_PRS_ID
					LEFT JOIN PKUB.PD30_PRS_ADR PD30
						ON PD10.DF_PRS_ID = PD30.DF_PRS_ID
						AND PD30.DC_ADR = 'L'
					LEFT JOIN PKUB.PD40_PRS_PHN PHN_H
						ON PD10.DF_PRS_ID = PHN_H.DF_PRS_ID
						AND PHN_H.DC_PHN = 'H'
					LEFT JOIN PKUB.PD40_PRS_PHN PHN_W
						ON PD10.DF_PRS_ID = PHN_W.DF_PRS_ID
						AND PHN_W.DC_PHN = 'W'
					LEFT JOIN PKUB.PD40_PRS_PHN PHN_A
						ON PD10.DF_PRS_ID = PHN_A.DF_PRS_ID
						AND PHN_A.DC_PHN = 'A'
					LEFT JOIN PKUB.PD40_PRS_PHN PHN_M
						ON PD10.DF_PRS_ID = PHN_M.DF_PRS_ID
						AND PHN_M.DC_PHN = 'M'
					LEFT JOIN PKUB.PD32_PRS_ADR_EML EML
						ON PD10.DF_PRS_ID = EML.DF_PRS_ID
						AND EML.DC_STA_PD32 = 'A'
						AND EML.DC_ADR_EML = 'H'
					/*DELINQUENCY*/
					LEFT JOIN PKUB.LN16_LON_DLQ_HST LN16
						ON LN10.BF_SSN = LN16.BF_SSN
						AND LN10.LN_SEQ = LN16.LN_SEQ
						AND LN16.LC_STA_LON16 = '1'
					/*SCHOOL SEPARATION AND SCHOOL INFO*/
					LEFT JOIN PKUB.LN13_LON_STU_OSD LN13
						ON LN10.BF_SSN = LN13.BF_SSN
						AND LN10.LN_SEQ = LN13.LN_SEQ
						AND LN10.LF_STU_SSN = LN13.LF_STU_SSN
						AND LN13.LC_STA_LON13 = 'A'
					LEFT JOIN PKUB.SD10_STU_SPR SD10
						ON LN13.LF_STU_SSN = SD10.LF_STU_SSN
						AND LN13.LN_STU_SPR_SEQ = SD10.LN_STU_SPR_SEQ
						AND SD10.LC_STA_STU10 = 'A'
					LEFT JOIN PKUB.SC10_SCH_DMO SC10
						ON LN10.LF_DOE_SCL_ORG = SC10.IF_DOE_SCL
					/*REPAYMENT SCHEDULE*/
					LEFT JOIN PKUB.LN65_LON_RPS LN65
						ON LN10.BF_SSN = LN65.BF_SSN
						AND LN10.LN_SEQ = LN65.LN_SEQ
						AND LN65.LC_STA_LON65 = 'A'
					LEFT JOIN PKUB.RS10_BR_RPD RS10
						ON LN65.BF_SSN = RS10.BF_SSN
						AND LN65.LN_RPS_SEQ = RS10.LN_RPS_SEQ
						AND RS10.LC_STA_RPST10 = 'A'
				WHERE 
					LN10.LA_CUR_PRI > 0 
					AND LN10.LC_STA_LON10 = 'R' 
					AND LN10.LF_DOE_SCL_ORG != '07777800'
				/*if given a subset of schools, use code below*/
/*					AND SUBSTR(LN10.LF_DOE_SCL_ORG,1,6) IN */
/*					(*/
/*						'041277',*/
/*						'010858',*/
/*						'041475',*/
/*						'010300'*/
/*					)*/
				ORDER BY 
					LN10.BF_SSN
					,LN10.LF_DOE_SCL_ORG
					,LN10.LN_SEQ

				FOR READ ONLY WITH UR
			);

	CREATE TABLE SCHL_DEMO AS
		SELECT 
			*
		FROM 
			CONNECTION TO DB2 
			(
				SELECT 
					SC25.IF_DOE_SCL
					,SC25.IC_SCL_DPT 
					,SC25.IM_SCL_CT
					,SC25.IC_SCL_DOM_ST
				FROM 
					PKUB.SC25_SCH_DPT SC25

				FOR READ ONLY WITH UR
			);

	DISCONNECT FROM DB2;
QUIT;
PROC SORT DATA=SCHL_DEMO NODUPKEY; 
	BY IF_DOE_SCL 
	   IC_SCL_DPT;
RUN;

PROC TRANSPOSE DATA=GEN OUT=SCL_LOANS(DROP=_NAME_ _LABEL_) PREFIX=LOAN;
	VAR IC_LON_PGM;
	BY BF_SSN 
	   LF_DOE_SCL_ORG_PRE;
RUN;

PROC TRANSPOSE DATA=GEN OUT=LOAN_STA(DROP=_NAME_ _LABEL_) PREFIX=LNSTA;
	VAR WC_DW_LON_STA;
	BY BF_SSN 
	   LF_DOE_SCL_ORG_PRE;
RUN;
/*RSUBMIT;*/

/*CREATE BORROWER/SCHOOL LEVEL DATA SET*/
PROC SQL;
	CREATE TABLE BOR_SUM_DAT AS 
		SELECT DISTINCT  
			A.BF_SSN
			,CASE
				WHEN DYS_DLQ > 30 
				THEN 'DELINQUENT'
				ELSE 'CURRENT'
			 END AS BOR_STAT
			,A.DM_PRS_1
			,A.DM_PRS_LST
			,A.DX_STR_ADR_1
			,A.DX_STR_ADR_2
			,A.DM_CT
			,A.DC_DOM_ST
			,A.DF_ZIP_CDE
			,A.HPHN
			,A.DI_PHN_VLD_H
			,A.WPHN
			,A.DI_PHN_VLD_W
			,A.APHN
			,A.DI_PHN_VLD_A
			,A.MPHN
			,A.DI_PHN_VLD_M
			,A.DX_ADR_EML
			,A.DI_VLD_ADR_EML
			,C.LA_CUR_PRI
			,C.WA_TOT_BRI_OTS
			,C.LA_CUR_PRI + C.WA_TOT_BRI_OTS AS TOT_BAL
			,B.LOAN1
			,B.LOAN2
			,B.LOAN3
			,B.LOAN4
			,B.LOAN5
			,B.LOAN6
			,D.LNSTA1
			,D.LNSTA2
			,D.LNSTA3
			,D.LNSTA4
			,D.LNSTA5
			,D.LNSTA6
			/*,A.WC_DW_LON_STA */ /*LOAN LEVEL - NEED TO DECIDE HOW TO ROLL TO BORROWER LEVEL*/
			,A.DYS_DLQ
			,A.DLQ_DT
			,A.LD_NTF_SCL_SPR
			,A.LC_TYP_SCH_DIS /*POSSIBLE LOAN LEVEL TOO*/
			,A.LD_RPS_1_PAY_DU
			,C.LN_CT
			,C.WD_LON_RPD_SR
			,YEAR(INTNX('MONTH',C.WD_LON_RPD_SR,3)) AS CHRT_YR
		/*	,A.LF_DOE_SCL_ORG*/
			,A.IM_SCL_FUL
			,A.LF_DOE_SCL_ORG_PRE
			,A.LF_DOE_SCL_ORG_BNCH
			,E.IM_SCL_CT
			,E.IC_SCL_DOM_ST
		FROM 
			GEN A
			INNER JOIN SCL_LOANS B
				ON A.BF_SSN = B.BF_SSN
				AND A.LF_DOE_SCL_ORG_PRE = B.LF_DOE_SCL_ORG_PRE
			INNER JOIN LOAN_STA D
				ON A.BF_SSN = D.BF_SSN
				AND A.LF_DOE_SCL_ORG_PRE = D.LF_DOE_SCL_ORG_PRE
			INNER JOIN 
			(
				SELECT 
					BF_SSN
					,LF_DOE_SCL_ORG_PRE
					,SUM(LA_CUR_PRI) 		AS LA_CUR_PRI
					,SUM(WA_TOT_BRI_OTS) 	AS WA_TOT_BRI_OTS
					,MIN(WD_LON_RPD_SR) 	AS WD_LON_RPD_SR
					,COUNT(*) 				AS LN_CT
				FROM 
					GEN
				GROUP BY 
					BF_SSN
					,LF_DOE_SCL_ORG_PRE
			) C
				ON A.BF_SSN = C.BF_SSN
				AND A.LF_DOE_SCL_ORG_PRE = C.LF_DOE_SCL_ORG_PRE
			LEFT JOIN SCHL_DEMO E
				ON A.LF_DOE_SCL_ORG = E.IF_DOE_SCL
			/*GROUP BY*/ 
			/*	A.BF_SSN*/
			/*	,A.LF_DOE_SCL_ORG_PRE*/
	;
QUIT;

ENDRSUBMIT;

DATA BOR_SUM_DAT;
	SET LEGEND.BOR_SUM_DAT;
RUN;

/*PROC EXPORT DATA= BOR_SUM_DAT*/
/*            OUTFILE= "T:\SAS\RawDataSchoolsWithSchoolCityandST.xls" */
/*            DBMS=EXCEL REPLACE;*/
/*RUN;*/

/*proc sort data=BOR_SUM_DAT; by lf_doe_scl_org dm_prs_lst dm_prs_1; run;*/

/*data BOR_SUM_DAT2;*/ 
/*set BOR_SUM_DAT;*/
/*im_scl_ful = tranwrd(im_scl_ful, '/','-');*/
/*im_scl_ful = tranwrd(im_scl_ful, '\','-');*/
/*im_scl_ful = tranwrd(im_scl_ful, ',','-');*/
/*im_scl_ful = tranwrd(im_scl_ful, "'",' ');*/
/*run;*/

DATA BAKUP;
	SET BOR_SUM_DAT;
RUN;

PROC SQL;
	CREATE TABLE UNIQUE_SCHOOLS AS
		SELECT DISTINCT
			LF_DOE_SCL_ORG_PRE||LF_DOE_SCL_ORG_BNCH AS LF_DOE_SCL_ORG
		FROM
			BOR_SUM_DAT
	;
QUIT;

DATA SCHOOLS;
	SET UNIQUE_SCHOOLS;
	PK = _N_;
/*	TTLCT = _N_;*/
RUN;

PROC SQL NOPRINT;
	SELECT 
		COUNT(DISTINCT LF_DOE_SCL_ORG) 
		INTO : TTLCT
	FROM 
		SCHOOLS;
QUIT;

%LET CT = 1;
%PUT &TTLCT;

%MACRO OneRpt(CDE);
      LIBNAME EX EXCEL "Y:\Development\SAS Test Files\School Reports\&CDE - %SYSFUNC(TODAY(),MMDDYY6.).xlsx";

      DATA EX.SchoolReport(DBLABEL=YES);
            SET BOR_SUM_DAT;
            IF LF_DOE_SCL_ORG_PRE||LF_DOE_SCL_ORG_BNCH = "&CDE" 
			THEN OUTPUT EX.SchoolReport;
      RUN;

      LIBNAME EX CLEAR;
%MEND OneRpt;

%MACRO SchRep();
%DO I=&CT %TO &TTLCT;
	PROC SQL NOPRINT;
	    SELECT 
			LF_DOE_SCL_ORG 
			INTO : SchNm 
	    FROM 
			SCHOOLS
	    WHERE 
			PK = &CT;
	QUIT;
	%PUT &SchNm;
	%OneRpt(&SchNm);
	%LET CT = &CT + 1;
	%PUT &CT;
%END;
%MEND SchRep;

PROC PRINTTO LOG="Y:\Development\SAS Test Files\School Reports\schoolreports.log";
RUN;

%SchRep;

PROC PRINTTO;
RUN;

%WINDOW Message COLUMNS=60 ROWS=15 IROW=15 ICOLUMN=15
 #4 @ 5 'School Reports query has finished running.';
%DISPLAY Message;
%PUT NOTE: School Reports query has finished running.
