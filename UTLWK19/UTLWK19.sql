USE UDW
GO

DECLARE 
	@Now DATETIME = GETDATE(),
	@Today DATE = GETDATE(),
	@ScriptId VARCHAR(20) = 'UTLWK19',
	@ExlusionDate DATE = DATEADD(DAY, -45, GETDATE()),
	@QueueName VARCHAR(20) = 'KSKEMAIL',
	@Lender VARCHAR(20) = '828476'

INSERT INTO OLS.olqtskbldr.Queues
(
	TargetId,
	QueueName,
	InstitutionId,
	InstitutionType,
	DateDue,
	TimeDue,
	Comment,
	SourceFilename,
	AddedAt,
	AddedBy
)
SELECT DISTINCT
	SKIP_POP.DF_PRS_ID AS TargetId,
	@QueueName AS QueueName,
	'' AS InstitutionId,
	'' AS InstitutionType,
	NULL AS DateDue,
	NULL AS TimeDue,
	CONCAT('OneLINK Email: ', RTRIM(LTRIM(ISNULL(SKIP_POP.DX_ADR_EML, ''))), ', Skip type: ', SKIP_TYP.SKP) AS Comment,
	'' AS SourceFileName,
	@Now AS AddedAt,
	@ScriptId AS AddedBy
FROM
(
	SELECT DISTINCT 
		PD32.DF_PRS_ID AS DF_PRS_ID,
		DX_ADR_EML
	FROM	
		PD27_PRS_SKP_PRC PD27
		INNER JOIN PD32_PRS_ADR_EML PD32
			ON PD27.DF_PRS_ID = PD32.DF_PRS_ID
		INNER JOIN DW01_DW_CLC_CLU DW01
			ON PD27.DF_PRS_ID = DW01.BF_SSN 
			AND DW01.WC_DW_LON_STA NOT IN ('16', '17', '18', '19', '20','21', '22')
		INNER JOIN LN10_LON LN10
			ON DW01.BF_SSN = LN10.BF_SSN
			AND DW01.LN_SEQ = LN10.LN_SEQ
			AND LN10.LF_LON_CUR_OWN IN (@Lender)
	WHERE 	
		PD32.DI_VLD_ADR_EML = 'Y' 
		AND PD27.DC_STA_SKP = '2'
		AND PD32.DC_STA_PD32 = 'A'
) SKIP_POP
INNER JOIN
(
	SELECT DISTINCT
		PD30.DF_PRS_ID AS DF_PRS_ID,
		CASE 
			WHEN PD30.DI_VLD_ADR = 'N' AND PD42.DI_PHN_VLD = 'Y' THEN 'A' --Address skip
			WHEN PD30.DI_VLD_ADR = 'Y' AND PD42.DI_PHN_VLD = 'N' THEN 'P'
			WHEN PD30.DI_VLD_ADR = 'N' AND PD42.DI_PHN_VLD = 'N' THEN 'B'
		END AS SKP
	FROM 	
		PD30_PRS_ADR PD30
		INNER JOIN PD42_PRS_PHN PD42
			ON PD30.DF_PRS_ID = PD42.DF_PRS_ID
		INNER JOIN PD32_PRS_ADR_EML PD32
			ON PD42.DF_PRS_ID = PD32.DF_PRS_ID
		INNER JOIN DW01_DW_CLC_CLU DW01
			ON PD30.DF_PRS_ID = DW01.BF_SSN 
			AND DW01.WC_DW_LON_STA NOT IN ('16', '17', '18', '19', '20','21', '22')
		JOIN LN10_LON LN10
			ON DW01.BF_SSN = LN10.BF_SSN
			AND DW01.LN_SEQ = LN10.LN_SEQ
			AND LN10.LF_LON_CUR_OWN IN (@Lender)
	WHERE 	
		PD30.DC_ADR = 'L'
		AND 
		(
			PD30.DI_VLD_ADR = 'N'
			OR PD42.DI_PHN_VLD = 'N'
		)
		AND PD42.DC_PHN = 'H'
		AND PD32.DI_VLD_ADR_EML = 'Y'
)	SKIP_TYP
	ON SKIP_TYP.DF_PRS_ID = SKIP_POP.DF_PRS_ID
LEFT JOIN ODW..AY01_BR_ATY AY01
	ON AY01.DF_PRS_ID = SKIP_POP.DF_PRS_ID
	AND PF_ACT = 'KEMAL'
	AND BD_ATY_PRF >= @ExlusionDate --Within the last 45 days
LEFT JOIN ODW..CT30_CALL_QUE  CT30
	ON CT30.DF_PRS_ID_BR = SKIP_POP.DF_PRS_ID
	AND	IF_WRK_GRP = @QueueName
	AND CT30.IC_TSK_STA IN ('A','W')
LEFT JOIN OLS.olqtskbldr.Queues Q
	ON Q.TargetId = SKIP_POP.DF_PRS_ID
	AND Q.QueueName = @QueueName
	AND 
	(
		CAST(Q.AddedAt AS DATE) = @Today
		OR Q.ProcessedAt IS NULL
	)
	AND Q.DeletedAt IS NULL
	AND Q.DeletedBy IS NULL
WHERE
	--EXCLUSIONS
	AY01.DF_PRS_ID IS NULL
	AND CT30.DF_PRS_ID_BR IS NULL
	AND Q.TargetId IS NULL --Duplicate Exclusion for same day