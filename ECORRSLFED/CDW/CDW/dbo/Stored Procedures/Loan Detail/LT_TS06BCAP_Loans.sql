CREATE PROCEDURE [dbo].[LT_TS06BCAP_Loans]
	@AccountNumber CHAR(10),
	@IsCoborrower BIT = 0
AS
BEGIN
	
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

DECLARE	@LetterId VARCHAR(10) = 'TS06BCAP'
DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

IF @IsCoborrower = 0
	BEGIN
		SELECT
			COALESCE(FMT.Label, LN10.IC_LON_PGM) AS [Loan Program],
			CONVERT(VARCHAR(10), LN10.LD_LON_1_DSB, 101) AS [First Disbursement Date],
			'$' + CAST(LN90.LA_FAT_CUR_PRI AS VARCHAR) AS [Amount Capitalized],
			'$' + CAST(LN10.LA_CUR_PRI AS VARCHAR) AS [Current Principal Balance]
		FROM
			PD10_PRS_NME PD10
			INNER JOIN LN10_LON LN10
				ON LN10.BF_SSN = PD10.DF_PRS_ID
			INNER JOIN 
			( -- GETS THE MOST RECENT CAPPED INTEREST FINANCIAL TRANSACTION
				SELECT
					LN90.BF_SSN,
					LN90.LN_SEQ,
					SUM(LN90.LA_FAT_CUR_PRI) AS LA_FAT_CUR_PRI,
					ROW_NUMBER() OVER (PARTITION BY LN90.BF_SSN, LN90.LN_SEQ ORDER BY LN90.LD_FAT_EFF DESC) AS SEQ
				FROM
					LN90_FIN_ATY LN90
					INNER JOIN PD10_PRS_NME PD10
						ON PD10.DF_PRS_ID = LN90.BF_SSN
				WHERE
					LN90.PC_FAT_TYP = '70' 
					AND LN90.PC_FAT_SUB_TYP = '01'
					AND LN90.LC_STA_LON90 = 'A'
					AND COALESCE(LN90.LC_FAT_REV_REA,'') = '' --NO REVERSAL REASON
					AND PD10.DF_SPE_ACC_ID = @AccountNumber
				GROUP BY
					LN90.BF_SSN,
					LN90.LN_SEQ,
					LN90.LD_FAT_EFF
			) LN90
				ON LN10.BF_SSN = LN90.BF_SSN
				AND LN10.LN_SEQ = LN90.LN_SEQ
				AND LN90.SEQ = 1
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN --GETS THE MOST RECENT ARC LEFT ON THE BORROWERS ACCOUNT TO GET THE LOAN SEQ'S THE LETTER APPLIES TO
					(
						SELECT
							AY10.BF_SSN,
							MAX(AY10.LN_ATY_SEQ) AS LN_ATY_SEQ,
							MAX(AY10.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
						FROM 
							AY10_BR_LON_ATY AY10
							INNER JOIN PD10_PRS_NME PD10
								ON PD10.DF_PRS_ID = AY10.BF_SSN
						WHERE
							AY10.PF_REQ_ACT = @PF_REQ_ACT
							AND PD10.DF_SPE_ACC_ID = @AccountNumber
						GROUP BY
							AY10.BF_SSN
					)AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ
			LEFT JOIN FormatTranslation FMT
				ON LN10.IC_LON_PGM = FMT.Start
				AND FMT.FmtName = '$LNPROG'
		WHERE 
			PD10.DF_SPE_ACC_ID = @AccountNumber
	END
ELSE
	BEGIN
		SELECT
			COALESCE(FMT.Label, LN10.IC_LON_PGM) AS [Loan Program],
			CONVERT(VARCHAR(10), LN10.LD_LON_1_DSB, 101) AS [First Disbursement Date],
			'$' + CAST(LN90.LA_FAT_CUR_PRI AS VARCHAR) AS [Amount Capitalized],
			'$' + CAST(LN10.LA_CUR_PRI AS VARCHAR) AS [Current Principal Balance]
		FROM
			PD10_PRS_NME PD10
			INNER JOIN LN20_EDS LN20
				ON LN20.LF_EDS = PD10.DF_PRS_ID
				AND LN20.LC_STA_LON20 = 'A'
				AND LN20.LC_EDS_TYP = 'M' --Coborrower
			INNER JOIN LN10_LON LN10
				ON LN10.BF_SSN = LN20.BF_SSN
				AND LN10.LN_SEQ = LN20.LN_SEQ
			INNER JOIN 
			( -- GETS THE MOST RECENT CAPPED INTEREST FINANCIAL TRANSACTION
				SELECT
					LN90.BF_SSN,
					LN90.LN_SEQ,
					SUM(LN90.LA_FAT_CUR_PRI) AS LA_FAT_CUR_PRI,
					ROW_NUMBER() OVER (PARTITION BY LN90.BF_SSN, LN90.LN_SEQ ORDER BY LN90.LD_FAT_EFF DESC) AS SEQ
				FROM
					LN90_FIN_ATY LN90
					INNER JOIN LN20_EDS LN20
						ON LN20.BF_SSN = LN90.BF_SSN
						AND LN20.LN_SEQ = LN90.LN_SEQ
						AND LN20.LC_EDS_TYP = 'M'
						AND LN20.LC_STA_LON20 = 'A'
					INNER JOIN PD10_PRS_NME PD10
						ON PD10.DF_PRS_ID = LN20.LF_EDS
				WHERE
					LN90.PC_FAT_TYP = '70' 
					AND LN90.PC_FAT_SUB_TYP = '01'
					AND LN90.LC_STA_LON90 = 'A'
					AND COALESCE(LN90.LC_FAT_REV_REA,'') = '' --NO REVERSAL REASON
					AND PD10.DF_SPE_ACC_ID = @AccountNumber
				GROUP BY
					LN90.BF_SSN,
					LN90.LN_SEQ,
					LN90.LD_FAT_EFF
			) LN90
				ON LN10.BF_SSN = LN90.BF_SSN
				AND LN10.LN_SEQ = LN90.LN_SEQ
				AND LN90.SEQ = 1
			INNER JOIN
			(
				SELECT
					LN85.BF_SSN,
					LN85.LN_SEQ
				FROM
					LN85_LON_ATY LN85
					INNER JOIN --GETS THE MOST RECENT ARC LEFT ON THE BORROWERS ACCOUNT TO GET THE LOAN SEQ'S THE LETTER APPLIES TO
					(
						SELECT
							AY10.BF_SSN,
							MAX(AY10.LN_ATY_SEQ) AS LN_ATY_SEQ,
							MAX(AY10.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
						FROM 
							AY10_BR_LON_ATY AY10
							INNER JOIN LN20_EDS LN20
								ON LN20.BF_SSN = AY10.BF_SSN
								AND LN20.LC_EDS_TYP = 'M'
								AND LN20.LC_STA_LON20 = 'A'
							INNER JOIN PD10_PRS_NME PD10
								ON PD10.DF_PRS_ID = LN20.LF_EDS
						WHERE
							AY10.PF_REQ_ACT = @PF_REQ_ACT
							AND PD10.DF_SPE_ACC_ID = @AccountNumber
						GROUP BY
							AY10.BF_SSN
					)AY10
						ON AY10.BF_SSN = LN85.BF_SSN
						AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
			)LN85
				ON LN85.BF_SSN = LN10.BF_SSN
				AND LN85.LN_SEQ = LN10.LN_SEQ
			LEFT JOIN FormatTranslation FMT
				ON LN10.IC_LON_PGM = FMT.Start
				AND FMT.FmtName = '$LNPROG'
		WHERE 
			PD10.DF_SPE_ACC_ID = @AccountNumber
			AND LN10.LA_CUR_PRI > 0
			AND LN10.LC_STA_LON10 = 'R'
	END
END
