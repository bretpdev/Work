/*UTLWD36 - Endorser/Comaker 30 Day Notice*/
/*-----Production settings-----*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/

/*-----Development settings-----*/
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
%LET RPTLIB = T:\SAS;

FILENAME REPORTZ "&RPTLIB/ULWD36.LWD36RZ";
FILENAME REPORT2 "&RPTLIB/ULWD36.LWD36R2";

RSUBMIT;

%macro sqlcheck ;
  %if  &sqlxrc ne 0  %then  %do  ;
    data _null_  ;
            file reportz notitles  ;
            put @01 " ********************************************************************* "
              / @01 " ****  The SQL code above has experienced an error.               **** "
              / @01 " ****  The SAS should be reviewed.                                **** "       
              / @01 " ********************************************************************* "
              / @01 " ****  The SQL error code is  &sqlxrc  and the SQL error message  **** "
              / @01 " ****  &sqlxmsg   **** "
              / @01 " ********************************************************************* "
            ;
         run  ;
  %end  ;
%mend  ;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE QUERY AS
SELECT
	DF_PRS_ID
	,DF_SPE_ACC_ID
	,ENDORSER_FIRST_NAME
	,ENDORSER_LAST_NAME
	,DX_STR_ADR_1
	,DX_STR_ADR_2
	,DM_CT
	,DC_DOM_ST
	,DF_ZIP
	,BORROWER_FIRST_NAME
	,BORROWER_LAST_NAME
	,SUM(
		LA_CLM_PRI
		+LA_CLM_INT
		-LA_PRI_COL
		+LA_INT_ACR
		+LA_CLM_INT_ACR
		-LA_INT_COL
	) AS LOAN_TOTAL
	,SUM(
		LA_LEG_CST_ACR
		+LA_LEG_CST_COL
		+LA_OTH_CHR_ACR
		-LA_OTH_CHR_COL
		+LA_COL_CST_ACR
		-LA_COL_CST_COL
		+LA_CLM_PRJ_COL_CST
	) AS OTHER_TOTAL
FROM CONNECTION TO DB2 (
SELECT DISTINCT
	PD01E.DF_PRS_ID
	,PD01E.DF_SPE_ACC_ID
	,PD01E.DM_PRS_1		AS ENDORSER_FIRST_NAME
	,PD01E.DM_PRS_LST	AS ENDORSER_LAST_NAME
	,PD01E.DX_STR_ADR_1
	,PD01E.DX_STR_ADR_2
	,PD01E.DM_CT
	,PD01E.DC_DOM_ST
	,PD01E.DF_ZIP
	,PD01B.DM_PRS_1		AS BORROWER_FIRST_NAME
	,PD01B.DM_PRS_LST	AS BORROWER_LAST_NAME
	,DC01.LA_CLM_PRI
	,DC01.LA_CLM_INT
	,DC01.LA_PRI_COL
	,DC01.LA_INT_ACR
	,DC02.LA_CLM_INT_ACR
	,DC01.LA_INT_COL
	,DC01.LA_LEG_CST_ACR
	,DC01.LA_LEG_CST_COL
	,DC01.LA_OTH_CHR_ACR
	,DC01.LA_OTH_CHR_COL
	,DC01.LA_COL_CST_ACR
	,DC01.LA_COL_CST_COL
	,DC02.LA_CLM_PRJ_COL_CST
FROM	OLWHRM1.PD01_PDM_INF PD01E
	INNER JOIN OLWHRM1.GA01_APP	GA01
		ON PD01E.DF_PRS_ID = GA01.DF_PRS_ID_EDS
	INNER JOIN OLWHRM1.PD01_PDM_INF PD01B
		ON GA01.DF_PRS_ID_BR = PD01B.DF_PRS_ID
	INNER JOIN OLWHRM1.DC12_EDS_CMK_ATY DC12
		ON GA01.AF_APL_ID = DC12.AF_APL_ID
	INNER JOIN OLWHRM1.DC01_LON_CLM_INF DC01
		ON DC12.AF_APL_ID = DC01.AF_APL_ID
		AND DC12.AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	INNER JOIN OLWHRM1.DC02_BAL_INT DC02
		ON DC01.AF_APL_ID = DC02.AF_APL_ID
		AND DC01.AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
	INNER JOIN OLWHRM1.BR02_BR_EMP BR02
		ON PD01E.DF_PRS_ID = BR02.BF_SSN
WHERE	DC01.LC_STA_DC10 = '03'
	AND	(DC01.LC_REA_CLM_ASN_DOE IS NULL OR DC01.LC_REA_CLM_ASN_DOE = '')
	AND	DC01.LC_AUX_STA = ''
	AND	DC12.LC_STA_DC12 IN ('01', '06')
	AND	DC12.LC_EDS_CMK_GRN = ''
	AND	PD01E.DC_ADR = 'L'
	AND	PD01E.DI_VLD_ADR = 'Y'
	AND	DC02.LA_CLM_BAL >= 100
	AND	BR02.BI_EMP_INF_OVR = 'Y'
	AND	PD01E.DM_FGN_CNY = ''
	AND DC01.LF_CRT_DTS_DC10 = (
		SELECT MAX(LF_CRT_DTS_DC10)
		FROM OLWHRM1.DC01_LON_CLM_INF
		WHERE AF_APL_ID = DC01.AF_APL_ID
		AND AF_APL_ID_SFX = DC01.AF_APL_ID_SFX
	)
	AND DC02.LF_CRT_DTS_DC10 = (
		SELECT MAX(LF_CRT_DTS_DC10)
		FROM OLWHRM1.DC02_BAL_INT
		WHERE AF_APL_ID = DC02.AF_APL_ID
		AND AF_APL_ID_SFX = DC02.AF_APL_ID_SFX
	)
	AND NOT EXISTS (
		SELECT	*
		FROM	OLWHRM1.DC11_LON_FAT DC11
		WHERE	DC11.BF_SSN = PD01B.DF_PRS_ID
			AND	DC11.LC_TRX_TYP IN ('BR', 'CS', 'EP')
			AND DAYS(CURRENT DATE) - DAYS(DC11.LD_TRX_EFF) <= 60
	)
	AND NOT EXISTS (
		SELECT 	*
		FROM OLWHRM1.AY01_BR_ATY AY01
		WHERE AY01.DF_PRS_ID = PD01B.DF_PRS_ID
		AND AY01.PF_ACT = 'DL30E'
	)
FOR READ ONLY WITH UR
)
GROUP BY
	DF_PRS_ID
	,DF_SPE_ACC_ID
	,ENDORSER_FIRST_NAME
	,ENDORSER_LAST_NAME
	,DX_STR_ADR_1
	,DX_STR_ADR_2
	,DM_CT
	,DC_DOM_ST
	,DF_ZIP
	,BORROWER_FIRST_NAME
	,BORROWER_LAST_NAME
;
DISCONNECT FROM DB2;

/*-----Production settings-----*/
/*%put  sqlxrc= >>> &sqlxrc <<< ||| sqlxmsg= >>> &sqlxmsg >>> ;  ** includes error messages to SAS log  ;*/
/*%sqlcheck;*/
/*quit;*/

/*-----Development settings-----*/
ENDRSUBMIT;
DATA QUERY; SET WORKLOCL.QUERY; RUN;

DATA QUERY; SET QUERY;
BALANCE = LOAN_TOTAL + OTHER_TOTAL;
COST_CENTER_CODE = 'MA2329';
RUN;

*CALCULATE KEYLINE;
%MACRO KEY_CLC(TBL);
DATA &TBL (DROP = KEYSSN MODAY KEYLINE CHKDIG DIG I 
	CHKDIG CHK1 CHK2 CHK3 CHKDIGIT CHECK);
SET &TBL;
KEYSSN = TRANSLATE(DF_PRS_ID,'MYLAUGHTER','0987654321');
MODAY = PUT(DATE(),MMDDYYN4.);
KEYLINE = "P"||KEYSSN||MODAY||'L';
CHKDIG = 0;
LENGTH DIG $2.;
DO I = 1 TO LENGTH(KEYLINE);
	IF I/2 NE ROUND(I/2,1) 
		THEN DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4) * 2, 2.);
	ELSE DIG = PUT(INPUT(SUBSTR(KEYLINE,I,1),BITS4.4), 2.);
	IF SUBSTR(DIG,1,1) = " " 
		THEN CHKDIG = CHKDIG + INPUT(SUBSTR(DIG,2,1),1.);
		ELSE DO;
			CHK1 = INPUT(SUBSTR(DIG,1,1),1.);
			CHK2 = INPUT(SUBSTR(DIG,2,1),1.);
			IF CHK1 + CHK2 >= 10
				THEN DO;
					CHK3 = PUT(CHK1 + CHK2,2.);
					CHK1 = INPUT(SUBSTR(CHK3,1,1),1.);
					CHK2 = INPUT(SUBSTR(CHK3,2,1),1.);
				END;
			CHKDIG = CHKDIG + CHK1 + CHK2;
		END;
END;
CHKDIGIT = 10 - INPUT(SUBSTR((RIGHT(PUT(CHKDIG,3.))),3,1),3.);
IF CHKDIGIT = 10 THEN CHKDIGIT = 0;
CHECK = PUT(CHKDIGIT,1.);
ACSKEY = "#"||KEYLINE||CHECK||"#";
RUN;
%MEND KEY_CLC;
%KEY_CLC(QUERY);

PROC SORT DATA=QUERY;
BY DC_DOM_ST DF_SPE_ACC_ID;
RUN;

DATA _NULL_;
SET QUERY;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT DF_SPE_ACC_ID $10. BALANCE 10.2;
IF _N_ = 1 THEN DO;
	PUT
		'AccountNumber'
		',FirstName'
		',LastName'
		',Address1'
		',Address2'
		',City'
		',State'
		',Zip'
		',KeyLine'
		',Balance'
		',BorrowerFirstName'
		',BorrowerLaseName'
		',STATE_IND'
		',COST_CENTER_CODE'
	;
END;
DO;
	PUT DF_SPE_ACC_ID $ @;
	PUT ENDORSER_FIRST_NAME $ @;
	PUT ENDORSER_LAST_NAME $ @;
	PUT DX_STR_ADR_1 @;
	PUT DX_STR_ADR_2 @;
	PUT DM_CT @;
	PUT DC_DOM_ST @;
	PUT DF_ZIP @;
	PUT ACSKEY @;
	PUT BALANCE @;
	PUT BORROWER_FIRST_NAME @;
	PUT BORROWER_LAST_NAME @;
	PUT DC_DOM_ST @;
	PUT COST_CENTER_CODE ;
END;
RUN;

