*-----------------------------------------------------*
| UTLWR01 - PLUS INTEREST CREDIT ADJ FOR PMT REVERSAL |
*-----------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR01.LWR01R2";
FILENAME REPORTZ "&RPTLIB/ULWR01.LWR01RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/***********************************
* PLUS LOAN SELECTION
************************************/
CREATE TABLE PLUS_1029 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,A.LN_SEQ
	,A.LN_FAT_SEQ
	,A.LD_FAT_EFF 
	,ABS(A.LA_FAT_CUR_PRI) AS LA_FAT_NSI
	,AY.LD_ATY_REQ_RCV 
	,AY.LX_ATY
	,C.WD_LON_RPD_SR AS RP_BEG
	,D.DF_SPE_ACC_ID 
	,D.DM_PRS_LST
	,D.DM_PRS_1
	,'' AS NON_PLUS_TYP
	,E.IC_LON_PGM
	,'1' AS SCN
FROM OLWHRM1.LN90_FIN_ATY A
INNER JOIN OLWHRM1.DW01_DW_CLC_CLU C
	ON A.BF_SSN = C.BF_SSN
	AND A.LN_SEQ = C.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME D
	ON A.BF_SSN = D.DF_PRS_ID
INNER JOIN OLWHRM1.LN10_LON E
	ON A.BF_SSN = E.BF_SSN
	AND A.LN_SEQ = E.LN_SEQ
LEFT OUTER JOIN (
	SELECT X.BF_SSN
		,Y.LN_SEQ
		,X.LD_ATY_REQ_RCV
		,W.LX_ATY
	FROM OLWHRM1.AY10_BR_LON_ATY X
	INNER JOIN OLWHRM1.LN85_LON_ATY Y
		ON X.BF_SSN = Y.BF_SSN
		AND X.LN_ATY_SEQ = Y.LN_ATY_SEQ 
	INNER JOIN OLWHRM1.AY15_ATY_CMT Z
		ON X.BF_SSN = Z.BF_SSN
		AND X.LN_ATY_SEQ = Z.LN_ATY_SEQ
	INNER JOIN OLWHRM1.AY20_ATY_TXT W
		ON Z.BF_SSN = W.BF_SSN
		AND Z.LN_ATY_SEQ = W.LN_ATY_SEQ
		AND Z.LN_ATY_CMT_SEQ = W.LN_ATY_CMT_SEQ
	WHERE X.PF_REQ_ACT IN 'R1029'
		AND X.LC_STA_ACTY10 = 'A'
		AND Z.LC_STA_AY15 = 'A'
	) AY
	ON A.BF_SSN = AY.BF_SSN
	AND A.LN_SEQ = AY.LN_SEQ
WHERE A.PC_FAT_TYP = '10'
AND A.PC_FAT_SUB_TYP = '29'
AND A.LC_FAT_REV_REA = ''
AND A.LC_STA_LON90 = 'A'
AND E.IC_LON_PGM = 'PLUS'
FOR READ ONLY WITH UR
);
/***********************************
* NON PLUS LOAN SELECTION
************************************/
CREATE TABLE NPLUS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT NON_PLUS.BF_SSN 
	,NON_PLUS_LN90.LD_FAT_EFF
	,NON_PLUS_LN90.LN_FAT_SEQ 
	,ABS(NON_PLUS_LN90.LA_FAT_CUR_PRI) AS LA_FAT_NSI
	,NON_PLUS.IC_LON_PGM AS NON_PLUS_TYP
FROM OLWHRM1.LN10_LON NON_PLUS
INNER JOIN OLWHRM1.LN90_FIN_ATY NON_PLUS_LN90
	ON NON_PLUS.BF_SSN = NON_PLUS_LN90.BF_SSN
	AND NON_PLUS.LN_SEQ = NON_PLUS_LN90.LN_SEQ
WHERE NON_PLUS.IC_LON_PGM != 'PLUS'
AND NON_PLUS_LN90.PC_FAT_TYP = '10'
AND NON_PLUS_LN90.PC_FAT_SUB_TYP = '29'
AND NON_PLUS_LN90.LC_FAT_REV_REA = ''
AND NON_PLUS_LN90.LC_STA_LON90 = 'A'
FOR READ ONLY WITH UR
);
/*************************************
* NON PLUS LOAN SELECTION - CONTINUED
**************************************/
CREATE TABLE NPLUS2 AS
SELECT DISTINCT A.BF_SSN 
	,B.LN_SEQ
	,A.LN_FAT_SEQ
	,A.LD_FAT_EFF 
	,A.LA_FAT_NSI
	,B.LD_ATY_REQ_RCV
	,B.LX_ATY
	,B.RP_BEG
	,B.DF_SPE_ACC_ID 
	,B.DM_PRS_LST
	,B.DM_PRS_1
	,A.NON_PLUS_TYP
	,B.IC_LON_PGM
	,B.SCN
FROM NPLUS A
INNER JOIN CONNECTION TO DB2 (
	SELECT PARC2.BF_SSN
		,PARC2.LN_SEQ
		,PARC2.LD_ATY_REQ_RCV
		,AY20.LX_ATY
		,DW01.WD_LON_RPD_SR AS RP_BEG
		,PD10.DF_SPE_ACC_ID 
		,PD10.DM_PRS_LST
		,PD10.DM_PRS_1
		,LN10.IC_LON_PGM
		,'2' AS SCN
	FROM (
			SELECT DISTINCT AY10.BF_SSN
				,LN85.LN_SEQ
				,CASE 
					WHEN PARC.LN_ATY_SEQ IS NULL THEN AY10.LN_ATY_SEQ
					ELSE PARC.LN_ATY_SEQ
				 END AS LN_ATY_SEQ
				,AY10.LD_ATY_REQ_RCV
			FROM OLWHRM1.AY10_BR_LON_ATY AY10
			INNER JOIN OLWHRM1.LN85_LON_ATY LN85
				ON LN85.BF_SSN = AY10.BF_SSN
				AND LN85.LN_ATY_SEQ = AY10.LN_ATY_SEQ
			LEFT OUTER JOIN (
				SELECT LJ1.BF_SSN
					,LJ2.LN_SEQ
					,LJ1.LN_ATY_SEQ
					,LJ1.LD_ATY_REQ_RCV
				FROM OLWHRM1.AY10_BR_LON_ATY LJ1
				INNER JOIN OLWHRM1.LN85_LON_ATY LJ2
					ON LJ1.BF_SSN = LJ2.BF_SSN
					AND LJ1.LN_ATY_SEQ = LJ2.LN_ATY_SEQ 
				WHERE LJ1.PF_REQ_ACT IN 'R1029'
					AND LJ1.LC_STA_ACTY10 = 'A'
				) PARC
				ON LN85.BF_SSN = PARC.BF_SSN
				AND LN85.LN_SEQ = PARC.LN_SEQ
			WHERE AY10.PF_REQ_ACT IN 'U1029'
			ORDER BY AY10.BF_SSN
		) PARC2
	INNER JOIN OLWHRM1.AY15_ATY_CMT AY15
		ON PARC2.BF_SSN = AY15.BF_SSN
		AND PARC2.LN_ATY_SEQ = AY15.LN_ATY_SEQ
		and AY15.LC_STA_AY15 = 'A'
	INNER JOIN OLWHRM1.AY20_ATY_TXT AY20
		ON AY15.BF_SSN = AY20.BF_SSN
		AND AY15.LN_ATY_SEQ = AY20.LN_ATY_SEQ
		AND AY15.LN_ATY_CMT_SEQ = AY20.LN_ATY_CMT_SEQ
	INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
		ON PARC2.BF_SSN = DW01.BF_SSN
		AND PARC2.LN_SEQ = DW01.LN_SEQ
	INNER JOIN OLWHRM1.PD10_PRS_NME PD10
		ON DW01.BF_SSN = PD10.DF_PRS_ID
	INNER JOIN OLWHRM1.LN10_LON LN10
		ON PARC2.BF_SSN = LN10.BF_SSN
		AND PARC2.LN_SEQ = LN10.LN_SEQ
	INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
		ON LN10.BF_SSN = LN90.BF_SSN
		AND LN10.LN_SEQ = LN90.LN_SEQ
	WHERE LN10.IC_LON_PGM = 'PLUS'
	AND LN90.PC_FAT_TYP = '10'
	AND LN90.PC_FAT_SUB_TYP IN (
		'10','11','70','80'
		)
	AND LN90.LC_FAT_REV_REA = ''
	AND LN90.LC_STA_LON90 = 'A'	
	AND AY15.LC_STA_AY15 = 'A'
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN
	AND A.LD_FAT_EFF = B.LD_ATY_REQ_RCV
;
/*****************************************
* COMBINE THE PLUS AND NON PLUS LOAN DATA
******************************************/
CREATE TABLE RS1 AS 
	SELECT DISTINCT A.BF_SSN 
		,A.LN_SEQ
		,A.LN_FAT_SEQ
		,A.LD_FAT_EFF 
		,A.LA_FAT_NSI
		,A.LD_ATY_REQ_RCV
		,A.LX_ATY
		,A.DF_SPE_ACC_ID 
		,A.DM_PRS_LST
		,A.DM_PRS_1
		,A.NON_PLUS_TYP
		,A.IC_LON_PGM
		,A.SCN
		,A.RP_BEG
		,INTNX('YEAR',A.RP_BEG,+1,'S')-1 AS RP_END
	FROM PLUS_1029 A
UNION 
	SELECT DISTINCT B.BF_SSN 
		,B.LN_SEQ
		,B.LN_FAT_SEQ
		,B.LD_FAT_EFF 
		,B.LA_FAT_NSI
		,B.LD_ATY_REQ_RCV
		,B.LX_ATY
		,B.DF_SPE_ACC_ID 
		,B.DM_PRS_LST
		,B.DM_PRS_1
		,B.NON_PLUS_TYP
		,B.IC_LON_PGM
		,B.SCN
		,B.RP_BEG
		,INTNX('YEAR',B.RP_BEG,+1,'S')-1 AS RP_END
	FROM NPLUS2 B
;
/*********************************************************
* GET FINANCIAL DATA FOR THE SELECTED LOANS AND CALCULATE 
* THE PLUS INTEREST CREDIT AMOUNT FOR COMPARISON
***********************************************************/
CREATE TABLE RS2 AS
SELECT DISTINCT RS1.BF_SSN
	,RS1.LN_SEQ
	,RS1.DF_SPE_ACC_ID 
	,RS1.DM_PRS_LST
	,RS1.DM_PRS_1
	,RS1.LD_FAT_EFF
	,RS1.LA_FAT_NSI
	,RS1.IC_LON_PGM
	,RS1.LX_ATY
	,RS1.LD_ATY_REQ_RCV
	,SUM(FIN_ACT.INT_AMT) AS TOT_INT
FROM RS1 
INNER JOIN CONNECTION TO DB2 (
	SELECT BF_SSN
		,LN_SEQ
		,LD_FAT_EFF
		,PC_FAT_TYP||PC_FAT_SUB_TYP AS TRX_TYP
		,ABS(LA_FAT_NSI) AS INT_AMT
	FROM OLWHRM1.LN90_FIN_ATY A
	WHERE PC_FAT_TYP = '10'
	AND PC_FAT_SUB_TYP IN ('10','11','70','80')
	AND LC_FAT_REV_REA = ''
	AND LC_STA_LON90 = 'A'
	FOR READ ONLY WITH UR
	) FIN_ACT
	ON RS1.BF_SSN = FIN_ACT.BF_SSN
	AND RS1.LN_SEQ = FIN_ACT.LN_SEQ
WHERE FIN_ACT.LD_FAT_EFF BETWEEN RS1.RP_BEG AND RS1.RP_END
GROUP BY RS1.BF_SSN
	,RS1.LN_SEQ
	,RS1.DF_SPE_ACC_ID 
	,RS1.DM_PRS_LST
	,RS1.DM_PRS_1
	,RS1.LD_FAT_EFF
	,RS1.LA_FAT_NSI
	,RS1.IC_LON_PGM
	,RS1.LX_ATY
	,RS1.LD_ATY_REQ_RCV
;

CREATE TABLE ACTX AS
SELECT DISTINCT BF_SSN 
FROM CONNECTION TO DB2 (
SELECT AY10.BF_SSN
	,AY20.LX_ATY
FROM OLWHRM1.AY10_BR_LON_ATY AY10
INNER JOIN OLWHRM1.AY15_ATY_CMT AY15
	ON AY10.BF_SSN = AY15.BF_SSN
	AND AY10.LN_ATY_SEQ = AY15.LN_ATY_SEQ
INNER JOIN OLWHRM1.AY20_ATY_TXT AY20
	ON AY15.BF_SSN = AY20.BF_SSN
	AND AY15.LN_ATY_SEQ = AY20.LN_ATY_SEQ
	AND AY15.LN_ATY_CMT_SEQ = AY20.LN_ATY_CMT_SEQ
WHERE AY10.PF_REQ_ACT = 'U1029'
AND SUBSTR(AY20.LX_ATY,40,6) = 'NELNET'
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR01.LWR01RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA RS2;SET WORKLOCL.RS2;RUN;
DATA ACTX;SET WORKLOCL.ACTX;RUN;
PROC SORT DATA=RS2;BY BF_SSN;RUN;
PROC SORT DATA=ACTX;BY BF_SSN;RUN;
DATA RS2;
MERGE RS2 (IN=A) ACTX (IN=B);
BY BF_SSN;
IF A AND NOT B;
RUN;
DATA RS2;
SET RS2;
IF LX_ATY NE '' AND SUBSTR(LX_ATY,1,1) = '$' 
	THEN LA_FAT_NSI = INPUT(SUBSTR(LX_ATY,2,INDEX(LX_ATY,'.')+1),DOLLAR10.2);
ELSE LA_FAT_NSI = LA_FAT_NSI;
RUN;
PROC SORT DATA=RS2;BY BF_SSN LN_SEQ LD_ATY_REQ_RCV;RUN;
DATA RS2;
SET RS2;
BY BF_SSN LN_SEQ LD_ATY_REQ_RCV;
IF LAST.LN_SEQ THEN OUTPUT;
RUN;

DATA RS2;
SET RS2;
WHERE ROUND(LA_FAT_NSI,.01) ^= ROUND(TOT_INT,.01)
		AND (ABS(ROUND(LA_FAT_NSI,.01) - ROUND(TOT_INT,.01))) > .99;
AMT_DIF = ABS(ROUND(LA_FAT_NSI,.01) - ROUND(TOT_INT,.01));
RUN;

PROC SORT DATA=RS2 NODUPKEY; 
BY DF_SPE_ACC_ID 
	LN_SEQ
	DM_PRS_LST
	DM_PRS_1
	TOT_INT
	LD_FAT_EFF
	LA_FAT_NSI
	AMT_DIF; 
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 DATE PAGENO=1 CENTER;
TITLE 'PLUS INTEREST CREDIT PAYMENT REVERSAL';
FOOTNOTE 'JOB = UTLWR01  	 REPORT = ULWR01.LWR01R2';
PROC CONTENTS DATA=RS2 OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      ////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWR01  	 REPORT = ULWR01.LWR01R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=RS2 WIDTH=UNIFORM WIDTH=MIN;
FORMAT TOT_INT LA_FAT_NSI AMT_DIF DOLLAR10.2 LD_FAT_EFF MMDDYY10.;
VAR DF_SPE_ACC_ID 
	LN_SEQ
	DM_PRS_LST
	DM_PRS_1
	TOT_INT
	LD_FAT_EFF
	LA_FAT_NSI
	AMT_DIF;
LABEL DF_SPE_ACC_ID = 'ACCT #'
	LN_SEQ = 'LOAN SEQ'
	DM_PRS_LST = 'LAST NAME'
	DM_PRS_1 = 'FIRST NAME'
	TOT_INT = 'TOTAL INTEREST AMOUNT'
	LD_FAT_EFF = 'EFFECTIVE DATE OF 1029'
	LA_FAT_NSI = 'CURRENT 1029 AMOUNT'
	AMT_DIF = 'AMOUNT DIFFERENCE';
RUN;
PROC PRINTTO;
RUN;

/******************************************************************
* USED IN TESTING
*******************************************************************/
/*PROC SQL;*/
/*CREATE TABLE X AS */
/*SELECT BF_SSN*/
/*	,LN_SEQ*/
/*	,COUNT(*) AS COUNT*/
/*FROM RS2B*/
/*GROUP BY BF_SSN*/
/*	,LN_SEQ*/
/*HAVING COUNT(*) > 1;*/
/**/
/*CREATE TABLE DUPS AS*/
/*SELECT A.**/
/*FROM RS2B A*/
/*INNER JOIN X */
/*	ON A.BF_SSN = X.BF_SSN*/
/*	AND A.LN_SEQ = X.LN_SEQ*/
/*ORDER BY A.BF_SSN*/
/*	,A.LN_SEQ;*/
/*QUIT;*/
/*PROC EXPORT DATA=DUPS*/
/*            OUTFILE= "T:\SAS\SASR 1598 DUPS.xls" */
/*            DBMS=EXCEL2000 REPLACE;*/
/*RUN;*/
