/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/UNWR04.NWR04R2";
FILENAME REPORTZ "&RPTLIB/UNWR04.NWR04RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=LEGEND  SLIBREF=WORK;
DATA _NULL_;
	CALL SYMPUTX('DT_RNG_BEG',"'"||PUT(INTNX('DAY',TODAY(),-45,'beginning'),MMDDYY10.)||"'");
	CALL SYMPUTX('DT_RNG_END',"'"||PUT(INTNX('DAY',TODAY(),-1,'beginning'),MMDDYY10.)||"'");
	CALL SYMPUTX('RUN_DT',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;
%SYSLPUT DT_RNG_BEG = &DT_RNG_BEG;
%SYSLPUT DT_RNG_END = &DT_RNG_END;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DNFPUTDL) ;
CREATE TABLE RARCTF AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT	ZC12.WF_FMS_BCH
	,ZC12.WF_FMS_BCH
	,ZC12.WF_RFD_ID
	,CASE 
		WHEN ZC12.WC_RFD_TRX_TYP = 'REFUND' THEN ZC12.WF_RFD_ID 
		ELSE ''
	 END AS REF_FIL_ID

	,ZC14.WN_TRY_CNF_CHK AS CHK_NUM
	,ZC14.WF_TRY_CNF_SCH_NUM AS SCH_NUM
	,ZC14.WD_TRY_CNF AS TSRY_CNF_DT
	,ZC12.WA_RFD_CHK_AMT AS PMT_AMT 
	,ZC12.WM_RFD_PYE AS NAME
	,ZC12.BF_SSN
	,ZC12.WM_RFD_PYE AS PAYEE_NAME 
	,ZC12.WA_RFD_CHK_AMT AS REF_AMT
	,ZC12.RC_DATE AS REF_FILE_DT
	,ZC12.RC_DATE AS REF_FILE_DT_S
	,ZC10.WA_TOT_AMT 
	,CNL.CAN_REF_FIL_ID
	,CNL.CAN_APRV_DT 
	,CNL.CAN_SCH_NMR

	,COALESCE(ZC14.WD_TRY_CNF,CURRENT DATE) AS S
FROM (
	SELECT SUBSTR(DF_SPE_ACC_ID,2) AS BF_SSN
		,SUBSTR(WF_RFD_ID,11,1) AS TYP_IND
		,WF_FMS_BCH
		,WF_RFD_ID
		,WC_RFD_TRX_TYP
		,WM_RFD_PYE
		,WC_RFD_PYE_TYP
		,DF_SPE_ACC_ID
		,WA_RFD_CHK_AMT
		,WF_CAN_SCH_NUM
		,WD_CAN
		,WX_CAN_REA
		,WF_BNK_ABA_NUM
		,WF_BNK_ACC_NUM
		,DATE(RTRIM(SUBSTR(WF_RFD_ID,7,2))||'/'||
			  RTRIM(SUBSTR(WF_RFD_ID,9,2))||'/'||
			  SUBSTR(WF_RFD_ID,3,4)) AS RC_DATE
	FROM FRDWODS.ZC12_FMS_RFD 
	) ZC12
LEFT OUTER JOIN FRDWODS.ZC10_FMS_HDR_TRL ZC10
	ON ZC12.WF_FMS_BCH = ZC10.WF_FMS_BCH
LEFT OUTER JOIN FRDWODS.ZC14_FMS_TRY_CNF ZC14
	ON ZC12.WF_RFD_ID = ZC14.WF_RFD_ID
LEFT OUTER JOIN (
	SELECT WF_RFD_ID AS CAN_REF_FIL_ID
		,WD_CAN AS CAN_APRV_DT 
		,WF_CAN_SCH_NUM AS CAN_SCH_NMR
	FROM FRDWODS.ZC12_FMS_RFD  
	WHERE WC_RFD_TRX_TYP = 'CANCEL'
	 ) CNL
	 ON ZC12.WF_RFD_ID = CNL.CAN_REF_FIL_ID
WHERE ZC12.WC_RFD_TRX_TYP = 'REFUND'
	AND COALESCE(ZC14.WD_TRY_CNF,&DT_RNG_END) BETWEEN &DT_RNG_BEG AND &DT_RNG_END
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA RARCTF;
	SET WORKLOCL.RARCTF;
RUN;

PROC SORT DATA=RARCTF;
	BY REF_FILE_DT_S WF_FMS_BCH REF_FIL_ID;
RUN;

PROC REPORT DATA=RARCTF NOWD SPLIT='~' OUT=RARCTF2;
	COLUMN REF_FILE_DT_S WF_FMS_BCH  REF_FIL_ID CHK_NUM SCH_NUM TSRY_CNF_DT PMT_AMT PAYEE_NAME BF_SSN NAME 
	REF_AMT REF_FILE_DT WA_TOT_AMT CAN_APRV_DT CAN_SCH_NMR CAN_REF_FIL_ID;
	DEFINE WF_FMS_BCH / ORDER ORDER=INTERNAL NOPRINT;
	DEFINE REF_FILE_DT_S / ORDER ORDER=INTERNAL;
	DEFINE REF_FIL_ID / DISPLAY ORDER=INTERNAL;
	DEFINE CHK_NUM / DISPLAY;
	DEFINE SCH_NUM / DISPLAY;
	DEFINE TSRY_CNF_DT / DISPLAY;
	DEFINE PMT_AMT / DISPLAY;
	DEFINE PAYEE_NAME / DISPLAY WIDTH=50;
	DEFINE BF_SSN / DISPLAY;
	DEFINE NAME / DISPLAY WIDTH=50; 
	DEFINE REF_AMT / ANALYSIS SUM;
	DEFINE REF_FILE_DT / DISPLAY ORDER=INTERNAL;
	DEFINE WA_TOT_AMT  / DISPLAY;
	DEFINE CAN_APRV_DT  / DISPLAY;
	DEFINE CAN_SCH_NMR / DISPLAY;
	DEFINE CAN_REF_FIL_ID/ DISPLAY;
	BREAK AFTER WF_FMS_BCH / SUMMARIZE SUPPRESS SKIP;
RUN;

OPTIONS MISSING='';
ODS LISTING CLOSE;
ODS TAGSETS.EXCELXP STYLE=minimal FILE=REPORT2 options(
	embedded_titles='yes' 
	sheet_name='UTNWR04.NWR04R2'
	width_points='1'
	width_fudge='1'
	absolute_column_width='80,80,80,120,80,80,80,80,80,80,80,80,80,80'
	embedded_footnotes='yes'
	sheet_interval='none'
	ORIENTATION = 'landscape'
	);
ODS ESCAPECHAR='^';
Title 'Refund and Refund Candellation Tracking';
Title2 "As of Report Date: &RUN_DT";
footnote; 
footnote2; 
PROC REPORT DATA=RARCTF2 NOWD SPLIT='~' 
	style(header)={background=lightgray font_size=9pt font=(Calibri) font_weight=bold}
	style(column)={font_size=8pt font=(Calibri) };
COLUMN REF_FIL_ID CHK_NUM SCH_NUM TSRY_CNF_DT PMT_AMT PAYEE_NAME BF_SSN NAME 
	REF_AMT REF_FILE_DT WA_TOT_AMT CAN_APRV_DT CAN_SCH_NMR CAN_REF_FIL_ID ;
	DEFINE REF_FIL_ID / DISPLAY  'Refund File ID';
	DEFINE CHK_NUM / DISPLAY 'Check Number';
	DEFINE SCH_NUM / DISPLAY  'Schedule Number' ;
	DEFINE TSRY_CNF_DT  / DISPLAY 'Treasury Confirmation Date' FORMAT=MMDDYY10. ;
	DEFINE PMT_AMT / DISPLAY 'Payment Amount'
		style(column)={tagattr='Format:$#,##0.00_);[Red]($#,##0.00)'};
	DEFINE PAYEE_NAME / DISPLAY 'Payee Name' ;
	DEFINE BF_SSN / DISPLAY 'SSN' style(column)={tagattr='Format:@'};
	DEFINE NAME / DISPLAY 'Name';
	DEFINE REF_AMT / DISPLAY 'Refund Amount'
		style(column)={tagattr='Format:$#,##0.00_);[Red]($#,##0.00)'};;
	DEFINE REF_FILE_DT / DISPLAY 'Refund File Date' FORMAT=MMDDYY10.;
	DEFINE WA_TOT_AMT  / DISPLAY 'Refund File Total'
		style(column)={tagattr='Format:$#,##0.00_);[Red]($#,##0.00)'};
	DEFINE CAN_APRV_DT / DISPLAY 'Canceled ApproVal Date' FORMAT=MMDDYY10.;
	DEFINE CAN_SCH_NMR  / DISPLAY 'Canceled Schedule Number';
	DEFINE CAN_REF_FIL_ID  / DISPLAY 'Refund File ID';
RUN;
ODS TAGSETS.EXCELXP CLOSE;
ODS LISTING;
OPTIONS MISSING=.;
