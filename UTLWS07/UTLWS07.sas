/*UTLWS07 IN HOUSE BORROWERS WITH INVALID ADDRESS OR PHONE*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS07.LWS07R2";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE IBWIZP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	A.DF_PRS_ID 
	,RTRIM(A.DM_PRS_LST)||', '||RTRIM(A.DM_PRS_1)||' '||A.DM_PRS_MID AS NAME
	,B.DX_STR_ADR_1
	,B.DX_STR_ADR_2
	,B.DX_STR_ADR_3
	,B.DM_CT
	,B.DC_DOM_ST
	,B.DF_ZIP_CDE
	,B.DM_FGN_ST
	,B.DM_FGN_CNY
	,B.DI_VLD_ADR
	,B.DD_VER_ADR
	,B.DC_SRC_ADR
	,RTRIM(C.DN_DOM_PHN_ARA)||'-'||RTRIM(C.DN_DOM_PHN_XCH)||'-'||RTRIM(C.DN_DOM_PHN_LCL) 
		AS LPH
	,RTRIM(C.DN_FGN_PHN_CNY)||'-'||RTRIM(C.DN_FGN_PHN_CT)||'-'||RTRIM(C.DN_FGN_PHN_LCL) 
		AS FPH
	,C.DI_PHN_VLD
	,C.DD_PHN_VER	
FROM OLWHRM1.PD10_PRS_NME A
INNER JOIN OLWHRM1.PD30_PRS_ADR B
	ON A.DF_PRS_ID = B.DF_PRS_ID
INNER JOIN OLWHRM1.PD42_PRS_PHN C
	ON B.DF_PRS_ID = C.DF_PRS_ID 
INNER JOIN OLWHRM1.LN10_LON D
	ON A.DF_PRS_ID = D.BF_SSN
WHERE D.LF_LON_CUR_OWN IN (&UHEAA_LIST)
AND D.LC_STA_LON10 = 'R'
AND D.LA_CUR_PRI > 0
AND B.DC_ADR = 'L'
AND C.DC_PHN = 'H'
AND (B.DI_VLD_ADR = 'N' OR C.DI_PHN_VLD = 'N')
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA IBWIZP;
SET WORKLOCL.IBWIZP;
RUN;

/*FORMAT THE ADDRESS*/
DATA IBWIZP;
LENGTH ADDRESS $ 118.;
SET IBWIZP;
ADDRESS = TRIM(DX_STR_ADR_1);
IF TRIM(DX_STR_ADR_2) NE ' ' 
	THEN ADDRESS = TRIM(ADDRESS)||'*'||TRIM(DX_STR_ADR_2);
IF TRIM(DX_STR_ADR_3) NE ' ' 
	THEN ADDRESS = TRIM(ADDRESS)||'*'||TRIM(DX_STR_ADR_3);
ADDRESS = TRIM(ADDRESS)||'*'||TRIM(DM_CT)||', '||TRIM(DC_DOM_ST)||' '||TRIM(DF_ZIP_CDE);
IF TRIM(DM_FGN_ST) NE ' ' THEN DO;
	ADDRESS = TRIM(ADDRESS)||'*'||TRIM(DM_FGN_ST);
	IF TRIM(DM_FGN_CNY) NE ' '
		THEN ADDRESS = TRIM(ADDRESS)||' '||TRIM(DM_FGN_CNY);
	END;
ELSE IF TRIM(DM_FGN_CNY) NE ' '
	THEN ADDRESS = TRIM(ADDRESS)||'*'||TRIM(DM_FGN_CNY);
/*CREATE SORT VALUE*/
LENGTH SORTTAG $ 35.;
IF DI_VLD_ADR = 'N' AND DI_PHN_VLD = 'Y' THEN DO;
	SORTBY = 1;
	SORTTAG = 'ONLY INVALID ADDRESS:  ';
	END;
ELSE IF DI_VLD_ADR = 'Y' AND DI_PHN_VLD = 'N' THEN DO;
	SORTBY = 2;
	SORTTAG = 'ONLY INVALID PHONE:  ';
	END;
ELSE IF DI_VLD_ADR = 'N' AND DI_PHN_VLD = 'N' THEN DO;
	SORTBY = 3;
	SORTTAG = 'INVALID ADDRESS AND INVALID PHONE:  ';
	END;
SORTTAG = TRIM(SORTTAG);
/*FORMAT THE PHONE*/
LENGTH PHONE $ 35.;
PHONE = TRIM(LPH);
IF TRIM(FPH) NE '--'
	THEN PHONE = TRIM(PHONE)||'*'||TRIM(FPH);
RUN;

DATA IBWIZP;
ATTRIB ADDRESS_SOURCE LENGTH= $23;
SET IBWIZP;
	IF DC_SRC_ADR = '01' THEN ADDRESS_SOURCE = 'ELECTRONIC MEDIA';
	ELSE IF DC_SRC_ADR = '02' THEN ADDRESS_SOURCE = 'APPLICATION';
	ELSE IF DC_SRC_ADR = '03' THEN ADDRESS_SOURCE = 'PROMISSSORY NOTE';
	ELSE IF DC_SRC_ADR = '04' THEN ADDRESS_SOURCE = 'APP/PROM NOTE';
	ELSE IF DC_SRC_ADR = '05' THEN ADDRESS_SOURCE = 'CORESPONDENCE';
	ELSE IF DC_SRC_ADR = '06' THEN ADDRESS_SOURCE = 'REPAYMENT OBLIGATION';
	ELSE IF DC_SRC_ADR = '07' THEN ADDRESS_SOURCE = 'CORRECT FORM';
	ELSE IF DC_SRC_ADR = '08' THEN ADDRESS_SOURCE = 'DEFERMENT FORM';
	ELSE IF DC_SRC_ADR = '09' THEN ADDRESS_SOURCE = 'FORBEARANCE FORM';
	ELSE IF DC_SRC_ADR = '10' THEN ADDRESS_SOURCE = 'GUARANTEE STATEMENT';
	ELSE IF DC_SRC_ADR = '11' THEN ADDRESS_SOURCE = 'CREDIT REPORT';
	ELSE IF DC_SRC_ADR = '12' THEN ADDRESS_SOURCE = 'PARENT';
	ELSE IF DC_SRC_ADR = '13' THEN ADDRESS_SOURCE = 'SPOUSE';
	ELSE IF DC_SRC_ADR = '14' THEN ADDRESS_SOURCE = 'SIBLING';
	ELSE IF DC_SRC_ADR = '15' THEN ADDRESS_SOURCE = 'ROOMMATE';
	ELSE IF DC_SRC_ADR = '16' THEN ADDRESS_SOURCE = 'NEIGHBOR';
	ELSE IF DC_SRC_ADR = '17' THEN ADDRESS_SOURCE = 'CERTIFIED MAIL';
	ELSE IF DC_SRC_ADR = '18' THEN ADDRESS_SOURCE = 'AUNT/UNCLE';
	ELSE IF DC_SRC_ADR = '19' THEN ADDRESS_SOURCE = 'GRANDPARENT';
	ELSE IF DC_SRC_ADR = '20' THEN ADDRESS_SOURCE = 'COUSIN';
	ELSE IF DC_SRC_ADR = '21' THEN ADDRESS_SOURCE = 'NIECE/NEPHEW';
	ELSE IF DC_SRC_ADR = '22' THEN ADDRESS_SOURCE = 'CHILD';
	ELSE IF DC_SRC_ADR = '23' THEN ADDRESS_SOURCE = 'EMPLOYER';
	ELSE IF DC_SRC_ADR = '24' THEN ADDRESS_SOURCE = 'DIRECTORY ASSIST.';
	ELSE IF DC_SRC_ADR = '25' THEN ADDRESS_SOURCE = 'POST OFFICE';
	ELSE IF DC_SRC_ADR = '26' THEN ADDRESS_SOURCE = 'DMV';
	ELSE IF DC_SRC_ADR = '27' THEN ADDRESS_SOURCE = 'LANDLORD';
	ELSE IF DC_SRC_ADR = '28' THEN ADDRESS_SOURCE = 'MILITARY';
	ELSE IF DC_SRC_ADR = '29' THEN ADDRESS_SOURCE = 'IRS';
	ELSE IF DC_SRC_ADR = '30' THEN ADDRESS_SOURCE = 'OUTSIDE COLLECTOR';
	ELSE IF DC_SRC_ADR = '31' THEN ADDRESS_SOURCE = 'UNKOWN';
	ELSE IF DC_SRC_ADR = '32' THEN ADDRESS_SOURCE = 'INFO FORM BORROWER';
	ELSE IF DC_SRC_ADR = '33' THEN ADDRESS_SOURCE = 'RETURNED EMAIL';
	ELSE IF DC_SRC_ADR = '41' THEN ADDRESS_SOURCE = 'BORROWER PHONE CALL';
	ELSE IF DC_SRC_ADR = '42' THEN ADDRESS_SOURCE = '2ND PARTY PHONE CALL';
	ELSE IF DC_SRC_ADR = '43' THEN ADDRESS_SOURCE = '3RD PARTY PHONE CALL';
	ELSE IF DC_SRC_ADR = '44' THEN ADDRESS_SOURCE = 'PRISON';
	ELSE IF DC_SRC_ADR = '45' THEN ADDRESS_SOURCE = 'FRIEND';
	ELSE IF DC_SRC_ADR = '46' THEN ADDRESS_SOURCE = 'STUDENT';
	ELSE IF DC_SRC_ADR = '47' THEN ADDRESS_SOURCE = 'PAROLE';
	ELSE IF DC_SRC_ADR = '48' THEN ADDRESS_SOURCE = 'REMITTANCE CHECK';
	ELSE IF DC_SRC_ADR = '49' THEN ADDRESS_SOURCE = 'CUPON STATEMNET';
	ELSE IF DC_SRC_ADR = '50' THEN ADDRESS_SOURCE = 'FORMER EMPLOYER';
	ELSE IF DC_SRC_ADR = '51' THEN ADDRESS_SOURCE = 'BAR ASSOCIATION';
	ELSE IF DC_SRC_ADR = '52' THEN ADDRESS_SOURCE = 'VR MAILBOX';
	ELSE IF DC_SRC_ADR = '53' THEN ADDRESS_SOURCE = 'FINALIST';
	ELSE IF DC_SRC_ADR = '54' THEN ADDRESS_SOURCE = 'ACS';
	ELSE IF DC_SRC_ADR = '55' THEN ADDRESS_SOURCE = 'WEBMASTER';
	ELSE IF DC_SRC_ADR = '56' THEN ADDRESS_SOURCE = 'GUARANTOR';
	ELSE IF DC_SRC_ADR = '98' THEN ADDRESS_SOURCE = 'CAM';
	ELSE IF DC_SRC_ADR = '99' THEN ADDRESS_SOURCE = 'ANNUAL STATEMENT';
RUN;

PROC SORT DATA=IBWIZP;
BY SORTBY DF_PRS_ID ;
RUN;

DATA _NULL_;
CALL SYMPUT('RUNDATE',PUT(INTNX('DAY',TODAY(),0,'beginning'), MMDDYY10.));
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
OPTIONS CENTER NODATE NUMBER PAGENO=1;
PROC REPORT DATA=IBWIZP NOWD SPACING=1 HEADSKIP SPLIT='*';
TITLE "INHOUSE BORROWERS WITH INVALID ADDRESS OR PHONE";
TITLE2 "&RUNDATE";
FOOTNOTE  'JOB = UTLWS07  	 REPORT = ULWS07.LWS07R2';
COLUMN SORTTAG SORTBY DF_PRS_ID NAME ADDRESS DI_VLD_ADR DD_VER_ADR ADDRESS_SOURCE
PHONE DI_PHN_VLD DD_PHN_VER N;
DEFINE SORTBY / GROUP NOPRINT;
DEFINE SORTTAG / GROUP NOPRINT;
DEFINE DF_PRS_ID / DISPLAY "SSN" WIDTH=9;
DEFINE NAME / DISPLAY "NAME" WIDTH=20;
DEFINE ADDRESS / DISPLAY WIDTH=30 FLOW;
DEFINE DI_VLD_ADR / DISPLAY "ADR*VLD" WIDTH=3;
DEFINE DD_VER_ADR / DISPLAY "ADDRESS*VERIFIED" FORMAT=MMDDYY10.;
DEFINE ADDRESS_SOURCE / DISPLAY "ADDRESS*SOURCE" WIDTH=20;
DEFINE PHONE / DISPLAY "PHONE #*DOMESTIC/*FOREIGN" WIDTH=12 FLOW;
DEFINE DI_PHN_VLD / DISPLAY "PHN*VLD" WIDTH=3;
DEFINE DD_PHN_VER / DISPLAY "PHONE*VERIFIED" FORMAT=MMDDYY10.;
DEFINE N / NOPRINT;

COMPUTE AFTER SORTBY;
LINE ' ';
LINE @35 'TOTAL BORROWERS WITH ' SORTTAG $35. @95 N COMMA5.;
LINE ' ';
LINE ' ';
ENDCOMP;

COMPUTE AFTER;
LINE @35 'TOTAL BORROWERS WITH INVALID ADDRESS OR INVALID PHONE:' @95 N COMMA5.;
LINE ' ';
LINE ' ';
ENDCOMP;
RUN;

PROC PRINTTO;
RUN;
