/*set up library to SQL Server and include common code*/
/* TEST*/
	LIBNAME SQL ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS_TEST.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= SCRA;
	LIBNAME UDW ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\UDW_TEST.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= dbo;
	%INCLUDE "Y:\Codebase\SAS\ArcAdd Common.SAS";
	%LET RPTLIB2 = X:\PADD\FTP\Test;

/*LIVE*/
/*	LIBNAME SQL ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\ULS.DSN; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= SCRA;*/
/*	LIBNAME UDW ODBC REQUIRED="FILEDSN=X:\PADR\ODBC\UDW.dsn; bl_keepnulls=no; READ_ISOLATION_LEVEL=RU" SCHEMA= dbo;*/
/*	%INCLUDE "Z:\Codebase\SAS\ArcAdd Common.SAS";*/
/*%LET RPTLIB2 = X:\Archive\UHEAA SCRA Historical files;*/

FILENAME SOURCE1 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20090701_01.txt";
FILENAME SOURCE2 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20090701_02.txt";
FILENAME SOURCE3 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20090701_03.txt";
FILENAME SOURCE4 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20100701_01.txt";
FILENAME SOURCE5 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20100701_02.txt";
FILENAME SOURCE6 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20100701_03.txt";
FILENAME SOURCE7 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20110701_01.txt";
FILENAME SOURCE8 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20110701_02.txt";
FILENAME SOURCE9 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20110701_03.txt";
FILENAME SOURCE10 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20120701_01.txt";
FILENAME SOURCE11 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20120701_02.txt";
FILENAME SOURCE12 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20120701_03.txt";
FILENAME SOURCE13 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20130701_01.txt";
FILENAME SOURCE14 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20130701_02.txt";
FILENAME SOURCE15 "&RPTLIB2\SCRA_3_0_ActiveDutyStatusDate20130701_03.txt";

%MACRO READER;
	%DO I=1 %TO 15;
		DATA WORK.SOURCE&I.;
		INFILE SOURCE&I.;
		INPUT 	SSN $ 1-9 
				NOTIFICATION_DATE $ 92-99
				LC_ACT_DUT $ 101
				LC_LFT_ACT_DUT $ 102
				LC_NTF_ACT_DUT $ 103
				ACT_DUT_END $ 104-111
				ACT_DUT_BEG $ 122-129
				EID_BEGIN_DATE $ 130-137 
		;
		FORMAT 	LD_ACT_DUT_BEG 
				LD_ACT_DUT_END
				LD_EID_BEGIN_DATE
				LD_NOTIFICATION_DATE MMDDYY10.
		;
		IF ACT_DUT_BEG ^= '00000000' THEN LD_ACT_DUT_BEG = INPUT(ACT_DUT_BEG, YYMMDD8.);
		IF ACT_DUT_END ^= '00000000' THEN LD_ACT_DUT_END = INPUT(ACT_DUT_END, YYMMDD8.);
		IF EID_BEGIN_DATE ^= '00000000' THEN LD_EID_BEGIN_DATE = INPUT(EID_BEGIN_DATE, YYMMDD8.);
		IF NOTIFICATION_DATE ^= '00000000' THEN LD_NOTIFICATION_DATE = INPUT(NOTIFICATION_DATE, YYMMDD8.);
		RUN;
	%END;
%MEND READER;
%READER

LIBNAME  DUSTER  REMOTE  SERVER=DUSTER  SLIBREF=WORK;

DATA DOD;
	SET	SOURCE1-SOURCE15;
	IF LC_ACT_DUT = 'Z' THEN DELETE;
	IF SSN = 'EOF' THEN DELETE;
	IF LC_ACT_DUT = 'N' AND LC_LFT_ACT_DUT = 'N' AND LC_NTF_ACT_DUT = 'N' THEN DELETE;
RUN;

PROC DATASETS NOLIST;
	DELETE SOURCE1-SOURCE15;
QUIT;

DATA DUSTER.DOD;
	SET DOD;
RUN;

RSUBMIT;
LIBNAME OLWHRM1 DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;

PROC SQL;
/*borrowers*/
CREATE TABLE BRWS AS
	SELECT DISTINCT
		PD10.DF_SPE_ACC_ID,
		PD10.DF_PRS_ID,
		PD10.DM_PRS_1,
		PD10.DM_PRS_LST,
		PD30.DC_ADR,
		PD30.DX_STR_ADR_1,
		PD30.DX_STR_ADR_2,
		PD30.DM_CT,
		PD30.DC_DOM_ST,
		PD30.DF_ZIP_CDE,
		PD30.DM_FGN_CNY,
		PD30.DM_FGN_ST,
		DOD.LD_ACT_DUT_BEG,
		DOD.LD_ACT_DUT_END,
		DOD.LC_ACT_DUT,
		DOD.LC_LFT_ACT_DUT,
		DOD.LC_NTF_ACT_DUT,
		DOD.LD_EID_BEGIN_DATE,
		DOD.LD_NOTIFICATION_DATE,
		PD30.DI_VLD_ADR,
		LN10.LN_SEQ,
		LN72.LR_ITR,
		LN72.LC_INT_RDC_PGM,
		LN72.LD_ITR_EFF_BEG,
		LN72.LD_ITR_EFF_END,
		PD32.DX_ADR_EML,
		LN10.LD_LON_1_DSB
	FROM
		DOD
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON DOD.SSN = PD10.DF_PRS_ID
		LEFT JOIN OLWHRM1.PD30_PRS_ADR PD30
			ON DOD.SSN = PD30.DF_PRS_ID
		LEFT JOIN OLWHRM1.PD32_PRS_ADR_EML PD32
			ON PD32.DF_PRS_ID = PD10.DF_PRS_ID
			AND PD32.DI_VLD_ADR_EML = 'Y'
		INNER JOIN OLWHRM1.LN10_LON LN10
			ON DOD.SSN = LN10.BF_SSN
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
			ON LN10.BF_SSN = DW01.BF_SSN
			AND LN10.LN_SEQ = DW01.LN_SEQ
		INNER JOIN OLWHRM1.LN72_INT_RTE_HST LN72
			ON LN10.BF_SSN = LN72.BF_SSN
			AND LN10.LN_SEQ = LN72.LN_SEQ
			AND TODAY() BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
			AND LN72.LC_STA_LON72 = 'A'
	WHERE
		LN10.LC_STA_LON10 IN ('D', 'R')
		AND COALESCE(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS,0) > 0
	ORDER BY 
		PD10.DF_SPE_ACC_ID
;

/*endorsers/co-makers*/
CREATE TABLE ENDS AS
	SELECT DISTINCT
		PD10.DF_SPE_ACC_ID,
		PD10E.DF_SPE_ACC_ID AS END_ACC_ID,
		DOD.LD_ACT_DUT_BEG,
		DOD.LD_ACT_DUT_END,
		DOD.LC_ACT_DUT,
		DOD.LC_LFT_ACT_DUT,
		DOD.LC_NTF_ACT_DUT,
		DOD.LD_EID_BEGIN_DATE,
		DOD.LD_NOTIFICATION_DATE,
		LN72.LR_ITR,
		LN72.LC_INT_RDC_PGM,
		LN72.LD_ITR_EFF_BEG,
		LN72.LD_ITR_EFF_END,
		LN10.LN_SEQ,
		LN20.LF_EDS,
		LN10.LD_LON_1_DSB
	FROM
		DOD
		INNER JOIN OLWHRM1.LN20_EDS LN20
			ON DOD.SSN = LN20.LF_EDS
		INNER JOIN OLWHRM1.LN10_LON LN10
			ON LN20.BF_SSN = LN10.BF_SSN
			AND LN20.LN_SEQ = LN10.LN_SEQ
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
			ON LN10.BF_SSN = DW01.BF_SSN
			AND LN10.LN_SEQ = DW01.LN_SEQ
		INNER JOIN OLWHRM1.LN72_INT_RTE_HST LN72
			ON LN10.BF_SSN = LN72.BF_SSN
			AND LN10.LN_SEQ = LN72.LN_SEQ
			AND TODAY() BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
			AND LN72.LC_STA_LON72 = 'A'
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON LN20.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10E
			ON LN20.LF_EDS = PD10E.DF_PRS_ID
	WHERE
		LN10.LC_STA_LON10 IN ('D', 'R')
		AND COALESCE(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS,0) > 0
	ORDER BY 
		PD10.DF_SPE_ACC_ID,
		LN10.LN_SEQ
;

/*borrowers for SSRS report*/
CREATE TABLE BRWS_SSRS AS
	SELECT DISTINCT
		PD10.DF_SPE_ACC_ID,
		DOD.LD_ACT_DUT_BEG,
		DOD.LD_ACT_DUT_END,
		DOD.LD_EID_BEGIN_DATE,
		CASE
			WHEN DW01.WC_DW_LON_STA IN ('22','12')
				THEN LN10.LD_PIF_RPT
			ELSE LN90_MAX.LD_FAT_EFF
		END AS PAID_IN_FULL_DATE,
		CASE
			WHEN DW01.WX_OVR_DW_LON_STA LIKE 'DECONVERTED%'
				THEN LN10.LD_STA_LON10
			ELSE .
		END AS DECONVERTED_DATE
	FROM
		DOD
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON DOD.SSN = PD10.DF_PRS_ID
		LEFT JOIN OLWHRM1.PD30_PRS_ADR PD30
			ON DOD.SSN = PD30.DF_PRS_ID
		LEFT JOIN OLWHRM1.PD32_PRS_ADR_EML PD32
			ON PD32.DF_PRS_ID = PD10.DF_PRS_ID
			AND PD32.DI_VLD_ADR_EML = 'Y'
		INNER JOIN OLWHRM1.LN10_LON LN10
			ON DOD.SSN = LN10.BF_SSN
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
			ON LN10.BF_SSN = DW01.BF_SSN
			AND LN10.LN_SEQ = DW01.LN_SEQ
		INNER JOIN OLWHRM1.LN72_INT_RTE_HST LN72
			ON LN10.BF_SSN = LN72.BF_SSN
			AND LN10.LN_SEQ = LN72.LN_SEQ
			AND TODAY() BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
			AND LN72.LC_STA_LON72 = 'A'
		LEFT JOIN OLWHRM1.LN90_FIN_ATY LN90
			ON LN10.BF_SSN = LN90.BF_SSN
			AND LN10.LN_SEQ = LN90.LN_SEQ
			AND LN90.PC_FAT_TYP = '04'
			AND LN90.PC_FAT_SUB_TYP = '96'
			AND LN90.LC_STA_LON90 = 'A'
		LEFT JOIN 
		(
			SELECT
				 LN90.BF_SSN
				,LN90.LN_SEQ
				,MAX(LN90.LD_FAT_EFF) AS LD_FAT_EFF
			FROM
				OLWHRM1.LN90_FIN_ATY LN90
				INNER JOIN DOD
					ON DOD.SSN = LN90.BF_SSN
			WHERE
				LN90.LC_STA_LON90 = 'A'
			GROUP BY
				LN90.BF_SSN
				,LN90.LN_SEQ
		)LN90_MAX	
			ON LN10.BF_SSN = LN90_MAX.BF_SSN
			AND LN10.LN_SEQ = LN90_MAX.LN_SEQ
		LEFT JOIN BRWS B
			ON B.DF_PRS_ID = LN10.BF_SSN
	WHERE
		LN10.LC_STA_LON10 IN ('D', 'R')
		AND COALESCE(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS,0) <= 0
		AND B.DF_PRS_ID IS NULL
		AND LN10.LD_LON_1_DSB <= DOD.LD_ACT_DUT_BEG
	ORDER BY 
		PD10.DF_SPE_ACC_ID
;

/*endorsers/co-makers for SSRS report*/
CREATE TABLE ENDS_SSRS AS
	SELECT DISTINCT
		PD10.DF_SPE_ACC_ID,
		PD10E.DF_SPE_ACC_ID AS END_ACC_ID,
		DOD.LD_ACT_DUT_BEG,
		DOD.LD_ACT_DUT_END,
		DOD.LD_EID_BEGIN_DATE,
		CASE
			WHEN DW01.WC_DW_LON_STA IN ('22','12')
				THEN LN10.LD_PIF_RPT
			ELSE LN90_MAX.LD_FAT_EFF
		END AS PAID_IN_FULL_DATE,
		CASE
			WHEN DW01.WX_OVR_DW_LON_STA LIKE 'DECONVERTED%'
				THEN LN10.LD_STA_LON10
			ELSE .
		END AS DECONVERTED_DATE
	FROM
		DOD
		INNER JOIN OLWHRM1.LN20_EDS LN20
			ON DOD.SSN = LN20.LF_EDS
		INNER JOIN OLWHRM1.LN10_LON LN10
			ON LN20.BF_SSN = LN10.BF_SSN
			AND LN20.LN_SEQ = LN10.LN_SEQ
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU DW01
			ON LN10.BF_SSN = DW01.BF_SSN
			AND LN10.LN_SEQ = DW01.LN_SEQ
		INNER JOIN OLWHRM1.LN72_INT_RTE_HST LN72
			ON LN10.BF_SSN = LN72.BF_SSN
			AND LN10.LN_SEQ = LN72.LN_SEQ
			AND TODAY() BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
			AND LN72.LC_STA_LON72 = 'A'
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10
			ON LN20.BF_SSN = PD10.DF_PRS_ID
		INNER JOIN OLWHRM1.PD10_PRS_NME PD10E
			ON LN20.LF_EDS = PD10E.DF_PRS_ID
		LEFT JOIN OLWHRM1.LN90_FIN_ATY LN90
			ON LN10.BF_SSN = LN90.BF_SSN
			AND LN10.LN_SEQ = LN90.LN_SEQ
			AND LN90.PC_FAT_TYP = '04'
			AND LN90.PC_FAT_SUB_TYP = '96'
			AND LN90.LC_STA_LON90 = 'A'
		LEFT JOIN 
		(
			SELECT
				 LN90.BF_SSN
				,LN90.LN_SEQ
				,MAX(LN90.LD_FAT_EFF) AS LD_FAT_EFF
			FROM
				OLWHRM1.LN90_FIN_ATY LN90
				INNER JOIN DOD
					ON DOD.SSN = LN90.BF_SSN
			WHERE
				LN90.LC_STA_LON90 = 'A'
			GROUP BY
				LN90.BF_SSN
				,LN90.LN_SEQ
		)LN90_MAX		
			ON LN10.BF_SSN = LN90_MAX.BF_SSN
			AND LN10.LN_SEQ = LN90_MAX.LN_SEQ
		LEFT JOIN ENDS E
			ON E.LF_EDS = LN20.LF_EDS
	WHERE
		LN10.LC_STA_LON10 IN ('D', 'R')
		AND COALESCE(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS,0) <= 0
		AND E.LF_EDS IS NULL
		AND LN10.LD_LON_1_DSB <= DOD.LD_ACT_DUT_BEG
	ORDER BY 
		PD10.DF_SPE_ACC_ID
;
QUIT;
ENDRSUBMIT;

/*gets data from duster*/
DATA BRWS;
	SET DUSTER.BRWS;
RUN;
DATA ENDS;
	SET DUSTER.ENDS;
RUN;
DATA SSRS;
	SET DUSTER.BRWS_SSRS
		DUSTER.ENDS_SSRS
	;
	FORMAT 
		PAID_IN_FULL_DATE 
		DECONVERTED_DATE
		MMDDYY10.
	;
RUN;

/*removing borrowers where all first disbursements are more recent than active duty begin date*/
PROC SQL NOPRINT;
CREATE TABLE MAX_BRWS AS
	SELECT DISTINCT
		DF_SPE_ACC_ID
		,MAX(LD_LON_1_DSB) AS MAX_LD_LON_1_DSB FORMAT=MMDDYY10.
		,COALESCE(LD_ACT_DUT_BEG, LD_EID_BEGIN_DATE) AS BEGIN_DATE FORMAT=MMDDYY10.
	FROM
		BRWS
	GROUP BY
		DF_SPE_ACC_ID
		,LD_ACT_DUT_BEG
		,LD_EID_BEGIN_DATE
	HAVING
		MAX_LD_LON_1_DSB > BEGIN_DATE
	;

CREATE TABLE MAX_ENDS AS
	SELECT
		DF_SPE_ACC_ID
		,MAX(LD_LON_1_DSB) AS MAX_LD_LON_1_DSB FORMAT=MMDDYY10.
		,COALESCE(LD_ACT_DUT_BEG, LD_EID_BEGIN_DATE) AS BEGIN_DATE FORMAT=MMDDYY10.
	FROM
		ENDS
	GROUP BY
		DF_SPE_ACC_ID
		,LD_ACT_DUT_BEG
		,LD_EID_BEGIN_DATE
	HAVING
		MAX_LD_LON_1_DSB > BEGIN_DATE
	;

CREATE TABLE _BRWS AS
	SELECT
		B.*
	FROM
		BRWS B
		LEFT JOIN MAX_BRWS M
			ON B.DF_SPE_ACC_ID = M.DF_SPE_ACC_ID
	WHERE
		M.DF_SPE_ACC_ID IS NULL
	;

CREATE TABLE _ENDS AS
	SELECT
		E.*
	FROM
		ENDS E
		LEFT JOIN MAX_ENDS M
			ON E.DF_SPE_ACC_ID = M.DF_SPE_ACC_ID
	WHERE
		M.DF_SPE_ACC_ID IS NULL
	;

CREATE TABLE _SSRS AS
	SELECT DISTINCT
		SSRS.*
	FROM
		SSRS
		LEFT JOIN 
		(
			SELECT * FROM MAX_BRWS
			UNION
			SELECT * FROM MAX_ENDS
		) MAX
			ON SSRS.DF_SPE_ACC_ID = MAX.DF_SPE_ACC_ID
	WHERE
		MAX.DF_SPE_ACC_ID IS NULL
		AND 
		(
			COALESCE(SSRS.PAID_IN_FULL_DATE,'01JAN2099'd) > '14JUL2008'd
			AND
			COALESCE(SSRS.DECONVERTED_DATE,'01JAN2099'd) > '14JUL2008'd
		)
	;
QUIT;
/*exclude from SSRS those whose PIF/Decon dates are before Active Duty Begin Date*/
PROC SQL;
	CREATE TABLE _SSRS_UTLWS76 AS
		SELECT
			*
		FROM
			_SSRS
		WHERE
			PAID_IN_FULL_DATE > COALESCE(LD_ACT_DUT_BEG, LD_EID_BEGIN_DATE)
	;
QUIT;
/*sends data to SQL Server*/
DATA SQL._BorrowersPopulation_UTLWS76;
	SET	_BRWS;
RUN;

DATA SQL._EndorsersPopulation_UTLWS76;
	SET	_ENDS;
RUN;

DATA SQL._SSRS_UTLWS76;
	SET _SSRS_UTLWS76;
RUN;
