/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = Z:\Batch\FTP;

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=work  ;
LIBNAME MDS 'Y:\Development\ExternalDataSources\MonthlyDataSets';
FILENAME REPORTZ "&RPTLIB/UNWR06.NWR06RZ";
FILENAME REPORT2 "&RPTLIB/UNWR06.NWR06R2";
FILENAME REPORT3 "&RPTLIB/UNWR06.NWR06R3";
FILENAME REPORT4 "&RPTLIB/UNWR06.NWR06R4";

DATA _NULL_;
	RUN_DAY = TODAY();
	CALL SYMPUT('MON_AGO_1_BEG',"'"||PUT(INTNX('MONTH',RUN_DAY,-1,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('CUR_MON_BEG',"'"||PUT(INTNX('MONTH',RUN_DAY,0,'B'), MMDDYYD10.)||"'");
	CALL SYMPUT('MON_AGO_1_END',"'"||PUT(INTNX('MONTH',RUN_DAY,-1,'E'), DATE9.)||"'");
	CALL SYMPUT('LST_MON',PUT(INTNX('MONTH',RUN_DAY,0,'E'), MMYYN6.));
	CALL SYMPUT('RUN_DATE',RUN_DAY);
	CALL SYMPUTX('TITLE_DATE',PUT(INTNX('MONTH',RUN_DAY,-1,'E'), worddate20.));
RUN;

%PUT &LST_MON;
%LET DW01 = pkub_dw01_dw_clc_clu_&LST_MON;
%PUT &DW01;
%LET LN10_EOM = pkus_ln10_lon_&LST_MON;
%PUT &LN10_EOM;
%SYSLPUT LN10_EOM = &LN10_EOM;
%SYSLPUT MON_AGO_1_BEG = &MON_AGO_1_BEG;
%SYSLPUT MON_AGO_1_END = &MON_AGO_1_END;
%SYSLPUT CUR_MON_BEG = &CUR_MON_BEG;

PROC SQL;
CREATE TABLE LN10_EOM AS
	SELECT
		*
	FROM
		MDS.&LN10_EOM
;
QUIT;

DATA LEGEND.LN10_EOM; SET WORK.LN10_EOM; RUN;

RSUBMIT LEGEND;
LIBNAME LGND '/sas/whse/progrevw';
LIBNAME PKUB DB2 DATABASE=DNFPUTDL OWNER=PKUB;
LIBNAME PKUS DB2 DATABASE=DNFPUTDL OWNER=PKUS;

PROC SQL;
CREATE TABLE MSBTFF0 AS 
	SELECT 
		PD10.DF_SPE_ACC_ID,
		LN10.BF_SSN,
		LN10.LN_SEQ,
		LN10.IC_LON_PGM,
		LN10.LC_FED_PGM_YR,
		LN10.LF_FED_CLC_RSK,
		LN10.LA_CUR_PRI,
		LN10.LA_NSI_OTS,
		0 AS LA_FAT_LTE_FEE,
		COALESCE(TODAY() - LN16.LD_DLQ_OCC,0) AS DAYS_DELQ,
		LN37.LA_OTS_PRI_ELG
	FROM 
		LN10_EOM LN10
/*		LEFT OUTER JOIN 
		(
			SELECT 
				BF_SSN,
				LN_SEQ,
				SUM(LA_FAT_LTE_FEE) AS LA_FAT_LTE_FEE
			FROM 
				PKUS.LN90_FIN_ATY 
			WHERE 
				PUT(LD_FAT_APL, DATE9.) <= &MON_AGO_1_END
			GROUP BY 
				BF_SSN,
				LN_SEQ
		) LN90
			ON LN90.BF_SSN = LN10.BF_SSN
			AND LN90.LN_SEQ = LN10.LN_SEQ*/
		INNER JOIN PKUB.PD10_PRS_NME PD10
			ON LN10.BF_SSN = PD10.DF_PRS_ID
		LEFT OUTER JOIN PKUB.LN16_LON_DLQ_HST LN16
			ON LN10.BF_SSN = LN16.BF_SSN
			AND LN10.LN_SEQ = LN16.LN_SEQ
			AND LN16.LC_STA_LON16 = '1'
			AND LN16.LC_DLQ_TYP = 'P'
		LEFT OUTER JOIN PKUS.LN37_LON_MTH_BAL LN37
			ON LN10.BF_SSN = LN37.BF_SSN
			AND LN10.LN_SEQ = LN37.LN_SEQ
			AND PUT(LN37.LD_EFF_MTH_BAL, DATE9.) = &MON_AGO_1_END
			AND LN37.LC_STA_LON37 = 'A'
	WHERE
		LN10.LC_STA_LON10 ^= 'P'
;

QUIT;

ENDRSUBMIT;
DATA MSBTFF0; SET LEGEND.MSBTFF0; RUN;

PROC SQL;
CREATE TABLE MSBTFF AS
	SELECT DISTINCT
		MSB.*,
		DW01.WC_DW_LON_STA
	FROM
		MSBTFF0 MSB
		LEFT JOIN  MDS.&DW01 DW01
			ON MSB.BF_SSN = DW01.BF_SSN
			AND MSB.LN_SEQ = DW01.LN_SEQ
	;
QUIT;

/*determine loan category for each loan*/
DATA R4_LOAN_CATEGORIES;
	SET MSBTFF;
	WHERE LA_CUR_PRI^=0 OR LA_NSI_OTS^=0;

	IF WC_DW_LON_STA = '03' AND DAYS_DELQ <= 30 THEN LC = 'CURR';
	ELSE IF WC_DW_LON_STA = '03' AND DAYS_DELQ > 30 THEN LC = 'DELQ';
	ELSE IF WC_DW_LON_STA = '05' THEN LC = 'FORB';
	ELSE IF WC_DW_LON_STA = '04' THEN LC = 'DEFR';
	ELSE IF WC_DW_LON_STA IN ('06','07','08','09','10','11','12','13','14','15','16','17','18','19','20','21','23','88') THEN LC = 'SUSP';
	ELSE LC = 'OTHR';
RUN;

/*get counts of loans in each category for each CRC code*/
PROC SQL;
CREATE TABLE R4_CNTS AS
	SELECT
		LF_FED_CLC_RSK,
		LC AS LOAN_CATEGORY,
		COUNT(DISTINCT CATX('',BF_SSN,LN_SEQ)) AS LOAN_COUNT
	FROM
		R4_LOAN_CATEGORIES
	GROUP BY
		LF_FED_CLC_RSK,
		LC
;
QUIT;

/*create separate data sets for counts in each loan category*/
DATA R4_CURR (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='CURR';
	CURR=LOAN_COUNT;
RUN;

DATA R4_FORB (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='FORB';
	FORB=LOAN_COUNT;
RUN;

DATA R4_DEFR (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='DEFR';
	DEFR=LOAN_COUNT;
RUN;

DATA R4_SUSP (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='SUSP';
	SUSP=LOAN_COUNT;
RUN;

DATA R4_DELQ (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='DELQ';
	DELQ=LOAN_COUNT;
RUN;

DATA R4_OTHR (DROP=LOAN_CATEGORY LOAN_COUNT);
	SET R4_CNTS;
	WHERE LOAN_CATEGORY='OTHR';
	OTHR=LOAN_COUNT;
RUN;

/*merge category data sets together to create one row for each CRC code with counts columns for each category*/
DATA R4;
	MERGE R4_CURR R4_FORB R4_DEFR  R4_SUSP R4_DELQ R4_OTHR;
	BY LF_FED_CLC_RSK;
RUN;

DATA MSBTFF;
	SET MSBTFF;
	IF WC_DW_LON_STA = '03' AND DAYS_DELQ <= 30 THEN LS = '3A';
	ELSE IF WC_DW_LON_STA = '03' AND DAYS_DELQ > 30 THEN LS = '3B';
	ELSE IF WC_DW_LON_STA IN ('06','07','08','09','10','11','12','13','14','15','16','17','18','19','20','21','23','88') THEN LS = 'IS';
	ELSE IF WC_DW_LON_STA = '22' AND LA_OTS_PRI_ELG < 0 THEN LS = 'CB';
	ELSE LS = WC_DW_LON_STA;
RUN;

PROC SORT DATA=MSBTFF;
	BY BF_SSN LC_FED_PGM_YR LS;
RUN;

DATA MSBTFF;
	SET MSBTFF;
	BY BF_SSN LC_FED_PGM_YR LS;
	IF FIRST.LS THEN BSC = 1;
	ELSE BSC = 0;
	LOAN_IND = 1;
	HAS_AMT = (LA_CUR_PRI^=0|LA_NSI_OTS^=0);
RUN;

PROC SORT DATA=MSBTFF;
	BY BF_SSN LC_FED_PGM_YR LF_FED_CLC_RSK LS;
RUN;

DATA MSBTFF;
	SET MSBTFF;
	BY BF_SSN LC_FED_PGM_YR LF_FED_CLC_RSK LS;
	IF FIRST.LS THEN BSLC = 1;
	ELSE BSLC = 0;
RUN;

PROC FORMAT;
	VALUE $LNSTA
	'3A' = 'In Repayment(0-30 days delinquent)'
	'3B' = 'In Repayment(> days delinquent)'
	'01' = 'Grace'
	'02' = 'In School'
	'04' = 'In Deferment'
	'05' = 'In Forbearance'
	'22' = 'PIF'
	'IS' = 'In Suspense/Specialty'
	'CB' = 'PIF w/ Credit Balance'
	'98' = 'Other';
RUN;

PROC SQL NOPRINT;
SELECT PUT(COUNT(DISTINCT BF_SSN),COMMA9.) INTO:UBC FROM MSBTFF WHERE HAS_AMT;
QUIT;
%PUT &UBC;

ODS _ALL_ CLOSE;
ODS LISTING;

PROC PRINTTO PRINT=REPORT2 NEW; RUN;

OPTIONS ORIENTATION = LANDSCAPE NODATE PAGENO=1 ;
OPTIONS PS=39 LS=127;
TITLE 'Monthly System Balancing to FMS by Loan Program';
TITLE2 "&TITLE_DATE";
FOOTNOTE 'JOB = UTNWR06  	 REPORT = UNWR06.NWR06R2';

PROC REPORT DATA=MSBTFF NOWD HEADSKIP SPLIT='~' SPACING=1;
	WHERE HAS_AMT;
	COLUMN LC_FED_PGM_YR LS BSC LOAN_IND LA_CUR_PRI LA_NSI_OTS LA_FAT_LTE_FEE;
	DEFINE LC_FED_PGM_YR / GROUP WIDTH=12 'LOAN PROGRAM';
	DEFINE LS / GROUP FORMAT=$LNSTA. 'LOAN STATUS'; 
	DEFINE BSC / SUM FORMAT=COMMA9. 'TOTAL BORROWER COUNT';
	DEFINE LOAN_IND / SUM  FORMAT=COMMA9. 'TOTAL LOAN COUNT';
	DEFINE LA_CUR_PRI / SUM FORMAT=COMMA18.2 'PRINCIPAL~BALANCE';
	DEFINE LA_NSI_OTS / SUM FORMAT=COMMA18.2 'INTEREST~BALANCE';
	DEFINE LA_FAT_LTE_FEE / SUM FORMAT=COMMA10.2 'LATE FEE~BALANCE';
	BREAK AFTER LC_FED_PGM_YR / SUMMARIZE DOL SKIP SUPPRESS;
	RBREAK AFTER / SUMMARIZE ;
	COMPUTE AFTER ;
		LINE @5 "UNIQUE BORROWER COUNT: &UBC";
	ENDCOMP;
RUN;

PROC PRINTTO;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW; RUN;

OPTIONS ORIENTATION = LANDSCAPE NODATE PAGENO=1;
OPTIONS PS=39 LS=127;
TITLE 'Monthly System Balancing to FMS by Loan Program and CRC Code';
TITLE2 "&TITLE_DATE";
FOOTNOTE 'JOB = UTNWR06  	 REPORT = UNWR06.NWR06R3';

PROC REPORT DATA=MSBTFF NOWD HEADSKIP SPLIT='~' SPACING=1;
	WHERE HAS_AMT;
	COLUMN LC_FED_PGM_YR LF_FED_CLC_RSK LS BSLC LOAN_IND LA_CUR_PRI LA_NSI_OTS LA_FAT_LTE_FEE;
	DEFINE LC_FED_PGM_YR / GROUP WIDTH=12'LOAN PROGRAM';
	DEFINE LF_FED_CLC_RSK / GROUP 'CRC CODE';
	DEFINE LS / GROUP FORMAT=$LNSTA. 'LOAN STATUS'; 
	DEFINE BSLC / SUM FORMAT=COMMA9. 'TOTAL BORROWER COUNT';
	DEFINE LOAN_IND / SUM  FORMAT=COMMA9. 'TOTAL LOAN COUNT';
	DEFINE LA_CUR_PRI / SUM FORMAT=COMMA18.2 'PRINCIPAL BALANCE';
	DEFINE LA_NSI_OTS / SUM FORMAT=COMMA18.2 'INTEREST BALANCE';
	DEFINE LA_FAT_LTE_FEE / SUM FORMAT=COMMA10.2 'LATE FEE BALANCE';
	BREAK AFTER LC_FED_PGM_YR / SUMMARIZE DOL SKIP SUPPRESS;
	RBREAK AFTER / SUMMARIZE ;
	COMPUTE AFTER ;
		LINE @3 "UNIQUE BORROWER COUNT: &UBC";
	ENDCOMP;
RUN;

PROC PRINTTO;
RUN;

/*print csv file*/
DATA _NULL_;
	SET R4;
	FILE REPORT4 DELIMITER=',' DSD DROPOVER LRECL=32767;;

	IF _N_ = 1 THEN PUT 'CRC Code,Current Loans,Loans in Forbearance,Loans in Deferment,Loans in Suspense/Specialty,Delinquent Loans,Other Loans';

	PUT LF_FED_CLC_RSK $ @;
	PUT CURR $ @;
	PUT FORB $ @;
	PUT DEFR $ @;
	PUT SUSP $ @;
	PUT DELQ $ @;
	PUT OTHR $ ;
RUN;
/*
DATA _NULL_;
	SET  MSBTFF;
	FILE 'T:\SAS\UTNWR06.detail.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;
	if _n_ = 1 then do;
	   put
	      "DF_SPE_ACC_ID"
	   ','
	      "BF_SSN"
	   ','
	      "LN_SEQ"
	   ','
	      "IC_LON_PGM"
	   ','
	      "LC_FED_PGM_YR"
	   ','
	      "LF_FED_CLC_RSK"
	   ','
	      "LA_CUR_PRI"
	   ','
	      "LA_NSI_OTS"
	   ','
	      "LA_FAT_LTE_FEE"
	   ','
	      "DAYS_DELQ"
	   ','
	      "LA_OTS_PRI_ELG"
	   ','
	      "WC_DW_LON_STA"
	   ','
	      "LS"
	   ','
	      "BSC"
	   ','
	      "LOAN_IND";
	end;
	   format DF_SPE_ACC_ID $10. ;
	   format BF_SSN $9. ;
	   format LN_SEQ 6. ;
	   format IC_LON_PGM $6. ;
	   format LC_FED_PGM_YR $3. ;
	   format LF_FED_CLC_RSK $6. ;
	   format LA_CUR_PRI 10.2 ;
	   format LA_NSI_OTS best12. ;
	   format LA_FAT_LTE_FEE best12. ;
	   format DAYS_DELQ 11. ;
	   format LA_OTS_PRI_ELG 10.2 ;
	   format WC_DW_LON_STA $2. ;
	   format LS $2. ;
	   format BSC best12. ;
	   format LOAN_IND best12. ;
	 do;
	   put DF_SPE_ACC_ID $ @;
	   put BF_SSN $ @;
	   put LN_SEQ @;
	   put IC_LON_PGM $ @;
	   put LC_FED_PGM_YR $ @;
	   put LF_FED_CLC_RSK $ @;
	   put LA_CUR_PRI @;
	   put LA_NSI_OTS @;
	   put LA_FAT_LTE_FEE @;
	   put DAYS_DELQ @;
	   put LA_OTS_PRI_ELG @;
	   put WC_DW_LON_STA $ @;
	   put LS $ @;
	   put BSC @;
	   put LOAN_IND ;
	end;
run;
*/
