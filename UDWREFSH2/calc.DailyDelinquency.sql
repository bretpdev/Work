USE UDW
GO

DELETE
	calc.DailyDelinquency
WHERE
	DATEDIFF(Month, AddedAt, GETDATE()) > 16

MERGE
	calc.DailyDelinquency DD
USING
	(
		SELECT
			LN16.BF_SSN,
			LN16.LN_SEQ,
			CAST(LN16.LD_DLQ_OCC AS DATE) AS LD_DLQ_OCC,
			CASE 
				WHEN DATEPART(DW, GETDATE()) = 1 THEN LN16.LN_DLQ_MAX + 2 --ACCOUNT FOR WAREHOUSE NOT UPDATING ON SUNDAY
				ELSE LN16.LN_DLQ_MAX + 1
			END LN_DLQ_MAX,
			CAST(GETDATE() AS DATE) AS AddedAt
		FROM	
			LN16_LON_DLQ_HST LN16
			INNER JOIN LN10_LON LN10
				ON LN10.BF_SSN = LN16.BF_SSN
				AND LN10.LN_SEQ = LN16.LN_SEQ
		WHERE
			LN16.LC_STA_LON16 = '1'
			AND LN16.LC_DLQ_TYP = 'P' -- INSTALLMENT
			AND 
			(
				DATEADD(DAY,-5,CAST(GETDATE() AS DATE)) <= LN16.LD_DLQ_MAX --Account for AES holidays that we dont honor
				OR LN16.LN_DLQ_MAX >= 360
			)
			AND LN10.LA_CUR_PRI > 0.00
			AND LN10.LC_STA_LON10 = 'R'
	) LN16
		ON LN16.BF_SSN = DD.BF_SSN
		AND LN16.LN_SEQ = DD.LN_SEQ
		AND LN16.LD_DLQ_OCC = DD.LD_DLQ_OCC
		AND LN16.AddedAt = DD.AddedAt
	WHEN MATCHED THEN UPDATE SET
			DD.LN_DLQ_MAX = LN16.LN_DLQ_MAX
	WHEN NOT MATCHED THEN
		INSERT
		(
			 [BF_SSN],
			 [LN_SEQ],
			 [LD_DLQ_OCC],
			 [LN_DLQ_MAX],
			 [AddedAt]
		)
		VALUES
		(
			LN16.[BF_SSN],
			LN16.[LN_SEQ],
			LN16.[LD_DLQ_OCC],
			LN16.[LN_DLQ_MAX],
			LN16.[AddedAt]
		)
;