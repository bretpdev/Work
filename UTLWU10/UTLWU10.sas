*-----------------------------------------------------------------------*
| UTLWU10 - QC - CO-BORROWER OR ENDORSER ACCOUNTS WITH NO M1411 COMMENT |
*-----------------------------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWU10.LWU10R2";
FILENAME REPORTZ "&RPTLIB/ULWU10.LWU10RZ";
DATA _NULL_;
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.));
RUN;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CEWNM AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID_BR 
	,A.DF_PRS_ID_EDS
	,B.AD_PRC
/*BORROWER DEMOGRAPHICS*/
	,BWR.DF_SPE_ACC_ID 		AS BR_DF_SPE_ACC_ID
	,BWR.DM_PRS_LST 		AS BR_DM_PRS_LST
	,BWR.DM_PRS_1 			AS BR_DM_PRS_1
	,BWR_ADD.DX_STR_ADR_1 	AS BR_DX_STR_ADR_1
	,BWR_ADD.DX_STR_ADR_2 	AS BR_DX_STR_ADR_2
	,BWR_ADD.DC_DOM_ST 		AS BR_DC_DOM_ST
	,BWR_ADD.DM_CT 			AS BR_DM_CT
	,BWR_ADD.DF_ZIP 		AS BR_DF_ZIP
	,BWR_ADD.DI_VLD_ADR 	AS BR_DI_VLD_ADR
	,BWR_ADD.DN_PHN 		AS BR_DN_PHN
	,BWR_ADD.DI_PHN_VLD 	AS BR_DI_PHN_VLD
/*ENDORSER DEMOGRAPHICS*/
	,EDR.DF_SPE_ACC_ID 		AS EDR_DF_SPE_ACC_ID
	,EDR.DM_PRS_LST 		AS EDR_DM_PRS_LST
	,EDR.DM_PRS_1 			AS EDR_DM_PRS_1
	,EDR_ADD.DX_STR_ADR_1 	AS EDR_DX_STR_ADR_1
	,EDR_ADD.DX_STR_ADR_2 	AS EDR_DX_STR_ADR_2
	,EDR_ADD.DC_DOM_ST 		AS EDR_DC_DOM_ST
	,EDR_ADD.DM_CT 			AS EDR_DM_CT
	,EDR_ADD.DF_ZIP 		AS EDR_DF_ZIP
	,EDR_ADD.DI_VLD_ADR 	AS EDR_DI_VLD_ADR
	,EDR_ADD.DN_PHN 		AS EDR_DN_PHN
	,EDR_ADD.DI_PHN_VLD 	AS EDR_DI_PHN_VLD
	,A.AC_EDS_TYP
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA C
	ON B.AF_APL_ID = C.AF_APL_ID
	AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
/*DEMOGRAPHIC INFO FOR BORROWER AND ENDORSER*/
INNER JOIN OLWHRM1.PD01_PDM_INF BWR
	ON A.DF_PRS_ID_BR = BWR.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD03_PRS_ADR_PHN BWR_ADD
	ON A.DF_PRS_ID_BR = BWR_ADD.DF_PRS_ID
	AND BWR_ADD.DC_ADR = 'L'
INNER JOIN OLWHRM1.PD01_PDM_INF EDR
	ON A.DF_PRS_ID_EDS = EDR.DF_PRS_ID
LEFT OUTER JOIN OLWHRM1.PD03_PRS_ADR_PHN EDR_ADD
	ON A.DF_PRS_ID_EDS = EDR_ADD.DF_PRS_ID
	AND EDR_ADD.DC_ADR = 'L'
/*ACTION CODE EXISTANCE*/
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY BWR_ACT
	ON A.DF_PRS_ID_BR = BWR_ACT.DF_PRS_ID
	AND BWR_ACT.PF_ACT = 'M1411'
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY EDR_ACT
	ON A.DF_PRS_ID_EDS = EDR_ACT.DF_PRS_ID
	AND EDR_ACT.PF_ACT = 'M1411'
WHERE C.AC_STA_GA14 = 'A'
AND C.AC_LON_STA_TYP IN (
	'CR','DA','FB','IA','ID','IG',
	'IM','RF','RP','UA','UB'
	)
AND A.AC_EDS_TYP IN (
	'E','C'
	)
AND 
	(
		BWR_ACT.DF_PRS_ID IS NULL OR 
		EDR_ACT.DF_PRS_ID IS NULL
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWU10.LWU10RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA CEWNM;
SET WORKLOCL.CEWNM;
RUN;

PROC SORT DATA=CEWNM;
BY BR_DF_SPE_ACC_ID;
RUN;

DATA _NULL_;
SET CEWNM ;
LENGTH DESCRIPTION $600.;
USER = ' ';
ACT_DT = AD_PRC ;
DESCRIPTION = CATX(',',
	BR_DF_SPE_ACC_ID
	,BR_DX_STR_ADR_1 
	,BR_DX_STR_ADR_2 
	,BR_DC_DOM_ST 
	,BR_DM_CT 
	,BR_DF_ZIP
	,BR_DI_VLD_ADR
	,BR_DI_PHN_VLD
	,EDR_DF_SPE_ACC_ID 
	,EDR_DX_STR_ADR_1 
	,EDR_DX_STR_ADR_2 
	,EDR_DC_DOM_ST 
	,EDR_DM_CT 
	,EDR_DF_ZIP
	,EDR_DI_VLD_ADR 
	,EDR_DI_PHN_VLD
);
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
FORMAT USER $10. ;
FORMAT ACT_DT MMDDYY10. ;
FORMAT DESCRIPTION $600. ;
IF _N_ = 1 THEN DO;
	PUT "USER,ACT_DT,DESCRIPTION";
END;
DO;
   PUT USER $ @;
   PUT ACT_DT @;
   PUT DESCRIPTION $ ;
END;
RUN;
