/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/ULWA37.LWA37RZ";
FILENAME REPORT2 "&RPTLIB/ULWA37.LWA37R2";
FILENAME REPORT3 "&RPTLIB/ULWA37.LWA37R3";


/*In production this job will always be run against the previous month:*/
/*To run for a different month change the value of run_mon*/
data _null_;
run_mon = -1;
	call symput('begin',intnx('month',today(),run_mon,'B'));
	call symput('FINISH',intnx('month',today(),run_mon,'E'));
	CALL SYMPUT('REP_END',PUT(INTNX('MONTH',TODAY(),run_mon,'END'),WORDDATE.));
RUN;

/*If a report for a period other than a monthly period is desired, 
uncomment the following 2 lines, and insert the date beginning and end points:*/
/*%let begin = '01jul2010'd;*/
/*%let finish = '30jun2011'd;*/


LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
%syslput begin = &begin;
%syslput finish = &finish;
RSUBMIT;
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;

PROC SQL;
CREATE TABLE INDEN AS
select distinct c.LF_LON_ALT
		,c.LN_LON_ALT_SEQ
		,C.LA_CUR_PRI
		,coalesce(CASE
			WHEN SUBSTR(IF_BND_ISS,1,3) = 'E88'	OR SUBSTR(IF_BND_ISS,1,2) = '88' 
				THEN '88 INDENTURE'
			WHEN SUBSTR(IF_BND_ISS,1,2) = '93' THEN '93 Indenture'
			WHEN SUBSTR(IF_BND_ISS,1,2) = '08' THEN '08 Indenture'
			WHEN SUBSTR(IF_BND_ISS,1,2) = '10' THEN '10 Indenture'
			WHEN SUBSTR(IF_BND_ISS,1,2) = '00' THEN 'STNF'
			ELSE IF_BND_ISS
		END, C.BF_SSN) as  INDENTURE
	from DLGSUTWH.LN10_LON C
	inner JOIN (SELECT BF_SSN
				,LN_SEQ
				,IF_OWN
				,MAX(LN_LON_OWN_SEQ) AS OWN_SEQ
			FROM DLGSUTWH.LN35_LON_OWN
			GROUP BY BF_SSN, LN_SEQ, IF_OWN	
	) F
		ON C.BF_SSN = F.BF_SSN
		AND C.LN_SEQ = F.LN_SEQ
		AND C.LF_LON_CUR_OWN = F.IF_OWN
	inner JOIN DLGSUTWH.LN35_LON_OWN E
		ON C.BF_SSN = E.BF_SSN
		AND C.LN_SEQ = E.LN_SEQ
		AND C.LF_LON_CUR_OWN  = E.IF_OWN
		AND F.OWN_SEQ = E.LN_LON_OWN_SEQ ;
QUIT;
PROC SORT DATA=INDEN; BY LF_LON_ALT LN_LON_ALT_SEQ DESCENDING LA_CUR_PRI; RUN;
DATA INDEN(DROP=LA_CUR_PRI); 
SET INDEN;  
BY LF_LON_ALT LN_LON_ALT_SEQ DESCENDING LA_CUR_PRI; 
	IF FIRST.LN_LON_ALT_SEQ;
RUN;

PROC SQL;
CREATE TABLE DEMO AS
SELECT DISTINCT B.AF_CUR_LON_SER_AGY
	,B.AF_CUR_LON_OPS_LDR
	,C.INDENTURE
	,CASE 
		WHEN A.LC_PCL_REA IN ('BC','BH','BO') THEN 'BANKRUPTCY'
		WHEN A.LC_PCL_REA IN ('DB','DF','DQ') THEN 'DEFAULT'
		WHEN A.LC_PCL_REA = 'CS' THEN 'CLOSED SCHOOL'
		WHEN A.LC_PCL_REA = 'DE' THEN 'DEATH'
		WHEN A.LC_PCL_REA = 'DI' THEN 'DISABILITY'
		WHEN A.LC_PCL_REA = 'DU' THEN 'ABBREVIATED CURE'
		WHEN A.LC_PCL_REA = 'FC' THEN 'FALSE CERTIFICATION'
		WHEN A.LC_PCL_REA = 'IN' THEN 'INELIGIBLE BORROWER'
		else ''
	 END AS PCL_REA
	,B.AC_LON_TYP
	,SUM(A.LA_CLM_PRI) AS LA_CLM_PRI
	,SUM(A.LA_CLM_INT) AS LA_CLM_INT
	,SUM(A.LA_CLM_UNS) AS LA_CLM_UNS
	,SUM(CASE
			WHEN A.LA_CLM_UNS > 0 THEN round(A.LA_CLM_PRI * .02,.01)
			ELSE 0
		END) AS LDR_PRI 
	,SUM(CASE
			WHEN A.LA_CLM_UNS > 0 THEN round(A.LA_CLM_INT * .02,.01)
			ELSE 0
		END) AS LDR_INT 
	,COUNT(*) AS TOT_LOANS
	,COUNT(DISTINCT A.BF_SSN) AS TOT_BORR
FROM	DLGSUTWH.DC01_LON_CLM_INF A
INNER JOIN DLGSUTWH.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
LEFT OUTER JOIN INDEN C
	ON B.AF_APL_ID = C.LF_LON_ALT
	AND B.AF_APL_ID_SFX = PUT(C.LN_LON_ALT_SEQ,Z2.0)
	AND B.AF_CUR_LON_SER_AGY in ('700126')
WHERE A.LD_LDR_POF BETWEEN &begin AND &finish
	AND B.AF_CUR_LON_SER_AGY IN ('700126','700121','700191')
	AND A.LC_STA_DC10 in ('03','04')
GROUP BY B.AF_CUR_LON_SER_AGY
	,B.AF_CUR_LON_OPS_LDR	
	,C.INDENTURE
	,PCL_REA
	,B.AC_LON_TYP
;
QUIT;

ENDRSUBMIT;
DATA DEMO;
	SET WORKLOCL.DEMO;
RUN;
PROC SORT DATA=DEMO;
	BY AF_CUR_LON_SER_AGY INDENTURE;
RUN;
DATA DEMO;
SET DEMO;
LDR_INT = LA_CLM_UNS - ldr_pri;
	LDR_PRI = LA_CLM_PRI - LDR_PRI;
	LDR_INT = LA_CLM_INT - LDR_INT;
RUN;



PROC PRINTTO PRINT=REPORT2 NEW;
RUN;

/*FOR LANDSCAPE REPORTS:*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1;
title ;
footnote;
TITLE 'Monthly Claims Paid Report for UHEAA';
TITLE2 "FOR THE MONTH ENDING &REP_END ";
FOOTNOTE   'JOB = UTLWA37  	 REPORT = ULWA37.LWA37R2';

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
WHERE AF_CUR_LON_SER_AGY = '700126';
FORMAT LA_CLM_PRI LA_CLM_INT LA_CLM_UNS DOLLAR18.2;
VAR AF_CUR_LON_SER_AGY INDENTURE PCL_REA AC_LON_TYP TOT_BORR TOT_LOANS 
	LA_CLM_PRI LA_CLM_INT LDR_PRI LDR_INT  ;
LABEL AF_CUR_LON_SER_AGY = 'SERVICER ID'
	PCL_REA = 'CLAIM TYPE'
	AC_LON_TYP = 'LOAN TYPE'
	TOT_BORR = 'NUMBER OF BORROWERS'
	TOT_LOANS = 'NUMBER OF LOANS'
	LA_CLM_PRI = 'BORROWER RESPONSIBILITY PRINCIPAL'
	LA_CLM_INT = 'BORROWER RESPONSIBILITY INTEREST'
	LDR_PRI = 'PAID TO LENDER PRINCIPAL'
	LDR_INT = 'PAID TO LENDER INTEREST'
;
RUN;

PROC PRINTTO PRINT=REPORT3 NEW;
RUN;
/*FOR LANDSCAPE REPORTS:*/
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PAGENO=1;
TITLE 'Monthly Claims Paid Report for Nelnet and Sallie Mae';
TITLE2 "FOR THE MONTH ENDING &REP_END ";
FOOTNOTE   'JOB = UTLWA37  	 REPORT = ULWA37.LWA37R3';

PROC PRINT NOOBS SPLIT='/' DATA=DEMO WIDTH=UNIFORM WIDTH=MIN LABEL;
WHERE AF_CUR_LON_SER_AGY in ('700121','700191');
FORMAT LA_CLM_PRI LA_CLM_INT LDR_PRI LDR_INT DOLLAR18.2;
VAR AF_CUR_LON_SER_AGY AF_CUR_LON_OPS_LDR PCL_REA AC_LON_TYP 
TOT_BORR TOT_LOANS LA_CLM_PRI LA_CLM_INT LDR_PRI LDR_INT ;
LABEL AF_CUR_LON_SER_AGY = 'SERVICER ID'
	AF_CUR_LON_OPS_LDR = 'LENDER ID'
	PCL_REA = 'CLAIM TYPE'
	AC_LON_TYP = 'LOAN TYPE'
	TOT_BORR = 'NUMBER OF BORROWERS'
	TOT_LOANS = 'NUMBER OF LOANS'
	LA_CLM_PRI = 'BORROWER RESPONSIBILITY PRINCIPAL'
	LA_CLM_INT = 'BORROWER RESPONSIBILITY INTEREST'
	LDR_PRI = 'PAID TO LENDER PRINCIPAL'
	LDR_INT = 'PAID TO LENDER INTEREST'
;

RUN;
PROC PRINTTO;
RUN;
