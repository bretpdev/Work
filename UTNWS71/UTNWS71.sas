%LET REPORT_DATE = TODAY();
%LET RPTLIB = T:\SAS;
FILENAME REPORTZ "&RPTLIB/UNWS71.NWS71RZ";
FILENAME REPORT2 "&RPTLIB/UNWS71.NWS71R2";

/*LIBNAME SAS_TAB V8 '/sas/whse/progrevw';*/
LIBNAME SAS_TAB V8 'Y:\Development\SAS Test Files\progrevw';

DATA _NULL_;
	SET SAS_TAB.CAMPAIGNS;
	CALL SYMPUT('BEG_DATE',BEGIN_DATE);
	CALL SYMPUT('END_DATE',END_DATE);
	WHERE &REPORT_DATE BETWEEN BEGIN_DATE AND END_DATE
		AND CAMPAIGN_NAME LIKE '%Late Stage Resolution';
RUN;

%SYSLPUT REPORT_DATE = &REPORT_DATE;
%SYSLPUT BEG_DATE = &BEG_DATE;
%SYSLPUT END_DATE = &END_DATE;

LIBNAME  LEGEND  REMOTE  SERVER=LEGEND SLIBREF=work;

PROC SQL;
      CREATE TABLE CURRENT_CAMPAIGN_BORROWERS AS
            SELECT
                  BORR.DF_SPE_ACC_ID
            FROM
                  SAS_TAB.CAMPAIGNS CAMP
                  JOIN SAS_TAB.CAMPAIGN_BORROWERS BORR
                        ON CAMP.CAMPAIGN_ID = BORR.CAMPAIGN_ID
            WHERE
                  &REPORT_DATE BETWEEN BEGIN_DATE AND END_DATE
				  AND CAMP.CAMPAIGN_NAME LIKE '%Late Stage Resolution'
      ;
QUIT;

PROC SQL;
      CREATE TABLE CURRENT_CAMPAIGN_ARCS AS
            SELECT
                  ARCS.ARC
            FROM
                  SAS_TAB.CAMPAIGNS CAMP
                  JOIN SAS_TAB.CAMPAIGN_ARCS ARCS
                        ON CAMP.CAMPAIGN_ID = ARCS.CAMPAIGN_ID
            WHERE
                  &REPORT_DATE BETWEEN BEGIN_DATE AND END_DATE
				  AND CAMP.CAMPAIGN_NAME LIKE '%Late Stage Resolution'
      ;
QUIT;

DATA LEGEND.CURRENT_CAMPAIGN_BORROWERS; SET CURRENT_CAMPAIGN_BORROWERS; RUN;
DATA LEGEND.CURRENT_CAMPAIGN_ARCS; SET CURRENT_CAMPAIGN_ARCS; RUN;

RSUBMIT LEGEND;
/*%let DB = DNFPRQUT;  *This is test;*/
/*%let DB = DNFPRUUT;  *This is VUK3 test;*/
%LET DB = DNFPUTDL;  *This is live;

LIBNAME PKUB DB2 DATABASE=&DB OWNER=PKUB;

PROC SQL;
	CREATE TABLE BASE_POP AS
		SELECT DISTINCT
			LN10.BF_SSN
			,PD10.DF_SPE_ACC_ID
/*			,DLQS.LN_DLQ_MAX*/
		FROM
			PKUB.LN10_LON LN10
			INNER JOIN PKUB.PD10_PRS_NME PD10
				ON LN10.BF_SSN = PD10.DF_PRS_ID		
			INNER JOIN CURRENT_CAMPAIGN_BORROWERS CCB
				ON PD10.DF_SPE_ACC_ID = CCB.DF_SPE_ACC_ID	
			LEFT JOIN (
					SELECT
						A.BF_SSN
					FROM
						PKUB.LN16_LON_DLQ_HST A
						INNER JOIN PKUB.PD10_PRS_NME B
							ON A.BF_SSN = B.DF_PRS_ID
						INNER JOIN CURRENT_CAMPAIGN_BORROWERS C
							ON B.DF_SPE_ACC_ID = C.DF_SPE_ACC_ID
					WHERE 
						A.LC_STA_LON16 = '1'
						) EXLD
				ON LN10.BF_SSN = EXLD.BF_SSN
			LEFT JOIN (
					SELECT
						A.BF_SSN
						,MAX.LN_DLQ_MAX
					FROM 
						PKUB.LN16_LON_DLQ_HST A
					INNER JOIN (
							SELECT
								Z.BF_SSN
								,MAX(Z.LN_DLQ_MAX) AS LN_DLQ_MAX
							FROM PKUB.LN16_LON_DLQ_HST Z
								INNER JOIN PKUB.PD10_PRS_NME Y
									ON Z.BF_SSN = Y.DF_PRS_ID
								INNER JOIN CURRENT_CAMPAIGN_BORROWERS X
									ON Y.DF_SPE_ACC_ID = X.DF_SPE_ACC_ID
							WHERE LC_STA_LON16 = '1'
							GROUP BY Z.BF_SSN
								) MAX
						ON A.BF_SSN = MAX.BF_SSN
					WHERE MAX.LN_DLQ_MAX < 29
						) DLQS
				ON LN10.BF_SSN = DLQS.BF_SSN
		WHERE 
				LN10.LC_STA_LON10 = 'R'
				AND
				LN10.LA_CUR_PRI > 0
				AND
				(EXLD.BF_SSN IS NULL OR DLQS.BF_SSN IS NOT NULL)			
;

PROC SQL;
CREATE TABLE THREE AS
	SELECT 
		BP.BF_SSN
		,BP.DF_SPE_ACC_ID
	FROM BASE_POP BP
		LEFT JOIN (
				SELECT
					A.BF_SSN
				FROM BASE_POP A
					INNER JOIN PKUB.DW01_DW_CLC_CLU B
						ON A.BF_SSN = B.BF_SSN
				WHERE B.WC_DW_LON_STA <> '03'
					) EXLD
			ON BP.BF_SSN = EXLD.BF_SSN
	WHERE EXLD.BF_SSN IS NULL
;

CREATE TABLE FBR AS
	SELECT DISTINCT
		P.BF_SSN
		,P.DF_SPE_ACC_ID
	FROM
		BASE_POP P
		INNER JOIN PKUB.FB10_BR_FOR_REQ FB10 
			ON FB10.BF_SSN = P.BF_SSN	
		INNER JOIN PKUB.LN60_BR_FOR_APV LN60 
			ON LN60.BF_SSN = FB10.BF_SSN 
			AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
		INNER JOIN (
						SELECT
							FB10.BF_SSN,
							MAX(LN60.LD_FOR_END) AS LD_FOR_END_MAX
						FROM
							BASE_POP P
							INNER JOIN PKUB.LN60_BR_FOR_APV LN60 
								ON LN60.BF_SSN = P.BF_SSN
							INNER JOIN PKUB.DW01_DW_CLC_CLU DW01
								ON P.BF_SSN = DW01.BF_SSN
								AND LN60.LN_SEQ = DW01.LN_SEQ
							INNER JOIN PKUB.FB10_BR_FOR_REQ FB10 
								ON FB10.BF_SSN = LN60.BF_SSN
								AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM							
								AND FB10.LC_FOR_STA = 'A'
								AND FB10.LC_STA_FOR10 = 'A'
								AND DW01.WC_DW_LON_STA = '05'
/*								AND LN60.LD_FOR_END < &END_DATE - 1*/
						WHERE
							LN60.LC_STA_LON60 = 'A'
/*							AND LN60.LD_FOR_APL BETWEEN &BEG_DATE AND &END_DATE*/
						GROUP BY
							FB10.BF_SSN
					) LN60M 
			ON LN60M.BF_SSN = P.BF_SSN 
			AND LN60M.LD_FOR_END_MAX = LN60.LD_FOR_END	
			AND FB10.LC_FOR_TYP NOT IN ('13', '14', '21', '33', '37', '38')
	WHERE
			LN60M.LD_FOR_END_MAX < &END_DATE - 1
		AND LN60.LD_FOR_APL BETWEEN &BEG_DATE AND &END_DATE
;

CREATE TABLE DFR AS
	SELECT DISTINCT
		P.BF_SSN
		,P.DF_SPE_ACC_ID
	FROM
		BASE_POP P
		INNER JOIN PKUB.DF10_BR_DFR_REQ DF10 
			ON DF10.BF_SSN = P.BF_SSN	
		INNER JOIN PKUB.LN50_BR_DFR_APV LN50 
			ON LN50.BF_SSN = DF10.BF_SSN 
			AND LN50.LF_DFR_CTL_NUM = DF10.LF_DFR_CTL_NUM
		INNER JOIN
				(
					SELECT
						DF10.BF_SSN,
						MAX(LN50.LD_DFR_END) AS LD_DFR_END_MAX
					FROM
						BASE_POP P
						INNER JOIN PKUB.LN50_BR_DFR_APV LN50 
							ON LN50.BF_SSN = P.BF_SSN
						INNER JOIN PKUB.DW01_DW_CLC_CLU DW01
								ON P.BF_SSN = DW01.BF_SSN
								AND LN50.LN_SEQ = DW01.LN_SEQ
						INNER JOIN PKUB.DF10_BR_DFR_REQ DF10 
							ON DF10.BF_SSN = LN50.BF_SSN
							AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM
							AND DF10.LC_DFR_STA = 'A'
							AND DF10.LC_STA_DFR10 = 'A'
							AND DW01.WC_DW_LON_STA = '04'
					WHERE
						LN50.LC_STA_LON50 = 'A'
					
					GROUP BY
						DF10.BF_SSN
				) LN50M 
			ON LN50M.BF_SSN = P.BF_SSN 
			AND LN50M.LD_DFR_END_MAX = LN50.LD_DFR_END

	WHERE
			LN50M.LD_DFR_END_MAX < &END_DATE - 1
		AND LN50.LD_DFR_APL BETWEEN &BEG_DATE AND &END_DATE
;
QUIT;

DATA JOINT;
	SET THREE FBR DFR;
RUN;

PROC SORT DATA=JOINT NODUPKEY; BY BF_SSN; RUN;

PROC SQL;
CREATE TABLE FNL AS
	SELECT DISTINCT
		J.DF_SPE_ACC_ID 
		,AY1.PF_REQ_ACT
		,AY2.LF_USR_REQ_ATY AS LST_USR_NOT
		,AY3.LF_USR_REQ_ATY AS LST_USR_SPK
	FROM 
		JOINT J
		LEFT JOIN (
				SELECT
					AY10.BF_SSN
					,AY10.PF_REQ_ACT
				FROM 
					PKUB.AY10_BR_LON_ATY AY10
					INNER JOIN (
							SELECT DISTINCT
									A.BF_SSN
									,MAX(B.LF_LST_DTS_AY10) AS LF_LST_DTS_AY10_MAX
							FROM JOINT A
								INNER JOIN PKUB.AY10_BR_LON_ATY B
									ON A.BF_SSN = B.BF_SSN
								INNER JOIN CURRENT_CAMPAIGN_ARCS C
									ON B.PF_REQ_ACT = C.ARC
							WHERE B.LD_ATY_REQ_RCV BETWEEN &BEG_DATE AND &END_DATE
							GROUP BY A.BF_SSN
								) MAX
						ON AY10.BF_SSN = MAX.BF_SSN
						AND AY10.LF_LST_DTS_AY10 = MAX.LF_LST_DTS_AY10_MAX
					) AY1
			ON J.BF_SSN = AY1.BF_SSN
		LEFT JOIN (
				SELECT
					AY10.BF_SSN
					,AY10.LF_USR_REQ_ATY
				FROM 
					PKUB.AY10_BR_LON_ATY AY10
					INNER JOIN (
							SELECT DISTINCT
								A.BF_SSN
								,MAX(B.LN_ATY_SEQ) AS LN_ATY_SEQ_MAX
							FROM JOINT A
								INNER JOIN PKUB.AY10_BR_LON_ATY B
									ON A.BF_SSN = B.BF_SSN
							WHERE B.LF_USR_REQ_ATY LIKE 'UT%'
								AND B.LF_USR_REQ_ATY NOT IN ('UT00204', 'UT00205', 'UT00221', 'UT00240', 'UT00314', 'UT00553', 'UT00554', 'UT00555', 'UT00564',
															'UT00801', 'UT00802', 'UT00803', 'UT00804', 'UT00805', 'UT00806', 'UT00807', 'UT00808', 'UT00809', 'UT00810') 
							GROUP BY A.BF_SSN
								) MAX
						ON AY10.BF_SSN = MAX.BF_SSN
						AND AY10.LN_ATY_SEQ = MAX.LN_ATY_SEQ_MAX
					) AY2
			ON J.BF_SSN = AY2.BF_SSN
		LEFT JOIN (
				SELECT
					AY10.BF_SSN
					,AY10.LF_USR_REQ_ATY
				FROM 
					PKUB.AY10_BR_LON_ATY AY10
					INNER JOIN (
							SELECT DISTINCT
								A.BF_SSN
								,MAX(B.LF_LST_DTS_AY10) AS LF_LST_DTS_AY10_MAX
							FROM JOINT A
								INNER JOIN PKUB.AY10_BR_LON_ATY B
									ON A.BF_SSN = B.BF_SSN
							WHERE B.LF_USR_REQ_ATY LIKE 'UT%'
								AND B.PF_REQ_ACT IN ('P199A','P200C')
								AND B.PF_RSP_ACT IN ('CNTCT')
							GROUP BY A.BF_SSN
								) MAX
						ON AY10.BF_SSN = MAX.BF_SSN
						AND AY10.LF_LST_DTS_AY10 = MAX.LF_LST_DTS_AY10_MAX
					) AY3
			ON J.BF_SSN = AY3.BF_SSN
;
QUIT;

ENDRSUBMIT;

DATA FNL; SET LEGEND.FNL; RUN;

/*write to comma delimited file*/
DATA _NULL_;
	SET		WORK.FNL;
	FILE
		REPORT2
		DELIMITER = ','
		DSD
		DROPOVER
		LRECL = 32767
	;

	/* write column names, remove this to create a file without a header row */
	IF _N_ = 1 THEN
		DO;
			PUT	
				'Borrower Account Number'
				','
				'Most Recent ARC Late Stage'
				','
				'Last User to Note the Account'
				','
				'Last User who Spoke to Borrower'
			;
		END;

	/* write data*/	
	DO;
		PUT DF_SPE_ACC_ID $ @;
		PUT PF_REQ_ACT $ @;
		PUT LST_USR_NOT $ @;
		PUT LST_USR_SPK $ ;
		;
	END;
RUN;
