USE ULS
GO

CREATE PROCEDURE dbo.FinalBillNotication
AS

DECLARE 
	@Arc VARCHAR(5) = 'FNBIL',
	@Today DATE = GETDATE(),
	@FutureDate DATE = DATEADD(YEAR, 1, GETDATE())
SELECT
	PD10.DF_SPE_ACC_ID AS AccountNumber,
	LN80.LN_SEQ AS LoanSequence,
	LN80.LD_BIL_CRT AS BillCreateDate,
	LN80.LN_SEQ_BIL_WI_DTE,
	LN80.LN_BIL_OCC_SEQ,
	LN80.LI_FNL_BIL_LON
FROM
	UDW..PD10_PRS_NME PD10
	INNER JOIN UDW..LN10_LON LN10
		ON LN10.BF_SSN = PD10.DF_PRS_ID
	INNER JOIN 
	(
		SELECT
			LN80.BF_SSN,
			LN80.LN_SEQ,
			LN80.LD_BIL_CRT,
			LN80.LN_SEQ_BIL_WI_DTE,
			LN80.LN_BIL_OCC_SEQ,
			LN80.LI_FNL_BIL_LON,
			LN80.LC_STA_LON80,
			LN80.LD_STA_LON80,
			LN80.LA_BIL_CUR_DU,
			LN80.LA_BIL_PAS_DU,
			MAX(LD_STA_LON80) OVER (PARTITION BY BF_SSN, LN_SEQ) STA_MAX
		FROM
			UDW..LN80_LON_BIL_CRF LN80
	) LN80
		ON LN80.BF_SSN = LN10.BF_SSN
		AND LN80.LN_SEQ = LN10.LN_SEQ
		AND LN80.LD_STA_LON80 = LN80.STA_MAX
	--Account must have the FNBIL Arc as well
	INNER JOIN 
	(
		SELECT
			LN85.BF_SSN,
			LN85.LN_SEQ
		FROM
			UDW..AY10_BR_LON_ATY AY10
			INNER JOIN UDW..LN85_LON_ATY LN85 
				ON LN85.LN_ATY_SEQ = AY10.LN_ATY_SEQ 
				AND LN85.BF_SSN = AY10.BF_SSN
		WHERE
			AY10.LC_STA_ACTY10 = 'A'
			AND AY10.PF_REQ_ACT = @Arc
		GROUP BY
			LN85.BF_SSN,
			LN85.LN_SEQ
	) LN85
		ON LN85.BF_SSN = LN80.BF_SSN
		AND LN85.LN_SEQ = LN80.LN_SEQ
	LEFT JOIN 
	(
		SELECT
			RS.BF_SSN,
			RS.LN_SEQ,
			RS.LN_RPS_SEQ,
			RS.LN_GRD_RPS_SEQ,
			RS.TermStartDate,
			RS.GradationMonths,
			RS.LN_RPS_TRM,
			ROW_NUMBER() OVER (PARTITION BY BF_SSN, LN_SEQ ORDER BY LN_RPS_SEQ DESC, LN_GRD_RPS_SEQ DESC) AS [Priority]
		FROM
			UDW.calc.RepaymentSchedules RS
	) RS
		ON RS.BF_SSN = LN85.BF_SSN
		AND RS.LN_SEQ = LN85.LN_SEQ
		AND RS.[Priority] = 1
	LEFT JOIN 
	(
		SELECT
			DW01.BF_SSN,
			DW01.LN_SEQ
		FROM
			UDW..DW01_DW_CLC_CLU DW01
		WHERE
			DW01.WC_DW_LON_STA IN ('06')
	) DW01
		ON DW01.BF_SSN = LN10.BF_SSN
		AND DW01.LN_SEQ = LN10.LN_SEQ
WHERE
	--The flag does not help indicate issues
	LN80.LI_FNL_BIL_LON = 'Y'
	AND DW01.BF_SSN IS NULL
	AND LN80.LC_STA_LON80 = 'A'
	AND LN10.LC_STA_LON10 = 'R'
	AND LN10.LA_CUR_PRI > 0.00
	--We want people who do not satisfy their outstanding principal with the current bill but have the final bill arc
	AND 
	(
		ISNULL(LN80.LA_BIL_CUR_DU,0.00) + ISNULL(LN80.LA_BIL_PAS_DU, 0.00) < LN10.LA_CUR_PRI
		OR @Today < DATEADD(MONTH, ISNULL(RS.LN_RPS_TRM - 1, 0), ISNULL(RS.TermStartDate, @FutureDate)) --The isnull exists to make sure the date is in the future
	)
