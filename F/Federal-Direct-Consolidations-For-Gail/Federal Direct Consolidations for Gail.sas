/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
%LET RPTLIB = %SYSGET(reportdir);
FILENAME REPORT2 "&RPTLIB/ULWAB1.LWAB1R2";*/

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE FEDDIR AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT
	C.df_prs_id_stu											AS SSN,
	RTRIM(E.DM_PRS_1)||' '||RTRIM(E.DM_PRS_LST)				AS NAME,
	A.af_apl_id||A.af_apl_id_sfx							AS CLID,
	B.ad_prc												AS GDATE,
	B.aa_gte_lon_amt										AS GAMT,
	C.af_apl_ops_scl										AS ORIGSCH,
	C.af_cur_apl_ops_ldr									AS ORIGLNDR,
	B.af_cur_lon_ops_ldr									AS CURRLNDR,
	C.ad_ist_trm_beg										AS BEGIN,
	C.ad_ist_trm_end										AS END,
	A.ad_lon_sta											AS CONSDATE,
	MONTHNAME(A.ad_lon_sta)||' '||CHAR(YEAR(A.ad_lon_sta))	AS CONSMO,
	MONTH(A.ad_lon_sta)										AS MONO,
	YEAR(A.ad_lon_sta)										AS YRNO
	
FROM	OLWHRM1.GA14_LON_STA A INNER JOIN
		OLWHRM1.GA10_LON_APP B ON
			A.af_apl_id = B.af_apl_id AND
			A.af_apl_id_sfx = B.af_apl_id_sfx AND
			A.ac_lon_sta_typ IN ('PC', 'PN') AND
			A.ad_lon_sta >= CURRENT DATE - 12 MONTHS INNER JOIN
		OLWHRM1.GA01_APP C ON
			A.af_apl_id = C.af_apl_id INNER JOIN
		OLWHRM1.SD02_STU_ENR D ON
			C.df_prs_id_stu = D.df_prs_id_stu AND
			D.lc_sta_sd02 = 'A' AND
			D.lc_stu_enr_sta = 'I' INNER JOIN
		OLWHRM1.PD01_PDM_INF E ON
			C.df_prs_id_stu = E.df_prs_id
			
);
DISCONNECT FROM DB2;
PROC SQL;
CREATE TABLE FDCLID AS
SELECT 
	CONSMO,
	COUNT(CLID)	AS CLIDCT
FROM FEDDIR
GROUP BY CONSMO;
PROC SQL;
CREATE TABLE FDSSNA AS
SELECT DISTINCT
	CONSMO,
	SSN
FROM FEDDIR;
PROC SQL;
CREATE TABLE FDSSN AS
SELECT DISTINCT
	CONSMO,
	COUNT(SSN)	AS SSNCT
FROM FDSSNA
GROUP BY CONSMO;
PROC SQL;
CREATE TABLE FDMOAMT AS
SELECT DISTINCT
	CONSMO,
	SUM(GAMT)	AS MOAMT
FROM FEDDIR
GROUP BY CONSMO;
PROC SQL;
CREATE TABLE FEDDIRC AS
SELECT *
FROM	FEDDIR A INNER JOIN
		FDSSN B ON
			A.CONSMO = B.CONSMO INNER JOIN
		FDCLID C ON
			A.CONSMO = C.CONSMO INNER JOIN
		FDMOAMT D ON
			A.CONSMO = D.CONSMO;
PROC SQL;
CREATE TABLE FDSUM AS
SELECT DISTINCT
	YRNO,
	MONO,
	CONSMO,
	SSNCT,
	CLIDCT,
	MOAMT
FROM FEDDIRC
ORDER BY YRNO, MONO;
PROC SORT DATA=FEDDIRC;
	BY CONSDATE;
RUN;
ENDRSUBMIT;
DATA _NULL_;
     EFFDT = PUT(TODAY()-1, MMDDYY10.);
     CALL SYMPUT('EFFDATE',EFFDT);
RUN;
OPTIONS CENTER NONUMBER LS=132 PS=41;
PROC REPORT DATA = WORKLOCL.FEDDIRC NOWD HEADLINE HEADSKIP SPLIT = '/' SPACING=1;

TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	'LOANS CONSOLIDATED BY FEDERAL DIRECT';
TITLE3	"FOR 12 MONTH PERIOD ENDING &EFFDATE";

COLUMN
	SSN
	NAME
	CLID
	GDATE
	GAMT
	ORIGSCH
	ORIGLNDR
	CURRLNDR
	BEGIN
	END
	CONSDATE
	CONSMO
	CLIDCT
	SSNCT;

DEFINE	CONSMO / GROUP NOPRINT;
DEFINE	SSNCT / GROUP NOPRINT;
DEFINE	CLIDCT / GROUP NOPRINT;
DEFINE 	NAME / WIDTH = 20;
DEFINE  CLID / 'UNIQUE ID';
DEFINE  GDATE / DISPLAY FORMAT = MMDDYY10. 'GUARANTEE/DATE';
DEFINE  GAMT / FORMAT = DOLLAR10. 'GUARANTEE/AMOUNT';
DEFINE  ORIGSCH / 'ORIGINAL/SCHOOL';
DEFINE  ORIGLNDR / 'ORIG/LENDER' WIDTH = 6;
DEFINE  CURRLNDR / 'CURR/HOLDER' WIDTH = 6;
DEFINE  BEGIN / DISPLAY FORMAT = MMDDYY10. 'TERM BEGIN/DATE';
DEFINE  END / DISPLAY FORMAT = MMDDYY10. 'TERM END/DATE';
DEFINE  CONSDATE / DISPLAY FORMAT = MMDDYY10. 'CONSOL/DATE';

COMPUTE BEFORE _PAGE_ / LEFT;
      LINE 'FOR THE MONTH OF ' CONSMO $14.;
	  LINE 'TOTAL NUMBER OF BORROWERS:  ' SSNCT COMMA5.;
	  LINE 'TOTAL NUMBER OF LOANS:      ' CLIDCT COMMA5.;
      LINE ' ';
ENDCOMP;

BREAK AFTER CONSMO / OL SUMMARIZE PAGE;
RUN;
OPTIONS LS=80;
PROC PRINT NOOBS DATA=WORKLOCL.FDSUM SPLIT='/' WIDTH = MIN;
SUM SSNCT CLIDCT MOAMT;
VAR CONSMO SSNCT CLIDCT MOAMT;
LABEL	CONSMO = 'MONTH'
		SSNCT = 'NO OF BORROWERS'
		CLIDCT = 'NO OF LOANS'
		MOAMT = 'AMOUNT GUARANTEED';
FORMAT  SSNCT COMMA6. CLIDCT COMMA6. MOAMT DOLLAR13.;
TITLE1	'UTAH HIGHER EDUCATION ASSISTANCE AUTHORITY';
TITLE2	'LOANS CONSOLIDATED BY FEDERAL DIRECT';
TITLE3	"FOR 12 MONTH PERIOD ENDING &EFFDATE";
RUN;
