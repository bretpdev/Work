*-------------------------------------------------*
| UTLWS40 - Borrower activity prior to resolution |
*-------------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS40.LWS40R2";
FILENAME REPORTZ "&RPTLIB/ULWS40.LWS40RZ";
DATA _NULL_;
	CALL SYMPUT('DAYS_AGO_7',"'"||PUT(INTNX('DAY',TODAY(),-7,'BEGINNING'), MMDDYYD10.)||"'");
	CALL SYMPUT('DAYS_AGO_21',"'"||PUT(INTNX('DAY',TODAY(),-21,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT DAYS_AGO_7 = &DAYS_AGO_7;
%SYSLPUT DAYS_AGO_21 = &DAYS_AGO_21;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
/*DELINQUENCY DATA SET - THIS IS THE BASE DATA SET*/
CREATE TABLE DEFS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.DF_SPE_ACC_ID
	,A.BF_SSN
	,MIN(A.LD_STA_LON16) AS LD_STA_LON16
FROM OLWHRM1.LN16_LON_DLQ_HST A
INNER JOIN OLWHRM1.PD10_PRS_NME B
	ON A.BF_SSN = B.DF_PRS_ID
LEFT OUTER JOIN (
	SELECT BF_SSN
		,LD_STA_LON16 
		,'X' AS ROL_DELQ
	FROM OLWHRM1.LN16_LON_DLQ_HST
	WHERE LC_STA_LON16 = '1'
	) C
	ON A.BF_SSN = C.BF_SSN
	AND A.LD_STA_LON16 = C.LD_STA_LON16
WHERE A.LD_STA_LON16 >= &DAYS_AGO_7
	AND A.LC_STA_LON16 IN ('2','3')
	AND A.LN_DLQ_MAX > 15
	AND C.ROL_DELQ IS NULL
GROUP BY B.DF_SPE_ACC_ID
	,A.BF_SSN
FOR READ ONLY WITH UR
);
/*COMPASS ACTIVITY */
CREATE TABLE AY10 AS
SELECT DISTINCT B.*
FROM DEFS A
INNER JOIN CONNECTION TO DB2 (
	SELECT DISTINCT BF_SSN
		,PF_REQ_ACT
		,LD_ATY_REQ_RCV
	FROM OLWHRM1.AY10_BR_LON_ATY 
	WHERE LD_ATY_REQ_RCV >= &DAYS_AGO_21
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN;
/*ONELINE ACTIVITY*/
CREATE TABLE AY01 AS
SELECT DISTINCT B.*
FROM DEFS A
INNER JOIN CONNECTION TO DB2 (
	SELECT DISTINCT DF_PRS_ID AS BF_SSN
		,PF_ACT
		,BD_ATY_PRF
	FROM OLWHRM1.AY01_BR_ATY
	WHERE BD_ATY_PRF >= &DAYS_AGO_21
	FOR READ ONLY WITH UR
	) B
	ON A.BF_SSN = B.BF_SSN;

DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWS40.LWS40RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA DEFS ;
	SET WORKLOCL.DEFS;
RUN;
DATA AY01 ;
	SET WORKLOCL.AY01;
RUN;
DATA AY10 ;
	SET WORKLOCL.AY10;
RUN;
%MACRO GET_ARCS(TAB,ARC,ARC_TAB,ARC_DT);
	PROC SQL;
		CREATE TABLE &TAB AS
		SELECT DISTINCT DF_SPE_ACC_ID
			,&ARC
		FROM DEFS A
		LEFT OUTER JOIN &ARC_TAB B
			ON A.BF_SSN = B.BF_SSN
		WHERE &ARC_DT BETWEEN LD_STA_LON16 - 14 AND LD_STA_LON16
		ORDER BY DF_SPE_ACC_ID
			,&ARC
		;
	QUIT;
	DATA &TAB;
		SET &TAB;
		LENGTH LIST $2000;
		BY DF_SPE_ACC_ID;
		RETAIN LIST;
		IF FIRST.DF_SPE_ACC_ID THEN 
			LIST = TRIM(&ARC);
		ELSE 
			LIST = TRIM(LIST)||","||TRIM(&ARC);
		IF LAST.DF_SPE_ACC_ID THEN 
			OUTPUT;
	RUN;
%MEND;
%GET_ARCS(CO,PF_REQ_ACT,AY10,LD_ATY_REQ_RCV);
%GET_ARCS(OL,PF_ACT,AY01,BD_ATY_PRF);
PROC SQL;
	CREATE TABLE APDR AS
	SELECT A.DF_SPE_ACC_ID
		,B.LIST AS ARCS
		,C.LIST AS ACTS
	FROM DEFS A
	LEFT OUTER JOIN CO B
		ON A.DF_SPE_ACC_ID = B.DF_SPE_ACC_ID
	LEFT OUTER JOIN OL C
		ON A.DF_SPE_ACC_ID = C.DF_SPE_ACC_ID
	;
QUIT;
DATA _NULL_;
SET APDR;
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT DF_SPE_ACC_ID $10. ;
   FORMAT ARCS $2000. ;
   FORMAT ACTS $2000. ;
	IF _N_ = 1 THEN DO;
		PUT "ACCOUNT NUMBER,ARCS,ACTION CODES";
	END;
	DO;
	   PUT DF_SPE_ACC_ID $ @;
	   PUT ARCS $ @;
	   PUT ACTS $ ;
	END;
RUN;
