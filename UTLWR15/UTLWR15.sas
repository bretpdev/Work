*-----------------------------------------------*
| UTLWR15 - Private Loan Borrower Cancellations |
*-----------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWR15.LWR15R2";
FILENAME REPORTZ "&RPTLIB/ULWR15.LWR15RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE PLBC AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT PD10.DF_SPE_ACC_ID
	,LN10.LN_SEQ
	,LN90.LD_FAT_EFF
	,COALESCE(LN90.LA_FAT_NSI,0) + COALESCE(LN90.LA_FAT_CUR_PRI,0) AS PAMT
FROM OLWHRM1.LN10_LON LN10
INNER JOIN OLWHRM1.LN90_FIN_ATY LN90
	ON LN10.BF_SSN = LN90.BF_SSN
	AND LN10.LN_SEQ = LN90.LN_SEQ
INNER JOIN OLWHRM1.LN15_DSB LN15
	ON LN10.BF_SSN = LN15.BF_SSN
	AND LN10.LN_SEQ = LN15.LN_SEQ
INNER JOIN OLWHRM1.PD10_PRS_NME PD10
	ON PD10.DF_PRS_ID = LN10.BF_SSN
WHERE LN10.IC_LON_PGM IN ('GATEUG','GATEGL','GATEMD')
AND DAYS(LN90.LD_FAT_EFF) <=  DAYS(LN15.LD_DSB) + 30 
AND LN90.LD_FAT_EFF >  LN15.LD_DSB
AND LN90.LC_STA_LON90 = 'A'
AND LN90.LC_FAT_REV_REA = ''
AND LN90.PC_FAT_TYP = '10'
AND LN90.PC_FAT_SUB_TYP = '10' 
AND COALESCE(LN15.LA_DSB,0) - COALESCE(LN15.LA_DSB_CAN,0) = COALESCE(LN90.LA_FAT_NSI,0) + COALESCE(LN90.LA_FAT_CUR_PRI,0)
AND LN15.LC_STA_LON15 IN ('1','3')
AND LN15.LC_DSB_TYP = '2'
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWR15.LWR15RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA PLBC ;
SET WORKLOCL.PLBC;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127;
TITLE 'TITLE ONE';
FOOTNOTE   'JOB = UTLWR15  	 REPORT = ULWR15.LWR15R2';
PROC CONTENTS DATA=PLBC OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 126*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWR15  	 REPORT = ULWR15.LWR15R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=PLBC WIDTH=UNIFORM WIDTH=MIN LABEL;
FORMAT LD_FAT_EFF MMDDYY10.;
VAR DF_SPE_ACC_ID LN_SEQ LD_FAT_EFF PAMT;
LABEL DF_SPE_ACC_ID  = 'ACCOUNT #'
	LN_SEQ  = 'LOAN SEQ'
	LD_FAT_EFF  = 'PAYMENT EFFECTIVE DATE'
	PAMT = 'PAYMENT AMOUNT';
RUN;

PROC PRINTTO;
RUN;
