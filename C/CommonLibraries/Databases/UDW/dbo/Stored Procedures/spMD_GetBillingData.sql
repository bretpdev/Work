CREATE PROCEDURE [dbo].[spMD_GetBillingData]  
	@AccountNumber				VARCHAR(10) 
AS
BEGIN
	SET NOCOUNT ON;
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
    SELECT DISTINCT 
		CONVERT(VARCHAR, LN80.LD_BIL_DU_LON, 101) AS Due,
		CONVERT(VARCHAR, BL10.LD_BIL_CRT, 101) AS Billed,
		CASE 
			WHEN SUM(LN80.LA_BIL_CUR_DU) OVER (PARTITION BY LN80.BF_SSN, LN80.LD_BIL_CRT, LN80.LN_SEQ_BIL_WI_DTE) = SUM(LA_TOT_BIL_STS)OVER (PARTITION BY LN80.BF_SSN, LN80.LD_BIL_CRT, LN80.LN_SEQ_BIL_WI_DTE) THEN 'Y'
			WHEN SUM(LA_TOT_BIL_STS)OVER (PARTITION BY LN80.BF_SSN, LN80.LD_BIL_CRT, LN80.LN_SEQ_BIL_WI_DTE) > 0 THEN 'P'
			ELSE 'N'
		END AS Satisfied,
		CASE 
			WHEN SUM(LA_BIL_CUR_DU) OVER (PARTITION BY LN80.BF_SSN, LN80.LD_BIL_CRT, LN80.LN_SEQ_BIL_WI_DTE) = 0 THEN  '0.00 PFH'
			WHEN BL10.LC_IND_BIL_SNT IN ('N','5') AND LD_BIL_DU_LON > GETDATE() THEN CONVERT(VARCHAR, BL10.LD_BIL_CRT, 101)
			ELSE ISNULL(CONVERT(VARCHAR, MAX(LN90.LD_FAT_EFF) OVER (PARTITION BY LN90.BF_SSN, LN80.LD_BIL_CRT, LN80.LN_SEQ_BIL_WI_DTE, LN80.LN_BIL_OCC_SEQ, LN80.LA_TOT_BIL_STS) , 101),'')
		END AS DateSatisfied,
		LN80.LD_BIL_DU_LON  AS orderdate
	FROM 
		BL10_BR_BIL BL10
		INNER JOIN PD10_PRS_NME PD10
			ON PD10.DF_PRS_ID = BL10.BF_SSN
		INNER JOIN LN80_LON_BIL_CRF LN80
			ON LN80.BF_SSN = BL10.BF_SSN
			AND LN80.LD_BIL_CRT = BL10.LD_BIL_CRT
			AND LN80.LN_SEQ_BIL_WI_DTE = BL10.LN_SEQ_BIL_WI_DTE
		LEFT OUTER JOIN LN75_BIL_LON_FAT LN75
			ON LN75.BF_SSN = LN80.BF_SSN
			AND LN75.LN_SEQ = LN80.LN_SEQ
			AND LN75.LD_BIL_CRT = LN80.LD_BIL_CRT
			AND LN75.LN_SEQ_BIL_WI_DTE = LN80.LN_SEQ_BIL_WI_DTE
			AND LN75.LN_BIL_OCC_SEQ = LN80.LN_BIL_OCC_SEQ
			AND LN80.LA_BIL_CUR_DU = LN80.LA_TOT_BIL_STS
		LEFT OUTER JOIN LN90_FIN_ATY LN90
			ON LN75.BF_SSN = LN90.BF_SSN
			AND LN75.LN_SEQ = LN90.LN_SEQ
			AND LN75.LN_FAT_SEQ = LN90.LN_FAT_SEQ	
	WHERE 
		DF_SPE_ACC_ID = @AccountNumber
		AND LN80.LC_BIL_TYP_LON = 'P'
		AND LN80.LC_STA_LON80 = 'A'
		AND BL10.LC_STA_BIL10 = 'A'
	ORDER BY 
		Satisfied,
		LD_BIL_DU_LON DESC
END
GO
GRANT EXECUTE
    ON OBJECT::[dbo].[spMD_GetBillingData] TO [UHEAA\Imaging Users]
    AS [dbo];

