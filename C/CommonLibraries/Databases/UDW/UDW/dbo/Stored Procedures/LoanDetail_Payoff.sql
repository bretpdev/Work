
CREATE PROCEDURE [dbo].[LoanDetail_Payoff]
	@LetterId varchar(10),
	@BF_SSN  CHAR(9)
AS

DECLARE @PF_REQ_ACT VARCHAR(5) = (SELECT AC11.PF_REQ_ACT FROM AC11_ACT_REQ_LTR AC11 WHERE PF_LTR = @LetterId)

SELECT DISTINCT
	ISNULL(FT.Label, LN10.IC_LON_PGM) AS IC_LON_PGM,
	CONVERT(VARCHAR(10), LN10.LD_LON_1_DSB, 101) AS LD_LON_1_DSB,
	'$' + CONVERT(VARCHAR(15), LN10.LA_LON_AMT_GTR, 1) AS OPAFEL,
	CONVERT(VARCHAR(10), LN72.LR_ITR,0) + '%' AS LR_INT_BIL,
	'$' + CONVERT(VARCHAR(15), LN10.LA_CUR_PRI, 1) AS LA_CUR_PRN_BIL,
	'$' + CONVERT(VARCHAR(15),isnull( LN80.LN_LTE_FEE,0), 1) AS LN_LTE_FEE,
	LN16.LN_DLQ_MAX AS DAYS_DELQ,
	'$' + CONVERT(VARCHAR(15), ISNULL(LN10.LA_CUR_PRI,0) + COALESCE(DW01.WA_TOT_BRI_OTS, DW01.LA_NSI_OTS, 0) + ISNULL(LN80.LN_LTE_FEE,0), 1) AS LNPIF
FROM
	UDW..LN10_LON LN10
	INNER JOIN UDW..DW01_DW_CLC_CLU DW01
		ON DW01.BF_SSN = LN10.BF_SSN
		AND DW01.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN UDW..FormatTranslation FT
		ON FT.Start = LN10.IC_LON_PGM
	INNER JOIN
	(
		SELECT
			LN85.BF_SSN,
			LN85.LN_SEQ
		FROM
			UDW..LN85_LON_ATY LN85
			INNER JOIN --GETS THE MOST RECENT ARC LEFT ON THE BORROWERS ACCOUNT TO GET THE LOAN SEQ'S THE LETTER APPLIES TO
			(
				SELECT
					AY10.BF_SSN,
					MAX(AY10.LN_ATY_SEQ) AS LN_ATY_SEQ,
					MAX(AY10.LD_ATY_REQ_RCV) AS LD_ATY_REQ_RCV
				FROM 
					UDW..AY10_BR_LON_ATY AY10
				WHERE
					AY10.PF_REQ_ACT = @PF_REQ_ACT
				GROUP BY
					AY10.BF_SSN
			)AY10
				ON AY10.BF_SSN = LN85.BF_SSN
				AND AY10.LN_ATY_SEQ = LN85.LN_ATY_SEQ
	)LN85
		ON LN85.BF_SSN = LN10.BF_SSN
		AND LN85.LN_SEQ = LN10.LN_SEQ
	INNER JOIN
	(
		SELECT
			LN72.BF_SSN,
			LN72.LN_SEQ,
			LN72.LR_ITR
		FROM
			UDW..LN72_INT_RTE_HST LN72
		WHERE
			LN72.LC_STA_LON72 = 'A'
			AND CAST(GETDATE() AS DATE) BETWEEN LN72.LD_ITR_EFF_BEG AND LN72.LD_ITR_EFF_END
	)LN72
		ON LN72.BF_SSN = LN10.BF_SSN
		AND LN72.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN 
	(
		SELECT	
			LN80.BF_SSN,
			LN80.LN_SEQ,
			ISNULL(LN80.LA_LTE_FEE_OTS_PRT,0.00) AS LN_LTE_FEE,
			LN80.LA_BIL_PAS_DU AS LA_BIL_PAS_DU
		FROM
			UDW..LN80_LON_BIL_CRF LN80
			INNER JOIN
			(
				SELECT	
					LN80.BF_SSN,
					LN80.LN_SEQ,
					MAX(LN80.LD_BIL_CRT) AS MAX_LD_BIL_CRT
				FROM
					UDW..LN80_LON_BIL_CRF LN80
				WHERE
					LN80.LC_STA_LON80 = 'A'
				GROUP BY
					LN80.BF_SSN,
					LN80.LN_SEQ
			)LN80_MAX
				ON LN80_MAX.BF_SSN = LN80.BF_SSN
				AND LN80_MAX.LN_SEQ = LN80.LN_SEQ
				AND LN80_MAX.MAX_LD_BIL_CRT = LN80.LD_BIL_CRT

	)LN80
		ON LN80.BF_SSN = LN10.BF_SSN
		AND LN80.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN
	(
		SELECT
			BF_SSN,
			LN_SEQ,
			SUM(ABS(CAST(COALESCE(LA_FAT_CUR_PRI,0) AS DECIMAL (18,2)))) AS TAPTP,
			SUM(ABS(CAST(COALESCE(LA_FAT_NSI,0) AS DECIMAL (18,2)))) AS TAPTI,
			SUM(ABS(CAST(COALESCE(LA_FAT_LTE_FEE,0)AS DECIMAL (18,2)))) AS TAPTF, /*cumulative late fees paid*/
			SUM(ABS(CAST(COALESCE(LA_FAT_CUR_PRI,0) AS DECIMAL(18,2)) + CAST(COALESCE(LA_FAT_NSI,0) AS DECIMAL(18,2)) + CAST(COALESCE(LA_FAT_LTE_FEE,0) AS DECIMAL(18,2)))) AS TAGAP
		FROM 
			UDW..LN90_FIN_ATY
		WHERE
			PC_FAT_TYP = '10'
			AND LC_FAT_REV_REA = ''
			AND LC_STA_LON90 = 'A'
		GROUP BY
			BF_SSN,
			LN_SEQ
	)LN90
		ON LN90.BF_SSN = LN10.BF_SSN
		AND LN90.LN_SEQ = LN10.LN_SEQ
	LEFT JOIN LN16_LON_DLQ_HST LN16
		ON LN16.BF_SSN = LN10.BF_SSN
		AND LN16.LN_SEQ = LN10.LN_SEQ
		AND LN16.LC_STA_LON16 = '1'
WHERE 
	LN10.BF_SSN = @BF_SSN

RETURN 0