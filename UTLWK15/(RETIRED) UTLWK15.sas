/*============================================*/
/*UTLWK15 COMPASS CHAPTER 7 BANKRUPTCY REVIEW*/
/*============================================*/
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWK15.LWK15R2";
FILENAME REPORTZ "&RPTLIB/ULWK15.LWK15RZ";

DATA _NULL_;
     CALL SYMPUT('DT1',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('DT2',"'"||PUT(INTNX('DAY',TODAY(),-120,'BEGINNING'), MMDDYYD10.)||"'");
     CALL SYMPUT('RUNDT',PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYS10.));
RUN;

%SYSLPUT DT1 = &DT1;
%SYSLPUT DT2 = &DT2;
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;

%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CC07BR1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.BF_SSN
	,B.DD_BKR_FIL
	,B.DC_BKR_TYP
	,B.DD_BKR_STA
	,B.DD_BKR_NTF
	,B.DF_COU_DKT 
	,D.MAXREVIEW
	,C.EXCLD
FROM OLWHRM1.LN10_LON A
INNER JOIN OLWHRM1.PD24_PRS_BKR B
	ON A.BF_SSN = B.DF_PRS_ID
	AND B.DC_BKR_STA = '06'
LEFT OUTER JOIN (
	 SELECT X.DF_PRS_ID
		,'Y' AS EXCLD
	 FROM OLWHRM1.AY01_BR_ATY X
	 INNER JOIN OLWHRM1.PD24_PRS_BKR Y
	 	ON X.DF_PRS_ID = Y.DF_PRS_ID
	 WHERE X.PF_ACT = 'DBKRW'
	 AND X.BD_ATY_PRF > &DT1
	 AND Y.DC_BKR_TYP = '07'
	 ) C
	ON A.BF_SSN = C.DF_PRS_ID
LEFT OUTER JOIN (
	 SELECT DF_PRS_ID
		,MAX(BD_ATY_PRF) AS MAXREVIEW
	 FROM OLWHRM1.AY01_BR_ATY 
	 WHERE PF_ACT = 'DBKRW'
	 GROUP BY DF_PRS_ID
	 ) D
	ON A.BF_SSN = D.DF_PRS_ID
WHERE A.LA_CUR_PRI > 0
AND A.LC_STA_LON10 = 'R'
AND B.DD_BKR_FIL <= &DT2 
AND B.DC_BKR_TYP = '07'
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.CT30_CALL_QUE X
	WHERE X.DF_PRS_ID_BR = A.BF_SSN
	AND X.IF_WRK_GRP = 'BKCP7RVW'
	)
FOR READ ONLY WITH UR
);

CREATE TABLE TILP_ONLY AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT A.*
FROM OLWHRM1.LN10_LON A
WHERE IC_LON_PGM = 'TILP'
AND NOT EXISTS (
	SELECT *
	FROM OLWHRM1.LN10_LON X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.IC_LON_PGM != 'TILP'
	)
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWK15.LWK15RZ);*/
/*QUIT;*/

/*EXCLUDE TILP ONLY BORROWERS*/
PROC SQL;
CREATE TABLE CC07BR AS 
SELECT DISTINCT A.*
FROM CC07BR1 A
WHERE NOT EXISTS (
	SELECT *
	FROM TILP_ONLY X
	WHERE X.BF_SSN = A.BF_SSN
	)
;
QUIT;
ENDRSUBMIT;

DATA CC07BR;
SET WORKLOCL.CC07BR;
RUN;

DATA CC07BR;
SET CC07BR;
IF EXCLD = 'Y' THEN DELETE;
ELSE OUTPUT;
RUN;

PROC SORT DATA=CC07BR;
BY BF_SSN;
RUN;
/*-----------------------------------------------------------------------------*
| THIS MACRO WILL CHANGE DATES INTO STIRNGS SO THEY CAN BE CONCATINATED IN THE |
| COMMENTS VARIABLE                                                            |
*-----------------------------------------------------------------------------*/
%MACRO DATE_2_CHAR(DS,CDATE,DAYPART);
DATA &DS;
SET &DS;
LENGTH &DAYPART $ 10. ;
IF &CDATE = . THEN DO;
	&DAYPART = '';
	END;
ELSE IF DAY(&CDATE) < 10 AND MONTH(&CDATE) < 10 THEN DO;
	&DAYPART = '0'||TRIM(LEFT(MONTH(&CDATE)))||'/'||'0'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE IF DAY(&CDATE) < 10 AND MONTH(&CDATE) >= 10 THEN DO;
	&DAYPART = TRIM(LEFT(MONTH(&CDATE)))||'/'||'0'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE IF DAY(&CDATE) >= 10 AND MONTH(&CDATE) < 10 THEN DO;
	&DAYPART ='0'||TRIM(LEFT(MONTH(&CDATE)))||'/'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
ELSE DO;
	&DAYPART = TRIM(LEFT(MONTH(&CDATE)))||'/'||TRIM(LEFT(DAY(&CDATE)))||'/'||TRIM(LEFT(YEAR(&CDATE)));
	END;
RUN;
%MEND DATE_2_CHAR;
%DATE_2_CHAR(CC07BR,DD_BKR_FIL,CHAR_DD_BKR_FIL);
%DATE_2_CHAR(CC07BR,DD_BKR_STA,CHAR_DD_BKR_STA);
%DATE_2_CHAR(CC07BR,DD_BKR_NTF,CHAR_DD_BKR_NTF);
%DATE_2_CHAR(CC07BR,MAXREVIEW,CHAR_MAXREVIEW);

/*------------------ VARIABLE INITIALIZATION ------------------*/
DATA CC07BR (KEEP=TARGET_ID QUEUE_NAME INSTITUTION_ID INSTITUTION_TYPE DATE_DUE TIME_DUE COMMENTS);
SET CC07BR ;
LENGTH COMMENTS $ 600.;
TARGET_ID = BF_SSN;
QUEUE_NAME = 'BKCP7RVW';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
COMMENTS = 'Compass BK Filed Date: '||TRIM(LEFT(CHAR_DD_BKR_FIL))||','||
	'BK Status: '||TRIM(LEFT(CHAR_DD_BKR_STA))||','||
	'BK Chapter: '||TRIM(LEFT(DC_BKR_TYP))||','||
	'BK Court: '||TRIM(LEFT(DF_COU_DKT))||','||
	'Last RVW: '||TRIM(LEFT(CHAR_MAXREVIEW))||','||
	'Date Not: '||TRIM(LEFT(CHAR_DD_BKR_NTF))
;
RUN;

DATA _NULL_;
SET  WORK.CC07BR; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;

