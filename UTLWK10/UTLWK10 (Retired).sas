/*UTLWK10 BORROWERS WITH FOREIGN ADDRESSES*/
LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS\;
FILENAME REPORT2 "&RPTLIB/ULWK10.LWK10R2";
FILENAME REPORTZ "&RPTLIB/ULWK10.LWK10RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE CFORADD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID
	,A.DM_FGN_CNY
	,'COMPASS' AS SYSTEM
	,MAX(B.LD_REQ_RSP_ATY_PRF) AS MFRGN_MX_DT
	,MAX(A.DD_VER_ADR) AS MX_UPDT
FROM OLWHRM1.PD30_PRS_ADR A
INNER JOIN OLWHRM1.LN10_LON C
	ON A.DF_PRS_ID = C.BF_SSN
LEFT OUTER JOIN OLWHRM1.AY10_BR_LON_ATY B
	ON A.DF_PRS_ID = B.BF_SSN
	AND B.PF_REQ_ACT = 'MFRGN'
WHERE A.DI_VLD_ADR = 'Y'
AND (
	A.DC_DOM_ST = 'FC' OR 
	A.DM_FGN_ST <> ' ' OR 
	A.DM_FGN_CNY <> ' '
	)
AND SUBSTR(A.DF_PRS_ID,1,1) <> 'P'
AND C.LA_CUR_PRI > 0
GROUP BY A.DF_PRS_ID ,A.DM_FGN_CNY
FOR READ ONLY WITH UR
);

CREATE TABLE OFORADD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_PRS_ID
	,A.DM_FGN_CNY
	,'OneLINK' AS SYSTEM
	,MAX(B.BD_ATY_PRF) AS MFRGN_MX_DT
	,MAX(A.DD_LST_UPD_ADR) AS MX_UPDT
FROM OLWHRM1.PD03_PRS_ADR_PHN A
LEFT OUTER JOIN OLWHRM1.AY01_BR_ATY B
	ON A.DF_PRS_ID = B.DF_PRS_ID
	AND B.PF_ACT = 'MFRGN'
WHERE (
	A.DC_DOM_ST = 'FC' OR 
	A.DM_FGN_CNY <> ' ' 
	)
AND A.DI_VLD_ADR = 'Y'
GROUP BY A.DF_PRS_ID 
	,A.DM_FGN_CNY
FOR READ ONLY WITH UR
);

CREATE TABLE EXCLD AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DF_PRS_ID_BR
FROM OLWHRM1.CT30_CALL_QUE 
WHERE IF_WRK_GRP = 'KFRGNADD'
FOR READ ONLY WITH UR
);

DISCONNECT FROM DB2;
%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;
%SQLCHECK (SQLRPT=ULWK10.LWK10RZ);
QUIT;
DATA OFORADD;
SET OFORADD;
IF MFRGN_MX_DT >= MX_UPDT THEN DELETE;
RUN;

DATA CFORADD;
SET CFORADD;
IF MFRGN_MX_DT >= MX_UPDT THEN DELETE;
RUN;

DATA OFORADD;
SET OFORADD CFORADD;
RUN;

PROC SQL;
CREATE TABLE BFA AS
SELECT A.DF_PRS_ID
	,CASE 
		WHEN A.DM_FGN_CNY = '' THEN 'ZZZZ'
		ELSE A.DM_FGN_CNY
	 END AS SVAR
	,DM_FGN_CNY
	,A.SYSTEM
FROM OFORADD A
WHERE A.DF_PRS_ID NOT IN (
	SELECT DISTINCT DF_PRS_ID_BR
	FROM EXCLD
	)
ORDER BY SVAR
	,DF_PRS_ID
;
QUIT;
ENDRSUBMIT;
DATA BFA;
SET WORKLOCL.BFA;
RUN;
/*------------------ VARIABLE INITIALIZATION ------------------*/
DATA BFA (KEEP=TARGET_ID QUEUE_NAME INSTITUTION_ID INSTITUTION_TYPE DATE_DUE TIME_DUE COMMENTS);
SET BFA ;
LENGTH COMMENTS $ 600.;
TARGET_ID = DF_PRS_ID;
QUEUE_NAME = 'KFRGNADD';
INSTITUTION_ID = '';
INSTITUTION_TYPE = '';
DATE_DUE = '';
TIME_DUE = '';
COMMENTS = TRIM(LEFT(SYSTEM))||','||'Country: '||TRIM(LEFT(DM_FGN_CNY));
RUN;

DATA _NULL_;
SET  WORK.BFA; 
FILE REPORT2 DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT COMMENTS $600. ;
   FORMAT TARGET_ID $9. ;
   FORMAT QUEUE_NAME $8. ;
   FORMAT INSTITUTION_ID $1. ;
   FORMAT INSTITUTION_TYPE $1. ;
   FORMAT DATE_DUE $1. ;
   FORMAT TIME_DUE $1. ;
DO;
	PUT TARGET_ID $ @;
	PUT QUEUE_NAME $ @;
	PUT INSTITUTION_ID $ @;
	PUT INSTITUTION_TYPE $ @;
	PUT DATE_DUE $ @;
	PUT TIME_DUE $ @;
	PUT COMMENTS $ ;
END;
RUN;
