/*UTLWD27 1099 NEEDED FOR COMPROMISE*/

/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWD27.LWD27R2";

OPTIONS SYMBOLGEN;

DATA _NULL_;
     CALL SYMPUT('BEGIN',"'"||PUT(INTNX('YEAR',TODAY(),-1,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('END',"'"||PUT(INTNX('MONTH',INTNX('YEAR',TODAY(),-1,'END'),0,'END'), MMDDYYD10.)||"'");
	 CALL SYMPUT('RUNDT',PUT(TODAY(),MMDDYY10.)); 
RUN;

%SYSLPUT BEGIN = &BEGIN;
%SYSLPUT END = &END;

LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE TNCOMP AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT 
	C.DF_SPE_ACC_ID
	,A.BF_SSN
	,C.DM_PRS_1
	,C.DM_PRS_LST
	,A.LD_STA_UPD_DC10
	,B.LD_TRX_EFF
	,SUM(B.LA_TRX) AS LA_TRX
/*	,A.LC_WOF_CPR*/
/*	,A.LC_STA_DC10*/
/*	,B.LC_TRX_TYP*/
/*	,B.LC_REV_IND_TYP*/
/*	,B.LD_TRX_EFF*/
/*	,D.PF_ACT*/
/*	,B.LA_TRX*/


FROM OLWHRM1.DC01_LON_CLM_INF A
INNER JOIN OLWHRM1.DC11_LON_FAT B
	ON A.AF_APL_ID = B.AF_APL_ID
	AND A.AF_APL_ID_SFX = B.AF_APL_ID_SFX
	AND A.LF_CRT_DTS_DC10 = B.LF_CRT_DTS_DC10
INNER JOIN OLWHRM1.PD01_PDM_INF C
	ON A.BF_SSN = C.DF_PRS_ID
INNER JOIN OLWHRM1.AY01_BR_ATY D
	ON D.DF_PRS_ID = A.BF_SSN

WHERE A.LC_STA_DC10 = '04'
AND B.LC_TRX_TYP IN ('AD','A1','A2','A3')
AND B.LC_REV_IND_TYP = ' '
AND B.LD_TRX_EFF BETWEEN &BEGIN AND &END
AND D.PF_ACT IN ('DRS99','DRBRZ')
AND B.LA_TRX >= 600.00

GROUP BY 
	C.DF_SPE_ACC_ID
	,A.BF_SSN
	,C.DM_PRS_1
	,C.DM_PRS_LST
	,A.LD_STA_UPD_DC10
	,B.LD_TRX_EFF
);
DISCONNECT FROM DB2;
ENDRSUBMIT;

DATA TNCOMP;
SET WORKLOCL.TNCOMP;
RUN;

PROC SORT DATA=TNCOMP;
BY DF_SPE_ACC_ID;
RUN;

PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER NODATE;
TITLE '1099 COMPROMISE';
TITLE2	"RUNDATE &RUNDT";
FOOTNOTE 'JOB = UTLWD27  	 REPORT = ULWD27.LWD27R2';

PROC CONTENTS DATA=TNCOMP OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 127*'-';
	PUT      ////////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT ////////
		@57 '-- END OF REPORT --';
	PUT ////////////
		@46 "JOB = UTLWD27  	 REPORT = ULWD27.LWD27R2";
	END;
RETURN;
RUN;

PROC PRINT NOOBS SPLIT='/' DATA=TNCOMP WIDTH=UNIFORM;
FORMAT LD_TRX_EFF MMDDYY10. LD_STA_UPD_DC10 MMDDYY10.;
VAR DF_SPE_ACC_ID DM_PRS_1	DM_PRS_LST LD_STA_UPD_DC10 LD_TRX_EFF LA_TRX;
LABEL DF_SPE_ACC_ID = 'BORROWER ACCOUNT'
	DM_PRS_1 = 'FIRST NAME'
	DM_PRS_LST = 'LAST NAME'
	LD_STA_UPD_DC10 = 'DATE PIF'
	LD_TRX_EFF = 'EFFECTIVE DATE'
	LA_TRX = 'AMOUNT APPLIED';
RUN;

PROC PRINTTO;
RUN;
