*---------------------------------------------*
|UTLWS23 REPAYMENT ACCOUNT NOT HELD BY UHEAA  |
*---------------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWS23.LWS23R2";
FILENAME REPORT3 "&RPTLIB/ULWS23.LWS23R3";
FILENAME REPORTZ "&RPTLIB/ULWS23.LWS23RZ";
DATA _NULL_;
     CALL SYMPUT('DAYS_30',"'"||PUT(INTNX('DAY',TODAY(),-30,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('DAYS_10',"'"||PUT(INTNX('DAY',TODAY(),-10,'BEGINNING'), MMDDYYD10.)||"'");
	 CALL SYMPUT('TODAY',"'"||PUT(INTNX('DAY',TODAY(),0,'BEGINNING'), MMDDYYD10.)||"'");
RUN;
%SYSLPUT DAYS_30 = &DAYS_30;
%SYSLPUT DAYS_10 = &DAYS_10;
%SYSLPUT TODAY = &TODAY;
LIBNAME  WORKLOCL  REMOTE  SERVER=DUSTER  SLIBREF=WORK;
RSUBMIT;
LIBNAME SAS_TAB V8 '/sas/whse/progrevw';
PROC SQL NOPRINT;
	SELECT "'"||TRIM(LENDER_ID)||"'"
		INTO :UHEAA_LIST SEPARATED BY ","
	FROM SAS_TAB.LDR_AFF
	WHERE AFFILIATION = 'UHEAA';
QUIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;

DATA LOAN_TYPES;
	INFILE '/sas/whse/progrevw/GENR_REF_LoanTypes.txt' DLM=',' DSD MISSOVER;
	FORMAT LOAN_PROGRAM $6. LOAN_TYPE $10.;
	INPUT LOAN_PROGRAM $ LOAN_TYPE $;
RUN;

PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE RANHBUQ1 AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT DISTINCT A.DF_SPE_ACC_ID
	,A.DM_PRS_1 || ' ' || A.DM_PRS_LST AS BORROWER_NAME
	,X.BF_SSN
	,X.IC_LON_PGM
	,X.LN_SEQ
	,X.LF_LON_CUR_OWN
	,X.WD_LON_RPD_SR
	,X.WC_DW_LON_STA
	,X.LA_CUR_PRI
	,X.TEN_IND
FROM OLWHRM1.PD10_PRS_NME A
INNER JOIN (
		SELECT A.BF_SSN
			,A.IC_LON_PGM
			,A.LN_SEQ
			,A.LF_LON_CUR_OWN
			,A.LA_CUR_PRI
			,B.WD_LON_RPD_SR
			,B.WC_DW_LON_STA
			,' ' AS TEN_IND
		FROM OLWHRM1.LN10_LON A
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
			ON A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
		WHERE A.LA_CUR_PRI > 0
		AND A.LF_LON_CUR_OWN NOT IN (&UHEAA_LIST)
		AND B.WD_LON_RPD_SR <= &TODAY
		AND A.LC_STA_LON10 = 'R'
	UNION
		SELECT A.BF_SSN
			,A.IC_LON_PGM
			,A.LN_SEQ
			,A.LF_LON_CUR_OWN
			,A.LA_CUR_PRI
			,B.WD_LON_RPD_SR
			,B.WC_DW_LON_STA
			,DEFOR.TEN_IND
		FROM OLWHRM1.LN10_LON A
		INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
			ON A.BF_SSN = B.BF_SSN
			AND A.LN_SEQ = B.LN_SEQ
		LEFT OUTER JOIN (
				SELECT LN50.BF_SSN
					,LN50.LN_SEQ
					,'Y' AS TEN_IND
				FROM OLWHRM1.DF10_BR_DFR_REQ DF10
				INNER JOIN OLWHRM1.LN50_BR_DFR_APV LN50
					ON DF10.BF_SSN = LN50.BF_SSN
					AND DF10.LF_DFR_CTL_NUM = LN50.LF_DFR_CTL_NUM
				WHERE DF10.LC_STA_DFR10 = 'A'
				AND LN50.LC_STA_LON50 = 'A'
				AND LN50.LD_DFR_END BETWEEN &DAYS_10 AND &TODAY
			UNION
				SELECT LN60.BF_SSN
					,LN60.LN_SEQ
					,'Y' AS TEN_IND
				FROM OLWHRM1.FB10_BR_FOR_REQ FB10
				INNER JOIN OLWHRM1.LN60_BR_FOR_APV LN60
					ON FB10.BF_SSN = LN60.BF_SSN
					AND FB10.LF_FOR_CTL_NUM = LN60.LF_FOR_CTL_NUM 
				WHERE FB10.LC_STA_FOR10 = 'A'
				AND LN60.LC_STA_LON60 = 'A'
				AND LN60.LD_FOR_END BETWEEN &DAYS_10 AND &TODAY
			) DEFOR
			ON A.BF_SSN = DEFOR.BF_SSN
			AND A.LN_SEQ = DEFOR.LN_SEQ
		WHERE A.LA_CUR_PRI > 0
		AND A.LF_LON_CUR_OWN NOT IN (&UHEAA_LIST)
		AND B.WC_DW_LON_STA IN ('03','04','05')
		AND A.LC_STA_LON10 = 'R'
	) X
	ON A.DF_PRS_ID = X.BF_SSN
FOR READ ONLY WITH UR
);

CREATE TABLE EMIGRE AS
SELECT DISTINCT A.*
FROM CONNECTION TO DB2 (
	SELECT A.BF_SSN
		,A.LN_SEQ
	FROM OLWHRM1.LN10_LON A
	INNER JOIN OLWHRM1.DW01_DW_CLC_CLU B
		ON A.BF_SSN = B.BF_SSN
		AND A.LN_SEQ = B.LN_SEQ
	WHERE A.LC_STA_LON10 = 'R'
			AND	(B.WD_LON_RPD_SR >= &DAYS_30
			AND A.IC_LON_PGM = 'PLUS')
	OR A.IC_LON_PGM = 'TILP'
FOR READ ONLY WITH UR
) A;
DISCONNECT FROM DB2;

PROC SQL;
CREATE TABLE RANHBU AS
SELECT DISTINCT A.*
FROM RANHBUQ1 A
WHERE NOT EXISTS (
	SELECT *
	FROM EMIGRE X
	WHERE X.BF_SSN = A.BF_SSN
	AND X.LN_SEQ = A.LN_SEQ
	)
AND A.IC_LON_PGM NOT IN (
	SELECT LOAN_PROGRAM
	FROM LOAN_TYPES
	WHERE LOAN_TYPE = 'Private'
)
;
QUIT;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWS23.LWS23RZ);*/
/*QUIT;*/
ENDRSUBMIT;

DATA RANHBU ;
SET WORKLOCL.RANHBU;
RUN;

PROC SORT DATA=RANHBU ;
BY BF_SSN LN_SEQ TEN_IND;
RUN;

DATA RANHBU;
SET RANHBU;
BY BF_SSN LN_SEQ;
IF LAST.LN_SEQ THEN OUTPUT;
RUN; 

DATA RANHBU_CDLM;
SET RANHBU;
WHERE TEN_IND = 'Y';
TARGET_ID = BF_SSN;
ARC = 'SLCON';
DATE_FROM = '';
DATE_TO = '';
NEEDED_BY = '';
RECIPIENT_ID = '';
REGARDS_TO_CODE = '';
REGARDS_TO_ID = '';
LOAN_INDICATOR = 'ALL';
TEXT_FIELD = '';
RUN;

PROC SORT DATA=RANHBU_CDLM NODUPKEY;
BY BF_SSN;
RUN;


PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = PORTRAIT;
OPTIONS PS=52 LS=96 PAGENO=1 CENTER;
TITLE 'REPAYMENT ACCOUNTS NOT HELD BY UHEAA';
FOOTNOTE 'JOB = UTLWS23  	 REPORT = ULWS23.LWS23R2';
PROC CONTENTS DATA=RANHBU OUT=EMPTYSET NOPRINT;
DATA _NULL_;
SET EMPTYSET;
FILE PRINT;
IF  NOBS=0 AND _N_ =1 THEN DO;
	PUT // 132*'-';
	PUT      //////
		@51 '**** NO OBSERVATIONS FOUND ****';
	PUT //////
		@57 '-- END OF REPORT --';
	PUT //////////////
		@46 "JOB = UTLWS23  	 REPORT = ULWS23.LWS23R2";
	END;
RETURN;
RUN;
PROC PRINT NOOBS SPLIT='/' DATA=RANHBU WIDTH=UNIFORM WIDTH=MIN;
FORMAT WD_LON_RPD_SR MMDDYY10. LA_CUR_PRI DOLLAR10.2;
VAR DF_SPE_ACC_ID
	BORROWER_NAME
	LN_SEQ
	WC_DW_LON_STA
	IC_LON_PGM
	LF_LON_CUR_OWN
	WD_LON_RPD_SR
	LA_CUR_PRI;
LABEL DF_SPE_ACC_ID = 'ACCOUNT NUMBER'
	BORROWER_NAME = 'NAME'
	LN_SEQ = 'LOAN SEQ'
	WC_DW_LON_STA = 'LOAN STATUS'
	IC_LON_PGM = 'LOAN TYPE'
	LF_LON_CUR_OWN = 'CURRENT OWNER'
	WD_LON_RPD_SR = 'REPAYMENT START DATE'
	LA_CUR_PRI = 'CURRENT BALANCE'
;
RUN;
PROC PRINTTO;
RUN;

/*DATA _NULL_;*/
/*SET  WORK.RANHBU_CDLM ;*/
/*FILE REPORT3 DELIMITER=',' DSD DROPOVER LRECL=32767;*/
/*FORMAT TARGET_ID $9. ;*/
/*FORMAT ARC $5. ;*/
/*FORMAT DATE_FROM $1. ;*/
/*FORMAT DATE_TO $1. ;*/
/*FORMAT NEEDED_BY $1. ;*/
/*FORMAT RECIPIENT_ID $1. ;*/
/*FORMAT REGARDS_TO_CODE $1. ;*/
/*FORMAT REGARDS_TO_ID $1. ;*/
/*FORMAT LOAN_INDICATOR $3. ;*/
/*FORMAT TEXT_FIELD $1. ;*/
/*DO;*/
/*	PUT TARGET_ID $ @;*/
/*	PUT ARC $ @;*/
/*	PUT DATE_FROM $ @;*/
/*	PUT DATE_TO $ @;*/
/*	PUT NEEDED_BY $ @;*/
/*	PUT RECIPIENT_ID $ @;*/
/*	PUT REGARDS_TO_CODE $ @;*/
/*	PUT REGARDS_TO_ID $ @;*/
/*	PUT LOAN_INDICATOR $ @;*/
/*	PUT TEXT_FIELD $ ;*/
/*END;*/
/*RUN;*/
