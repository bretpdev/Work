CREATE PROCEDURE [covidforb].[GetDelinquentPopulation]
AS
	DECLARE @NOW DATETIME = GETDATE();
	DECLARE @TODAY DATE = CONVERT(DATE,@NOW);

SELECT
	PD10.DF_SPE_ACC_ID AS AccountNumber,
	MIN(LD_DLQ_OCC) AS DelinquencyOccurence,
	CAST(MAX(CASE WHEN LN10.IC_LON_PGM IN ('UNSPC','SUBSPC') THEN 1 ELSE 0 END) AS BIT) AS ComakerEligibility
FROM
	UDW..PD10_PRS_NME PD10
	INNER JOIN UDW..LN10_LON LN10
		ON PD10.DF_PRS_ID = LN10.BF_SSN
	INNER JOIN UDW..LN16_LON_DLQ_HST LN16
		ON LN10.BF_SSN = LN16.BF_SSN
		AND LN10.LN_SEQ = LN16.LN_SEQ
	INNER JOIN UDW..DW01_DW_CLC_CLU DW01
		ON LN10.BF_SSN = DW01.BF_SSN
		AND LN10.LN_SEQ = DW01.LN_SEQ
	LEFT JOIN
	(
		SELECT
			FB10.BF_SSN
		FROM
			UDW..FB10_BR_FOR_REQ FB10
			INNER JOIN UDW..LN60_BR_FOR_APV LN60
				ON LN60.BF_SSN = FB10.BF_SSN
				AND LN60.LF_FOR_CTL_NUM = FB10.LF_FOR_CTL_NUM
		WHERE
			FB10.LC_FOR_STA = 'A'
			AND FB10.LC_STA_FOR10 = 'A'
			AND LN60.LC_STA_LON60 = 'A'
			AND CAST(LN60.LD_FOR_APL AS DATE) = @TODAY
		GROUP BY
			FB10.BF_SSN
	) EXISTING_FORB
		ON EXISTING_FORB.BF_SSN = PD10.DF_PRS_ID

WHERE
	LN10.LC_STA_LON10 = 'R'
	AND LN10.LA_CUR_PRI > 0.00
	AND LN10.LI_CON_PAY_STP_PUR != 'Y' --stop pursuit
	AND DW01.WC_DW_LON_STA NOT IN ('06','16','17','18','19','20','21','08','09','10','11','12')--06:cure, 16-21:Alleged/Verified DDB, 8-12:claims
	AND LN16.LN_DLQ_MAX >= 29
	AND LN16.LC_STA_LON16 = '1'
	AND EXISTING_FORB.BF_SSN IS NULL
	AND LN10.IC_LON_PGM != 'TILP'
GROUP BY
	PD10.DF_SPE_ACC_ID
;
RETURN 0
