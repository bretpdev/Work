*---------------------------------------*
| UTLWQ25 OneLINK Open Loans Statistics |
*---------------------------------------*;
/*LIBNAME DLGSUTWH DB2 DATABASE=DLGSUTWH OWNER=OLWHRM1;*/
/*%LET RPTLIB = %SYSGET(reportdir);*/
%LET RPTLIB = T:\SAS;
FILENAME REPORT2 "&RPTLIB/ULWQ25.LWQ25R2";
FILENAME REPORTZ "&RPTLIB/ULWQ25.LWQ25RZ";
LIBNAME  WORKLOCL  REMOTE  SERVER=CYPRUS  SLIBREF=WORK;
RSUBMIT;
%MACRO SQLCHECK (SQLRPT= );
%IF &SQLXRC NE 0 %THEN %DO;
	DATA _NULL_;
    FILE REPORTZ NOTITLES;
    PUT @01 " ********************************************************************* "
      / @01 " ****  THE SQL CODE ABOVE HAS EXPERIENCED AN ERROR.               **** "
      / @01 " ****  THE SAS LOG IN &SQLRPT SHOULD BE REVIEWED.          **** "       
      / @01 " ********************************************************************* "
      / @01 " ****  THE SQL ERROR CODE IS  &SQLXRC  AND THE SQL ERROR MESSAGE  **** "
      / @01 " ****  &SQLXMSG   **** "
      / @01 " ********************************************************************* ";
	RUN;
%END;
%MEND;
PROC SQL;
CONNECT TO DB2 (DATABASE=DLGSUTWH);
CREATE TABLE OOLS AS
SELECT *
FROM CONNECTION TO DB2 (
SELECT B.AF_APL_ID||B.AF_APL_ID_SFX AS CLUID
	,B.AA_GTE_LON_AMT
	,C.AC_LON_STA_TYP
	,D.LC_STA_DC10
FROM OLWHRM1.GA01_APP A
INNER JOIN OLWHRM1.GA10_LON_APP B
	ON A.AF_APL_ID = B.AF_APL_ID
INNER JOIN OLWHRM1.GA14_LON_STA C
	ON B.AF_APL_ID = C.AF_APL_ID
	AND B.AF_APL_ID_SFX = C.AF_APL_ID_SFX
LEFT OUTER JOIN (
	SELECT D1.AF_APL_ID
		,D1.AF_APL_ID_SFX
		,D1.LC_STA_DC10 
	FROM OLWHRM1.DC01_LON_CLM_INF D1
	INNER JOIN (
		SELECT AF_APL_ID
			,AF_APL_ID_SFX
			,MAX(LF_CRT_DTS_DC10) AS LF_CRT_DTS_DC10
		FROM OLWHRM1.DC01_LON_CLM_INF
		GROUP BY AF_APL_ID
			,AF_APL_ID_SFX
		) D2
		ON D1.AF_APL_ID = D2.AF_APL_ID
		AND D1.AF_APL_ID_SFX = D2.AF_APL_ID_SFX
		AND D1.LF_CRT_DTS_DC10 = D2.LF_CRT_DTS_DC10
	) D
	ON C.AF_APL_ID = D.AF_APL_ID
	AND C.AF_APL_ID_SFX = D.AF_APL_ID_SFX
WHERE B.AC_PRC_STA = 'A'
	AND C.AC_STA_GA14 = 'A'
	AND C.AC_LON_STA_TYP NOT IN ('CA','PN','PF','PC','DN','UI','UC','UD')
FOR READ ONLY WITH UR
);
DISCONNECT FROM DB2;
/*%PUT  SQLXRC= >>> &SQLXRC <<< ||| SQLXMSG= >>> &SQLXMSG >>>  ;  * INCLUDES ERROR MESSAGES TO SAS LOG  ;*/
/*%SQLCHECK (SQLRPT=ULWQ25.LWQ25RZ);*/
/*QUIT;*/
ENDRSUBMIT;
DATA OOLS;
	SET WORKLOCL.OOLS;
RUN;
DATA OOLS;
	SET OOLS;
	V=1;
	IF AC_LON_STA_TYP = 'CP' 
		AND LC_STA_DC10 = '04' THEN 
		DELETE;
	ELSE IF AC_LON_STA_TYP = 'CP' 
		AND LC_STA_DC10 = '03' THEN 
		CAT = 'COLLECTION LOANS';
	ELSE 
		CAT = 'LENDER LOANS';
RUN;
PROC SQL;
	CREATE TABLE SDAT AS
		SELECT CAT
			,COUNT(*) AS LOANS 
			,SUM(AA_GTE_LON_AMT) AS GUAR
			,COUNT(*) / B.ALOANS AS LNPRCT
			,SUM(AA_GTE_LON_AMT) / AGUAR AS GRPRCT
		FROM OOLS A
		INNER JOIN (
			SELECT V
				,COUNT(*) AS ALOANS
				,SUM(AA_GTE_LON_AMT) AS AGUAR
			FROM OOLS
			GROUP BY V
			) B
			ON A.V = B.V
		GROUP BY CAT
	;
QUIT;
PROC PRINTTO PRINT=REPORT2 NEW;
RUN;
OPTIONS ORIENTATION = LANDSCAPE;
OPTIONS PS=39 LS=127 PAGENO=1 CENTER DATE;
TITLE 'ONELINK OPEN LOANS STATISTICS';
FOOTNOTE   'JOB = UTLWQ25  	 REPORT = ULWQ25.LWQ25R2';
PROC PRINT NOOBS SPLIT='/' DATA=SDAT WIDTH=UNIFORM WIDTH=MIN LABEL;
	FORMAT LOANS COMMA10. GUAR DOLLAR20.2 LNPRCT GRPRCT PERCENT.;
	VAR CAT LOANS GUAR LNPRCT GRPRCT;
	LABEL CAT = '/'	LOANS = '# LOANS' GUAR = '$ GUARANTY' LNPRCT = '% # LOANS' GRPRCT = '% * GUARANTY';
	SUM LOANS GUAR LNPRCT GRPRCT;
RUN;
PROC PRINTTO;
RUN;
/*DETAIL FILE*/
DATA _NULL_;
SET  WORK.OOLS;
FILE 'T:\SAS\UTLWQ25.Detail.txt' DELIMITER=',' DSD DROPOVER LRECL=32767;
   FORMAT CLUID $19. ;
   FORMAT AA_GTE_LON_AMT 10.2 ;
   FORMAT AC_LON_STA_TYP $2. ;
   FORMAT LC_STA_DC10 $2. ;
   FORMAT CAT $16. ;
IF _N_ = 1 THEN DO;
   PUT 'CLUID,AA_GTE_LON_AMT,AC_LON_STA_TYP,LC_STA_DC10,CAT';
END;
DO;
	PUT CLUID $ @;
	PUT AA_GTE_LON_AMT @;
	PUT AC_LON_STA_TYP $ @;
	PUT LC_STA_DC10 $ @;
	PUT CAT $ ;
END;
RUN;
